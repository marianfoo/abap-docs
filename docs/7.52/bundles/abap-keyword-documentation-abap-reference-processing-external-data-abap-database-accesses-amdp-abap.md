# ABAP - Keyword Documentation / ABAP - Reference / Processing External Data / ABAP Database Accesses / AMDP - ABAP Managed Database Procedures / AMDP - Examples

Included pages: 13


### abenamdp_abexas.htm

  

* * *

SAP NetWeaver AS ABAP Release 752, ©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\)) →  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\)) →  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\)) →  [ABAP Database Accesses](javascript:call_link\('abenabap_sql.htm'\)) →  [AMDP - ABAP Managed Database Procedures](javascript:call_link\('abenamdp.htm'\)) → 

AMDP - Examples

Continue
![Example](exa.gif "Example") [AMDP, Implementation of a SQLScript Procedure](javascript:call_link\('abenamdp_abexa.htm'\))
![Example](exa.gif "Example") [AMDP, Calling an AMDP Procedure from SQLScript](javascript:call_link\('abenamdp_call_amdp_abexa.htm'\))
![Example](exa.gif "Example") [AMDP, Calling an SQLScript Procedure from AMDP](javascript:call_link\('abenamdp_call_abexa.htm'\))
![Example](exa.gif "Example") [AMDP, AMDP Methods in Interfaces and Superclasses](javascript:call_link\('abenamdp_polymorphism_abexa.htm'\))
![Example](exa.gif "Example") [AMDP, SQL Script with Tabular CHANGING Parameter](javascript:call_link\('abenamdp_changing_abexa.htm'\))
![Example](exa.gif "Example") [AMDP, AMDP Method with Specified Service Connection](javascript:call_link\('abenamdp_connection_abexa.htm'\))
![Example](exa.gif "Example") [AMDP, Filling a Mesh with SQLScript](javascript:call_link\('abenmesh_amdp_abexa.htm'\))
![Example](exa.gif "Example") [AMDP, Comparison of SQLScript with Open SQL](javascript:call_link\('abenamdp_vs_open_sql_abexa.htm'\))
![Example](exa.gif "Example") [AMDP, AMDP Functions](javascript:call_link\('abenamdp_functions_abexa.htm'\))
![Example](exa.gif "Example") [AMDP, Access to ABAP Types](javascript:call_link\('abenamdp_abap_types_abexa.htm'\))
![Example](exa.gif "Example") [AMDP, Access to Database Schemas](javascript:call_link\('abenamdp_db_schema_abexa.htm'\))
![Example](exa.gif "Example") [AMDP, Implementation of an L Procedure](javascript:call_link\('abenamdp_l_abexa.htm'\))


### abenamdp_abexa.htm

  

* * *

SAP NetWeaver AS ABAP Release 752, ©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\)) →  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\)) →  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\)) →  [ABAP Database Accesses](javascript:call_link\('abenabap_sql.htm'\)) →  [AMDP - ABAP Managed Database Procedures](javascript:call_link\('abenamdp.htm'\)) →  [AMDP - Examples](javascript:call_link\('abenamdp_abexas.htm'\)) → 

AMDP, Implementation of a SQLScript Procedure

This example demonstrates how an SQLScript procedure is implemented using AMDP.

Source Code

REPORT demo\_amdp.
CLASS demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
  METHOD main.
    IF NOT cl\_abap\_dbfeatures=>use\_features(
          EXPORTING
            requested\_features =
              VALUE #( ( cl\_abap\_dbfeatures=>call\_amdp\_method ) ) ).
      cl\_demo\_output=>display(
        \`Current database system does not support AMDP procedures\` ).
      RETURN.
    ENDIF.
    DATA incprice TYPE sflight-price VALUE '0.1'.
    DATA(client)     = abap\_false.
    DATA(cds\_client) = abap\_false.
    cl\_demo\_input=>new(
      )->add\_field( CHANGING
                      field = incprice
      )->add\_field( EXPORTING
                      as\_checkbox = abap\_true
                      text        = \`Use session variable CLIENT\`
                    CHANGING
                      field = client
      )->add\_field( EXPORTING
                      as\_checkbox = abap\_true
                      text        = \`Use session variable CDS\_CLIENT\`
                    CHANGING
                      field = cds\_client
      )->request( ).
    IF incprice IS INITIAL OR
       client IS NOT INITIAL AND cds\_client IS NOT INITIAL.
      RETURN.
    ENDIF.
    SELECT price
           FROM sflight
           ORDER BY carrid, connid, fldate
           INTO @DATA(price\_before)
           UP TO 1 ROWS.
    ENDSELECT.
    IF client IS INITIAL AND cds\_client IS INITIAL.
      TRY.
          NEW cl\_demo\_amdp(
            )->increase\_price( clnt = sy-mandt
                               inc  = incprice ).
        CATCH cx\_amdp\_error INTO DATA(amdp\_error).
          cl\_demo\_output=>display( amdp\_error->get\_text( ) ).
          RETURN.
      ENDTRY.
    ELSEIF client IS NOT INITIAL.
      TRY.
          NEW cl\_demo\_amdp(
            )->increase\_price\_client( inc  = incprice ).
        CATCH cx\_amdp\_error INTO amdp\_error.
          cl\_demo\_output=>display( amdp\_error->get\_text( ) ).
          RETURN.
      ENDTRY.
    ELSEIF cds\_client IS NOT INITIAL.
      TRY.
          NEW cl\_demo\_amdp(
            )->increase\_price\_cds\_client( inc  = incprice ).
        CATCH cx\_amdp\_error INTO amdp\_error.
          cl\_demo\_output=>display( amdp\_error->get\_text( ) ).
          RETURN.
      ENDTRY.
    ENDIF.
    SELECT price
           FROM sflight
           ORDER BY carrid, connid, fldate
           INTO @DATA(price\_after)
           UP TO 1 ROWS.
    ENDSELECT.
    IF price\_after - price\_before = incprice.
      cl\_demo\_output=>display( \`Price increased succesfully\` ).
    ENDIF.  ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
  demo=>main( ).

Description

A simple [SQLScript procedure](javascript:call_link\('abensql_script_procedure_glosry.htm'\) "Glossary Entry") is implemented in three AMDP methods (showing the different options for self-defined [client handling](javascript:call_link\('abenamdp_client_handling.htm'\))) in the AMDP class CL\_DEMO\_AMDP:

-   INCREASE\_PRICE

METHOD increase\_price BY DATABASE PROCEDURE FOR HDB
                         LANGUAGE SQLSCRIPT
                         USING sflight.
  update sflight set price = price + :inc
                 where mandt = :clnt;
ENDMETHOD.

In this implementation, the input parameter clnt is used to access the data of the passed client, as [recommended](javascript:call_link\('abenamdp_client_handling.htm'\)).

-   INCREASE\_PRICE\_CLIENT

METHOD increase\_price\_client BY DATABASE PROCEDURE FOR HDB
                             LANGUAGE SQLSCRIPT
                             USING sflight.
  update sflight set price = price + :inc
                 where mandt = SESSION\_CONTEXT('CLIENT');
ENDMETHOD.

In this implementation, the [ABAP-specific session variable](javascript:call_link\('abenhana_session_variables.htm'\)) CLIENT is used to access the data of the current client.

-   INCREASE\_PRICE\_CDS\_CLIENT

METHOD increase\_price\_cds\_client BY DATABASE PROCEDURE FOR HDB
                                 LANGUAGE SQLSCRIPT
                                 USING sflight.
  update sflight set price = price + :inc
                 where mandt = SESSION\_CONTEXT('CDS\_CLIENT');
ENDMETHOD.

In this implementation, the [ABAP-specific session variable](javascript:call_link\('abenhana_session_variables.htm'\)) CDS\_CLIENT is used to access the data of the current client. The addition [AMDP OPTIONS CDS SESSION CLIENT CURRENT](javascript:call_link\('abapmethods_amdp_options.htm'\)) is used for this purpose in the declaration of the method.

The database table SPFLI defined in ABAP Dictionary must be specified after USING. On a SAP HANA database, this program works in the same way as the executable examples for [ADBC](javascript:call_link\('abenadbc_procedure_abexa.htm'\)) and for [CALL DATABASE PROCEDURE](javascript:call_link\('abencall_db_procedure_abexa.htm'\)). AMDP replaces these technologies when calling database procedures for an SAP HANA database that is the [standard database](javascript:call_link\('abenstandard_db_glosry.htm'\) "Glossary Entry") of an AS ABAP.

It is generally preferable to pass the client ID to an input parameter of a use of the ABAP-specific session variable CLIENT or CDS\_CLIENT.

Note

This is a syntax example. The same function can be provided with the same efficiency in Open SQL. AMDP is not needed in simple cases like this.


### abenamdp_call_amdp_abexa.htm

  

* * *

SAP NetWeaver AS ABAP Release 752, ©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\)) →  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\)) →  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\)) →  [ABAP Database Accesses](javascript:call_link\('abenabap_sql.htm'\)) →  [AMDP - ABAP Managed Database Procedures](javascript:call_link\('abenamdp.htm'\)) →  [AMDP - Examples](javascript:call_link\('abenamdp_abexas.htm'\)) → 

AMDP, Calling an AMDP Procedure from SQLScript

This example demonstrates how an AMDP procedure is called from an AMDP procedure.

Source Code

REPORT demo\_amdp\_call\_amdp\_procedure.
CLASS demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
  METHOD main.
    DATA incprice TYPE sflight-price.
    IF NOT cl\_abap\_dbfeatures=>use\_features(
          EXPORTING
            requested\_features =
              VALUE #( ( cl\_abap\_dbfeatures=>call\_amdp\_method ) ) ).
      cl\_demo\_output=>display(
        \`Current database system does not support AMDP procedures\` ).
      RETURN.
    ENDIF.
    cl\_demo\_input=>request( CHANGING field = incprice ).
    IF incprice IS INITIAL.
      RETURN.
    ENDIF.
    SELECT price
           FROM sflight
           ORDER BY carrid, connid, fldate
           INTO @DATA(price\_before)
           UP TO 1 ROWS.
    ENDSELECT.
    TRY.
        cl\_demo\_amdp\_call\_amdp=>increase\_price( clnt     = sy-mandt
                                                incprice = incprice ).
      CATCH cx\_amdp\_error INTO DATA(amdp\_error).
        cl\_demo\_output=>display( amdp\_error->get\_text( ) ).
        RETURN.
    ENDTRY.
    SELECT price
           FROM sflight
           ORDER BY carrid, connid, fldate
           INTO @DATA(price\_after)
           UP TO 1 ROWS.
    ENDSELECT.
    IF price\_after - price\_before = incprice.
      cl\_demo\_output=>display( \`Price increased succesfully\` ).
    ENDIF.  ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
  demo=>main( ).

Description

The [SQLScript procedure](javascript:call_link\('abensql_script_procedure_glosry.htm'\) "Glossary Entry") of the AMDP method INCREASE\_PRICE of the AMDP class CL\_DEMO\_AMDP\_CALL\_AMDP calls a further AMDP procedure implemented in the private AMDP method INCREASE\_PRICE\_AMDP in the same class:

Method was not found

The called database procedure must be specified after the addition [USING](javascript:call_link\('abapmethod_by_db_proc.htm'\)) of the statement METHOD. Compare the [executable example](javascript:call_link\('abenamdp_call_abexa.htm'\)) for calling a further database procedure not managed using AMDP.


### abenamdp_call_abexa.htm

  

* * *

SAP NetWeaver AS ABAP Release 752, ©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\)) →  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\)) →  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\)) →  [ABAP Database Accesses](javascript:call_link\('abenabap_sql.htm'\)) →  [AMDP - ABAP Managed Database Procedures](javascript:call_link\('abenamdp.htm'\)) →  [AMDP - Examples](javascript:call_link\('abenamdp_abexas.htm'\)) → 

AMDP, Calling an SQLScript Procedure from AMDP

This example demonstrates how a database procedure is called from an AMDP procedure.

Source Code

REPORT demo\_amdp\_call\_db\_procedure.
CLASS demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
  METHOD main.
    DATA incprice TYPE sflight-price.
    IF NOT cl\_abap\_dbfeatures=>use\_features(
          EXPORTING
            requested\_features =
              VALUE #( ( cl\_abap\_dbfeatures=>call\_amdp\_method ) ) ).
      cl\_demo\_output=>display(
        \`Current database system does not support AMDP procedures\` ).
      RETURN.
    ENDIF.
    cl\_demo\_input=>request( CHANGING field = incprice ).
    IF incprice IS INITIAL.
      RETURN.
    ENDIF.
    SELECT price
           FROM sflight
           ORDER BY carrid, connid, fldate
           INTO @DATA(price\_before)
           UP TO 1 ROWS.
    ENDSELECT.
    TRY.
        cl\_demo\_amdp\_call=>increase\_price( incprice ).
      CATCH cx\_amdp\_error INTO DATA(amdp\_error).
        cl\_demo\_output=>display( amdp\_error->get\_text( ) ).
        RETURN.
    ENDTRY.
    SELECT price
           FROM sflight
           ORDER BY carrid, connid, fldate
           INTO @DATA(price\_after)
           UP TO 1 ROWS.
    ENDSELECT.
    IF price\_after - price\_before = incprice.
      cl\_demo\_output=>display( \`Price increased succesfully\` ).
    ENDIF.  ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
  demo=>main( ).

Description

The [SQLScript procedure](javascript:call_link\('abensql_script_procedure_glosry.htm'\) "Glossary Entry") of the AMDP method INCREASE\_PRICE in the AMDP class CL\_DEMO\_AMDP\_CALL calls a database procedure (not managed by AMDP) of the current database schema in the namespace /1BCAMDP/:

METHOD increase\_price BY DATABASE PROCEDURE FOR HDB LANGUAGE SQLSCRIPT.
  call "/1BCAMDP/ABAP\_DOCU\_DEMO\_INCPRICE"( INC => :INCPRICE );
ENDMETHOD.

The called database procedure is created dynamically in the static constructor of the AMDP class CL\_DEMO\_AMDP\_CALL using [ADBC](javascript:call_link\('abenadbc.htm'\)). It is in the same database schema, but due to the namespace /1BCAMDP/ it cannot and must not be used after the addition [USING](javascript:call_link\('abapmethod_by_db_proc.htm'\)) of the METHOD statement. Compare this [executable example](javascript:call_link\('abenamdp_call_amdp_abexa.htm'\)) when calling a further AMDP procedure.


### abenamdp_polymorphism_abexa.htm

  

* * *

SAP NetWeaver AS ABAP Release 752, ©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\)) →  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\)) →  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\)) →  [ABAP Database Accesses](javascript:call_link\('abenabap_sql.htm'\)) →  [AMDP - ABAP Managed Database Procedures](javascript:call_link\('abenamdp.htm'\)) →  [AMDP - Examples](javascript:call_link\('abenamdp_abexas.htm'\)) → 

AMDP, AMDP Methods in Interfaces and Superclasses

This example demonstrates how AMDP methods are implemented from interfaces and superclasses.

Source Code

REPORT demo\_amdp\_polymorphism.
CLASS demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
  METHOD main.
    DATA: iref TYPE REF TO if\_demo\_amdp\_interface,
          cref TYPE REF TO cl\_demo\_amdp\_superclass.
    IF cl\_abap\_dbfeatures=>use\_features(
          EXPORTING
            requested\_features =
              VALUE #( ( cl\_abap\_dbfeatures=>call\_amdp\_method ) ) ).
      cref = NEW cl\_demo\_amdp\_subclass\_hdb( ).
      iref = NEW cl\_demo\_amdp\_implement\_hdb( ).
    ELSE.
      cref = NEW cl\_demo\_amdp\_subclass\_open( ).
      iref = NEW cl\_demo\_amdp\_implement\_open( ).
    ENDIF.
    TRY.
        iref->get\_scarr( EXPORTING clnt     = sy-mandt
                         IMPORTING carriers = DATA(result1) ).
        cref->get\_scarr( EXPORTING clnt     = sy-mandt
                         IMPORTING carriers = DATA(result2) ).
      CATCH cx\_amdp\_error INTO DATA(amdp\_error).
        cl\_demo\_output=>display( amdp\_error->get\_text( ) ).
        RETURN.
    ENDTRY.
    ASSERT result1 = result2.
    cl\_demo\_output=>display( name = 'Result'
                             data = result1 ).  ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
  demo=>main( ).

Description

An interface IF\_DEMO\_AMDP\_INTERFACE and an abstract superclass CL\_DEMO\_AMDP\_SUPERCLASS implement the tag interface IF\_AMDP\_MARKER\_HDB and contain the method GET\_CARRIER, which meets the prerequisites of an [AMDP method](javascript:call_link\('abenamdp_method_glosry.htm'\) "Glossary Entry").

The interface method is implemented once as an AMDP method and once as a regular method in the classes CL\_DEMO\_AMDP\_IMPLEMENT\_HDB and CL\_DEMO\_AMDP\_IMPLEMENT\_OPEN. In the same way, the method of the abstract superclass is redefined in the fixed subclasses CL\_DEMO\_AMDP\_SUBCLASS\_HDB and CL\_DEMO\_AMDP\_SUBCLASS\_OPEN.

This program demonstrates how objects are created using either the AMDP method or the regular method and called polymorphically, as specified by the current database.


### abenamdp_changing_abexa.htm

  

* * *

SAP NetWeaver AS ABAP Release 752, ©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\)) →  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\)) →  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\)) →  [ABAP Database Accesses](javascript:call_link\('abenabap_sql.htm'\)) →  [AMDP - ABAP Managed Database Procedures](javascript:call_link\('abenamdp.htm'\)) →  [AMDP - Examples](javascript:call_link\('abenamdp_abexas.htm'\)) → 

AMDP, SQL Script with Tabular CHANGING Parameter

This example demonstrates how an SQLScript procedure is implemented using AMDP with a tabular CHANGING parameter.

Source Code

REPORT demo\_amdp\_changing.
CLASS demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
  METHOD main.
    IF NOT cl\_abap\_dbfeatures=>use\_features(
          EXPORTING
            requested\_features =
              VALUE #( ( cl\_abap\_dbfeatures=>call\_amdp\_method ) ) ).
      cl\_demo\_output=>display(
        \`Current database system does not support AMDP procedures\` ).
      RETURN.
    ENDIF.
    DATA lower TYPE scarr-carrid VALUE 'AA'.
    DATA upper TYPE scarr-carrid VALUE 'BA'.
    DATA call\_flag TYPE abap\_bool.
    cl\_demo\_input=>new(
       )->add\_field( CHANGING field = lower
       )->add\_field( CHANGING field = upper
       )->add\_line(
       )->add\_field( EXPORTING text = 'Indirect call'
                               as\_checkbox = abap\_true
                     CHANGING  field = call\_flag
       )->request( ).
    DATA carriers TYPE cl\_demo\_amdp\_changing=>t\_carriers.
    SELECT mandt, carrid
           FROM scarr
           WHERE carrid BETWEEN @lower AND @upper
           ORDER BY mandt, carrid
           INTO CORRESPONDING FIELDS OF TABLE @carriers.
    DATA(out) = cl\_demo\_output=>new( )->write( carriers ).
    TRY.
        IF call\_flag IS INITIAL.
          NEW cl\_demo\_amdp\_changing(
            )->get\_carriers( CHANGING carriers = carriers ).
        ELSE.
          NEW cl\_demo\_amdp\_changing(
            )->call\_get\_carriers( CHANGING carriers = carriers ).
        ENDIF.
      CATCH cx\_amdp\_error INTO DATA(amdp\_error).
        cl\_demo\_output=>display( amdp\_error->get\_text( ) ).
        RETURN.
    ENDTRY.
    out->display( carriers ).  ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
  demo=>main( ).

Description

The following [SQLScript procedure](javascript:call_link\('abensql_script_procedure_glosry.htm'\) "Glossary Entry") is implemented in the AMDP method GET\_CARRIERS of the AMDP class CL\_DEMO\_AMDP\_CHANGING:

METHOD get\_carriers BY DATABASE PROCEDURE FOR HDB
                       LANGUAGE SQLSCRIPT
                    USING scarr.
  carriers  = select s.\*
                   from scarr as s
                   inner join :carriers as c
                     on s.mandt  = c.mandt and
                        s.carrid = c.carrid;
ENDMETHOD.

The tabular CHANGING parameter carriers can be used in this way in the procedure in reading positions and writing positions. Internally, the database procedure uses an identically named output parameter to which the initial value of the CHANGING parameter is assigned (using the invisible IN parameter carrriers\_\_in\_\_) when the procedure is first executed (see [SQL Script for the SAP HANA Database](javascript:call_link\('abenamdp_hdb_sqlscript.htm'\))).

A further method, CALL\_GET\_CARRIERS, demonstrates how the SQL Script procedure implemented in GET\_CARRIERS is called from a different SQL Script procedure.

METHOD call\_get\_carriers BY DATABASE PROCEDURE FOR HDB
                            LANGUAGE SQLSCRIPT
                         USING cl\_demo\_amdp\_changing=>get\_carriers.
  call "CL\_DEMO\_AMDP\_CHANGING=>GET\_CARRIERS"(
    CARRIERS\_\_IN\_\_  => :CARRIERS,
    CARRIERS        => :CARRIERS );
ENDMETHOD.

In this case, the implicit IN parameter carrriers\_\_in\_\_ must be filled explicit with the modified actual parameter.

Note

This is a syntax example. The same function can be provided with the same efficiency in Open SQL. AMDP is not needed in simple cases like this.


### abenamdp_connection_abexa.htm

  

* * *

SAP NetWeaver AS ABAP Release 752, ©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\)) →  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\)) →  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\)) →  [ABAP Database Accesses](javascript:call_link\('abenabap_sql.htm'\)) →  [AMDP - ABAP Managed Database Procedures](javascript:call_link\('abenamdp.htm'\)) →  [AMDP - Examples](javascript:call_link\('abenamdp_abexas.htm'\)) → 

AMDP, AMDP Method with Specified Service Connection

The example demonstrates an AMDP method with the input parameter connection.

Source Code

REPORT demo\_amdp\_connection.
CLASS demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
  METHOD main.
    IF NOT cl\_abap\_dbfeatures=>use\_features(
          EXPORTING
            requested\_features =
              VALUE #( ( cl\_abap\_dbfeatures=>call\_amdp\_method ) ) ).
      cl\_demo\_output=>display(
        \`Current database system does not support AMDP procedures\` ).
      RETURN.
    ENDIF.
    DATA connection TYPE dbcon\_name VALUE 'R/3\*my\_conn'.
    cl\_demo\_input=>request( CHANGING field = connection ).
    TRY.
        NEW cl\_demo\_amdp\_connection(
              )->get\_scarr( EXPORTING
                              connection = connection
                              clnt       = sy-mandt
                            IMPORTING
                              carriers = DATA(result) ).
      CATCH cx\_amdp\_error INTO DATA(amdp\_error).
        cl\_demo\_output=>display( amdp\_error->get\_text( ) ).
        RETURN.
    ENDTRY.
    cl\_demo\_output=>display( result ).  ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
  demo=>main( ).

Description

A simple [SQLScript procedure](javascript:call_link\('abensql_script_procedure_glosry.htm'\) "Glossary Entry") is implemented in the AMDP method GET\_SCARR of the AMDP class CL\_DEMO\_AMDP:

METHOD get\_scarr BY DATABASE PROCEDURE FOR HDB
                      LANGUAGE SQLSCRIPT
                      USING scarr.
  carriers  = select \*
                     from scarr
                     WHERE mandt  = clnt;
ENDMETHOD.

The method also has an optional input parameter with the predefined name connection. This is used to specify whether the [standard connection](javascript:call_link\('abenstandard_db_connection_glosry.htm'\) "Glossary Entry") or a [service connection](javascript:call_link\('abenservice_connection_glosry.htm'\) "Glossary Entry") is used. If a permitted name such as "DEFAULT" or "R/3\*my\_conn" is passed, the method is executed using either the standard connection or a service connection. Invalid names raise an exception and an exception is text is displayed.


### abenmesh_amdp_abexa.htm

  

* * *

SAP NetWeaver AS ABAP Release 752, ©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\)) →  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\)) →  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\)) →  [ABAP Database Accesses](javascript:call_link\('abenabap_sql.htm'\)) →  [AMDP - ABAP Managed Database Procedures](javascript:call_link\('abenamdp.htm'\)) →  [AMDP - Examples](javascript:call_link\('abenamdp_abexas.htm'\)) → 

AMDP, Filling a Mesh with SQLScript

This example demonstrates how a mesh is filled with a database procedure.

Source Code

REPORT demo\_amdp\_mesh.
CLASS demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS main.
  PRIVATE SECTION.
    TYPES:
      BEGIN OF output\_line,
        position TYPE string,
        product  TYPE string,
      END OF output\_line,
      output TYPE STANDARD TABLE OF output\_line WITH EMPTY KEY.
ENDCLASS.
CLASS demo IMPLEMENTATION.
  METHOD main.
    DATA order\_ids TYPE RANGE OF snwd\_so-so\_id.
    DATA(rows) = 1.
    cl\_demo\_input=>request( EXPORTING text = \`Orders\`
                            CHANGING field = rows ).
    SELECT 'I' AS sign, 'EQ' AS option, so\_id AS low
           FROM snwd\_so
           ORDER BY low
           INTO CORRESPONDING FIELDS OF TABLE @order\_ids
           UP TO @rows ROWS. "#EC CI\_NOWHERE
    DATA(out) = cl\_demo\_output=>new( ).
    TRY.
        DATA(order) = NEW cl\_demo\_amdp\_mesh( )->select( order\_ids ).
      CATCH cx\_amdp\_error INTO DATA(amdp\_error).
        out->display( amdp\_error->get\_text( ) ).
        RETURN.
    ENDTRY.
    LOOP AT order-orders ASSIGNING FIELD-SYMBOL(<order>).
      out->begin\_section( |Order #{ <order>-so\_id }| ).
      ASSIGN order-orders\\\_buyers\[ <order> \]
        TO FIELD-SYMBOL(<buyer>).
      out->begin\_section(
        |Buyer: { <buyer>-company\_name } | &&
        |in { order-bupas\\\_addresses\[ <buyer> \]-city } | ).
      out->write(
        name  = 'Sales Order Items'
        data = VALUE output(
          FOR <item> IN order-orders\\\_items\[ <order> \]
          ( position = <item>-so\_item\_pos
            product = order-items\\\_products\[ <item> \]-product\_id
          ) ) ).
      out->end\_section( )->end\_section( ).
    ENDLOOP.
    out->display( ).  ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
  demo=>main( ).

Description

The AMDP class CL\_DEMO\_AMDP\_MESH has a private AMDP method SELECT\_HDB called by the public method SELECT if the current [standard database](javascript:call_link\('abenstandard_db_glosry.htm'\) "Glossary Entry") is the SAP HANA database. For all other databases, the private method SELECT\_OPEN\_SQL is called. Both methods return the same results, verified by a [unit test](javascript:call_link\('abenunit_test_glosry.htm'\) "Glossary Entry") in the class.

The methods fill the nodes of a [mesh](javascript:call_link\('abenabap_meshes.htm'\)) for sales orders whose type is declared in the AMDP class. The number of sales orders is restricted using a ranges table. In the method SELECT\_OPEN\_SQL, the Open SQL addition FOR ALL ENTRIES is used, but in SELECT\_OPEN\_SQL the intermediate results can be evaluated in subqueries.

SQLScript implementation of the AMDP method

METHOD select\_hdb  BY DATABASE PROCEDURE
                   FOR HDB LANGUAGE SQLSCRIPT
                   OPTIONS READ-ONLY
                   USING snwd\_so snwd\_so\_i snwd\_pd snwd\_bpa snwd\_ad.
  orders = select \* from snwd\_so
    where client = :clnt and so\_id in (
      select low from :order\_ids );
  bupas = select \* from snwd\_bpa
    where client = :clnt and node\_key in (
      select DISTINCT buyer\_guid from :orders );
  adresses = select \* from snwd\_ad
    where client = :clnt and node\_key in (
      select address\_guid from :bupas );
  items = select \* from snwd\_so\_i
    where parent\_key in ( select node\_key from :orders );
  products = select \* from snwd\_pd as product
    where client = :clnt and node\_key in (
      select distinct product\_guid from :items );
ENDMETHOD.

ABAP implementation of the regular method

METHOD select\_open\_sql.
  SELECT \*
         FROM snwd\_so
         INTO TABLE @orders
         WHERE so\_id IN @order\_ids.
  SELECT \*
         FROM snwd\_so\_i
         FOR ALL ENTRIES IN @orders
         WHERE parent\_key = @orders-node\_key
         INTO TABLE @items.
  SELECT \*
        FROM snwd\_pd
        FOR ALL ENTRIES IN @items
        WHERE node\_key = @items-product\_guid
        INTO TABLE @products.
  SELECT \*
        FROM snwd\_bpa
        INTO TABLE bupas
        FOR ALL ENTRIES IN orders
        WHERE node\_key = orders-buyer\_guid
        ##SELECT\_FAE\_WITH\_LOB\[WEB\_ADDRESS\].
  SELECT \*
         FROM snwd\_ad
         FOR ALL ENTRIES IN @bupas
         WHERE node\_key = @bupas-address\_guid
         INTO TABLE @adresses.
ENDMETHOD.

Note

This is a syntax example. The same function can be provided with the same efficiency in Open SQL. AMDP is not needed in simple cases like this.


### abenamdp_vs_open_sql_abexa.htm

  

* * *

SAP NetWeaver AS ABAP Release 752, ©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\)) →  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\)) →  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\)) →  [ABAP Database Accesses](javascript:call_link\('abenabap_sql.htm'\)) →  [AMDP - ABAP Managed Database Procedures](javascript:call_link\('abenamdp.htm'\)) →  [AMDP - Examples](javascript:call_link\('abenamdp_abexas.htm'\)) → 

AMDP, Comparison of SQLScript with Open SQL

This example demonstrates the performance of SQL in AMDP when compared with Open SQL.

Source Code

REPORT demo\_amdp\_vs\_open\_sql.
CLASS demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
  METHOD main.
    DATA(out) = cl\_demo\_output=>new( ).
    IF NOT cl\_abap\_dbfeatures=>use\_features(
          EXPORTING
            requested\_features =
              VALUE #( ( cl\_abap\_dbfeatures=>call\_amdp\_method ) ) ).
      cl\_demo\_output=>display(
        \`Current database system does not support AMDP procedures\` ).
      RETURN.
    ENDIF.
    DATA time\_stamps TYPE TABLE OF timestampl.
    SELECT changed\_at
           FROM snwd\_so\_inv\_head UP TO 100 ROWS
           INTO TABLE time\_stamps.                      "#EC CI\_NOWHERE
    IF time\_stamps IS INITIAL.
      out->display( 'You must create database entries' &
                    ' with program RS\_EPM\_DGC\_HANA first ...' ).
      RETURN.
    ENDIF.
    DATA(rnd) =  cl\_abap\_random\_int=>create(
                   seed = CONV i( sy-uzeit )
                   min  = 1
                   max  = lines( time\_stamps ) )->get\_next( ).
    CONVERT TIME STAMP time\_stamps\[ rnd \] TIME ZONE 'UTC'
                       INTO DATE DATA(payment\_date).
    cl\_demo\_input=>request( CHANGING field = payment\_date ).
    DATA(oref) = NEW cl\_demo\_amdp\_vs\_open\_sql( ).
    TRY.
        GET RUN TIME FIELD DATA(t01).
        oref->amdp(
          EXPORTING iv\_payment\_date   = payment\_date
                    iv\_clnt           = sy-mandt
          IMPORTING et\_invoice\_header = DATA(invoice\_header)
                    et\_invoice\_item   = DATA(invoice\_item)
                    et\_customer\_info  = DATA(customer\_info) ) .
        GET RUN TIME FIELD DATA(t02).
      CATCH cx\_amdp\_error INTO DATA(amdp\_error).
        out->display( amdp\_error->get\_text( ) ).
        RETURN.
    ENDTRY.
    GET RUN TIME FIELD DATA(t11).
    oref->open\_sql\_nested\_select(
      EXPORTING iv\_payment\_date   = payment\_date
      IMPORTING et\_invoice\_header = DATA(invoice\_header1)
                et\_invoice\_item   = DATA(invoice\_item1)
                et\_customer\_info  = DATA(customer\_info1) ) .
    GET RUN TIME FIELD DATA(t12).
    GET RUN TIME FIELD DATA(t21).
    oref->open\_sql\_for\_all\_entries(
      EXPORTING iv\_payment\_date   = payment\_date
      IMPORTING et\_invoice\_header = DATA(invoice\_header2)
                et\_invoice\_item   = DATA(invoice\_item2)
                et\_customer\_info  = DATA(customer\_info2) ) .
    GET RUN TIME FIELD DATA(t22).
    GET RUN TIME FIELD DATA(t31).
    oref->open\_sql\_subquery(
      EXPORTING iv\_payment\_date   = payment\_date
      IMPORTING et\_invoice\_header = DATA(invoice\_header3)
                et\_invoice\_item   = DATA(invoice\_item3)
                et\_customer\_info  = DATA(customer\_info3) ) .
    GET RUN TIME FIELD DATA(t32).
    IF lines( invoice\_header ) IS INITIAL.
      out->display( 'Nothing found' ).
      RETURN.
    ENDIF.
    out->begin\_section( \`Lines of tables filled\`
      )->write( |INVOICE\_HEADER: {
                   lines( invoice\_header ) }\\n\\n| &&
                |INVOICE\_ITEM:   {
                   lines( invoice\_item ) }\\n\\n|  &&
                |CUSTOMER\_INFO:  {
                   lines( customer\_info ) }| ).
    out->next\_section( \`Runtime AMDP \`
      )->write( |{ CONV decfloat34(
                  ( t02 - t01 ) / 1000000 ) } seconds| ).
    IF invoice\_header  = invoice\_header1 AND
       invoice\_item    = invoice\_item1   AND
       customer\_info   = customer\_info1.
      out->next\_section( \`Runtime Open SQL with Nested SELECT\`
        )->write( |{ CONV decfloat34(
                      ( t12 - t11 ) / 1000000 ) } seconds| ).
    ELSE.
      out->write( 'Different results in Open SQL with Nested SELECT' ).
    ENDIF.
    IF invoice\_header  = invoice\_header2 AND
       invoice\_item    = invoice\_item2   AND
       customer\_info   = customer\_info2.
      out->next\_section( \`Runtime Open SQL with FOR ALL ENTRIES\`
        )->write( |{ CONV decfloat34(
                      ( t22 - t21 ) / 1000000 ) } seconds| ).
    ELSE.
      out->write(
        'Different results in Open SQL with FOR ALL ENTRIES' ).
    ENDIF.
    IF invoice\_header  = invoice\_header3 AND
       invoice\_item    = invoice\_item3   AND
       customer\_info   = customer\_info3.
      out->next\_section( \`Runtime Open SQL with Subquery\`
        )->write( |{ CONV decfloat34(
                      ( t32 - t31 ) / 1000000 ) } seconds| ).
    ELSE.
      out->write( 'Different results in Open SQL with Subquery' ).
    ENDIF.
    out->display( ).  ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
  demo=>main( ).

Description

The AMDP class CL\_DEMO\_AMDP\_VS\_OPEN\_SQL has an AMDP method AMDP and the regular methods OPEN\_SQL\_NESTED\_SELECT, OPEN\_SQL\_FOR\_ALL\_ENTRIES, and OPEN\_SQL\_SUBQUERY. All take the same data from database tables in the EPM model. These database tables can be filled using the program RS\_EPM\_DGC\_HANA. Create the master data again here (X) and enter, for example, the values 100000 for Number of Sales Orders and 90000 for Number Delivered, plus a time interval of approximately three months.

The example program selects a random date from the existing change data and uses it to make the selections. The selections pick invoices paid on this date, plus the associated business partners and product information.

The method OPEN\_SQL\_NESTED\_SELECT uses nested SELECT statements and provides the worst result, as expected. However, the use of FOR ALL ENTRIES in OPEN\_SQL\_FOR\_ALL\_ENTRIES or the same subqueries in OPEN\_SQL\_SUBQUERY as in AMDP demonstrates that AMDP does not provide any performance benefits as long as only SQL is implemented that can also be expressed in Open SQL.

SQLScript implementation of the method AMDP

METHOD amdp BY DATABASE PROCEDURE FOR HDB
       LANGUAGE SQLSCRIPT OPTIONS READ-ONLY
       USING snwd\_ad snwd\_bpa snwd\_so\_inv\_head snwd\_so\_inv\_item.
  --  Selection of invoices paid on a specified date
  --  plus business partner and product information
  -- Retrieve all invoice header which were paid on the requested date
  et\_invoice\_header = select
      node\_key            as invoice\_guid,
      created\_at as created\_at,
      changed\_at as paid\_at,
      buyer\_guid
    from
      snwd\_so\_inv\_head
    where
      client         = :iv\_clnt
      and payment\_status = 'P' -- only paid invoices
      and left(changed\_at, 8) = :iv\_payment\_date
      order by invoice\_guid;
  -- Get the items of those invoices
  et\_invoice\_item = select
      node\_key   as item\_guid,
      parent\_key as invoice\_guid,
      product\_guid,
      gross\_amount,
      currency\_code
    from snwd\_so\_inv\_item
    where parent\_key in
        ( select invoice\_guid
            from :et\_invoice\_header )
      order by item\_guid, invoice\_guid, product\_guid;
  -- Get the information about the customers
  et\_customer\_info = select
      bpa.node\_key     as customer\_guid,
      bpa.bp\_id        as customer\_id,
      bpa.company\_name as customer\_name,
      ad.country,
      ad.postal\_code,
      ad.city
    from snwd\_bpa as bpa
    join snwd\_ad as ad on ad.node\_key = bpa.address\_guid
    where bpa.node\_key in ( select distinct buyer\_guid
                              from :et\_invoice\_header )
    order by customer\_id;
ENDMETHOD.

ABAP implementation of the method OPEN\_SQL\_NESTED\_SELECT

METHOD open\_sql\_nested\_select.
  "Selection of invoices paid on a specified date
  "plus business partner and product information
  DATA ls\_invoice\_head  TYPE ty\_invoice\_header.
  DATA lt\_invoice\_item  TYPE tt\_invoice\_item.
  DATA lt\_customer\_info TYPE tt\_customer\_info.
  DATA lv\_payment\_date\_min TYPE timestamp.
  DATA lv\_payment\_date\_max TYPE timestamp.
  CONVERT DATE iv\_payment\_date TIME '0001'
    INTO TIME STAMP lv\_payment\_date\_min TIME ZONE 'UTC'.
  CONVERT DATE iv\_payment\_date TIME '2359'
    INTO TIME STAMP lv\_payment\_date\_max TIME ZONE 'UTC'.
  "Retrieve all invoice header which were paid on the requested date
  SELECT
    node\_key       AS invoice\_guid,
    created\_at     AS created\_at,
    changed\_at     AS paid\_at,
    buyer\_guid
  FROM
    snwd\_so\_inv\_head
  WHERE                                               "#EC CI\_NOFIELD
    payment\_status = 'P'
    AND changed\_at BETWEEN @lv\_payment\_date\_min AND @lv\_payment\_date\_max
  ORDER BY invoice\_guid
  INTO @ls\_invoice\_head.
    CLEAR lt\_invoice\_item.
    CLEAR lt\_customer\_info.
    "Get the items of invoice
    SELECT
      node\_key   AS item\_guid,
      parent\_key AS invoice\_guid,
      product\_guid,
      gross\_amount,
      currency\_code
    FROM snwd\_so\_inv\_item
    WHERE parent\_key = @ls\_invoice\_head-invoice\_guid
    ORDER BY item\_guid, invoice\_guid, product\_guid
    INTO TABLE @lt\_invoice\_item.
    "Get the information about the customers
    SELECT
     bpa~node\_key     AS customer\_guid,
     bpa~bp\_id        AS customer\_id,
     bpa~company\_name AS customer\_name,
     ad~country,
     ad~postal\_code,
     ad~city
   FROM snwd\_bpa AS bpa
   JOIN snwd\_ad AS ad ON ad~node\_key = bpa~address\_guid
   WHERE bpa~node\_key = @ls\_invoice\_head-buyer\_guid
   INTO TABLE @lt\_customer\_info.
    APPEND ls\_invoice\_head           TO et\_invoice\_header.
    APPEND LINES OF lt\_invoice\_item  TO et\_invoice\_item.
    APPEND LINES OF lt\_customer\_info TO et\_customer\_info.
  ENDSELECT.
  "Remove duplicates
  SORT et\_customer\_info BY customer\_guid.
  DELETE ADJACENT DUPLICATES FROM et\_customer\_info.
ENDMETHOD.

ABAP implementation of the method OPEN\_SQL\_FOR\_ALL\_ENTRIES

METHOD open\_sql\_for\_all\_entries.
  "Selection of invoices paid on a specified date
  "plus business partner and product information
  DATA lv\_payment\_date\_min TYPE timestamp.
  DATA lv\_payment\_date\_max TYPE timestamp.
  CONVERT DATE iv\_payment\_date TIME '0001'
    INTO TIME STAMP lv\_payment\_date\_min TIME ZONE 'UTC'.
  CONVERT DATE iv\_payment\_date TIME '2359'
    INTO TIME STAMP lv\_payment\_date\_max TIME ZONE 'UTC'.
  "Retrieve all invoice header which were paid on the requested date
  SELECT
    node\_key       AS invoice\_guid,
    created\_at     AS created\_at,
    changed\_at     AS paid\_at,
    buyer\_guid
  FROM
    snwd\_so\_inv\_head
  INTO TABLE @et\_invoice\_header
  WHERE                                               "#EC CI\_NOFIELD
    payment\_status = 'P'
    AND changed\_at BETWEEN @lv\_payment\_date\_min AND @lv\_payment\_date\_max
  ORDER BY invoice\_guid.
  "Get the items of those invoices
  SELECT
      node\_key   AS item\_guid,
      parent\_key AS invoice\_guid,
      product\_guid,
      gross\_amount,
      currency\_code
   from snwd\_so\_inv\_item
   into table @et\_invoice\_item
   for all entries in @et\_invoice\_header
   where  parent\_key = @et\_invoice\_header-invoice\_guid
   order by primary key.
  "Get the information about the customers
  SELECT
      bpa~node\_key     AS customer\_guid,
      bpa~bp\_id        AS customer\_id,
      bpa~company\_name AS customer\_name,
      ad~country,
      ad~postal\_code,
      ad~city
    FROM snwd\_bpa AS bpa
    JOIN snwd\_ad AS ad ON ad~node\_key = bpa~address\_guid
    INTO TABLE @et\_customer\_info
    FOR ALL ENTRIES IN @et\_invoice\_header
    WHERE bpa~node\_key = @et\_invoice\_header-buyer\_guid.
  SORT et\_customer\_info BY customer\_guid.
ENDMETHOD.

ABAP implementation of the method OPEN\_SQL\_SUBQUERY

METHOD open\_sql\_subquery.
  "Selection of invoices paid on a specified date
  "plus business partner and product information
  DATA lv\_payment\_date\_min TYPE timestamp.
  DATA lv\_payment\_date\_max TYPE timestamp.
  CONVERT DATE iv\_payment\_date TIME '0001'
    INTO TIME STAMP lv\_payment\_date\_min TIME ZONE 'UTC'.
  CONVERT DATE iv\_payment\_date TIME '2359'
    INTO TIME STAMP lv\_payment\_date\_max TIME ZONE 'UTC'.
  "Retrieve all invoice header which were paid on the requested date
  SELECT
   node\_key       AS invoice\_guid,
   created\_at     AS created\_at,
   changed\_at     AS paid\_at,
   buyer\_guid
FROM
   snwd\_so\_inv\_head
     WHERE
   payment\_status = 'P'
   AND changed\_at BETWEEN @lv\_payment\_date\_min AND @lv\_payment\_date\_max
ORDER BY invoice\_guid
INTO TABLE @et\_invoice\_header.
  "Get the items of those invoices
  SELECT
      node\_key   AS item\_guid,
      parent\_key AS invoice\_guid,
      product\_guid,
      gross\_amount,
      currency\_code
   FROM snwd\_so\_inv\_item
   WHERE  parent\_key IN
        ( SELECT node\_key FROM snwd\_so\_inv\_head
             WHERE payment\_status = 'P'
               AND changed\_at
                     BETWEEN @lv\_payment\_date\_min AND @lv\_payment\_date\_max )
   ORDER BY item\_guid, invoice\_guid, product\_guid
   INTO TABLE @et\_invoice\_item.
  "Get the information about the customers
  SELECT
      bpa~node\_key     AS customer\_guid,
      bpa~bp\_id        AS customer\_id,
      bpa~company\_name AS customer\_name,
      ad~country,
      ad~postal\_code,
      ad~city
    FROM snwd\_bpa AS bpa
    JOIN snwd\_ad AS ad ON ad~node\_key = bpa~address\_guid
    WHERE bpa~node\_key IN ( SELECT buyer\_guid FROM snwd\_so\_inv\_head
             WHERE payment\_status = 'P'
               AND changed\_at
                     BETWEEN @lv\_payment\_date\_min AND @lv\_payment\_date\_max )
    ORDER BY customer\_id
    INTO TABLE @et\_customer\_info.
ENDMETHOD.


### abenamdp_functions_abexa.htm

  

* * *

SAP NetWeaver AS ABAP Release 752, ©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\)) →  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\)) →  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\)) →  [ABAP Database Accesses](javascript:call_link\('abenabap_sql.htm'\)) →  [AMDP - ABAP Managed Database Procedures](javascript:call_link\('abenamdp.htm'\)) →  [AMDP - Examples](javascript:call_link\('abenamdp_abexas.htm'\)) → 

AMDP, AMDP Functions

This example demonstrates AMDP functions and how they are used.

Source Code

REPORT demo\_amdp\_functions\_inpcl.
CLASS demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
  METHOD main.
    IF NOT cl\_abap\_dbfeatures=>use\_features(
          EXPORTING
            requested\_features =
              VALUE #( ( cl\_abap\_dbfeatures=>call\_amdp\_method )
                       ( cl\_abap\_dbfeatures=>amdp\_table\_function ) ) ).
      cl\_demo\_output=>display(
        \`System does not support AMDP or CDS table functions\` ).
      RETURN.
    ENDIF.
    DATA carrid TYPE s\_carr\_id VALUE 'LH'.
    cl\_demo\_input=>request( CHANGING field = carrid ).
    carrid = to\_upper( carrid ).
    "Database function selected in database procedure
    TRY.
        NEW cl\_demo\_amdp\_functions\_inpcl( )->select\_get\_scarr\_spfli(
          EXPORTING clnt   = sy-mandt
                    carrid = carrid
          IMPORTING scarr\_spfli\_tab = DATA(result1) ).
      CATCH cx\_amdp\_error INTO DATA(amdp\_error).
        cl\_demo\_output=>display( amdp\_error->get\_text( ) ).
        RETURN.
    ENDTRY.
    "Database function selected via CDS entity
    SELECT \*
           FROM demo\_cds\_get\_scarr\_spfli\_inpcl( carrid = @carrid )
           INTO TABLE @DATA(result2)
           ##db\_feature\_mode\[amdp\_table\_function\].
    ASSERT result1 = result2.
    cl\_demo\_output=>display( result1 ).  ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
  demo=>main( ).

Description

This example access [AMDP functions](javascript:call_link\('abenamdp_function_glosry.htm'\) "Glossary Entry") that are declared and implemented in the [AMDP class](javascript:call_link\('abenamdp_class_glosry.htm'\) "Glossary Entry") CL\_DEMO\_AMDP\_FUNCTIONS\_INPCL.

-   The method GET\_SCARR\_SPFLI is an [AMDP function implementation](javascript:call_link\('abenamdp_function_methods.htm'\)) for other AMDP methods with explicitly declared interfaces. It cannot be called in ABAP.

METHOD get\_scarr\_spfli BY DATABASE FUNCTION FOR HDB
                       LANGUAGE SQLSCRIPT
                       OPTIONS READ-ONLY
                       USING scarr spfli.
  RETURN SELECT sc.carrname, sp.connid, sp.cityfrom, sp.cityto
                FROM scarr AS sc
                  INNER JOIN spfli AS sp ON sc.mandt = sp.mandt AND
                                            sc.carrid = sp.carrid
                  WHERE sp.mandt = :clnt AND sp.carrid = :carrid
                  ORDER BY sc.mandt, sc.carrname, sp.connid;
ENDMETHOD.

-   The method SELECT\_GET\_SCARR\_SPFLI is an [AMDP procedure implementation](javascript:call_link\('abenamdp_procedure_methods.htm'\)) that uses SELECT to access the AMDP function implemented in the method GET\_SCARR\_SPFLI. This method is called in the example program.

METHOD select\_get\_scarr\_spfli
       BY DATABASE PROCEDURE FOR HDB
       LANGUAGE SQLSCRIPT
       OPTIONS READ-ONLY
       USING cl\_demo\_amdp\_functions\_inpcl=>get\_scarr\_spfli.
  SCARR\_SPFLI\_TAB =
  SELECT \*
         FROM "CL\_DEMO\_AMDP\_FUNCTIONS\_INPCL=>GET\_SCARR\_SPFLI"(
                clnt => :clnt,
                carrid => :carrid );
ENDMETHOD.

-   The method GET\_SCARR\_SPFLI\_FOR\_CDS is an [AMDP function implementation](javascript:call_link\('abenamdp_function_methods.htm'\)) for the [CDS table function](javascript:call_link\('abencds_table_function_glosry.htm'\) "Glossary Entry") DEMO\_CDS\_GET\_SCARR\_SPFLI\_INPCL shown as an example of the DDL statement [DEFINE TABLE FUNCTION](javascript:call_link\('abencds_f1_define_table_function.htm'\)).

@ClientHandling.type: #CLIENT\_DEPENDENT
define table function DEMO\_CDS\_GET\_SCARR\_SPFLI\_INPCL
  with parameters
    @Environment.systemField: #CLIENT
    clnt   :abap.clnt,
    carrid :s\_carr\_id
  returns
  {
    client   :s\_mandt;
    carrname :s\_carrname;
    connid   :s\_conn\_id;
    cityfrom :s\_from\_cit;
    cityto   :s\_to\_city;
  }
  implemented by method
    CL\_DEMO\_AMDP\_FUNCTIONS\_INPCL=>GET\_SCARR\_SPFLI\_FOR\_CDS;

This method is declared using the addition [FOR TABLE FUNCTION](javascript:call_link\('abapclass-methods_for_tabfunc.htm'\)) and its interface is determined using theCDS table function. The example program uses [SELECT](javascript:call_link\('abapselect.htm'\)) to access the CDS table function. The input parameter is filled here.

METHOD get\_scarr\_spfli\_for\_cds
       BY DATABASE FUNCTION FOR HDB
       LANGUAGE SQLSCRIPT
       OPTIONS READ-ONLY
       USING scarr spfli.
  RETURN SELECT sc.mandt as client,
                sc.carrname, sp.connid, sp.cityfrom, sp.cityto
                FROM scarr AS sc
                  INNER JOIN spfli AS sp ON sc.mandt = sp.mandt AND
                                            sc.carrid = sp.carrid
                  WHERE sp.mandt = :clnt AND
                        sp.carrid = :carrid
                  ORDER BY sc.mandt, sc.carrname, sp.connid;
ENDMETHOD.

Both accesses produce the same result (verified by the statement [ASSERT](javascript:call_link\('abapassert.htm'\))).

Note

The CDS table function DEMO\_CDS\_GET\_SCARR\_SPFLI\_INPCL used here has one input parameter for the client, annotated using the annotation [@Environment.systemField](javascript:call_link\('abencds_f1_parameter_annotations.htm'\)) and the predefined value [#CLIENT](javascript:call_link\('abencds_f1_parameter_annotations.htm'\)). This input parameter is filled explicitly with the ID of the current client by the Open SQL statement SELECT and used in the implementation of the associated AMDP method for selecting the data. The nearly identical program DEMO\_AMDP\_FUNCTIONS uses the following CDS table function DEMO\_CDS\_GET\_SCARR\_SPFLI in which the current client is selected from the results set of the table function.

@ClientHandling.type: #CLIENT\_DEPENDENT
@AccessControl.authorizationCheck:#NOT\_ALLOWED
define table function DEMO\_CDS\_GET\_SCARR\_SPFLI
  with parameters
    carrid :s\_carr\_id
  returns
  {
    client   :s\_mandt;
    carrname :s\_carrname;
    connid   :s\_conn\_id;
    cityfrom :s\_from\_cit;
    cityto   :s\_to\_city;
  }
  implemented by method
    CL\_DEMO\_AMDP\_FUNCTIONS=>GET\_SCARR\_SPFLI\_FOR\_CDS;


### abenamdp_abap_types_abexa.htm

  

* * *

SAP NetWeaver AS ABAP Release 752, ©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\)) →  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\)) →  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\)) →  [ABAP Database Accesses](javascript:call_link\('abenabap_sql.htm'\)) →  [AMDP - ABAP Managed Database Procedures](javascript:call_link\('abenamdp.htm'\)) →  [AMDP - Examples](javascript:call_link\('abenamdp_abexas.htm'\)) → 

AMDP, Access to ABAP Types

The example demonstrates how to access ABAP types via the [AMDP macro](javascript:call_link\('abenamdp_macro_glosry.htm'\) "Glossary Entry") [$ABAP.type](javascript:call_link\('abenamdp_abap_types.htm'\)).

Source Code

REPORT demo\_amdp\_abap\_types.
CLASS demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
  METHOD main.
    DATA itab TYPE cl\_demo\_amdp\_abap\_types=>itab.
    cl\_demo\_amdp\_abap\_types=>demo\_abap\_types( IMPORTING itab = itab ).
    cl\_demo\_output=>display( itab ).  ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
  demo=>main( ).

Description

This example calls the following [AMDP method](javascript:call_link\('abenamdp_method_glosry.htm'\) "Glossary Entry") of class CL\_DEMO\_AMDP\_ABAP\_TYPES:

METHOD demo\_abap\_types BY DATABASE PROCEDURE
                       FOR HDB LANGUAGE SQLSCRIPT.
  DECLARE mytab table( mandt  "$ABAP.type( mandt )",
                       uname  "$ABAP.type( syst\_uname )",
                       langu  "$ABAP.type( syst\_langu )",
                       datum  "$ABAP.type( syst\_datum )",
                       text   "$ABAP.type( line-text )",
                       number "$ABAP.type( f )" );
  mytab.mandt\[1\]  := session\_context('CLIENT');
  mytab.uname\[1\]  := session\_context('APPLICATIONUSER');
  mytab.langu\[1\]  := session\_context('LOCALE\_SAP');
  mytab.datum\[1\]  := session\_context('SAP\_SYSTEM\_DATE');
  mytab.text\[1\]   := cast( 0123456789 as "$ABAP.type( line-text )" );
  mytab.number\[1\] := 333 ;
  itab = select \* from :mytab;
ENDMETHOD.

In [SQLScript](javascript:call_link\('abensql_script_glosry.htm'\) "Glossary Entry") a local table variable mytab is declared with DECLARE in the method. All columns of the table variable are declared via the [AMDP macro](javascript:call_link\('abenamdp_macro_glosry.htm'\) "Glossary Entry") [$ABAP.type](javascript:call_link\('abenamdp_abap_types.htm'\)) with reference to ABAP types. The first four ABAP types MANDT, SYST\_UNAME, SYST\_LANGU and SYST\_DATUM are defined in ABAP Dictionary. Type line\_text comes from a TYPES statement of its own class. f stands for the built-in numeric ABAP type.

The table variable is filled using assignments to columns with one row, whereby the specification of the AMDP macro $ABAP.type is displayed in a CAST expression. Finally, the content of the table variable is imported into the tabular output parameter itab, whose row type is made up of components of the same ABAP types, which are used to define the table variable.


### abenamdp_db_schema_abexa.htm

  

* * *

SAP NetWeaver AS ABAP Release 752, ©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\)) →  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\)) →  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\)) →  [ABAP Database Accesses](javascript:call_link\('abenabap_sql.htm'\)) →  [AMDP - ABAP Managed Database Procedures](javascript:call_link\('abenamdp.htm'\)) →  [AMDP - Examples](javascript:call_link\('abenamdp_abexas.htm'\)) → 

AMDP, Access to Database Schemas

This example demonstrates how a table is accessed in a different [database schema](javascript:call_link\('abendatabase_schema_glosry.htm'\) "Glossary Entry").

Source Code

REPORT demo\_amdp\_db\_schema.
CLASS demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
  METHOD main.
    IF NOT cl\_abap\_dbfeatures=>use\_features(
          EXPORTING
            requested\_features =
              VALUE #( ( cl\_abap\_dbfeatures=>call\_amdp\_method ) ) ).
      cl\_demo\_output=>display(
        \`Current database system does not support AMDP procedures\` ).
      RETURN.
    ENDIF.
    TRY.
        cl\_demo\_amdp\_db\_schema=>get\_schemas\_physical(
          IMPORTING schemas = DATA(schemas\_test) ).
      CATCH cx\_amdp\_error INTO DATA(amdp\_error).
        cl\_demo\_output=>display( amdp\_error->get\_text( ) ).
        RETURN.
    ENDTRY.
    TRY.
        cl\_demo\_amdp\_db\_schema=>get\_schemas\_logical(
          IMPORTING schemas = DATA(schemas) ).
      CATCH cx\_amdp\_error INTO amdp\_error.
        cl\_demo\_output=>display( amdp\_error->get\_text( ) ).
        RETURN.
    ENDTRY.
    ASSERT schemas\_test = schemas.
    cl\_demo\_output=>display( schemas ).  ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
  demo=>main( ).

Description

This example accesses [AMDP methods](javascript:call_link\('abenamdp_method_glosry.htm'\) "Glossary Entry") that are declared and implemented in the [AMDP class](javascript:call_link\('abenamdp_class_glosry.htm'\) "Glossary Entry") CL\_DEMO\_AMDP\_DB\_SCHEMA.

-   The method GET\_SCHEMAS\_PHYSICAL accesses the table SCHEMAS of the physical database schema SYS by specifying the schema directly. All existing database schema are saved in this table.

METHOD get\_schemas\_physical BY DATABASE PROCEDURE
                            FOR HDB LANGUAGE SQLSCRIPT.
  schemas =
    select schema\_name
      FROM "SYS"."SCHEMAS";
ENDMETHOD.

-   The method GET\_SCHEMAS\_LOGICAL accesses the physical database schema that is mapped to the [logical database schema](javascript:call_link\('abenlogical_database_schema_glosry.htm'\) "Glossary Entry") DEMO\_LOGICAL\_DB\_SCHEMA in transaction DB\_SCHEMA\_MAP by specifying the [AMDP macro](javascript:call_link\('abenamdp_macro_glosry.htm'\) "Glossary Entry"). If this is SYS, the same table is accessed.

METHOD get\_schemas\_logical BY DATABASE PROCEDURE
                           FOR HDB LANGUAGE SQLSCRIPT.
  schemas =
    select schema\_name
      FROM "$ABAP.schema( DEMO\_LOGICAL\_DB\_SCHEMA )"."SCHEMAS";
ENDMETHOD.


### abenamdp_l_abexa.htm

  

* * *

SAP NetWeaver AS ABAP Release 752, ©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\)) →  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\)) →  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\)) →  [ABAP Database Accesses](javascript:call_link\('abenabap_sql.htm'\)) →  [AMDP - ABAP Managed Database Procedures](javascript:call_link\('abenamdp.htm'\)) →  [AMDP - Examples](javascript:call_link\('abenamdp_abexas.htm'\)) → 

AMDP, Implementation of an L Procedure

This example demonstrates the implementation of a procedure in the programming language [L](javascript:call_link\('abenllang_glosry.htm'\) "Glossary Entry") using AMDP.

Source Code

REPORT demo\_amdp\_l\_language.
CLASS demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
  METHOD main.
    IF cl\_db\_sys=>is\_in\_memory\_db = abap\_false.
      cl\_demo\_output=>display(
        \`Example can be executed on SAP HANA Database only\` ).
      LEAVE PROGRAM.
    ENDIF.
    DATA(text) = \`World\`.
    cl\_demo\_input=>request( CHANGING field = text ).
    IF text IS INITIAL.
      RETURN.
    ENDIF.
    TRY.
        NEW cl\_demo\_amdp\_l\_hello\_world(
              )->hello\_world( EXPORTING text  = text
                              IMPORTING texts = DATA(texts) ).
      CATCH cx\_amdp\_error INTO DATA(amdp\_error).
        cl\_demo\_output=>display( amdp\_error->get\_text( ) ).
        RETURN.
    ENDTRY.
    cl\_demo\_output=>display( texts ).  ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
  demo=>main( ).

Description

The following [L procedure](javascript:call_link\('abenl_procedure_glosry.htm'\) "Glossary Entry") is implemented in the AMDP method HELLO\_WORLD of the AMDP class CL\_DEMO\_AMDP\_L\_HELLO\_WORLD:

METHOD hello\_world BY DATABASE PROCEDURE
                   FOR HDB LANGUAGE LLANG
                   OPTIONS READ-ONLY.
\* Hello World in L
  typedef Table <String "TEXT"> TT\_TABLE;  /\* Type definition
                                              repeated  \*/
  //Main entry point
  export Void main(String text, TT\_TABLE & texts)
    { String hello = String("Hello ");
      texts."TEXT"\[0z\] = hello.append( text ).append( "!" ); }
ENDMETHOD.

The tabular output parameter is created from a predefined text and an interactive input made in the ABAP program.

Note

The programming language L can only be used internally and with restrictions at SAP. For general programming with the SAP HANA database, [SQL](javascript:call_link\('abensql_glosry.htm'\) "Glossary Entry") or the script language [SQLScript](javascript:call_link\('abensql_script_glosry.htm'\) "Glossary Entry") must be used. The use of L by customers and partners is not supported.
