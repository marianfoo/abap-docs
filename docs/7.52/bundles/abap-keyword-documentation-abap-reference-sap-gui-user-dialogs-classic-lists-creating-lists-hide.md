# ABAP - Keyword Documentation / ABAP - Reference / SAP GUI User Dialogs / Classic Lists / Creating Lists / HIDE

Included pages: 2



## ğŸ”— Source References

**Base URL**: [ABAP Keyword Documentation 7.52](https://help.sap.com/doc/abapdocu_752_index_htm/7.52/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abaphide.htm](https://help.sap.com/doc/abapdocu_752_index_htm/7.52/en-US/abaphide.htm)
- [abenlist_hide_abexa.htm](https://help.sap.com/doc/abapdocu_752_index_htm/7.52/en-US/abenlist_hide_abexa.htm)

**Bundle Contains**: 2 documentation pages
**Version**: ABAP 7.52
**Generated**: 2025-09-01T11:25:46.511Z

---

### abaphide.htm

> **ğŸ“– Official SAP Documentation**: [abaphide.htm](https://help.sap.com/doc/abapdocu_752_index_htm/7.52/en-US/abaphide.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

SAP NetWeaver AS ABAP Release 752, Â©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [Classic Lists](javascript:call_link\('abenabap_dynpro_list.htm'\))Â â†’Â  [Creating Lists](javascript:call_link\('abenabap_lists.htm'\))Â â†’Â 

HIDE

[Quick Reference](javascript:call_link\('abaphide_shortref.htm'\))

Syntax

HIDE dobj.

Effect

This statement stores the content of a variable dobj together with the current list line whose line number is contained in sy-linno in the [hide area](javascript:call_link\('abenhide_area_glosry.htm'\) "Glossary Entry") of the current list level. The data type of the variable dobj must be [flat](javascript:call_link\('abenflat_glosry.htm'\) "Glossary Entry") and no field symbols or components of [Boxed Components](javascript:call_link\('abenboxed_component_glosry.htm'\) "Glossary Entry") can be specified that point to rows of internal tables, and no class attributes can be specified. The saved values can be read as followed:

-   Any user action on a displayed screen list that causes a list event assigns all values saved using HIDE to the relevant variables.
    
-   If a list line of a list level is read or modified using the statements [READ LINE](javascript:call_link\('abapread_line.htm'\)) or [MODIFY LINE](javascript:call_link\('abapmodify_line.htm'\)), all the values of this line saved using HIDE are assigned to the relevant variables.
    

Notes

-   The HIDE statement works independently of whether the [list cursor](javascript:call_link\('abenlist_cursor_glosry.htm'\) "Glossary Entry") was set. In particular, variables for empty list lines can be stored (that is, lines in which the list cursor was positioned using statements like [SKIP](javascript:call_link\('abapskip.htm'\))).
    
-   The HIDE statement should be executed directly in the statement that has set the list cursor in the line.
    
-   Using the HIDE statement on a field symbol can cause runtime errors. The syntax check displays a warning.
    

Example

Saving square numbers and cubic numbers for a list of numbers. The example shows that any variable can be stored independently of the line content. In the real world, one would more likely save only the number and execute the calculation, when required, in the the event block for [AT LINE-SELECTION](javascript:call_link\('abapat_line-selection.htm'\)).

REPORT ...
DATA: square TYPE i,
Â Â Â Â Â Â cubeÂ Â  TYPE i.
START-OF-SELECTION.
Â Â FORMAT HOTSPOT.
Â Â DO 10 TIMES.
Â Â Â Â square = sy-index \*\* 2.
Â Â Â Â cubeÂ Â  = sy-index \*\* 3.
Â Â Â Â WRITE / sy-index.
Â Â Â Â HIDE: square, cube.
Â Â ENDDO.
AT LINE-SELECTION.
Â Â WRITE: square, cube.

[Exceptions](javascript:call_link\('abenabap_language_exceptions.htm'\))

Non-Handleable Exceptions

-   Cause: The field is too long for HIDE.
    Runtime error: HIDE\_FIELD\_TOO\_LARGE
    
-   Cause: HIDE in a table row or a component in a table row is not possible.
    Runtime error: HIDE\_ILLEGAL\_ITAB\_SYMBOL
    
-   Cause: HIDE in a local field is not possible.
    Runtime error: HIDE\_NO\_LOCAL: HIDE
    
-   Cause: HIDE is not possible on an empty page.
    Runtime error: HIDE\_ON\_EMPTY\_PAGE
    
-   Cause: Illegal field
    Runtime error: HIDE\_NOT\_GLOBAL
    
-   Cause: Permitted number of HIDE statements per list line exceeded
    Runtime error: HIDE\_TOO\_MANY\_HIDES
    

Continue
![Example](exa.gif "Example") [Lists, HIDE Technique](javascript:call_link\('abenlist_hide_abexa.htm'\))



**ğŸ“– Source**: [abaphide.htm](https://help.sap.com/doc/abapdocu_752_index_htm/7.52/en-US/abaphide.htm)

### abenlist_hide_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenlist_hide_abexa.htm](https://help.sap.com/doc/abapdocu_752_index_htm/7.52/en-US/abenlist_hide_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ğŸ“– Source**: [abenlist_hide_abexa.htm](https://help.sap.com/doc/abapdocu_752_index_htm/7.52/en-US/abenlist_hide_abexa.htm)


Â  

* * *

SAP NetWeaver AS ABAP Release 752, Â©Copyright 2017 SAP AG. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [Classic Lists](javascript:call_link\('abenabap_dynpro_list.htm'\))Â â†’Â  [Creating Lists](javascript:call_link\('abenabap_lists.htm'\))Â â†’Â  [HIDE](javascript:call_link\('abaphide.htm'\))Â â†’Â 

Lists, HIDE Technique

The example shows how to implement a program-controlled data transfer of lists to the ABAP program using HIDE.

Source Code

REPORT demo\_list\_hide NO STANDARD PAGE HEADING.
TABLES: spfli, sbook.
DATA: num TYPE i,
Â Â Â Â Â Â dat TYPE d.
START-OF-SELECTION.
Â Â num = 0.
Â Â SET PF-STATUS 'FLIGHT'.
GET spfli.
Â Â num = num + 1.
Â Â WRITE: / spfli-carrid, spfli-connid,
Â Â Â Â Â Â Â Â Â Â  spfli-cityfrom, spfli-cityto.
Â Â HIDE:Â Â Â Â spfli-carrid, spfli-connid, num.
END-OF-SELECTION.
Â Â CLEAR num.
TOP-OF-PAGE.
Â Â WRITE 'List of Flights'.
Â Â ULINE.
Â Â WRITE 'CAÂ Â CONN FROMÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  TO'.
Â Â ULINE.
TOP-OF-PAGE DURING LINE-SELECTION.
Â Â CASE sy-pfkey.
Â Â Â Â WHEN 'BOOKING'.
Â Â Â Â Â Â WRITE sy-lisel.
Â Â Â Â Â Â ULINE.
Â Â Â Â WHEN 'WIND'.
Â Â Â Â Â Â WRITE:Â Â 'Booking', sbook-bookid,
Â Â Â Â Â Â Â Â Â Â  /Â Â 'DateÂ Â  ', sbook-fldate.
Â Â Â Â Â Â ULINE.
Â Â ENDCASE.
AT USER-COMMAND.
Â Â CASE sy-ucomm.
Â Â Â Â WHEN 'SELE'.
Â Â Â Â Â Â IF num NE 0.
Â Â Â Â Â Â Â Â SET PF-STATUS 'BOOKING'.
Â Â Â Â Â Â Â Â CLEAR dat.
Â Â Â Â Â Â Â Â SELECT \* FROM sbook WHERE carrid = spfli-carrid
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ANDÂ Â  connid = spfli-connid.
Â Â Â Â Â Â Â Â Â Â IF sbook-fldate NE dat.
Â Â Â Â Â Â Â Â Â Â Â Â dat = sbook-fldate.
Â Â Â Â Â Â Â Â Â Â Â Â SKIP.
Â Â Â Â Â Â Â Â Â Â Â Â WRITE / sbook-fldate.
Â Â Â Â Â Â Â Â Â Â Â Â POSITION 16.
Â Â Â Â Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â Â Â Â Â NEW-LINE.
Â Â Â Â Â Â Â Â Â Â Â Â POSITION 16.
Â Â Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â Â Â WRITE sbook-bookid.
Â Â Â Â Â Â Â Â Â Â HIDE: sbook-bookid, sbook-fldate, sbook-custtype,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â sbook-smoker, sbook-luggweight, sbook-class.
Â Â Â Â Â Â Â Â ENDSELECT.
Â Â Â Â Â Â Â Â IF sy-subrc NE 0.
Â Â Â Â Â Â Â Â Â Â WRITE / 'No bookings for this flight'.
Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â num = 0.
Â Â Â Â Â Â Â Â CLEAR sbook-bookid.
Â Â Â Â Â Â ENDIF.
Â Â Â Â WHEN 'INFO'.
Â Â Â Â Â Â IF NOT sbook-bookid IS INITIAL.
Â Â Â Â Â Â Â Â SET PF-STATUS 'WIND'.
Â Â Â Â Â Â Â Â SET TITLEBAR 'BKI'.
Â Â Â Â Â Â Â Â WINDOW STARTING AT 30 5 ENDING ATÂ Â 60 10.
Â Â Â Â Â Â Â Â WRITE: 'Customer typeÂ Â  :', sbook-custtype,
Â Â Â Â Â Â Â Â Â Â Â Â  / 'SmokerÂ Â Â Â Â Â Â Â Â Â :', sbook-smoker,
Â Â Â Â Â Â Â Â Â Â Â Â  / 'Luggage weigtht :', sbook-luggweight UNIT 'KG',
Â Â Â Â Â Â Â Â Â Â Â Â  / 'ClassÂ Â Â Â Â Â Â Â Â Â  :', sbook-class.
Â Â Â Â Â Â ENDIF.
Â Â ENDCASE.

Description

At the event START-OF-SELECTION, the status FLIGHT is set for the basic list. In the status FLIGHT, the function key F2 and a pushbutton have been assigned the function code SELE (text SELECT). As a result, the user actions of double-clicking and choosing F2 or the pushbutton SELECT trigger the event AT USER-COMMAND.

The three fields spfli-carrid, spfli-connid, and num are written to the [Hide area](javascript:call_link\('abenhide_area_glosry.htm'\) "Glossary Entry") while the basic list is created. After a line is selected, the details list defined for the function code SELE in AT USER-COMMAND is displayed. At the event AT USER-COMMAND, all stored Hide fields of the selected line are reloaded. num is used to check if a line of the actual list has been selected. The details list gets the status BOOKING where F2 is assigned the function code INFO (text: Booking Information). The details list displays data which can be selected using the Hide fields of the basic list. For each list line output, the system stores additional information in the [Hide area](javascript:call_link\('abenhide_area_glosry.htm'\) "Glossary Entry").

Selecting a line on the details list displays the information stored in a dialog box with the status WIND. For the status WIND, the default settings have been adopted with comparison for the list status. sbook-bookid checks whether the line selection is valid.

All page headers and the title bar of the dialog box are set in the program.
