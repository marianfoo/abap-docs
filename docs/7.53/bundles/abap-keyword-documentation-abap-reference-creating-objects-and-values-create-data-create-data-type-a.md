# ABAP Keyword Documentation / ABAP âˆ’ Reference / Creating Objects and Values / CREATE DATA / CREATE DATA - TYPE abap_type

Included pages: 2



## ğŸ”— Source References

**Base URL**: [ABAP Keyword Documentation 7.53](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abapcreate_data_built_in.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abapcreate_data_built_in.htm)
- [abencreate_simple_data_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abencreate_simple_data_abexa.htm)

**Bundle Contains**: 2 documentation pages
**Version**: ABAP 7.53
**Generated**: 2025-09-01T11:25:46.179Z

---

### abapcreate_data_built_in.htm

> **ğŸ“– Official SAP Documentation**: [abapcreate_data_built_in.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abapcreate_data_built_in.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Creating Objects and Values](javascript:call_link\('abencreate_objects.htm'\))Â â†’Â  [CREATE DATA](javascript:call_link\('abapcreate_data.htm'\))Â â†’Â 

CREATE DATA - TYPE abap\_type

[Quick Reference](javascript:call_link\('abapcreate_data_shortref.htm'\))

Syntax

CREATE DATA dref *\[*[area\_handle](javascript:call_link\('abapcreate_data_area_handle.htm'\))*\]*
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  TYPE *{*abap\_type*|*(name)*}*
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â *\[*LENGTH len*\]* *\[*DECIMALS dec*\]*.

Effect

Creates a data object with an explicitly specified data type. For the data type, all [built-in data types](javascript:call_link\('abenpredefined_data_type_glosry.htm'\) "Glossary Entry") (apart from b and s) can be used that are more specific than the static type of dref or are identical to it. The name of the type can be specified as follows:

-   abap\_type
    

Specified directly and statically as abap\_type.

-   (name)
    

Specified as the content of a character-like data object name (not case-sensitive). The following can be specified for name:

-   Literal or constant
    If the data object name is specified as a character literal or as a constant, it can be evaluated statically and the specified type is identified as the used object.

-   Variable
    If the data object name is specified as a variable, it is specified only dynamically and the content is not evaluated statically.

When the statement is executed, name is not evaluated until runtime (in both cases).

In the ABAP types c, n, p, and x, the length of the data type dtype can be determined by specifying a numeric data object len after the addition LENGTH, which, when the statement is executed, contains a value within the [length range](javascript:call_link\('abenbuilt_in_types_complete.htm'\)) defined for the type in question. If the addition LENGTH is not specified, the standard length from the table is used. For all other ABAP types, the length is determined by the value in the table and the addition LENGTH cannot be specified.

In the ABAP type p, the number of [decimal places](javascript:call_link\('abenfractional_portion_glosry.htm'\) "Glossary Entry") is determined by specifying a numeric data object dec after the addition DECIMALS. If DECIMALS is specified, the same applies as in the statement [TYPES](javascript:call_link\('abaptypes_simple.htm'\)). If the addition DECIMALS is not specified, no decimal places are created. The decimal separator is ignored in operations with packed numbers if the [program attribute](javascript:call_link\('abenprogram_attribute_glosry.htm'\) "Glossary Entry") fixed point arithmetic is not set. If it is not set, the addition DECIMALS is applied only to dynpros and in the statement [WRITE *\[*TO*\]*](javascript:call_link\('abapwrite_to.htm'\)). For all other ABAP types and if name is specified dynamically, the addition DECIMALS is not permitted.

Note

When a data type abap\_type is used, the [instance operator](javascript:call_link\('abeninstance_operator_glosry.htm'\) "Glossary Entry") [NEW](javascript:call_link\('abenconstructor_expression_new.htm'\)) acts like the statement CREATE DATA dref TYPE abap\_type and can be used in [general expression positions](javascript:call_link\('abengeneral_expr_position_glosry.htm'\) "Glossary Entry"). The content of name cannot be specified dynamically here.

Example

Creation of an anonymous data object of packed number type with length specification and decimals. As the static data type of the data reference variable is generic, data referencing is only possible by assignment to a field symbol.

DATA dref TYPE REF TO data.
CREATE DATA dref TYPE p LENGTH 8 DECIMALS 3.
ASSIGN dref->\* TO FIELD-SYMBOL(<fs>).
<fs> = 1 / 3.
cl\_demo\_output=>display( <fs> ).

Executable Example

[Creating Elementary Data Objects](javascript:call_link\('abencreate_simple_data_abexa.htm'\))

Continue
![Example](exa.gif "Example") [Creating Elementary Data Objects](javascript:call_link\('abencreate_simple_data_abexa.htm'\))



**ğŸ“– Source**: [abapcreate_data_built_in.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abapcreate_data_built_in.htm)

### abencreate_simple_data_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abencreate_simple_data_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abencreate_simple_data_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ğŸ“– Source**: [abencreate_simple_data_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abencreate_simple_data_abexa.htm)


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Creating Objects and Values](javascript:call_link\('abencreate_objects.htm'\))Â â†’Â  [CREATE DATA](javascript:call_link\('abapcreate_data.htm'\))Â â†’Â  [CREATE DATA - TYPE abap\_type](javascript:call_link\('abapcreate_data_built_in.htm'\))Â â†’Â 

Creating Elementary Data Objects

This example demonstrates how elementary data objects are created.

Source Code

REPORT demo\_create\_simple\_data.
CLASS create\_demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
Â Â PRIVATE SECTION.
Â Â Â Â CLASS-METHODS create\_data IMPORTING
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â VALUE(typ) TYPE c
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â VALUE(len) TYPE i
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â VALUE(dec) TYPE i
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â RETURNING
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â VALUE(dref) TYPE REF TO data
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â RAISING cx\_sy\_create\_data\_error.
ENDCLASS.
CLASS create\_demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA dref TYPE REF TO data.
Â Â Â Â FIELD-SYMBOLS <fs> TYPE any.
Â Â Â Â DATA: typeÂ Â Â Â  LENGTH 10 TYPE c,
Â Â Â Â Â Â Â Â Â Â lengthÂ Â  TYPE i,
Â Â Â Â Â Â Â Â Â Â decimals TYPE i.
Â Â Â Â cl\_demo\_input=>add\_field( EXPORTING textÂ Â = \`Builtin ABAP Type\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â CHANGINGÂ Â field = type ).
Â Â Â Â cl\_demo\_input=>add\_field( EXPORTING textÂ Â = \`Length\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â CHANGINGÂ Â field = length ).
Â Â Â Â cl\_demo\_input=>request(Â Â  EXPORTING textÂ Â = \`Decimals\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â CHANGINGÂ Â field = decimals ).
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â IF to\_lower( type ) = 'p' AND decimals > 2 \* length - 1.
Â Â Â Â Â Â Â Â Â Â "Would lead to undefined state for packed number
Â Â Â Â Â Â Â Â Â Â RAISE EXCEPTION TYPE cx\_sy\_create\_data\_error.
Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â dref = create\_data( typ = type
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â len = length
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â dec = decimals ).
Â Â Â Â Â Â Â Â ASSIGN dref->\* TO <fs>.
Â Â Â Â Â Â Â Â DESCRIBE FIELD <fs> TYPE type
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â LENGTH length IN BYTE MODE
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â DECIMALS decimals.
Â Â Â Â Â Â Â Â cl\_demo\_output=>display( |{ type } { length } { decimals }| ).
Â Â Â Â Â Â CATCH cx\_sy\_create\_data\_error.
Â Â Â Â Â Â Â Â cl\_demo\_output=>display( |Error creating { type } {
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  length } {
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  decimals }| ).
Â Â Â Â ENDTRY.
Â Â ENDMETHOD.
Â Â METHOD create\_data.
Â Â Â Â TRANSLATE typ TO LOWER CASE.
Â Â Â Â CASE typ.
Â Â Â Â Â Â WHEN 'd' OR 'decfloat16' OR 'decfloat34' OR 'f' OR 'i'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  OR 'string' OR 't' OR 'xstring'.
Â Â Â Â Â Â Â Â CREATE DATA dref TYPE (typ).
Â Â Â Â Â Â WHEN 'c' OR 'n' OR 'x'.
Â Â Â Â Â Â Â Â CREATE DATA dref TYPE (typ) LENGTH len.
Â Â Â Â Â Â WHEN 'p'.
Â Â Â Â Â Â Â Â CREATE DATA dref TYPE p LENGTH len DECIMALS dec.
Â Â Â Â Â Â WHEN OTHERS.
Â Â Â Â Â Â Â Â RAISE EXCEPTION TYPE cx\_sy\_create\_data\_error.
Â Â Â Â ENDCASE.
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â create\_demo=>main( ).

Description

The method create\_data creates all elementary data objects that are possible using built-in ABAP types, depending on the input parameters passed.
