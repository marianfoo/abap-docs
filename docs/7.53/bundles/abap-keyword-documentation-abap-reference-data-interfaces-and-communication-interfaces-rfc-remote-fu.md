# ABAP Keyword Documentation / ABAP âˆ’ Reference / Data Interfaces and Communication Interfaces / RFC - Remote Function Call / Examples for Remote Function Call

Included pages: 6



## ðŸ”— Source References

**Base URL**: [ABAP Keyword Documentation 7.53](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abenrfc_abexas.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_abexas.htm)
- [abenrfc_parameters_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_parameters_abexa.htm)
- [abenrfc_exceptions_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_exceptions_abexa.htm)
- [abenparallel_rfc_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenparallel_rfc_abexa.htm)
- [abenrfc_dynamic_dest_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_dynamic_dest_abexa.htm)
- [abenrfc_logon_data_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_logon_data_abexa.htm)

**Bundle Contains**: 6 documentation pages
**Version**: ABAP 7.53
**Generated**: 2025-09-01T11:25:46.187Z

---

### abenrfc_abexas.htm

> **ðŸ“– Official SAP Documentation**: [abenrfc_abexas.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_abexas.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Data Interfaces and Communication Interfaces](javascript:call_link\('abenabap_data_communication.htm'\))Â â†’Â  [RFC - Remote Function Call](javascript:call_link\('abenrfc.htm'\))Â â†’Â 

Examples for Remote Function Call

Continue
![Example](exa.gif "Example") [Parameter Passing in sRFC and aRFC](javascript:call_link\('abenrfc_parameters_abexa.htm'\))
![Example](exa.gif "Example") [Exception Handling in RFC](javascript:call_link\('abenrfc_exceptions_abexa.htm'\))
![Example](exa.gif "Example") [Parallel Asynchronous RFC](javascript:call_link\('abenparallel_rfc_abexa.htm'\))
![Example](exa.gif "Example") [Dynamic RFC Destination](javascript:call_link\('abenrfc_dynamic_dest_abexa.htm'\))
![Example](exa.gif "Example") [Implicit Logon Data in RFC](javascript:call_link\('abenrfc_logon_data_abexa.htm'\))



**ðŸ“– Source**: [abenrfc_abexas.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_abexas.htm)

### abenrfc_parameters_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenrfc_parameters_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_parameters_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Data Interfaces and Communication Interfaces](javascript:call_link\('abenabap_data_communication.htm'\))Â â†’Â  [RFC - Remote Function Call](javascript:call_link\('abenrfc.htm'\))Â â†’Â  [Examples for Remote Function Call](javascript:call_link\('abenrfc_abexas.htm'\))Â â†’Â 

Parameter Passing in sRFC and aRFC

This example demonstrates parameter passing in two RFC variants.

Source Code

REPORT demo\_rfc\_parameters.
CLASS demo\_rfc DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS:
Â Â Â Â Â Â main,
Â Â Â Â Â Â callback IMPORTING p\_task TYPE clike.
Â Â PRIVATE SECTION.
Â Â Â Â CLASS-DATA:
Â Â Â Â Â Â val\_inÂ Â Â Â  TYPE string,
Â Â Â Â Â Â val\_outÂ Â Â Â TYPE string,
Â Â Â Â Â Â val\_in\_out TYPE string,
Â Â Â Â Â Â msgÂ Â Â Â Â Â Â Â TYPE c LENGTH 80,
Â Â Â Â Â Â outÂ Â Â Â Â Â Â Â TYPE REF TO if\_demo\_output.
ENDCLASS.
CLASS demo\_rfc IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â out = cl\_demo\_output=>new( ).
Â Â Â Â "Synchronous RFC
Â Â Â Â val\_inÂ Â Â Â  = \`val\_in\`.
Â Â Â Â val\_outÂ Â Â Â = \`\`.
Â Â Â Â val\_in\_out = \`val\_in\_out\`.
Â Â Â Â CALL FUNCTION 'DEMO\_RFM\_PARAMETERS'
Â Â Â Â Â Â DESTINATION 'NONE'
Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â p\_inÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â = val\_in
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â p\_outÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  = val\_out
Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â p\_in\_outÂ Â Â Â Â Â Â Â Â Â Â Â Â Â = val\_in\_out
Â Â Â Â Â Â EXCEPTIONS
Â Â Â Â Â Â Â Â system\_failureÂ Â Â Â Â Â Â Â = 2 MESSAGE msg
Â Â Â Â Â Â Â Â communication\_failure = 4 MESSAGE msg.
Â Â Â Â IF sy-subrc <> 0.
Â Â Â Â Â Â out->display( |Error when calling sRFC.\\n{ msg }| ).
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â out->next\_section( 'sRFC'
Â Â Â Â Â Â )->write( |{ val\_out }\\n{ val\_in\_out }| ).
Â Â Â Â "Asynchronous RFC
Â Â Â Â val\_inÂ Â Â Â  = \`val\_in\`.
Â Â Â Â val\_outÂ Â Â Â = \`\`.
Â Â Â Â val\_in\_out = \`val\_in\_out\`.
Â Â Â Â CALL FUNCTION 'DEMO\_RFM\_PARAMETERS'
Â Â Â Â Â Â DESTINATION 'NONE'
Â Â Â Â Â Â STARTING NEW TASK 'DEMO'
Â Â Â Â Â Â CALLING demo\_rfc=>callback ON END OF TASK
Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â p\_inÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â = val\_in
Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â p\_in\_outÂ Â Â Â Â Â Â Â Â Â Â Â Â Â = val\_in\_out
Â Â Â Â Â Â EXCEPTIONS
Â Â Â Â Â Â Â Â system\_failureÂ Â Â Â Â Â Â Â = 2 MESSAGE msg
Â Â Â Â Â Â Â Â communication\_failure = 4 MESSAGE msg.
Â Â Â Â IF sy-subrc <> 0.
Â Â Â Â Â Â out->display( |Error when calling aRFC.\\n{ msg }| ).
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â WAIT FOR ASYNCHRONOUS TASKS UNTIL val\_out IS NOT INITIAL
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â UP TO 10 SECONDS.
Â Â Â Â out->next\_section( 'aRFC'
Â Â Â Â Â Â )->write( |{ val\_out }\\n{ val\_in\_out }| ).
Â Â Â Â out->display( ).
Â Â ENDMETHOD.
Â Â METHOD callback.
Â Â Â Â val\_outÂ Â Â Â = \`\`.
Â Â Â Â val\_in\_out = \`\`.
Â Â Â Â RECEIVE RESULTS FROM FUNCTION 'DEMO\_RFM\_PARAMETERS'
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â p\_outÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  = val\_out
Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â p\_in\_outÂ Â Â Â Â Â Â Â Â Â Â Â Â Â = val\_in\_out
Â Â Â Â Â Â EXCEPTIONS
Â Â Â Â Â Â Â Â system\_failureÂ Â Â Â Â Â Â Â = 2 MESSAGE msg
Â Â Â Â Â Â Â Â communication\_failure = 4 MESSAGE msg.
Â Â Â Â IF sy-subrc <> 0.
Â Â Â Â Â Â out->display( '|Error when receiving aRFC.\\n{ msg }|' ).
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo\_rfc=>main( ).

Description

The function module DEMO\_RFM\_PARAMETERS is called using [synchronous](javascript:call_link\('abapcall_function_destination.htm'\)) and [asynchronous](javascript:call_link\('abapcall_function_starting.htm'\)) RFC.

-   In synchronous calls, parameters are passed and applied directly in the statement CALL FUNCTION.

-   In asynchronous calls, parameters can only be passed in the statement CALL FUNCTION. Parameters are applied using the statement [RECEIVE](javascript:call_link\('abapreceive.htm'\)) in a callback routine. The statement [WAIT FOR ASYNCHRONOUS TASKS](javascript:call_link\('abapwait_until.htm'\)) is used to wait for this routine.



**ðŸ“– Source**: [abenrfc_parameters_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_parameters_abexa.htm)

### abenrfc_exceptions_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenrfc_exceptions_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_exceptions_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Data Interfaces and Communication Interfaces](javascript:call_link\('abenabap_data_communication.htm'\))Â â†’Â  [RFC - Remote Function Call](javascript:call_link\('abenrfc.htm'\))Â â†’Â  [Examples for Remote Function Call](javascript:call_link\('abenrfc_abexas.htm'\))Â â†’Â 

Exception Handling in RFC

This example demonstrates exception handling in RFC.

Source Code

REPORT demo\_rfc\_exceptions.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA msg TYPE c LENGTH 255.
Â Â Â Â DATA(out) = cl\_demo\_output=>new( ).
Â Â Â Â "Classical exception handling
Â Â Â Â CALL FUNCTION 'DEMO\_RFM\_CLASSIC\_EXCEPTION'
Â Â Â Â Â Â DESTINATION 'NONE'
Â Â Â Â Â Â EXCEPTIONS
Â Â Â Â Â Â Â Â system\_failureÂ Â Â Â Â Â Â Â = 1Â Â MESSAGE msg
Â Â Â Â Â Â Â Â communication\_failure = 2Â Â MESSAGE msg
Â Â Â Â Â Â Â Â OTHERSÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â = 3.
Â Â Â Â CASE sy-subrc.
Â Â Â Â Â Â WHEN 1.
Â Â Â Â Â Â Â Â out->write( |EXCEPTION SYSTEM\_FAILURE | && msg ).
Â Â Â Â Â Â WHEN 2.
Â Â Â Â Â Â Â Â out->write( |EXCEPTION COMMUNICATION\_FAILURE | && msg ).
Â Â Â Â Â Â WHEN 3.
Â Â Â Â Â Â Â Â out->write( |EXCEPTION OTHERS| ).
Â Â Â Â ENDCASE.
Â Â Â Â out->display( ).Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

The DEMO\_RFM\_CLASSIC\_EXCEPTION function module triggers the non-class-based exception CLASSIC\_EXCEPTION.

When DEMO\_RFM\_CLASSIC\_EXCEPTION is called, EXCEPTIONS is specified and values for sy-subrc are assigned to the possible classic exceptions. If none of the predefined RFC interface exceptions are raised, sy-subrc is set to 3.



**ðŸ“– Source**: [abenrfc_exceptions_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_exceptions_abexa.htm)

### abenparallel_rfc_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenparallel_rfc_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenparallel_rfc_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Data Interfaces and Communication Interfaces](javascript:call_link\('abenabap_data_communication.htm'\))Â â†’Â  [RFC - Remote Function Call](javascript:call_link\('abenrfc.htm'\))Â â†’Â  [Examples for Remote Function Call](javascript:call_link\('abenrfc_abexas.htm'\))Â â†’Â 

Parallel Asynchronous RFC

This example demonstrates parallel asynchronous RFC processing.

Source Code

REPORT demo\_parallel\_rfc.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS: main,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  callbback\_meth IMPORTING p\_task TYPE clike.
Â Â PRIVATE SECTION.
Â Â Â Â TYPES: BEGIN OF task\_type,
Â Â Â Â Â Â Â Â Â Â  name TYPE string,
Â Â Â Â Â Â Â Â Â Â  dest TYPE string,
Â Â Â Â Â Â Â Â  END OF task\_type.
Â Â Â Â CLASS-DATA: task\_list TYPE STANDARD TABLE OF task\_type,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â task\_waÂ Â  TYPE task\_type,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â rcv\_jobsÂ Â TYPE i,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â messÂ Â Â Â Â Â TYPE c LENGTH 80.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA: snd\_jobsÂ Â TYPE i,
Â Â Â Â Â Â Â Â Â Â exc\_flagÂ Â TYPE i,
Â Â Â Â Â Â Â Â Â Â indxÂ Â Â Â Â Â TYPE c LENGTH 4,
Â Â Â Â Â Â Â Â Â Â nameÂ Â Â Â Â Â TYPE c LENGTH 8.
Â Â Â Â DATA(out) = cl\_demo\_output=>new( ).
Â Â Â Â DO 10 TIMES.
Â Â Â Â Â Â indx = sy-index.
Â Â Â Â Â Â name = 'Task' && indx.
Â Â Â Â Â Â CALL FUNCTION 'RFC\_SYSTEM\_INFO'
Â Â Â Â Â Â Â Â STARTING NEW TASK name
Â Â Â Â Â Â Â Â DESTINATION IN GROUP DEFAULT
Â Â Â Â Â Â Â Â CALLING callbback\_meth ON END OF TASK
Â Â Â Â Â Â Â Â EXCEPTIONS
Â Â Â Â Â Â Â Â Â Â system\_failureÂ Â Â Â Â Â Â Â = 1 MESSAGE mess
Â Â Â Â Â Â Â Â Â Â communication\_failure = 2 MESSAGE mess
Â Â Â Â Â Â Â Â Â Â resource\_failureÂ Â Â Â Â Â = 3.
Â Â Â Â Â Â CASE sy-subrc.
Â Â Â Â Â Â Â Â WHEN 0.
Â Â Â Â Â Â Â Â Â Â snd\_jobs = snd\_jobs + 1.
Â Â Â Â Â Â Â Â WHEN 1 OR 2.
Â Â Â Â Â Â Â Â Â Â out->write\_text( mess ).
Â Â Â Â Â Â Â Â WHEN 3.
Â Â Â Â Â Â Â Â Â Â IF snd\_jobs >= 1 AND
Â Â Â Â Â Â Â Â Â Â Â Â  exc\_flag = 0.
Â Â Â Â Â Â Â Â Â Â Â Â  exc\_flag = 1.
Â Â Â Â Â Â Â Â Â Â Â Â WAIT UNTIL rcv\_jobs >= snd\_jobs
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  UP TO 5 SECONDS.
Â Â Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â Â Â IF sy-subrc = 0.
Â Â Â Â Â Â Â Â Â Â Â Â exc\_flag = 0.
Â Â Â Â Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â Â Â Â Â out->display( 'Resource failure' ).
Â Â Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â WHEN OTHERS.
Â Â Â Â Â Â Â Â Â Â out->display( 'Other error' ).
Â Â Â Â Â Â ENDCASE.
Â Â Â Â ENDDO.
Â Â Â Â WAIT UNTIL rcv\_jobs >= snd\_jobs.
Â Â Â Â out->display( task\_list ).Â Â ENDMETHOD.
Â Â METHOD callbback\_meth.
Â Â Â Â DATA info TYPE rfcsi.
Â Â Â Â task\_wa-name = p\_task.
Â Â Â Â rcv\_jobs = rcv\_jobs + 1.
Â Â Â Â RECEIVE RESULTS FROM FUNCTION 'RFC\_SYSTEM\_INFO'
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â rfcsi\_export = info
Â Â Â Â Â Â EXCEPTIONS
Â Â Â Â Â Â Â Â system\_failureÂ Â Â Â Â Â Â Â = 1 MESSAGE mess
Â Â Â Â Â Â Â Â communication\_failure = 2 MESSAGE mess.
Â Â Â Â IF sy-subrc = 0.
Â Â Â Â Â Â task\_wa-dest = info-rfcdest.
Â Â Â Â ELSE.
Â Â Â Â Â Â task\_wa-dest = mess.
Â Â Â Â ENDIF.
Â Â Â Â APPEND task\_wa TO task\_list.
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

Parallel asynchronous processing of the function module RFC\_SYSTEM\_INFO using [asynchronous remote function calls](javascript:call_link\('abenasynchronous_rfc_glosry.htm'\) "Glossary Entry"). Ten calls with different task IDs name are made, which each run in a separate [work process](javascript:call_link\('abenwork_process_glosry.htm'\) "Glossary Entry"). The callback routine callbback\_meth counts the completed function modules and receives information about the target system.

The addition GROUP DEFAULT is used to distribute the execution across all [AS Instances](javascript:call_link\('abenapplication_server_glosry.htm'\) "Glossary Entry")s of the current AS ABAP. If no more free work processes are available after at least one successful call, the execution of the program is stopped until all function modules started up to that point have been completed. This stoppage is limited to a maximum of 5 seconds.

After all function modules have been started, the system waits until all callback routines have been executed. After that, the internal table task\_list filled there is produced. The output shows the order in which the individual tasks were completed and on which AS Instance.



**ðŸ“– Source**: [abenparallel_rfc_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenparallel_rfc_abexa.htm)

### abenrfc_dynamic_dest_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenrfc_dynamic_dest_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_dynamic_dest_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Data Interfaces and Communication Interfaces](javascript:call_link\('abenabap_data_communication.htm'\))Â â†’Â  [RFC - Remote Function Call](javascript:call_link\('abenrfc.htm'\))Â â†’Â  [Examples for Remote Function Call](javascript:call_link\('abenrfc_abexas.htm'\))Â â†’Â 

Dynamic RFC Destination

This example demonstrates how [dynamic destinations](javascript:call_link\('abenrfc_destination.htm'\)) are created.

Source Code

REPORT demo\_rfc\_dynamic\_dest.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS:
Â Â Â Â Â Â main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA:
Â Â Â Â Â Â val\_inÂ Â Â Â  TYPE string VALUE \`val\_in\`,
Â Â Â Â Â Â val\_in\_out TYPE string VALUE \`val\_in\_out\`,
Â Â Â Â Â Â val\_outÂ Â Â Â TYPE string,
Â Â Â Â Â Â msgÂ Â Â Â Â Â Â Â TYPE c LENGTH 80.
Â Â Â Â IF sy-uname IS INITIAL.
Â Â Â Â Â Â cl\_demo\_output=>display(
Â Â Â Â Â Â Â Â |Example not possible for anonymous user| ).
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â DATA(in) = cl\_demo\_input=>new( ).
Â Â Â Â DATA(client) = sy-mandt.
Â Â Â Â in->add\_field( CHANGING field = client ).
Â Â Â Â DATA(uname) = sy-uname.
Â Â Â Â in->add\_field( CHANGING field = uname ).
Â Â Â Â DATA(langu) = sy-langu.
Â Â Â Â in->add\_field( CHANGING field = langu ).
Â Â Â Â DATA(sysid) = sy-sysid.
Â Â Â Â in->add\_field( CHANGING field = sysid ).
Â Â Â Â DATA(host) = CONV rfchost( sy-host ).
Â Â Â Â in->add\_field( CHANGING field = host ).
Â Â Â Â DATA(group) = CONV rfcload( 'PUBLIC' ).
Â Â Â Â in->add\_field( CHANGING field = group ).
Â Â Â Â in->request( ).
Â Â Â Â DATA(dest) = cl\_dynamic\_destination=>create\_rfc\_destination(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  logon\_clientÂ Â  = client
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  logon\_userÂ Â Â Â  = uname
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  logon\_language = langu
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  sidÂ Â Â Â Â Â Â Â Â Â Â Â = sysid
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  serverÂ Â Â Â Â Â Â Â  = host
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  groupÂ Â Â Â Â Â Â Â Â Â = group ).
Â Â Â Â CALL FUNCTION 'DEMO\_RFM\_PARAMETERS'
Â Â Â Â Â Â DESTINATION dest
Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â p\_inÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â = val\_in
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â p\_outÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  = val\_out
Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â p\_in\_outÂ Â Â Â Â Â Â Â Â Â Â Â Â Â = val\_in\_out
Â Â Â Â Â Â EXCEPTIONS
Â Â Â Â Â Â Â Â system\_failureÂ Â Â Â Â Â Â Â = 2 MESSAGE msg
Â Â Â Â Â Â Â Â communication\_failure = 4 MESSAGE msg.
Â Â Â Â IF sy-subrc <> 0.
Â Â Â Â Â Â cl\_demo\_output=>display( |Error when calling sRFC.\\n{ msg }| ).
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â cl\_demo\_output=>display( |{ val\_out }\\n{ val\_in\_out }| ).Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

The method CREATE\_RFC\_DESTINATION of class CL\_DYNAMIC\_DESTINATION is used to create a [dynamic destination](javascript:call_link\('abenrfc_destination.htm'\)). Input values, whose default values are determined from system fields for the current AS ABAP, are passed as parameters. A remote-enabled function module is called using the dynamic destination. The call is only successful if the destination is created correctly.



**ðŸ“– Source**: [abenrfc_dynamic_dest_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_dynamic_dest_abexa.htm)

### abenrfc_logon_data_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenrfc_logon_data_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_logon_data_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ðŸ“– Source**: [abenrfc_logon_data_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenrfc_logon_data_abexa.htm)


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Data Interfaces and Communication Interfaces](javascript:call_link\('abenabap_data_communication.htm'\))Â â†’Â  [RFC - Remote Function Call](javascript:call_link\('abenrfc.htm'\))Â â†’Â  [Examples for Remote Function Call](javascript:call_link\('abenrfc_abexas.htm'\))Â â†’Â 

Implicit Logon Data in RFC

This example demonstrates how the logon data of an RFC session is set implicitly.

Source Code

REPORT demo\_rfc\_implicit\_logon\_data.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA:
Â Â Â Â Â Â BEGIN OF logon\_data,
Â Â Â Â Â Â Â Â unameÂ Â Â Â Â Â  TYPE sy-uname,
Â Â Â Â Â Â Â Â mandtÂ Â Â Â Â Â  TYPE sy-mandt,
Â Â Â Â Â Â Â Â logon\_langu TYPE sy-langu,
Â Â Â Â Â Â Â Â languÂ Â Â Â Â Â  TYPE sy-langu,
Â Â Â Â Â Â END OF logon\_data.
Â Â Â Â IF cl\_abap\_syst=>get\_logon\_language( ) <> 'E'.
Â Â Â Â Â Â SET LOCALE LANGUAGE 'E'.
Â Â Â Â ELSE.
Â Â Â Â Â Â SET LOCALE LANGUAGE 'D'.
Â Â Â Â ENDIF.
Â Â Â Â logon\_data = VALUE #(
Â Â Â Â Â Â Â Â unameÂ Â Â Â Â Â  = sy-uname
Â Â Â Â Â Â Â Â mandtÂ Â Â Â Â Â  = sy-mandt
Â Â Â Â Â Â Â Â logon\_langu = cl\_abap\_syst=>get\_logon\_language( )
Â Â Â Â Â Â Â Â languÂ Â Â Â Â Â  = cl\_abap\_syst=>get\_language( ) ).
Â Â Â Â ASSERT logon\_data-langu = sy-langu.
Â Â Â Â DATA(out) = cl\_demo\_output=>new(
Â Â Â Â Â Â )->next\_section( 'Parameters of Caller'
Â Â Â Â Â Â )->write( logon\_data ).
Â Â Â Â CALL FUNCTION 'DEMO\_RFM\_LOGON\_DATA' DESTINATION 'NONE'
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â unameÂ Â Â Â Â Â  = logon\_data-uname
Â Â Â Â Â Â Â Â mandtÂ Â Â Â Â Â  = logon\_data-mandt
Â Â Â Â Â Â Â Â logon\_langu = logon\_data-logon\_langu
Â Â Â Â Â Â Â Â languÂ Â Â Â Â Â  = logon\_data-langu.
Â Â Â Â out->next\_section( 'Parameters of Callee'
Â Â Â Â Â Â )->write( logon\_data )->display( ).Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

A remote-enabled function module DEMO\_RFM\_LOGON\_DATA is called with the destination "NONE" using the RFC interface. The logon data user name, client, and logon language of the corresponding [RFC session](javascript:call_link\('abenrfc_context.htm'\)) are provided implicitly. To demonstrate that the [logon language](javascript:call_link\('abenlogon_language_glosry.htm'\) "Glossary Entry") of the RFC session is set by the language of the [text environment](javascript:call_link\('abentext_environment_glosry.htm'\) "Glossary Entry") of the caller, this is set before the call with the statement [SET LOCALE LANGUAGE](javascript:call_link\('abapset_locale.htm'\)) to a language different from the logon language of the caller. In the called RFM the language of the text environment of the RFC session is also changed and is then different to the logon language there as well.

If the function module DEMO\_RFM\_LOGON\_DATA is called remotely again, the RFC session is reused with the existing logon data. Changing the language of the text environment again in the caller would have no effect.
