# ABAP Keyword Documentation / ABAP âˆ’ Reference / Processing Internal Data / Internal Tables / System Class for Internal Tables

Included pages: 7



## ğŸ”— Source References

**Base URL**: [ABAP Keyword Documentation 7.53](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abencl_abap_itab.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abencl_abap_itab.htm)
- [abenvirtual_sort_abexas.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_abexas.htm)
- [abenvirtual_sort_simple_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_simple_abexa.htm)
- [abenvirtual_sort_combined_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_combined_abexa.htm)
- [abenvirtual_sort_filter_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_filter_abexa.htm)
- [abenvirtual_sort_flights_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_flights_abexa.htm)

**Bundle Contains**: 6 documentation pages
**Version**: ABAP 7.53
**Generated**: 2025-09-01T11:25:46.215Z

---

### abencl_abap_itab.htm

> **ğŸ“– Official SAP Documentation**: [abencl_abap_itab.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abencl_abap_itab.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Internal Tables](javascript:call_link\('abenitab.htm'\))Â â†’Â 

System Class for Internal Tables

The class CL\_ABAP\_ITAB\_UTILITIES contains the following methods:

-   FLUSH\_ITAB\_KEY and FLUSH\_ITAB\_KEYS can be used to update individual [secondary table keys](javascript:call_link\('abensecondary_table_key_glosry.htm'\) "Glossary Entry") or all secondary table keys of an internal table explicitly. Otherwise the update takes place as a [delayed](javascript:call_link\('abendelayed_update_glosry.htm'\) "Glossary Entry") or [lazy update](javascript:call_link\('abenlazy_update_glosry.htm'\) "Glossary Entry").

-   READ\_BINARY\_SEARCH\_CHECK can be used with the addition [BINARY SEARCH](javascript:call_link\('abapread_table_free.htm'\)) to activate checks on the required sort for test purposes. If the required sort does not exist, the runtime error ITAB\_ILLEGAL\_ORDER is raised when the check is activated.

-   HAS\_COLLECT\_HASH\_ADMIN can be used to check whether the temporary hash management of the statement [COLLECT](javascript:call_link\('abapcollect.htm'\)) exists for a standard table.

-   INDEX\_HEALTH\_CHECK can be used to check the consistency of [primary](javascript:call_link\('abenprimary_table_index_glosry.htm'\) "Glossary Entry") and [secondary table indexes](javascript:call_link\('abensecondary_table_index_glosry.htm'\) "Glossary Entry").

-   VIRTUAL\_SORT can be used to apply virtual sorts to a set of internal tables with the same number of rows. The internal tables are treated internally like a single combined table containing all the columns of the involved internal tables. The result is an array of row numbers of the virtually sorted combined table. See the related [executable examples](javascript:call_link\('abenvirtual_sort_abexas.htm'\)).

Continue
[Examples of Virtual Sorting of Internal Tables](javascript:call_link\('abenvirtual_sort_abexas.htm'\))



**ğŸ“– Source**: [abencl_abap_itab.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abencl_abap_itab.htm)

### abenvirtual_sort_abexas.htm

> **ğŸ“– Official SAP Documentation**: [abenvirtual_sort_abexas.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_abexas.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Internal Tables](javascript:call_link\('abenitab.htm'\))Â â†’Â  [System Class for Internal Tables](javascript:call_link\('abencl_abap_itab.htm'\))Â â†’Â 

Examples of Virtual Sorting of Internal Tables

Continue
![Example](exa.gif "Example") [Virtual Sorting of an Internal Table](javascript:call_link\('abenvirtual_sort_simple_abexa.htm'\))
![Example](exa.gif "Example") [Virtual Sorting of Two Internal Tables](javascript:call_link\('abenvirtual_sort_combined_abexa.htm'\))
![Example](exa.gif "Example") [Virtual Sorting of an Internal Table with Filters](javascript:call_link\('abenvirtual_sort_filter_abexa.htm'\))
![Example](exa.gif "Example") [Virtual Sorting of Flight Data](javascript:call_link\('abenvirtual_sort_flights_abexa.htm'\))



**ğŸ“– Source**: [abenvirtual_sort_abexas.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_abexas.htm)

### abenvirtual_sort_simple_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenvirtual_sort_simple_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_simple_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Internal Tables](javascript:call_link\('abenitab.htm'\))Â â†’Â  [System Class for Internal Tables](javascript:call_link\('abencl_abap_itab.htm'\))Â â†’Â  [Examples of Virtual Sorting of Internal Tables](javascript:call_link\('abenvirtual_sort_abexas.htm'\))Â â†’Â 

Virtual Sorting of an Internal Table

This example demonstrates the virtual sorting of a single internal table.

Source Code

REPORT demo\_virtual\_sort\_simple.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â TYPES:
Â Â Â Â Â Â BEGIN OF line,
Â Â Â Â Â Â Â Â col1 TYPE i,
Â Â Â Â Â Â Â Â col2 TYPE i,
Â Â Â Â Â Â Â Â col3 TYPE string,
Â Â Â Â Â Â Â Â col4 TYPE string,
Â Â Â Â Â Â END OF line,
Â Â Â Â Â Â itab TYPE STANDARD TABLE OF line WITH EMPTY KEY.
Â Â Â Â DATA(rnd) = cl\_abap\_random\_int=>create( seed = + sy-uzeit
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â minÂ Â = 1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â maxÂ Â = 10 ).
Â Â Â Â DATA(itab) = VALUE itab( FOR i = 1 UNTIL i > 10
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( col1 = rnd->get\_next( )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  col2 = rnd->get\_next( )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  col3 = substring(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â val = sy-abcde
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â off = rnd->get\_next( ) - 1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â len = 1 )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  col4 = substring(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â val = sy-abcde
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â off = rnd->get\_next( ) - 1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â len = 1 ) ) ).
Â Â Â Â DATA(out) = cl\_demo\_output=>new( ).
Â Â Â Â out->write( itab ).
Â Â Â Â out->next\_section( \`Virtual Sort by col1, col2, Ascending\` ).
Â Â Â Â DATA(v\_index) =
Â Â Â Â Â Â cl\_abap\_itab\_utilities=>virtual\_sort(
Â Â Â Â Â Â Â Â im\_virtual\_source = VALUE #(
Â Â Â Â Â Â Â Â  ( sourceÂ Â Â Â  = REF #( itab )
Â Â Â Â Â Â Â Â Â Â  components = VALUE #( ( name = 'col1' )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( name = 'col2' ) ) ) ) ).
Â Â Â Â out->write( v\_index ).
Â Â Â Â DATA sorted\_tab TYPE itab.
Â Â Â Â sorted\_tab = VALUE #( FOR idx IN v\_index ( itab\[ idx \] ) ).
Â Â Â Â DATA(test\_tab) = itab.
Â Â Â Â SORT test\_tab STABLE BY col1 col2.
Â Â Â Â ASSERT sorted\_tab = test\_tab.
Â Â Â Â out->write( sorted\_tab ).
Â Â Â Â out->next\_section( \`Virtual Sort by col3, col4, Descending\` ).
Â Â Â Â v\_index =
Â Â Â Â Â Â cl\_abap\_itab\_utilities=>virtual\_sort(
Â Â Â Â Â Â Â Â im\_virtual\_source = VALUE #(
Â Â Â Â Â Â Â Â  ( sourceÂ Â Â Â  = REF #( itab )
Â Â Â Â Â Â Â Â Â Â  components = VALUE #(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( name = 'col3'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  astext = abap\_true
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  descending = abap\_true )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( name = 'col4'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  astext = abap\_true
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  descending = abap\_true ) ) ) ) ).
Â Â Â Â out->write( v\_index ).
Â Â Â Â sorted\_tab = VALUE #( FOR idx IN v\_index ( itab\[ idx \] ) ).
Â Â Â Â test\_tab = itab.
Â Â Â Â SORT test\_tab STABLE BY col3 AS TEXT DESCENDING
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â col4 AS TEXT DESCENDING.
Â Â Â Â ASSERT sorted\_tab = test\_tab.
Â Â Â Â out->write( sorted\_tab ).
Â Â Â Â out->display( ).Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

An internal table filled with random numbers and letters is sorted virtually using the method VIRTUAL\_SORT of system class CL\_ABAP\_ITAB\_UTILITIES, first in ascending order by the first two columns and then in descending alphabetical order by the last two columns. The returned array contains the row numbers in the respective sort order. This array is used to fill an internal table sorted\_tab according to the sorting. To compare, an internal table test\_tab with the same content is constructed and sorted with the statement [SORT](javascript:call_link\('abapsort_itab.htm'\)). The results are the same in both cases.



**ğŸ“– Source**: [abenvirtual_sort_simple_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_simple_abexa.htm)

### abenvirtual_sort_combined_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenvirtual_sort_combined_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_combined_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Internal Tables](javascript:call_link\('abenitab.htm'\))Â â†’Â  [System Class for Internal Tables](javascript:call_link\('abencl_abap_itab.htm'\))Â â†’Â  [Examples of Virtual Sorting of Internal Tables](javascript:call_link\('abenvirtual_sort_abexas.htm'\))Â â†’Â 

Virtual Sorting of Two Internal Tables

This example demonstrates the virtual sorting of two internal tables.

Source Code

REPORT demo\_virtual\_sort\_combined.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â TYPES:
Â Â Â Â Â Â BEGIN OF line1,
Â Â Â Â Â Â Â Â col1 TYPE i,
Â Â Â Â Â Â Â Â col2 TYPE i,
Â Â Â Â Â Â END OF line1,
Â Â Â Â Â Â itab1 TYPE STANDARD TABLE OF line1 WITH EMPTY KEY,
Â Â Â Â Â Â BEGIN OF line2,
Â Â Â Â Â Â Â Â col1 TYPE string,
Â Â Â Â Â Â Â Â col2 TYPE string,
Â Â Â Â Â Â END OF line2,
Â Â Â Â Â Â itab2 TYPE STANDARD TABLE OF line2 WITH EMPTY KEY.
Â Â Â Â TYPES:
Â Â Â Â Â Â BEGIN OF test\_line,
Â Â Â Â Â Â Â Â col11 TYPE i,
Â Â Â Â Â Â Â Â col12 TYPE i,
Â Â Â Â Â Â Â Â col21 TYPE string,
Â Â Â Â Â Â Â Â col22 TYPE string,
Â Â Â Â Â Â END OF test\_line,
Â Â Â Â Â Â test\_tab TYPE STANDARD TABLE OF test\_line WITH EMPTY KEY.
Â Â Â Â DATA(rnd) = cl\_abap\_random\_int=>create( seed = + sy-uzeit
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â minÂ Â = 0
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â maxÂ Â = 1 ).
Â Â Â Â DATA(itab1) = VALUE itab1( FOR i = 1 UNTIL i > 10
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( col1 = rnd->get\_next( )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â  col2 = rnd->get\_next( ) ) ).
Â Â Â Â DATA(itab2) =
Â Â Â Â Â Â VALUE itab2( FOR i = 1 UNTIL i > 10
Â Â Â Â Â Â Â Â ( col1 = cond #( when rnd->get\_next( ) = 0 THEN \`X\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ELSE \`Y\` )
Â Â Â Â Â Â Â Â Â Â col2 = cond #( when rnd->get\_next( ) = 0 THEN \`X\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ELSE \`Y\` ) ) ).
Â Â Â Â DATA(out) = cl\_demo\_output=>new( ).
Â Â Â Â out->write( itab1
Â Â Â Â Â Â )->write( itab2 ).
Â Â Â Â out->next\_section(Â Â \`Virtual Sort of Combined Tables\`
Â Â Â Â Â Â )->begin\_section( \`itab1 by col1, col2, Ascending\`
Â Â Â Â Â Â )->next\_section(Â Â \`itab2 by col1, col2, Descending\` ).
Â Â Â Â DATA(v\_index) =
Â Â Â Â Â Â cl\_abap\_itab\_utilities=>virtual\_sort(
Â Â Â Â Â Â Â Â im\_virtual\_source = VALUE #(
Â Â Â Â Â Â Â Â  ( sourceÂ Â Â Â  = REF #( itab1 )
Â Â Â Â Â Â Â Â Â Â  components = VALUE #( ( name = 'col1' )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( name = 'col2' ) ) )
Â Â Â Â Â Â Â Â  ( sourceÂ Â Â Â  = REF #( itab2 )
Â Â Â Â Â Â Â Â Â Â  components = VALUE #( ( name = 'col1'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  astext = abap\_true
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  descending = abap\_true )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( name = 'col2'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  astext = abap\_true
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  descending = abap\_true ) ) ) ) ).
Â Â Â Â out->write( v\_index ).
Â Â Â Â DATA sorted\_tab1 TYPE itab1.
Â Â Â Â sorted\_tab1 = VALUE #( FOR idx IN v\_index ( itab1\[ idx \] ) ).
Â Â Â Â DATA sorted\_tab2 TYPE itab2.
Â Â Â Â sorted\_tab2 = VALUE #( FOR idx IN v\_index ( itab2\[ idx \] ) ).
Â Â Â Â DATA(comb\_tab) = VALUE test\_tab( FOR i = 1 UNTIL i > 10
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( col11 = sorted\_tab1\[ i \]-col1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â col12 = sorted\_tab1\[ i \]-col2
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â col21 = sorted\_tab2\[ i \]-col1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â col22 = sorted\_tab2\[ i \]-col2 ) ).
Â Â Â Â DATA(test\_tab) = VALUE test\_tab( FOR i = 1 UNTIL i > 10
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( col11 = itab1\[ i \]-col1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â col12 = itab1\[ i \]-col2
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â col21 = itab2\[ i \]-col1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â col22 = itab2\[ i \]-col2 ) ).
Â Â Â Â SORT test\_tab STABLE BY col11
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â col12
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â col21 DESCENDING AS TEXT
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â col22 DESCENDING AS TEXT.
Â Â Â Â ASSERT comb\_tab = test\_tab.
Â Â Â Â out->write( comb\_tab ).
Â Â Â Â out->display( ).Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

An internal table itab1 containing random numbers and an internal table itab2 containing random letters are sorted together virtually using the method VIRTUAL\_SORT of system class CL\_ABAP\_ITAB\_UTILITIES, which sorts both columns of itab1 in ascending order and both columns of itab2 in descending alphabetical order. The returned array contains the row numbers in the sort order. This array is used to fill the internal table comb\_tab, which combines the columns of the involved tables, according to the sorting. To compare, another table test\_tab, which combines the content from itab1 and itab2, is constructed and sorted using the statement [SORT](javascript:call_link\('abapsort_itab.htm'\)). The results are the same. However, in a real example the combined tables are not usually of interest. See the executable example [Virtual Sorting of Flight Data](javascript:call_link\('abenvirtual_sort_flights_abexa.htm'\)).



**ğŸ“– Source**: [abenvirtual_sort_combined_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_combined_abexa.htm)

### abenvirtual_sort_filter_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenvirtual_sort_filter_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_filter_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Internal Tables](javascript:call_link\('abenitab.htm'\))Â â†’Â  [System Class for Internal Tables](javascript:call_link\('abencl_abap_itab.htm'\))Â â†’Â  [Examples of Virtual Sorting of Internal Tables](javascript:call_link\('abenvirtual_sort_abexas.htm'\))Â â†’Â 

Virtual Sorting of an Internal Table with Filters

This example demonstrates the virtual sorting of an internal table with row filtering.

Source Code

REPORT demo\_virtual\_sort\_filter.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â TYPES
Â Â Â Â Â Â itab TYPE STANDARD TABLE OF i WITH EMPTY KEY.
Â Â Â Â DATA(rnd) = cl\_abap\_random\_int=>create( seed = + sy-uzeit
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â minÂ Â = 1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â maxÂ Â = 10 ).
Â Â Â Â DATA(itab) = VALUE itab( FOR i = 1 UNTIL i > 10
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( rnd->get\_next( ) ) ).
Â Â Â Â DATA(out) = cl\_demo\_output=>new( ).
Â Â Â Â out->write( itab ).
Â Â Â Â out->next\_section( \`Virtual Sort by table\_line with Filter\` ).
Â Â Â Â DATA(v\_index) =
Â Â Â Â Â Â cl\_abap\_itab\_utilities=>virtual\_sort(
Â Â Â Â Â Â Â Â im\_virtual\_source = VALUE #(
Â Â Â Â Â Â Â Â  ( sourceÂ Â Â Â  = REF #( itab )
Â Â Â Â Â Â Â Â Â Â  components = VALUE #( ( name = 'table\_line' ) ) ) )
Â Â Â Â Â Â Â Â im\_filter\_index =Â Â VALUE #( ( 1 ) ( 3 ) ( 5 ) ( 7 ) ( 9 ) ) ).
Â Â Â Â out->write( v\_index ).
Â Â Â Â DATA sorted\_tab TYPE itab.
Â Â Â Â sorted\_tab = VALUE #( FOR idx IN v\_index ( itab\[ idx \] ) ).
Â Â Â Â out->write( sorted\_tab ).
Â Â Â Â out->display( ).Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

An internal table filled with random numbers is sorted in ascending order by its row content using the method VIRTUAL\_SORT of system class CL\_ABAP\_ITAB\_UTILITIES. This passes a table filter\_tab containing the row numbers to be sorted. The returned array contains exactly these row numbers in the sort order. A table sorted\_tab is constructed in this order from the respective rows of itab.



**ğŸ“– Source**: [abenvirtual_sort_filter_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_filter_abexa.htm)

### abenvirtual_sort_flights_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenvirtual_sort_flights_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_flights_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Internal Tables](javascript:call_link\('abenitab.htm'\))Â â†’Â  [System Class for Internal Tables](javascript:call_link\('abencl_abap_itab.htm'\))Â â†’Â  [Examples of Virtual Sorting of Internal Tables](javascript:call_link\('abenvirtual_sort_abexas.htm'\))Â â†’Â 

Virtual Sorting of Flight Data

This example demonstrates the virtual sorting of an internal table containing flight data.

Source Code

REPORT demo\_virtual\_sort\_flights.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â TYPES:
Â Â Â Â Â Â BEGIN OF flight,
Â Â Â Â Â Â Â Â carridÂ Â  TYPE s\_carr\_id,
Â Â Â Â Â Â Â Â connidÂ Â  TYPE s\_conn\_id,
Â Â Â Â Â Â Â Â cityfrom TYPE s\_city,
Â Â Â Â Â Â Â Â citytoÂ Â  TYPE s\_city,
Â Â Â Â Â Â END OF flight,
Â Â Â Â Â Â flights TYPE STANDARD TABLE OF flight
Â Â Â Â Â Â Â Â Â Â Â Â Â Â WITH EMPTY KEY,
Â Â Â Â Â Â BEGIN OF city,
Â Â Â Â Â Â Â Â cityÂ Â Â Â Â Â TYPEÂ Â s\_city,
Â Â Â Â Â Â Â Â latitudeÂ Â TYPEÂ Â s\_lati,
Â Â Â Â Â Â Â Â longitude TYPEÂ Â s\_long,
Â Â Â Â Â Â END OF city,
Â Â Â Â Â Â cities TYPE STANDARD TABLE OF city
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WITH EMPTY KEY.
Â Â Â Â CLASS-DATA:
Â Â Â Â Â Â flight\_tabÂ Â Â Â TYPE flights,
Â Â Â Â Â Â from\_city\_tab TYPE cities,
Â Â Â Â Â Â to\_city\_tabÂ Â  TYPE cities.
Â Â Â Â CLASS-METHODS:
Â Â Â Â Â Â main,
Â Â Â Â Â Â class\_constructor.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â cl\_demo\_output=>new(
Â Â Â Â Â Â )->next\_section(
Â Â Â Â Â Â \`Ascending Sort by Latitude, Longitude of CITYFROM, CITYTO\`
Â Â Â Â Â Â )->write( VALUE flights(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â FOR <idx>
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â IN cl\_abap\_itab\_utilities=>virtual\_sort(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  im\_virtual\_source =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  VALUE #(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( sourceÂ Â Â Â  = REF #( from\_city\_tab )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  components =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  VALUE #( ( name = 'latitude' )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( name = 'longitude' ) ) )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( sourceÂ Â Â Â  = REF #( to\_city\_tab )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  components =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  VALUE #( ( name = 'latitude' )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( name = 'longitude' ) ) )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( sourceÂ Â Â Â  = REF #( flight\_tab )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  components =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  VALUE #( ( name = 'carrid' )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( name = 'connid' ) ) ) ) )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( flight\_tab\[ <idx> \] ) )
Â Â Â Â Â Â )->next\_section(
Â Â Â Â Â Â \`Descending Sort by Latitude, Longitude of CITYFROM, CITYTO\`
Â Â Â Â Â Â )->write( VALUE flights(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â FOR <idx>
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â IN cl\_abap\_itab\_utilities=>virtual\_sort(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  im\_virtual\_source =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  VALUE #(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( sourceÂ Â Â Â  = REF #( from\_city\_tab )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  components =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  VALUE #( ( name = 'latitude'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â descending = abap\_true )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( name = 'longitude'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â descending = abap\_true ) ) )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( sourceÂ Â Â Â  = REF #( to\_city\_tab )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  components =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  VALUE #( ( name = 'latitude'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â descending = abap\_true )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( name = 'longitude'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â descending = abap\_true ) ) )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( sourceÂ Â Â Â  = REF #( flight\_tab )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  components =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  VALUE #( ( name = 'carrid' )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( name = 'connid' ) ) ) ) )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( flight\_tab\[ <idx> \] ) )
Â Â Â Â Â Â )->display( ).Â Â ENDMETHOD.
Â Â METHOD class\_constructor.
Â Â Â Â SELECT carrid, connid, cityfrom, cityto
Â Â Â Â Â Â Â Â Â Â  FROM spfli
Â Â Â Â Â Â Â Â Â Â  INTO CORRESPONDING FIELDS OF TABLE @flight\_tab.
Â Â Â Â SELECT city, latitude, longitude
Â Â Â Â Â Â Â Â Â Â  FROM sgeocity
Â Â Â Â Â Â Â Â Â Â  INTO TABLE @DATA(cities).
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â from\_city\_tab = VALUE #( FOR <fs> IN flight\_tab
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( cities\[ city = <fs>-cityfrom \] ) ).
Â Â Â Â Â Â Â Â to\_city\_tabÂ Â  = VALUE #( FOR <fs> IN flight\_tab
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( cities\[ city = <fs>-cityto \] ) ).
Â Â Â Â Â Â CATCH cx\_sy\_itab\_line\_not\_found.
Â Â Â Â Â Â Â Â MESSAGE 'Flight model data not consistent,' &&
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ' use program SAPBC\_DATA\_GENERATOR' &&
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ' to create the data.' TYPE 'X'.
Â Â Â Â ENDTRY.
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

In this example, an internal table flight\_tab containing flight data is sorted virtually by the longitudes and latitudes of the departure and arrival cities. To do this, two additional tables from\_city\_tab and to\_city\_tab are constructed. The rows of these tables contain the longitudes and latitudes of the departure and arrival cities from the respective rows of flight\_tab.

Virtual sorting with the method VIRTUAL\_SORT of class CL\_ABAP\_ITAB\_UTILITIES takes place at the operand position of a [FOR](javascript:call_link\('abenfor_itab.htm'\)) expression for a table iteration. The virtual sorting involves all three internal tables. The temporary result of the sorting is used to construct a sorted internal table from the rows of flight\_tab. This table is only temporary, and is an input parameter of the method WRITE of class CL\_DEMO\_OUTPUT.

It is sorted once in ascending order and once in descending order. This does not change the order of the rows in the internal tables that are involved. These remain in their original unsorted state. Virtual sorting makes it possible to generate various sorted output data without affecting the original data.



**ğŸ“– Source**: [abenvirtual_sort_flights_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_flights_abexa.htm)

### abenvirtual_sort_abexas.htm

> **ğŸ“– Official SAP Documentation**: [abenvirtual_sort_abexas.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_abexas.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ğŸ“– Source**: [abenvirtual_sort_abexas.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenvirtual_sort_abexas.htm)


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Internal Tables](javascript:call_link\('abenitab.htm'\))Â â†’Â  [System Class for Internal Tables](javascript:call_link\('abencl_abap_itab.htm'\))Â â†’Â 

Examples of Virtual Sorting of Internal Tables

Continue
![Example](exa.gif "Example") [Virtual Sorting of an Internal Table](javascript:call_link\('abenvirtual_sort_simple_abexa.htm'\))
![Example](exa.gif "Example") [Virtual Sorting of Two Internal Tables](javascript:call_link\('abenvirtual_sort_combined_abexa.htm'\))
![Example](exa.gif "Example") [Virtual Sorting of an Internal Table with Filters](javascript:call_link\('abenvirtual_sort_filter_abexa.htm'\))
![Example](exa.gif "Example") [Virtual Sorting of Flight Data](javascript:call_link\('abenvirtual_sort_flights_abexa.htm'\))
