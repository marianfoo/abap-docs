# ABAP Keyword Documentation / ABAP âˆ’ Reference / Processing Internal Data / Numeric Calculations / Numeric Functions

Included pages: 12



## ğŸ”— Source References

**Base URL**: [ABAP Keyword Documentation 7.53](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abenmathematical_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenmathematical_functions.htm)
- [abennumerical_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abennumerical_functions.htm)
- [abenpower_function.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenpower_function.htm)
- [abennmax_nmin_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abennmax_nmin_functions.htm)
- [abenfloating_point_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenfloating_point_functions.htm)
- [abendec_floating_point_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abendec_floating_point_functions.htm)
- [abennumerical_functions_abexas.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abennumerical_functions_abexas.htm)
- [abenmath_func_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenmath_func_abexa.htm)
- [abenipow_function_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenipow_function_abexa.htm)
- [abennmax_nmin_function_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abennmax_nmin_function_abexa.htm)
- [abenround_function_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenround_function_abexa.htm)

**Bundle Contains**: 11 documentation pages
**Version**: ABAP 7.53
**Generated**: 2025-09-01T11:25:46.219Z

---

### abenmathematical_functions.htm

> **ğŸ“– Official SAP Documentation**: [abenmathematical_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenmathematical_functions.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Numeric Calculations](javascript:call_link\('abencompute_expressions.htm'\))Â â†’Â 

Numeric Functions

Numeric functions are a type of [built-in function](javascript:call_link\('abenbuilt_in_functions.htm'\)). The main argument of a numeric function must represent a numeric value. The data type of the return value is determined either by the argument of the function (overloaded functions), or by the function.

Outside of an [arithmetic expression](javascript:call_link\('abenarithmetic_expression_glosry.htm'\) "Glossary Entry"), the main argument of a numeric function must be a single, numeric data object. Within an arithmetic expression, the following are possible as main arguments of a numeric function:

-   [Numeric data object](javascript:call_link\('abennumeric_data_object_glosry.htm'\) "Glossary Entry")

-   [Arithmetic expression](javascript:call_link\('abenarithmetic_expression_glosry.htm'\) "Glossary Entry")

-   [Built-in function](javascript:call_link\('abenpredefined_function_glosry.htm'\) "Glossary Entry")

-   [Functional method](javascript:call_link\('abenfunctional_method_glosry.htm'\) "Glossary Entry")

The numeric functions are divided into:

-   [General numeric functions](javascript:call_link\('abennumerical_functions.htm'\))

-   [Numeric extremum functions](javascript:call_link\('abennmax_nmin_functions.htm'\))

-   [Floating point functions](javascript:call_link\('abenfloating_point_functions.htm'\))

-   [Rounding functions](javascript:call_link\('abendec_floating_point_functions.htm'\))

Handleable exceptions

The handleable exceptions that can be raised during the calculation of a numeric function are subclasses of the classes CX\_SY\_ARITHMETIC\_ERROR and CX\_SY\_CONVERSION\_ERROR.

Continue
[abs, sign, ceil, floor, trunc,](javascript:call_link\('abennumerical_functions.htm'\))
[ipow - Integer Power Function](javascript:call_link\('abenpower_function.htm'\))
[nmax, nmin - Numeric Extremum Functions](javascript:call_link\('abennmax_nmin_functions.htm'\))
[acos, sin, tanh, exp, log,sqrt](javascript:call_link\('abenfloating_point_functions.htm'\))
[round, rescale - Rounding Functions](javascript:call_link\('abendec_floating_point_functions.htm'\))
[Examples of Numeric Functions](javascript:call_link\('abennumerical_functions_abexas.htm'\))



**ğŸ“– Source**: [abenmathematical_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenmathematical_functions.htm)

### abennumerical_functions.htm

> **ğŸ“– Official SAP Documentation**: [abennumerical_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abennumerical_functions.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Numeric Calculations](javascript:call_link\('abencompute_expressions.htm'\))Â â†’Â  [Numeric Functions](javascript:call_link\('abenmathematical_functions.htm'\))Â â†’Â 

abs, sign, ceil, floor, trunc, frac - Numeric Functions

The following table shows the general numeric functions for a single unnamed argument with any numeric data type. These functions are overloaded with the effect that the return value can have different numeric types.

Syntax

... func( arg ) ...

Effect

The argument of a general numeric function must be an individual data object outside an arithmetic expression, and can itself be a numeric expression within an arithmetic expression.

Effect of the general numeric functions.

Functionfunc

Return value

abs

Absolute value of argument arg

sign

Sign of argument arg: -1, if the value of arg is negative; 0, if the value of arg is 0; 1, if the value of arg is positive

ceil

Smallest integer that is not less than the value of the argument arg is

floor

Largest integer that is not greater than the value of the argument arg is

trunc

Value of the integer part of the argument arg; negative if arg is negative

frac

Value of the [decimal places](javascript:call_link\('abenfractional_portion_glosry.htm'\) "Glossary Entry") of the argument arg; negative if arg is negative

The following applies with regard to the data type of the return value:

-   With the exception of an [arithmetic expression](javascript:call_link\('abenarithmetic_expression_glosry.htm'\) "Glossary Entry"), the data type of the argument determines the data type of the return value.

-   Within an arithmetic expression, the argument of the function contributes to the [calculation type](javascript:call_link\('abencalculation_type_glosry.htm'\) "Glossary Entry") of the entire expression and the function is calculated using the calculation type. If the argument itself is an arithmetic expression, its operands contribute to the entire calculation type and the argument is also calculated with this type.

-   If the argument arg is a [numeric expression](javascript:call_link\('abennumerical_expression_glosry.htm'\) "Glossary Entry"), the function works like an arithmetic operator and it is handled in its operand position like an [arithmetic expression](javascript:call_link\('abenarithmetic_expression_glosry.htm'\) "Glossary Entry").

If the argument of a numeric function outside of an arithmetic expression does not have a numeric data type i, int8, p, decfloat16, decfloat34, or f, its data type determines the type of return value as follows:

-   d and t give i

-   c, n and string give p

-   x and xstring givei

Before the calculation of the function, the argument is [converted](javascript:call_link\('abenconversion_rules.htm'\)) into the respective type..

Note

The functions described here are some of the functions that can be used in the obsolete [extended functional operand positions](javascript:call_link\('abenextended_functional_positions.htm'\)), even if their argument is a single data object.

Example

The following assertion should always be correct.

DATA pack TYPE p LENGTH 8 DECIMALS 4 VALUE '-1234.5678'.
cl\_demo\_input=>request( CHANGING field = pack ).
ASSERT packÂ Â = trunc( pack ) + frac( pack ).

Executable Example

[Numeric Functions](javascript:call_link\('abenmath_func_abexa.htm'\))



**ğŸ“– Source**: [abennumerical_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abennumerical_functions.htm)

### abenpower_function.htm

> **ğŸ“– Official SAP Documentation**: [abenpower_function.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenpower_function.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Numeric Calculations](javascript:call_link\('abencompute_expressions.htm'\))Â â†’Â  [Numeric Functions](javascript:call_link\('abenmathematical_functions.htm'\))Â â†’Â 

ipow - Integer Power Function

Syntax

... ipow( base = arg exp = n ) ...

Effect

This function raises the argument arg passed to base to the exponent n passed to exp. The arguments arg and n are [numeric expression positions](javascript:call_link\('abennumerical_expr_position_glosry.htm'\) "Glossary Entry"). Any [numeric data object](javascript:call_link\('abennumeric_data_object_glosry.htm'\) "Glossary Entry") can be specified for arg. n expects the type i and exponents of other types are [converted](javascript:call_link\('abenconversion_elementary.htm'\)) to i. If the argument arg has the value 0, the value of the exponent n must be greater than or equal to 0.

The function ipow is overloaded with the effect that the return value can have different numeric types.

-   With the exception of an [arithmetic expression](javascript:call_link\('abenarithmetic_expression_glosry.htm'\) "Glossary Entry"), the data type of the argument arg determines the data type of the return value.

-   Within an arithmetic expression, the argument arg contributes to the [calculation type](javascript:call_link\('abencalculation_type_glosry.htm'\) "Glossary Entry") of the entire expression and the function is calculated using the calculation type.

-   If the argument arg is a [numeric expression](javascript:call_link\('abennumerical_expression_glosry.htm'\) "Glossary Entry"), ipow works like an arithmetic operator and the function is handled in its operand position like an [arithmetic expression](javascript:call_link\('abenarithmetic_expression_glosry.htm'\) "Glossary Entry").

This function can be specified in [general](javascript:call_link\('abengeneral_expr_position_glosry.htm'\) "Glossary Entry") and [numeric expression positions](javascript:call_link\('abennumerical_expr_position_glosry.htm'\) "Glossary Entry"). The calculation type is determined from the argument arg in the same way as with the other [numeric functions](javascript:call_link\('abennumerical_functions.htm'\)). The argument n does not have any effect on the data type of the return value.

Notes

-   The power function ipow can replace [arg \*\* n](javascript:call_link\('abenarith_operators.htm'\)) calculations, if the [calculation type](javascript:call_link\('abencalculation_type_glosry.htm'\) "Glossary Entry") f is to be avoided. This makes sense if the type f is not precise enough.

-   In many cases, the power function ipow displays better performance than using the arithmetic operator [\*\*](javascript:call_link\('abenarith_operators.htm'\)).

Example

The results are 1.4399999999999999 for \*\* and 1.4400 for ipow. The result of ipow is more precise.

cl\_demo\_output=>display( |\*\*Â Â : { '1.2' \*\* 2 } \\n| &&
Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  |ipow: { ipow( base = '1.2' exp = 2 ) }| ).

Executable Example

[Power Function ipow](javascript:call_link\('abenipow_function_abexa.htm'\))



**ğŸ“– Source**: [abenpower_function.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenpower_function.htm)

### abennmax_nmin_functions.htm

> **ğŸ“– Official SAP Documentation**: [abennmax_nmin_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abennmax_nmin_functions.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Numeric Calculations](javascript:call_link\('abencompute_expressions.htm'\))Â â†’Â  [Numeric Functions](javascript:call_link\('abenmathematical_functions.htm'\))Â â†’Â 

nmax, nmin - Numeric Extremum Functions

Syntax Forms

... nmax*|*nmin( val1 = arg1 val2 = arg2 *\[*val3 = arg3*\]* ... *\[*val9 = arg9*\]* ) ...

Effect

These functions return the value of the greatest or the least of the arguments passed. A minimum of two arguments, arg1 and arg2, and a maximum of nine arguments must be passed. Here, the optional input parameters val3 to val9 must be filled in ascending order without gaps. The arguments arg1 to arg9 are [numeric expression positions](javascript:call_link\('abennumerical_expr_position_glosry.htm'\) "Glossary Entry").

The following applies to the data type of the return value:

-   Outside of an [arithmetic expression](javascript:call_link\('abenarithmetic_expression_glosry.htm'\) "Glossary Entry"), a [calculation type](javascript:call_link\('abencalculation_type_glosry.htm'\) "Glossary Entry") is determined from all the arguments, and it is used to perform the comparison. The calculation type is determined just like an [arithmetic expression](javascript:call_link\('abenarith_type.htm'\)) and also determines the data type of the return value.

-   In an arithmetic expression, the arguments of the function contribute to the [calculation type](javascript:call_link\('abencalculation_type_glosry.htm'\) "Glossary Entry") of the entire expression and the function is calculated using the calculation type. If an argument itself is an arithmetic expression, its operands contribute to the entire calculation type and the argument is also calculated using this type.

Notes

-   The extremum functions [cmax](javascript:call_link\('abencmax_cmin_functions.htm'\)) and [cmin](javascript:call_link\('abencmax_cmin_functions.htm'\)) can be used to determine character-like extreme values.
    
-   When using two input parameters:
    

result =Â Â nmax*|*nmin( val1 = arg1 val2 = arg2 )

the evaluation of the functions is equivalent to:

IF num1 >= num2 *|* num1 <= num2.
Â Â result = num1.
ELSE.
Â Â result = num2.
ENDIF.

When using more than two input parameters, an equivalent control structure would be more complex.

Example

Determines the lesser of two time stamps. Here, the initial value of the conditional operator [COND](javascript:call_link\('abenconditional_expression_cond.htm'\)) is not regarded as the least value.

CONSTANTS max\_ts TYPE timestamp VALUE 999999999999999.
DATA: ts1 TYPE timestamp,
Â Â Â Â Â Â ts2 TYPE timestamp.
GET TIME STAMP FIELD ts1.
DATA(min\_ts) = nmin( val1 = COND timestamp( WHEN ts1 IS INITIAL
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â THEN max\_ts ELSE ts1 )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  val2 = COND timestamp( WHEN ts2 IS INITIAL
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â THEN max\_ts ELSE ts2 ) ).

Executable Example

[Extremum Functions nmax, nmin](javascript:call_link\('abennmax_nmin_function_abexa.htm'\))



**ğŸ“– Source**: [abennmax_nmin_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abennmax_nmin_functions.htm)

### abenfloating_point_functions.htm

> **ğŸ“– Official SAP Documentation**: [abenfloating_point_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenfloating_point_functions.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Numeric Calculations](javascript:call_link\('abencompute_expressions.htm'\))Â â†’Â  [Numeric Functions](javascript:call_link\('abenmathematical_functions.htm'\))Â â†’Â 

acos, sin, tanh, exp, log,sqrt - Floating Point Functions

The following table shows the floating point functions that expect a [floating point number](javascript:call_link\('abenfloating_point_number_2_glosry.htm'\) "Glossary Entry") as an unnamed argument. Floating point functions are overloaded so that the return code can have the type decfloat34 or f. [Decimal floating point numbers](javascript:call_link\('abendecfloat_glosry.htm'\) "Glossary Entry") are still only possible as arguments of exp, log, log10, and sqrt.

Syntax

... func( arg ) ...

Effect

The argument of a floating point function must be a single data object outside an arithmetic expression and can be an arithmetic expression itself within an arithmetic expression.

Effect of the floating point functions:

Function func

Meaning

Definition Range

acos

arccosine

\[-1,1\], no decfloat34

asin

arcsine

\[-1,1\], no decfloat34

atan

arctangent

\-, no decfloat34

cos

cosine

\-, no decfloat34

sin

sine

\-, no decfloat34

tan

tangent

\-, no decfloat34

cosh

hyperbolic cosine

\-, no decfloat34

sinh

hyperbolic sine

\-, no decfloat34

tanh

hyperbolic tangent

\-, no decfloat34

exp

Exponential function for base e

\[-709, 709\] for type f and \[-14144, 14149\] for type decfloat34

log

Natural logarithm

\> 0

log10

Logarithm to base 10

\> 0

sqrt

Square root

\>= 0

Functions that specify "no decfloat34" cannot currently have the [calculation type](javascript:call_link\('abencalculation_type_glosry.htm'\) "Glossary Entry") decfloat34. If one of these functions is specified in an expression with this calculation type, a syntax error occurs or the exception CX\_SY\_UNSUPPORTED\_FUNCTION is raised.

The following applies to the [floating point arithmetic](javascript:call_link\('abenfloating_point_arith_glosry.htm'\) "Glossary Entry") in which a floating point function is calculated, and to the data type of the return code:

-   If the argument has the type decfloat16 or decfloat34, a floating point function is calculated in decimal floating point arithmetic and the return code has the type decfloat34.

-   If a floating point function is used in an [arithmetic expression](javascript:call_link\('abenarithmetic_expression_glosry.htm'\) "Glossary Entry") whose [calculation type](javascript:call_link\('abencalculation_type_glosry.htm'\) "Glossary Entry") is decfloat34, or that contains a [numeric expression](javascript:call_link\('abennumerical_expression_glosry.htm'\) "Glossary Entry") of type decfloat34 as an argument, it also calculates a return code with the type decfloat34 and the argument is first converted to the data type decfloat34, if necessary.

-   In all other cases, floating point functions use binary floating point arithmetic to calculate a return code with type f and the argument is first converted to the data type f, if necessary.

Functions with a definition range require the value of arg to be within the specified limits. Arguments within the definition ranges are guaranteed to be error-free for the exponential function exp, since the results are then within the [value ranges](javascript:call_link\('abenvalue_range_glosry.htm'\) "Glossary Entry") for [binary](javascript:call_link\('abenbinfloat_glosry.htm'\) "Glossary Entry") or [decimal](javascript:call_link\('abendecfloat_glosry.htm'\) "Glossary Entry") [floating point numbers](javascript:call_link\('abenfloating_point_number_2_glosry.htm'\) "Glossary Entry") in accordance with IEEE-754. For arguments less than -709, the result for binary floating point numbers is (depending on the platform) a subnormal number, 0, or a handleable exception of the class CX\_SY\_ARITHMETIC\_OVERFLOW is raised from a specific value.

The trigonometric functions sin, cos, and tan are defined for any arguments but the results become imprecise if the argument is greater than approximately 100,000,000.

Notes

-   The atan function is undefined for odd number multiples of pi/2, but the definition range of atan is nevertheless restricted since an argument of this function can never contain the precise value of pi/2.

-   The functions described here are some of the functions that can be used in the obsolete [extended functional operand positions](javascript:call_link\('abenextended_functional_positions.htm'\)), even if their argument is a single data object.

Example

The results in the internal table should all have the value 1 or a value very close to this.

DATA itab TYPE TABLE OF f WITH EMPTY KEY.
itab = VALUE #( FOR i = 0 UNTIL i > 64
Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â LET n = CONV f( i / 10 ) IN
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( sin( n ) \*\* 2 + cos( n ) \*\* 2 ) ).
cl\_demo\_output=>display( itab ).

[Exceptions](javascript:call_link\('abenabap_language_exceptions.htm'\))

Handleable Exceptions

CX\_SY\_ARG\_OUT\_OF\_DOMAIN

-   Cause: Invalid call of acos
    Runtime error: COMPUTE\_ACOS\_DOMAIN
    
-   Cause: Invalid call of asin
    Runtime error: COMPUTE\_ASIN\_DOMAIN
    
-   Cause: Invalid call of cos
    Runtime error: COMPUTE\_COS\_DOMAIN
    
-   Cause: Invalid call of log10
    Runtime error: COMPUTE\_LOG10\_ERROR
    
-   Cause: Invalid call of log
    Runtime error: COMPUTE\_LOG\_ERROR
    
-   Cause: Invalid call of sin
    Runtime error: COMPUTE\_SIN\_DOMAIN
    
-   Cause: Invalid call of sqrt
    Runtime error: COMPUTE\_SQRT\_DOMAIN
    
-   Cause: Invalid call of tan
    Runtime error: COMPUTE\_TAN\_DOMAIN



**ğŸ“– Source**: [abenfloating_point_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenfloating_point_functions.htm)

### abendec_floating_point_functions.htm

> **ğŸ“– Official SAP Documentation**: [abendec_floating_point_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abendec_floating_point_functions.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Numeric Calculations](javascript:call_link\('abencompute_expressions.htm'\))Â â†’Â  [Numeric Functions](javascript:call_link\('abenmathematical_functions.htm'\))Â â†’Â 

round, rescale - Rounding Functions

Syntax

... round*|*rescale( val = arg ...Â Â ) ...

Effect

The rounding functions expect a [decimal floating point number](javascript:call_link\('abendecfloat_glosry.htm'\) "Glossary Entry") as a main argument val and additional arguments that describe how this floating point number is handled. The type of the return value of a rounding function is always decfloat34. Within an arithmetic expression, the argument for the decimal floating point number can either be an arithmetic expression or a function. The other arguments must always be specified as numeric data objects.

-   [Rounding Function](#abendec-floating-point-functions-1--------rescaling-function---@ITOC@@ABENDEC_FLOATING_POINT_FUNCTIONS_2)

Note

The class CL\_ABAP\_MATH includes the method NORMALIZE for normalizing a decimal floating point object. The mantissa does not have any closing zeros in a normalized floating point number.

Rounding Function

The rounding function round can be implemented in [operand positions](javascript:call_link\('abenoperands_expressions.htm'\)) using the following syntax:

Syntax

... round( val = arg *{*dec = n*}**|**{*prec = n*}* *\[*mode = m*\]* ) ...

Effect

This function rounds a decimal floating point number declared as an argument for the parameter val. A data object specified for arg is converted to the data type decfloat34 before the function is executed, if necessary.

Either the parameter dec or the parameter prec must be given a value, and rounding must be to either a particular number of decimal places or [precision](javascript:call_link\('abenprecision_glosry.htm'\) "Glossary Entry"):

-   If the parameter dec is given a value, the value entered is rounded to the number of decimal places specified in n and returned. n expects data objects of the type i. The value of these data objects cannot be less than -6144. If a negative value is given, the relevant whole number place is rounded.

-   If the parameter prec is given a value, the value entered is rounded to the [precision](javascript:call_link\('abenprecision_glosry.htm'\) "Glossary Entry") specified in n and returned. n expects data objects of the type i. The value of these data objects must be greater than 0.

A rounding can reduce [scaling](javascript:call_link\('abenscale_glosry.htm'\) "Glossary Entry") and [precision](javascript:call_link\('abenprecision_glosry.htm'\) "Glossary Entry") but cannot increase them. If dec is specified, the mantissa of the return code does not contain any zeroes after the place where the rounding applies. If prec is specified, the input value is returned without being changed, if the specified precision is greater than or equal to the input value.

The parameter mode (optional) can be used to set the rounding type. For m it is only possible to specify values that exist as ROUND\_... constants in class CL\_ABAP\_MATH. The following table shows the possible rounding rules. If mode is not given a value, commercial rounding is used.

Constant

Rounding rule

ROUND\_HALF\_UP

The value is rounded down to the next round figure. If the value falls precisely halfway between two rounded values, it is rounded up away from zero (commercial rounding).

ROUND\_HALF\_DOWN

The value is rounded down to the next round figure. If the value falls precisely halfway between two round values, it is rounded down towards zero.

ROUND\_HALF\_EVEN

The value is rounded down to the next round figure. If the value falls precisely halfway between two rounded values, it is rounded to the value which has an even number in the last place.

ROUND\_UP

The value is always rounded away from zero/to the larger absolute value.

ROUND\_DOWN

The value is always rounded to zero/to the smaller absolute value.

ROUND\_CEILING

The value is always rounded in a positive direction/to the larger value.

ROUND\_FLOOR

The value is always rounded in a positive direction/to the larger value.

Example

The following tables show the results of commercial rounding of the decimal floating point number 1234.56789 (scaling 5, precision 9) with various values for dec and prec. The displayed results are generated by executing the program DEMO\_ROUND\_AND\_RESCALE.

dec

Result

Scaling

Precision

\-5

0E+5

\-5

1

\-4

0E+4

\-4

1

\-3

1E+3

\-3

1

\-2

1.2E+3

\-2

2

\-1

1.23E+3

\-1

3

0

1235

0

4

1

1234.6

1

5

2

1234.57

2

6

3

1234.568

3

7

4

1234.5679

4

8

5

1234.56789

5

9

6

1234.56789

5

9

prec

Result

Scaling

Precision

1

1E+3

\-3

1

2

1.2E+3

\-2

2

3

1.23E+3

\-1

3

4

1235

0

4

5

1234.6

1

5

6

1234.57

2

6

7

1234.568

3

7

8

1234.5679

4

8

9

1234.56789

5

9

10

1234.56789

5

9

Executable Example

[Rounding Function round](javascript:call_link\('abenround_function_abexa.htm'\))

Rescaling Function

The rescaling function rescale can be implemented in [operand positions](javascript:call_link\('abenoperands_expressions.htm'\)) using the following syntax:

Syntax

... rescale( val = arg *{*dec = n*}**|**{*prec = n*}* *\[*mode = m*\]* ) ...

Effect

This function changes the [scaling](javascript:call_link\('abenscale_glosry.htm'\) "Glossary Entry") of a decimal floating point number declared as an argument for the parameter val. A data object specified for arg is converted to the data type decfloat34 before the function is executed, if necessary.

Either the parameter dec or the parameter prec must be given a value, where either the [scaling](javascript:call_link\('abenscale_glosry.htm'\) "Glossary Entry") or the [precision](javascript:call_link\('abenprecision_glosry.htm'\) "Glossary Entry") is set:

-   If the parameter dec is given a value, the value entered is rounded using the [scaling](javascript:call_link\('abenscale_glosry.htm'\) "Glossary Entry") specified in n and returned. n expects data objects of the type i. The value of these data objects cannot be less than -6144. If the scaling produces more than 34 places in the mantissa of the return value, a handleable exception is raised.

-   If the parameter prec is given a value, the value entered is returned with the [precision](javascript:call_link\('abenprecision_glosry.htm'\) "Glossary Entry") specified in n and appropriate [scaling](javascript:call_link\('abenscale_glosry.htm'\) "Glossary Entry") and returned. n expects data objects of the type i. The value of these data objects must be greater than 0 and less than 34.

A rescaling can both reduce and increase [scaling](javascript:call_link\('abenscale_glosry.htm'\) "Glossary Entry") and [precision](javascript:call_link\('abenprecision_glosry.htm'\) "Glossary Entry"). An increase adds zeros on the right.

The input value is rounded if required. The optional parameter mod can be used to specify the rounding rule, as described under the function round. The default is commercial rounding.

Examples

The following tables show the results of rescaling of the decimal floating point number 1234.56789 (scaling 5, precision 9) with various values for dec and prec, if commercial rounding is used. The displayed results are generated by executing the program DEMO\_ROUND\_AND\_RESCALE.

dec

Result

Scaling

Precision

\-5

0E+5

\-5

1

\-4

0E+4

\-4

1

\-3

1E+3

\-3

1

\-2

1.2E+3

\-2

2

\-1

1.23E+3

\-1

3

0

1235

0

4

1

1234.6

1

5

2

1234.57

2

6

3

1234.568

3

7

4

1234.5679

4

8

5

1234.56789

5

9

6

1234.567890

6

10

7

1234.5678900

7

11

8

1234.56789000

8

12

prec

Result

Scaling

Precision

1

1E+3

\-3

1

2

1.2E+3

\-2

2

3

1.23E+3

\-1

3

4

1235

0

4

5

1234.6

1

5

6

1234.57

2

6

7

1234.568

3

7

8

1234.5679

4

8

9

1234.56789

5

9

10

1234.567890

6

10

11

1234.5678900

7

11

12

1234.56789000

8

12



**ğŸ“– Source**: [abendec_floating_point_functions.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abendec_floating_point_functions.htm)

### abennumerical_functions_abexas.htm

> **ğŸ“– Official SAP Documentation**: [abennumerical_functions_abexas.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abennumerical_functions_abexas.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Numeric Calculations](javascript:call_link\('abencompute_expressions.htm'\))Â â†’Â  [Numeric Functions](javascript:call_link\('abenmathematical_functions.htm'\))Â â†’Â 

Examples of Numeric Functions

Continue
![Example](exa.gif "Example") [Numeric Functions](javascript:call_link\('abenmath_func_abexa.htm'\))
![Example](exa.gif "Example") [Integer Power Function ipow](javascript:call_link\('abenipow_function_abexa.htm'\))
![Example](exa.gif "Example") [Extremum Functions nmax, nmin](javascript:call_link\('abennmax_nmin_function_abexa.htm'\))
![Example](exa.gif "Example") [Rounding Function round](javascript:call_link\('abenround_function_abexa.htm'\))



**ğŸ“– Source**: [abennumerical_functions_abexas.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abennumerical_functions_abexas.htm)

### abenmath_func_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenmath_func_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenmath_func_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Numeric Calculations](javascript:call_link\('abencompute_expressions.htm'\))Â â†’Â  [Numeric Functions](javascript:call_link\('abenmathematical_functions.htm'\))Â â†’Â  [Examples of Numeric Functions](javascript:call_link\('abennumerical_functions_abexas.htm'\))Â â†’Â 

Numeric Functions

This example demonstrates the use of general numeric functions.

Source Code

REPORT demo\_numerical\_function.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA n TYPE decfloat16.
Â Â Â Â DATA m TYPE decfloat16 VALUE '-5.55'.
Â Â Â Â DATA(out) = cl\_demo\_output=>new( ).
Â Â Â Â n = abs( m ).
Â Â Â Â out->write( |ABS: { n }| ).
Â Â Â Â n = sign( m ).
Â Â Â Â out->write( |SIGN: { n }| ).
Â Â Â Â n = ceil( m ).
Â Â Â Â out->write( |CEIL: { n }| ).
Â Â Â Â n = floor( m ).
Â Â Â Â out->write( |FLOOR: { n }| ).
Â Â Â Â n = trunc( m ).
Â Â Â Â out->write( |TRUNC: { n }| ).
Â Â Â Â n = frac( m ).
Â Â Â Â out->write( |FRAC: { n }| ).
Â Â Â Â out->display( ).Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

The output of the program shows how each of the general numeric functions affects the number -5.55.



**ğŸ“– Source**: [abenmath_func_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenmath_func_abexa.htm)

### abenipow_function_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenipow_function_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenipow_function_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Numeric Calculations](javascript:call_link\('abencompute_expressions.htm'\))Â â†’Â  [Numeric Functions](javascript:call_link\('abenmathematical_functions.htm'\))Â â†’Â  [Examples of Numeric Functions](javascript:call_link\('abennumerical_functions_abexas.htm'\))Â â†’Â 

Integer Power Function ipow

This example demonstrates the integer power function [ipow](javascript:call_link\('abenpower_function.htm'\)).

Source Code

REPORT demo\_ipow.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA n TYPE i.
Â Â Â Â DATA arg1 TYPE p DECIMALS 1.
Â Â Â Â DATA arg2 TYPE int8.
Â Â Â Â n = 2.
Â Â Â Â arg1 = \`1.2\`.
Â Â Â Â DATA(out) = cl\_demo\_output=>new(
Â Â Â Â Â Â )->write( |\*\*Â Â : { arg1 \*\* n }|
Â Â Â Â Â Â )->write( |ipow: { ipow( base = arg1 exp = n ) }| ).
Â Â Â Â cl\_demo\_output=>line( ).
Â Â Â Â n = 62.
Â Â Â Â arg2 = 2.
Â Â Â Â out->write( |\*\*Â Â : { arg2 \*\* n }|
Â Â Â Â Â Â )->write( |ipow: { ipow( base = arg2 exp = n ) }| ).
Â Â Â Â out->display( ).Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

The program demonstrates that the integer power function ipow can be used to achieve more precise results than with the [arithmetic operator \*\*](javascript:call_link\('abenarith_operators.htm'\)). In the cases shown here, the operator \*\* produces the [calculation type](javascript:call_link\('abencalculation_type_glosry.htm'\) "Glossary Entry") f. If ipow is used, the calculation type is determined by the arguments arg1 and arg2.



**ğŸ“– Source**: [abenipow_function_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenipow_function_abexa.htm)

### abennmax_nmin_function_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abennmax_nmin_function_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abennmax_nmin_function_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Numeric Calculations](javascript:call_link\('abencompute_expressions.htm'\))Â â†’Â  [Numeric Functions](javascript:call_link\('abenmathematical_functions.htm'\))Â â†’Â  [Examples of Numeric Functions](javascript:call_link\('abennumerical_functions_abexas.htm'\))Â â†’Â 

Extremum Functions nmax, nmin

This example demonstrates the extremum functions [nmax](javascript:call_link\('abennmax_nmin_functions.htm'\)) and [nmin](javascript:call_link\('abennmax_nmin_functions.htm'\)).

Source Code

REPORT demo\_nmax\_nmin.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA: a TYPE i VALUE 1,
Â Â Â Â Â Â Â Â Â Â b TYPE i VALUE 0,
Â Â Â Â Â Â Â Â Â Â c TYPE i VALUE 0.
Â Â Â Â cl\_demo\_input=>new(
Â Â Â Â Â Â )->add\_text( \`Parabola:\`
Â Â Â Â Â Â )->add\_field( CHANGING field = a
Â Â Â Â Â Â )->add\_field( CHANGING field = b
Â Â Â Â Â Â )->add\_field( CHANGING field = c )->request( ).
Â Â Â Â IF a = 0.
Â Â Â Â Â Â cl\_demo\_output=>display(
Â Â Â Â Â Â Â Â 'You must enter a non-zero value for a' ).
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â CONSTANTS: xmin TYPE i VALUE -100,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  xmax TYPE i VALUE 100,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  nÂ Â Â Â TYPE i VALUE 20000.
Â Â Â Â DATA: xÂ Â TYPE decfloat34,
Â Â Â Â Â Â Â Â Â Â yÂ Â TYPE decfloat34,
Â Â Â Â Â Â Â Â Â Â y0 TYPE decfloat34.
Â Â Â Â DATAÂ Â Â Â Â Â  txtÂ Â TYPE string.
Â Â Â Â DO n + 1 TIMES.
Â Â Â Â Â Â x = ( xmax - xmin ) / n \* ( sy-index - 1 ) + xmin.
Â Â Â Â Â Â y = a \* x \*\* 2 + b \* x + c.
Â Â Â Â Â Â IF sy-index = 1.
Â Â Â Â Â Â Â Â y0 = y.
Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â IF a > 0.
Â Â Â Â Â Â Â Â Â Â txt = 'Minimum'.
Â Â Â Â Â Â Â Â Â Â y0 = nmin( val1 = y0 val2 = y ).
Â Â Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â Â Â txt = 'Maximum'.
Â Â Â Â Â Â Â Â Â Â y0 = nmax( val1 = y0 val2 = y ).
Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â ENDIF.
Â Â Â Â ENDDO.
Â Â Â Â cl\_demo\_output=>display( |{ txt } value of parabola is: { y0 }| ).Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

This program determines the minimum or maximum value of a parabola opened up or down and whose parameters can be entered on the selection screen.



**ğŸ“– Source**: [abennmax_nmin_function_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abennmax_nmin_function_abexa.htm)

### abenround_function_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenround_function_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenround_function_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Numeric Calculations](javascript:call_link\('abencompute_expressions.htm'\))Â â†’Â  [Numeric Functions](javascript:call_link\('abenmathematical_functions.htm'\))Â â†’Â  [Examples of Numeric Functions](javascript:call_link\('abennumerical_functions_abexas.htm'\))Â â†’Â 

Rounding Function round

This example demonstrates the rounding function [round](javascript:call_link\('abendec_floating_point_functions.htm'\)).

Source Code

REPORT demo\_round.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
Â Â PRIVATE SECTION.
Â Â Â Â CLASS-DATA: BEGIN OF mode,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â value LIKE cl\_abap\_math=>round\_half\_up,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â nameÂ Â TYPE abap\_attrdescr-name,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â END OF mode,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â modes LIKE SORTED TABLE OF mode
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WITH UNIQUE KEY name.
Â Â Â Â CLASS-METHODS get\_modes.
Â Â Â Â TYPES:
Â Â Â Â Â Â BEGIN OF line,
Â Â Â Â Â Â Â Â numberÂ Â Â Â Â Â Â Â Â Â TYPE decfloat34,
Â Â Â Â Â Â Â Â ceilingÂ Â  TYPE decfloat34,
Â Â Â Â Â Â Â Â downÂ Â Â Â Â Â TYPE decfloat34,
Â Â Â Â Â Â Â Â floorÂ Â Â Â  TYPE decfloat34,
Â Â Â Â Â Â Â Â half\_down TYPE decfloat34,
Â Â Â Â Â Â Â Â half\_even TYPE decfloat34,
Â Â Â Â Â Â Â Â half\_upÂ Â  TYPE decfloat34,
Â Â Â Â Â Â Â Â upÂ Â Â Â Â Â Â Â TYPE decfloat34,
Â Â Â Â Â Â END OF line.
Â Â Â Â CLASS-DATA output TYPE TABLE OF line.
Â Â Â Â CLASS-METHODS write\_output IMPORTING VALUE(idx) TYPE i
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  VALUE(col) TYPE i
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  textÂ Â Â Â Â Â  TYPE clike.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA number TYPE decfloat34.
Â Â Â Â cl\_demo\_output=>begin\_section( \`Rounding Function\` ).
Â Â Â Â get\_modes( ).
Â Â Â Â DO 21 TIMES.
Â Â Â Â Â Â number = - ( sy-index - 11 ) / 10.
Â Â Â Â Â Â write\_output(
Â Â Â Â Â Â Â Â idx = sy-index
Â Â Â Â Â Â Â Â col = '1'
Â Â Â Â Â Â Â Â text = |{ number }| ).
Â Â Â Â Â Â LOOP AT modes INTO mode.
Â Â Â Â Â Â Â Â write\_output(
Â Â Â Â Â Â Â Â Â Â idx = sy-index
Â Â Â Â Â Â Â Â Â Â col = sy-tabix + 1
Â Â Â Â Â Â Â Â Â Â text = |{ round( valÂ Â = number
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  decÂ Â = 0
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  mode = mode-value ) }| ).
Â Â Â Â Â Â ENDLOOP.
Â Â Â Â ENDDO.
Â Â Â Â cl\_demo\_output=>display( output ).Â Â ENDMETHOD.
Â Â METHOD get\_modes.
Â Â Â Â DATA: modesÂ Â  TYPE abap\_attrdescr\_tab,
Â Â Â Â Â Â Â Â Â Â modeÂ Â Â Â LIKE LINE OF modes.
Â Â Â Â FIELD-SYMBOLS <mode> LIKE cl\_abap\_math=>round\_half\_up.
Â Â Â Â modes =
Â Â Â Â Â Â CAST cl\_abap\_classdescr(
Â Â Â Â Â Â Â Â Â Â Â Â  cl\_abap\_classdescr=>describe\_by\_name( 'CL\_ABAP\_MATH' )
Â Â Â Â Â Â Â Â Â Â Â Â  )->attributes.
Â Â Â Â DELETE modes WHERE name NP 'ROUND\_\*' OR is\_constant <> 'X'.
Â Â Â Â LOOP AT modes INTO mode.
Â Â Â Â Â Â ASSIGN cl\_abap\_math=>(mode-name) TO <mode>.
Â Â Â Â Â Â demo=>mode-value = <mode>.
Â Â Â Â Â Â demo=>mode-name = mode-name.
Â Â Â Â Â Â INSERT demo=>mode INTO TABLE demo=>modes.
Â Â Â Â ENDLOOP.
Â Â ENDMETHOD.
Â Â METHOD write\_output.
Â Â Â Â ASSIGN output\[ idx \] TO FIELD-SYMBOL(<line>).
Â Â Â Â IF sy-subrc <> 0.
Â Â Â Â Â Â DO.
Â Â Â Â Â Â Â Â APPEND INITIAL LINE TO output ASSIGNING <line>.
Â Â Â Â Â Â Â Â IF sy-tabix = idx.
Â Â Â Â Â Â Â Â Â Â EXIT.
Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â ENDDO.
Â Â Â Â ENDIF.
Â Â Â Â ASSIGN COMPONENT col OF STRUCTURE <line> TO FIELD-SYMBOL(<col>).
Â Â Â Â <col> = text.
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

For fraction numbers between 1 and -1, the program depicts the effect of all rounding types from the class CL\_ABAP\_MATH in the rounding function [round](javascript:call_link\('abendec_floating_point_functions.htm'\)). The program reads the possible rounding methods using RTTI and sorts them by name.



**ğŸ“– Source**: [abenround_function_abexa.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abenround_function_abexa.htm)

### abennumerical_functions_abexas.htm

> **ğŸ“– Official SAP Documentation**: [abennumerical_functions_abexas.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abennumerical_functions_abexas.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ğŸ“– Source**: [abennumerical_functions_abexas.htm](https://help.sap.com/doc/abapdocu_753_index_htm/7.53/en-US/abennumerical_functions_abexas.htm)


Â  

* * *

AS ABAP Release 753, Â©Copyright 2019 SAP AG. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing Internal Data](javascript:call_link\('abenabap_data_working.htm'\))Â â†’Â  [Numeric Calculations](javascript:call_link\('abencompute_expressions.htm'\))Â â†’Â  [Numeric Functions](javascript:call_link\('abenmathematical_functions.htm'\))Â â†’Â 

Examples of Numeric Functions

Continue
![Example](exa.gif "Example") [Numeric Functions](javascript:call_link\('abenmath_func_abexa.htm'\))
![Example](exa.gif "Example") [Integer Power Function ipow](javascript:call_link\('abenipow_function_abexa.htm'\))
![Example](exa.gif "Example") [Extremum Functions nmax, nmin](javascript:call_link\('abennmax_nmin_function_abexa.htm'\))
![Example](exa.gif "Example") [Rounding Function round](javascript:call_link\('abenround_function_abexa.htm'\))
