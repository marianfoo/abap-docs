# ABAP Keyword Documentation / ABAP âˆ’ Reference / ABAP RESTful Programming Model / Behavior Implementations / Handler Class

Included pages: 5



## ğŸ”— Source References

**Base URL**: [ABAP Keyword Documentation 7.54](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abenrpm_handler_class.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenrpm_handler_class.htm)
- [abenhandler_method_modify.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenhandler_method_modify.htm)
- [abenhandler_method_read.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenhandler_method_read.htm)
- [abenhandler_method_lock.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenhandler_method_lock.htm)
- [abenrpm_handling_messages.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenrpm_handling_messages.htm)

**Bundle Contains**: 5 documentation pages
**Version**: ABAP 7.54
**Generated**: 2025-09-01T11:25:45.928Z

---

### abenrpm_handler_class.htm

> **ğŸ“– Official SAP Documentation**: [abenrpm_handler_class.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenrpm_handler_class.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP RESTful Programming Model](javascript:call_link\('abenrestful_abap_programming.htm'\))Â â†’Â  [Behavior Implementations](javascript:call_link\('abenabap_behavior_implementations.htm'\))Â â†’Â 

Handler Class

Syntax

CLASS lcl\_handler\_name DEFINITION
Â Â Â Â Â Â  INHERITING FROM cl\_abap\_behavior\_handler
Â Â Â Â Â Â  *\[*ABSTRACT*\]* *\[*FINAL*\]*.
Â Â PRIVATE SECTION.
Â Â Â Â TYPES ...
Â Â Â Â DATA ...
Â Â Â Â CONSTANTS ...
Â Â Â Â METHODS method\_name FOR MODIFY
Â Â Â Â Â Â *\[*IMPORTING*\]*
Â Â Â Â Â Â Â Â create\_import\_parameter FOR CREATE entity\_name
Â Â Â Â Â Â Â Â create\_import\_parameter FOR CREATE entity\_name\\association
Â Â Â Â Â Â Â Â update\_import\_parameter FOR UPDATE entity\_name
Â Â Â Â Â Â Â Â delete\_import\_parameter FOR DELETE entity\_name
Â Â Â Â Â Â Â Â action\_import\_parameter FOR ACTION entity\_name~action\_name
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â *\[*RESULT action\_export\_parameter*\]*.
Â Â Â Â METHODS method\_name FOR LOCK
Â Â Â Â Â Â *\[*IMPORTING*\]*
Â Â Â Â Â Â Â Â lock\_import\_parameter FOR LOCK entity\_name.
Â Â Â Â METHODS method\_name FOR READ
Â Â Â Â Â Â *\[*IMPORTING*\]*
Â Â Â Â Â Â Â Â read\_import\_parameter FOR READ entity\_name
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â *\[*RESULT read\_export\_parameter*\]*.
ENDCLASS.
CLASS lcl\_handler\_name IMPLEMENTATION.
Â Â METHOD modify\_method\_name.
Â Â  ...
Â Â ENDMETHOD.
Â Â METHOD lock\_method\_name.
Â Â  ...
Â Â ENDMETHOD.
Â Â METHOD read\_method\_name.
Â Â  ...
Â Â ENDMETHOD.
ENDCLASS.

Effect

Within the [behavior pool](javascript:call_link\('abenbehavior_pool_glosry.htm'\) "Glossary Entry"), one or more local handler classes for handling the [behavior](javascript:call_link\('abenbehavior_glosry.htm'\) "Glossary Entry") of the business object are defined. In this interaction phase of the [behavior implementation](javascript:call_link\('abenbehavior_implement_glosry.htm'\) "Glossary Entry") of a business object, the application performs writes and reads that need to be evaluated and whose effects (if without errors) need to be saved to a transactional buffer. Here, it is defined for which operations a method is responsible. In extreme cases, this is the full set of all write (MODIFY) or read (READ) operations permitted by the [behavior definition](javascript:call_link\('abenbehavior_definition_glosry.htm'\) "Glossary Entry").

A handler class is defined implicitly as ABSTRACT and FINAL and derived from the basis class CL\_ABAP\_BEHAVIOR\_HANDLER. There are no special rules for the name lcl\_handler\_name of the handler classes.

To make a reference to the entities entity\_name, the [alias](javascript:call_link\('abenbdl_alias.htm'\)) granted in the behavior definition (if available) is used.

The method name method\_name can be specified freely in handler classes. The category of the method is specified for the FOR clause (FOR MODIFY*|*LOCK*|*READ).

Here, a single operation (also known as a trigger) consists of the combination of an operation (CREATE, UPDATE, DELETE, LOCK, READ, orACTION) with an entity of an entity part (namely an action or association), for example update SalesOrderItem or read SalesOrderItem\\\_Product.

The trigger is associated with a freely specified parameter name (for example, update\_import\_parameter). The trigger is also associated with one or more further parameter names, if the trigger is associated with output data (for example, with the parameter action\_export\_parameter for an action with the addition RESULT or read\_export\_parameter for the operation READ).

The following transactional methods can be implemented in the handler class:

-   [FOR MODIFY](javascript:call_link\('abenhandler_method_modify.htm'\))
    Covers all change operations (CREATE, UPDATE, DELETE, and certain actions specified in the behavior definition) of an entity.

-   [FOR LOCK](javascript:call_link\('abenhandler_method_lock.htm'\))
    Implements entity locks. Based on the [lock properties](javascript:call_link\('abenlate_numbering_glosry.htm'\) "Glossary Entry") defined in the behavior definition.

-   [FOR READ](javascript:call_link\('abenhandler_method_read.htm'\))
    Processes reads.

Note

Exceptions cannot be raised in handler methods. The statement RAISE EXCEPTION produces a [runtime error](javascript:call_link\('abenruntime_error_glosry.htm'\) "Glossary Entry").

Example

In the following example, the data from the ABAP flight data reference scenario (or flight data scenario for short) is used. It represents a legacy business logic that can be used to create and edit flight bookings. The root entity Travel represents the business object for managing flight trips. The underlying data model and the behavior of the root entity Travel are described in [ABAP BDL - Example](javascript:call_link\('abenbdl_example.htm'\)).

The following example shows the definition and the schema of the implementation of the handler class of the business object. The handler class implements the handler that processes:

-   Standard operations create, update, and delete (methods modify\_create, modify\_update, and modify\_delete)

-   Read requests for the entity Travel (method read\_travel)

-   Lock method lock\_travel

-   Application-specific action set\_status\_booked

CLASS lcl\_travel\_handler DEFINITION
Â Â Â Â Â Â INHERITING FROM cl\_abap\_behavior\_handler.
Â Â PRIVATE SECTION.
Â Â Â Â METHODS modify\_createÂ Â FOR MODIFY
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â it\_travel\_createÂ Â Â Â Â Â  FOR CREATE travel.
Â Â Â Â METHODS modify\_updateÂ Â FOR MODIFY
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â it\_travel\_updateÂ Â Â Â Â Â  FOR UPDATE travel.
Â Â Â Â METHODS modify\_deleteÂ Â FOR MODIFY
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â it\_travel\_deleteÂ Â Â Â Â Â  FOR DELETE travel.
Â Â Â Â METHODS set\_statusÂ Â Â Â  FOR MODIFY
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â it\_travel\_set\_status\_booked
Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  FOR ACTION travel~set\_status\_booked
Â Â Â Â Â Â RESULT
Â Â Â Â Â Â Â Â et\_travel\_set\_status\_booked.
Â Â Â Â METHODS lock\_travelÂ Â Â Â FOR LOCK
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â it\_travel\_keyÂ Â Â Â Â Â Â Â Â Â FOR LOCK travel.
Â Â Â Â METHODS read\_travelÂ Â Â Â FOR READ
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â it\_travel\_readÂ Â Â Â Â Â Â Â  FOR READ travel
Â Â Â Â Â Â RESULT
Â Â Â Â Â Â Â Â et\_travel.
ENDCLASS.

CLASS lcl\_travel\_handler IMPLEMENTATION.
Â Â METHOD modify\_create.
Â Â Â Â ...
Â Â ENDMETHOD.
Â Â METHOD modify\_update.
Â Â Â Â ...
Â Â ENDMETHOD.
Â Â METHOD modify\_delete.
Â Â Â Â ...
Â Â ENDMETHOD.
Â Â METHOD set\_status.
Â Â Â Â ...
Â Â ENDMETHOD.
Â Â METHOD read\_travel.
Â Â Â Â ...
Â Â ENDMETHOD.
Â Â METHOD lock\_travel.
Â Â Â Â ...
Â Â ENDMETHOD.
ENDCLASS.

Continue
[FOR MODIFY](javascript:call_link\('abenhandler_method_modify.htm'\))
[FOR READ](javascript:call_link\('abenhandler_method_read.htm'\))
[FOR LOCK](javascript:call_link\('abenhandler_method_lock.htm'\))
[Example - Handling of Messages](javascript:call_link\('abenrpm_handling_messages.htm'\))



**ğŸ“– Source**: [abenrpm_handler_class.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenrpm_handler_class.htm)

### abenhandler_method_modify.htm

> **ğŸ“– Official SAP Documentation**: [abenhandler_method_modify.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenhandler_method_modify.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP RESTful Programming Model](javascript:call_link\('abenrestful_abap_programming.htm'\))Â â†’Â  [Behavior Implementations](javascript:call_link\('abenabap_behavior_implementations.htm'\))Â â†’Â  [Handler Class](javascript:call_link\('abenrpm_handler_class.htm'\))Â â†’Â 

FOR MODIFY

Syntax

METHODS method\_name FOR MODIFY
Â Â *\[*IMPORTING*\]*
Â Â Â Â create\_import\_parameter FOR CREATE entity\_name
Â Â Â Â create\_import\_parameter FOR CREATE entity\_name\\association
Â Â Â Â update\_import\_parameter FOR UPDATE entity\_name
Â Â Â Â delete\_import\_parameter FOR DELETE entity\_name
Â Â Â Â action\_import\_parameter FOR ACTION entity\_name~action
Â Â Â Â Â Â Â Â Â Â \[RESULT action\_export\_parameter\]
Â Â  ...
Â Â  *\[*CHANGINGÂ Â failedÂ Â  TYPE DATA
Â Â Â Â Â Â Â Â Â Â Â Â Â Â mappedÂ Â  TYPE DATA
Â Â Â Â Â Â Â Â Â Â Â Â Â Â reported TYPE DATA*\]*.

Effect

The method MODIFY implements the handler that makes changes to the entities in the context of the ABAP RESTful programming model. This method is called if the application is processing a change request containing at least one of the change operations (CREATE, UPDATE, DELETE, or ACTION) specified in the [behavior definition](javascript:call_link\('abenbehavior_definition_glosry.htm'\) "Glossary Entry"). The command ... parameter IS \[NOT\] INITIAL can be used to determine which operations are actually used.

Like the method [READ](javascript:call_link\('abenhandler_method_read.htm'\)), the method MODIFY is mass-enabled and bundle-enabled (which means it can handle mass requests and can implement multiple operations). Multiple operations in a MODIFY method, however, are not permitted.

No rules apply to the order in which individual operations are processed within a MODIFY method. The application processes all individual passed operations in an appropriate order, for example create operations before update operations.

The method name method\_name can be specified freely. Using this generalization, it is possible to accommodate multiple MODIFY methods in a single [handler class](javascript:call_link\('abenrpm_handler_class.htm'\)). For example, each action can be defined as a method in the same handler class. This enables the behavior implementation of a business object without introducing a corresponding number of handler classes.

entity\_name refers to the name of the entity or the [alias](javascript:call_link\('abenbdl_alias.htm'\)), if one is defined in the behavior definition.

Input Parameters

The name of the input parameter (for example, create\_import\_parameter) can be specified freely.

The row type of the input parameters for the corresponding operations contains the following (the associated [parameters](javascript:call_link\('abencomponents_derived_types.htm'\)) are flagged with "x"):

Operation

ID

%CID

%CID\_REF

%KEY

%PID

%CONTROL

%DATA

%PARAM

CREATE

\-

x

\-

\-

\-

x

\-

\-

UPDATE

\-

\-

x

x

x

x

x

\-

DELETE

x

\-

x

\-

x

\-

\-

\-

ACTION

x

\-

x

\-

x

\-

\-

x

Output Parameters

The name of the output parameter (for example, create\_export\_parameter) can be specified freely.

For an action with the addition RESULT, a named output parameter must be filled. Other operations do not necessarily have visible results. If they do, the results are written implicitly to three return structures (failed, mapped, and reported for errors, mappings, or messages). They can, however, be declared explicitly as [CHANGING](javascript:call_link\('abenchanging_type_data.htm'\)) parameters in the method signature using the generic type DATA:

CHANGING failedÂ Â  TYPE DATA
Â Â Â Â Â Â Â Â  mappedÂ Â  TYPE DATA
Â Â Â Â Â Â Â Â  reported TYPE DATA

The parameters failed, mapped and reported donâ€™t have fixed data types and are assigned with [derived types](javascript:call_link\('abenrpm_derived_types.htm'\)) from the behavior definition instead.

Remarks

-   The old syntax METHODS modify FOR BEHAVIOR ... is also valid but is not recommended.

-   The keyword IMPORTING in the syntax of the method method\_name is not mandatory and can be specified in front of the input parameter.

-   The parameters can also be declared explicitly as REFERENCE (...).

-   A declaration as VALUE (...) is not allowed. This means that the input parameters in a MODIFY method cannot be changed.

Example

In the following example, the data from the ABAP flight data reference scenario (or flight data scenario for short) is used. It represents a legacy business logic that can be used to create and edit flight bookings. The root entity Travel represents the business object for managing flight trips. The underlying data model and the behavior of the root entity Travel are described in [ABAP BDL - Example](javascript:call_link\('abenbdl_example.htm'\)).

The following example shows the definitions and implementations

-   of the standard operations create, update, and delete (for creating, updating, and deleting instances of the entity Travel) and

-   the application-specific action set\_status\_booked (for setting the trip status to booked).

In the example, each of the operations and the action are implemented in the methods modify\_create, modify\_update, modify\_delete, and set\_status. The basic structure of the implementation of the methods is similar. For example, the implementation of the method modify\_create includes

-   a loop for all new instances of the entity Travel to be created,

-   calling the function module of the legacy business logic /DMO/FLIGHT\_TRAVEL\_CREATE for creating new Travel instances, and

-   message handling for processing instance-specific messages if an error occurs (msgty = 'E' for an error, msgty = 'A' for an abortion).

The second step can generate failed keys (ls\_travel\_create-%cid) and messages (lt\_messages). Failed keys are saved in the table failed while all instance-specific messages are saved in the table reported. If successful (lt\_messages IS INITIAL), the content ID (%CID) and the new key travel\_id are written to the table mapped.

For the sake of readability, the methods for handling the messages are implemented in the separate class zcl\_messages. The class zcl\_messages is described in [Example - Handling of Messages](javascript:call_link\('abenrpm_handling_messages.htm'\)).

CLASS lcl\_travel\_handler DEFINITION
Â Â Â Â INHERITING FROM cl\_abap\_behavior\_handler.
Â Â PRIVATE SECTION.
Â Â Â Â METHODS modify\_create FOR MODIFY
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â it\_travel\_createÂ Â Â Â FOR CREATE travel.
Â Â Â Â METHODS modify\_update FOR MODIFY
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â it\_travel\_updateÂ Â Â Â FOR UPDATE travel.
Â Â Â Â METHODS modify\_delete FOR MODIFY
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â it\_travel\_deleteÂ Â Â Â FOR DELETE travel.
Â Â Â Â METHODS set\_statusÂ Â Â Â FOR MODIFY
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â it\_travel\_set\_status\_booked
Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â FOR ACTION travel~set\_status\_booked
Â Â Â Â Â Â RESULT
Â Â Â Â Â Â Â Â et\_travel\_set\_status\_booked.
ENDCLASS.

CLASS lcl\_travel\_handler IMPLEMENTATION.
Â Â METHOD modify\_create.
Â Â Â Â DATA ls\_travel\_out TYPE /dmo/travel.
Â Â Â Â DATA lt\_messagesÂ Â  TYPE /dmo/if\_flight\_legacy=>tt\_message.
Â Â Â Â LOOP AT it\_travel\_create INTO DATA(ls\_travel\_create).
Â Â Â Â Â Â CALL FUNCTION '/DMO/FLIGHT\_TRAVEL\_CREATE'
Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â is\_travelÂ Â  = CORRESPONDING
Â Â Â Â Â Â Â Â Â Â Â Â /dmo/if\_flight\_legacy=>ts\_travel\_in( ls\_travel\_create )
Â Â Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â Â Â es\_travelÂ Â  = ls\_travel\_out
Â Â Â Â Â Â Â Â Â Â et\_messages = lt\_messages.
Â Â Â Â Â Â IF lt\_messages IS NOT INITIAL.
Â Â Â Â Â Â Â Â zcl\_messages=>handle\_messages(
Â Â Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â Â Â iv\_cidÂ Â Â Â Â Â = ls\_travel\_create-%cid
Â Â Â Â Â Â Â Â Â Â Â Â it\_messages = lt\_messages
Â Â Â Â Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â Â Â Â Â failedÂ Â  = failed-travel
Â Â Â Â Â Â Â Â Â Â Â Â reported = reported-travel ).
Â Â Â Â Â Â Â Â CONTINUE.
Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â INSERT VALUE #( %cidÂ Â Â Â = ls\_travel\_create-%cid
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â travel\_id = ls\_travel\_out-travel\_id )
Â Â Â Â Â Â Â Â INTO TABLE mapped-travel.
Â Â Â Â ENDLOOP.
Â Â ENDMETHOD.
Â Â METHOD modify\_update.
Â Â Â Â DATA ls\_travel\_out TYPE /dmo/travel.
Â Â Â Â DATA lt\_messagesÂ Â  TYPE /dmo/if\_flight\_legacy=>tt\_message.
Â Â Â Â LOOP AT it\_travel\_update INTO DATA(ls\_travel\_update).
Â Â Â Â Â Â ls\_travel\_update-travel\_id = COND #(
Â Â Â Â Â Â Â Â WHEN ls\_travel\_update-travel\_id IS INITIAL
Â Â Â Â Â Â Â Â OR ls\_travel\_update-travel\_id = ''
Â Â Â  Â Â Â Â THEN mapped-travel\[ %cid = ls\_travel\_update-%cid\_ref \]-travel\_id
Â Â Â Â Â Â Â Â ELSE ls\_travel\_update-travel\_id ).
Â Â Â Â Â Â DATA(ls\_travelx) = zcl\_messages=>map\_travel\_control(
Â Â Â Â Â Â Â Â is\_travel = ls\_travel\_update ).
Â Â Â Â Â Â CALL FUNCTION '/DMO/FLIGHT\_TRAVEL\_UPDATE'
Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â is\_travelÂ Â  = CORRESPONDING
Â Â Â Â Â Â Â Â Â Â Â Â /dmo/if\_flight\_legacy=>ts\_travel\_in( ls\_travel\_update )
Â Â Â Â Â Â Â Â Â Â is\_travelxÂ Â = ls\_travelx
Â Â Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â Â Â et\_messages = lt\_messages.
Â Â Â Â Â Â LOOP AT lt\_messages INTO DATA(ls\_message)
Â Â Â Â Â Â Â Â Â Â WHERE msgty = 'E' OR msgty = 'A'.
Â Â Â Â Â Â Â Â zcl\_messages=>handle\_messages(
Â Â Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â  Â Â Â Â Â Â iv\_cidÂ Â Â Â Â Â  = ls\_travel\_update-%cid\_ref
Â Â Â Â Â Â Â Â Â Â Â Â iv\_travel\_id = ls\_travel\_update-travel\_id
Â Â Â Â Â Â Â Â Â Â Â Â it\_messagesÂ Â = lt\_messages
Â Â Â Â Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â Â Â Â Â failedÂ Â  = failed-travel
Â Â Â Â Â Â Â Â Â Â Â Â reported = reported-travel ).
Â Â Â Â Â Â ENDLOOP.
Â Â Â Â ENDLOOP.
Â Â ENDMETHOD.
Â Â METHOD modify\_delete.
Â Â Â Â DATA: lt\_messagesÂ Â  TYPE /dmo/if\_flight\_legacy=>tt\_message,
Â Â Â Â Â Â Â Â Â Â ls\_travel\_out TYPE /dmo/travel.
Â Â Â Â LOOP AT it\_travel\_delete INTO DATA(ls\_travel\_delete).
Â Â Â Â Â Â ls\_travel\_delete-travel\_id = COND #(
Â Â Â Â Â Â Â Â WHEN ls\_travel\_delete-travel\_id IS INITIAL
Â Â Â Â Â Â Â Â OR ls\_travel\_delete-travel\_id = ''
Â Â Â  Â Â Â Â THEN mapped-travel\[ %cid = ls\_travel\_delete-%cid\_ref \]-travel\_id
Â Â Â Â Â Â Â Â ELSE ls\_travel\_delete-travel\_id ).
Â Â Â Â Â Â CALL FUNCTION '/DMO/FLIGHT\_TRAVEL\_DELETE'
Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â iv\_travel\_id = ls\_travel\_delete-travel\_id
Â Â Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â Â Â et\_messagesÂ Â = lt\_messages.
Â Â Â Â Â Â zcl\_messages=>handle\_messages(
Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â iv\_cidÂ Â Â Â Â Â  = ls\_travel\_delete-%cid\_ref
Â Â Â Â Â Â Â Â Â Â iv\_travel\_id = ls\_travel\_delete-travel\_id
Â Â Â Â Â Â Â Â Â Â it\_messagesÂ Â = lt\_messages
Â Â Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â Â Â failedÂ Â Â Â Â Â  = failed-travel
Â Â Â Â Â Â Â Â Â Â reportedÂ Â Â Â  = reported-travel ).
Â Â Â Â ENDLOOP.
Â Â ENDMETHOD.
Â Â METHOD set\_status.
Â Â Â Â DATA lt\_messages TYPE /dmo/if\_flight\_legacy=>tt\_message.
Â Â Â Â CLEAR et\_travel\_set\_status\_booked.
Â Â Â Â LOOP AT it\_travel\_set\_status\_booked
Â Â Â Â Â Â Â Â ASSIGNING FIELD-SYMBOL(<fs\_travel\_set\_status\_booked>).
Â Â Â Â Â Â DATA(lv\_travel\_id) =
Â Â Â Â Â Â Â Â <fs\_travel\_set\_status\_booked>-travel\_id.
Â Â Â Â Â Â IF lv\_travel\_id IS INITIAL OR lv\_travel\_id = ''.
Â Â Â Â Â Â Â Â lv\_travel\_id = mapped-travel\[
Â Â Â Â Â Â Â Â Â Â %cid = <fs\_travel\_set\_status\_booked>-%cid\_ref
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â \]-travel\_id.
Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â CALL FUNCTION '/DMO/FLIGHT\_TRAVEL\_SET\_BOOKING'
Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â iv\_travel\_id = lv\_travel\_id
Â Â Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â Â Â et\_messagesÂ Â = lt\_messages.
Â Â Â Â Â Â zcl\_messages=>handle\_messages(
Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â iv\_cidÂ Â Â Â Â Â  = <fs\_travel\_set\_status\_booked>-%cid\_ref
Â Â Â Â Â Â Â Â Â Â iv\_travel\_id = lv\_travel\_id
Â Â Â Â Â Â Â Â Â Â it\_messagesÂ Â = lt\_messages
Â Â Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â Â Â failedÂ Â Â Â Â Â  = failed-travel
Â Â Â Â Â Â Â Â Â Â reportedÂ Â Â Â  = reported-travel ).
Â Â Â Â ENDLOOP.
Â Â ENDMETHOD.
ENDCLASS.



**ğŸ“– Source**: [abenhandler_method_modify.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenhandler_method_modify.htm)

### abenhandler_method_read.htm

> **ğŸ“– Official SAP Documentation**: [abenhandler_method_read.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenhandler_method_read.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP RESTful Programming Model](javascript:call_link\('abenrestful_abap_programming.htm'\))Â â†’Â  [Behavior Implementations](javascript:call_link\('abenabap_behavior_implementations.htm'\))Â â†’Â  [Handler Class](javascript:call_link\('abenrpm_handler_class.htm'\))Â â†’Â 

FOR READ

Syntax

METHODS method\_name FOR READ
Â Â *\[*IMPORTING*\]*
Â Â Â Â  read\_import\_parameterÂ Â  FOR READ entity\_name
Â Â Â Â Â Â  *\[*RESULT read\_export\_parameter*\]*
Â Â Â Â  read\_import\_param\_assoc FOR READ entity\_name\\\_assoc
Â Â Â Â Â Â Â Â FULLÂ Â  read\_export\_param\_full
Â Â Â Â Â Â Â Â RESULT read\_export\_param\_assoc
Â Â Â Â Â Â Â Â LINKÂ Â  read\_export\_param\_link
Â Â Â Â  ...
Â Â *\[*image TYPE if\_abap\_behv=>t\_image*\]*
Â Â ...
Â Â *\[*CHANGING failedÂ Â  TYPE DATA
Â Â Â Â Â Â Â Â Â Â Â Â reported TYPE DATA*\]*.

Effect

The READ method implements the handler that processes read requests for the entities in the context of the ABAP RESTful programming model. The method is used to return data from the application buffer. If the buffer is empty, the data must be read from the database.

The READ method can be called to support handling of an [ETag](javascript:call_link\('abenentity_tag_glosry.htm'\) "Glossary Entry"). A change operation (such as update) is triggered by the if match condition. First the [LOCK](javascript:call_link\('abenhandler_method_lock.htm'\)) method is called. If the LOCK method was successful, the READ method is triggered to request the current ETag value. The structure [%CONTROL](javascript:call_link\('abencomponents_derived_types.htm'\)) requests the ID fields and the ETag attributes. The data is then checked using the if match condition. If the check is passed, the change method is called.

Like [MODIFY](javascript:call_link\('abenhandler_method_modify.htm'\)) methods, the READmethod is mass-enabled and bundle-enabled. This means that it can handle mass requests and implement multiple operations. Multiple operations in a READ method, however, are not permitted.

The method name method\_name is arbitrary. Using this generalization, it is possible to accommodate multiple READ methods in a single [handler class](javascript:call_link\('abenrpm_handler_class.htm'\)). This enables the [behavior implementation](javascript:call_link\('abenbehavior_implement_glosry.htm'\) "Glossary Entry") of a [business object](javascript:call_link\('abenbusiness_object_glosry.htm'\) "Glossary Entry") without introducing a corresponding number of handler classes.

entity\_name refers to the name of the entity or the [alias](javascript:call_link\('abenbdl_alias.htm'\)), if one is defined in the [behavior definition](javascript:call_link\('abenbehavior_definition_glosry.htm'\) "Glossary Entry").

Just like the direct operation read, the operation read-by-association uses a table with instance keys of the source entity as input parameter read\_import\_param\_assoc. The output is as follows:

-   FULL
    Boolean parameter indicating whether the consumer is making a query based only on the links or whether full results are queried.

-   RESULT
    A table with the full row type of the target entity.

-   LINK
    A table in which source keys and target keys are set in relationship with each other.

Input Parameters

The name of the input parameter (for example, read\_import\_parameter) can be specified freely.

The row structure of the input parameter contains the following [components](javascript:call_link\('abencomponents_derived_types.htm'\)):

-   %PID

-   ID fields

-   %CONTROL.

Furthermore, the implicit input parameter image can be declared explicitly as an optional parameter:

Â Â *\[*IMPORTING*\]* image TYPE if\_abap\_behv=>t\_image

The values of the parameter image are as follows:

-   transactional
    This is the parameter's default value. The state of the entities is queried from the transactional buffer.

-   before
    The state of the entities is queried from the database.

Output Parameters

The name of the output parameter (for example, read\_export\_parameter) can be specified freely.

The output parameter read\_export\_parameter, which can be declared explicitly, has the successfully read data as its output. Its row structure contains the following components:

-   %PID

-   All fields of the entity.

The output parameters failed and reported for errors or messages are added implicitly (automatically). They can, however, be declared explicitly as [CHANGING](javascript:call_link\('abenchanging_type_data.htm'\)) parameters in the method signature using the generic type DATA:

CHANGING failedÂ Â  TYPE DATA
Â Â Â Â Â Â Â Â  reported TYPE DATA

The parameters failed and reported donâ€™t have fixed data types and are assigned with [derived types](javascript:call_link\('abenrpm_derived_types.htm'\)) from the behavior definition instead. The parameter mapped is part of the method signature, but it is not used in the method.

Tip

The control structure %CONTROL shows which elements are requested by the consumer. It may, however, be a good idea to return all elements and not just the requested elements.

Remarks

-   The old syntax METHODS read FOR BEHAVIOR ... is also valid but is not recommended.

-   The keyword IMPORTING is not mandatory and can be specified in front of the input parameter.

Example

In the following example, the data from the ABAP flight data reference scenario (or flight data scenario for short) is used. It represents a legacy business logic that can be used to create and edit flight bookings. The root entity Travel represents the business object for managing flight trips. The underlying data model and the behavior of the root entity Travel are described in [ABAP BDL - Example](javascript:call_link\('abenbdl_example.htm'\)).

The following example shows the definition and the implementation of the READ method read\_travel for processing read requests for the entity Travel. The implementation of the method includes

-   a loop for all instances of the entity Travel to be read,

-   calling the function module of the legacy business logic /DMO/FLIGHT\_TRAVEL\_READ for reading Travel instances, and

-   message handling for processing instance-specific messages if an error occurs. For the sake of readability, the methods for handling the messages are implemented in the separate class zcl\_messages. The class zcl\_messages is described in [Example - Handling of Messages](javascript:call_link\('abenrpm_handling_messages.htm'\)).

CLASS lcl\_travel\_handler DEFINITION
Â Â Â Â INHERITING FROM cl\_abap\_behavior\_handler.
Â Â PRIVATE SECTION.
Â Â Â Â METHODS read\_travel FOR READ
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â it\_travel\_read FOR READ travel
Â Â Â Â Â Â RESULT et\_travel.
ENDCLASS.

CLASS lcl\_travel\_handler IMPLEMENTATION.
Â Â METHOD read\_travel.
Â Â Â Â DATA: lt\_messagesÂ Â  TYPE /dmo/if\_flight\_legacy=>tt\_message,
Â Â Â Â Â Â Â Â Â Â ls\_travel\_out TYPE /dmo/travel.
Â Â Â Â LOOP AT it\_travel\_read INTO DATA(ls\_travel\_read).
Â Â Â Â Â Â CALL FUNCTION '/DMO/FLIGHT\_TRAVEL\_READ'
Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â iv\_travel\_id = ls\_travel\_read-travel\_id
Â Â Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â Â Â es\_travelÂ Â Â Â = ls\_travel\_out
Â Â Â Â Â Â Â Â Â Â et\_messagesÂ Â = lt\_messages.
Â Â Â Â Â Â IF lt\_messages IS NOT INITIAL.
Â Â Â Â Â Â Â Â zcl\_messages=>handle\_messages(
Â Â Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â Â Â iv\_travel\_id = ls\_travel\_read-travel\_id
Â Â Â Â Â Â Â Â Â Â Â Â it\_messagesÂ Â = lt\_messages
Â Â Â Â Â Â Â Â Â Â CHANGING
Â Â Â Â Â  Â Â Â Â Â Â failedÂ Â Â Â Â Â  = failed-travel
Â  Â Â Â Â Â Â Â Â Â Â reportedÂ Â Â Â  = reported-travel
Â Â Â Â Â Â Â Â ).
Â Â Â Â Â Â Â Â CONTINUE.
Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â INSERT CORRESPONDING #( ls\_travel\_out ) INTO TABLE et\_travel.
Â Â Â Â ENDLOOP.
Â Â ENDMETHOD.
ENDCLASS.



**ğŸ“– Source**: [abenhandler_method_read.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenhandler_method_read.htm)

### abenhandler_method_lock.htm

> **ğŸ“– Official SAP Documentation**: [abenhandler_method_lock.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenhandler_method_lock.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP RESTful Programming Model](javascript:call_link\('abenrestful_abap_programming.htm'\))Â â†’Â  [Behavior Implementations](javascript:call_link\('abenabap_behavior_implementations.htm'\))Â â†’Â  [Handler Class](javascript:call_link\('abenrpm_handler_class.htm'\))Â â†’Â 

FOR LOCK

Syntax

METHODS method\_name FOR LOCK
Â Â Â Â *\[*IMPORTING*\]*
Â Â Â Â Â Â Â Â lock\_import\_parameter FOR LOCK entity\_name.
Â Â Â Â  ...
Â Â Â Â *\[*CHANGING failedÂ Â  TYPE DATA
Â Â Â Â Â Â Â Â Â Â Â Â Â Â reported TYPE DATA*\]*.

Effect

The LOCK implements the handler that locks entities. However, the [lock properties](javascript:call_link\('abenbdl_lock.htm'\)) lock master or lock dependent must be specified in the [behavior definition](javascript:call_link\('abenbehavior_definition_glosry.htm'\) "Glossary Entry") of a [business object](javascript:call_link\('abenbusiness_object_glosry.htm'\) "Glossary Entry"). The method lock is called automatically before a change operation (for example, update).

The method name method\_name is arbitrary. Using this generalization, it is possible to accommodate multiple LOCK methods in a single [handler class](javascript:call_link\('abenrpm_handler_class.htm'\)).

entity\_name refers to the name of the entity or the [alias](javascript:call_link\('abenbdl_alias.htm'\)), if it exists, as defined in the behavior definition.

Input Parameters

The following points apply to the input parameter lock\_import\_parameter:

-   The name of the input parameter can be freely defined.

-   The row type of the input parameter provides the ID fields. This structure contains the fields of the entity that are specified as keys.

-   The [fields](javascript:call_link\('abencomponents_derived_types.htm'\)) %CID, %CID\_REF, and %PID generated by ABAP Compiler are not required in the context of locks, since locks are only relevant for instances saved in the database.

Output Parameters

The output parameters failed and reported for errors or messages are added automatically. They can, however, be declared explicitly as [CHANGING](javascript:call_link\('abenchanging_type_data.htm'\)) parameters in the method signature using the generic type DATA:

CHANGING failedÂ Â  TYPE DATA
Â Â Â Â Â Â Â Â  reported TYPE DATA

The parameters failed and reported donâ€™t have fixed data types and are assigned with derived types from the behavior definition instead. The parameter mapped is part of the method signature, but it is not used in the method.

Remarks

-   The old syntax METHODS lock FOR BEHAVIOR ... is also valid but is no longer recommended.

-   The keyword IMPORTING is not mandatory and can be specified in front of the input parameter.

Example

In the following example, the data from the ABAP flight data reference scenario (or flight data scenario for short) is used. It represents a legacy business logic that can be used to create and edit flight bookings. The root entity Travel represents the business object for managing flight trips. The underlying data model and the behavior of the root entity Travel are described in [ABAP BDL - Example](javascript:call_link\('abenbdl_example.htm'\)).

The following example shows the definition and implementation of the LOCK method lock\_travel. The implementation of the method includes

-   a loop for all instances of the entity Travel to be locked,

-   calling the function module of the legacy business logic ENQUEUE\_/DMO/ETRAVEL, and

-   message handling for processing instance-specific messages. For the sake of readability, the methods for handling the messages are implemented in the separate class zcl\_messages. The class zcl\_messages is described in [Example - Handling of Messages](javascript:call_link\('abenrpm_handling_messages.htm'\)).

CLASS lcl\_travel\_handler DEFINITION
Â Â Â Â INHERITING FROM cl\_abap\_behavior\_handler.
Â Â PRIVATE SECTION.
Â Â Â Â METHODS lock\_travel FOR LOCK
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â it\_travel\_key FOR LOCK travel.
ENDCLASS.

CLASS lcl\_travel\_handler IMPLEMENTATION.
Â Â METHOD lock\_travel.
Â Â Â Â LOOP AT it\_travel\_key ASSIGNING FIELD-SYMBOL(<fs\_travel>).
Â Â Â Â Â Â CALL FUNCTION 'ENQUEUE\_/DMO/ETRAVEL'
Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â travel\_idÂ Â Â Â Â Â = <fs\_travel>-travel\_id
Â Â Â Â Â Â Â Â EXCEPTIONS
Â Â Â Â Â Â Â Â Â Â foreign\_lockÂ Â  = 1
Â Â Â Â Â Â Â Â Â Â system\_failure = 2
Â Â Â  Â Â Â Â Â Â OTHERSÂ Â Â Â Â Â Â Â  = 3.
Â Â Â Â Â Â ASSERT sy-subrc < 2.
Â Â Â Â Â Â IF sy-subrc = 1.
Â Â Â Â Â Â Â Â INSERT zcl\_messages=>map\_travel\_message(
Â Â Â Â Â Â Â Â Â Â  iv\_travel\_id = <fs\_travel>-travel\_id
Â Â Â Â Â Â Â Â Â Â  is\_messageÂ Â  = VALUE #( msgid = '/DMO/CM\_FLIGHT\_LEGAC'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  msgty = 'E'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  msgno = '032'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  msgv1 = <fs\_travel>-travel\_id
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  msgv2 = sy-msgv1 ) )
Â Â Â Â Â Â Â Â Â Â INTO TABLE reported-travel.
Â Â Â Â Â Â Â Â APPEND VALUE #( travel\_id = <fs\_travel>-travel\_id )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  TO failed-travel.
Â Â Â Â Â Â ENDIF.
Â Â Â Â ENDLOOP.
Â Â ENDMETHOD.
ENDCLASS.



**ğŸ“– Source**: [abenhandler_method_lock.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenhandler_method_lock.htm)

### abenrpm_handling_messages.htm

> **ğŸ“– Official SAP Documentation**: [abenrpm_handling_messages.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenrpm_handling_messages.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ğŸ“– Source**: [abenrpm_handling_messages.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenrpm_handling_messages.htm)


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP RESTful Programming Model](javascript:call_link\('abenrestful_abap_programming.htm'\))Â â†’Â  [Behavior Implementations](javascript:call_link\('abenabap_behavior_implementations.htm'\))Â â†’Â  [Handler Class](javascript:call_link\('abenrpm_handler_class.htm'\))Â â†’Â 

Example - Handling of Messages

Example

For the sake of readability, the methods for handling the messages are implemented in the separate class zcl\_messages. These methods are used to handle the messages in the methods of the handler class.

Messages that come from the legacy business logic must be assigned to the messages of the business object Travel. This is implemented in the method map\_travel\_message.

CLASS zcl\_messages DEFINITION
Â Â PUBLIC
Â Â FINAL
Â Â CREATE PUBLIC.
Â Â PUBLIC SECTION.
Â Â Â Â TYPES:
Â Â Â Â Â Â ts\_travel\_control\_map TYPE STRUCTURE FOR UPDATE /DMO/I\_Travel,
Â Â Â Â Â Â tt\_travel\_reportedÂ Â Â Â TYPE TABLE FOR REPORTEDÂ Â  /DMO/I\_Travel,
Â Â Â Â Â Â tt\_travel\_failedÂ Â Â Â Â Â TYPE TABLE FOR FAILEDÂ Â Â Â  /DMO/I\_Travel.
Â Â Â Â CLASS-METHODS:
Â Â Â Â Â Â handle\_messages
Â Â Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â  Â Â iv\_cidÂ Â Â Â Â Â  TYPE abp\_behv\_cidÂ Â  OPTIONAL
Â  Â Â Â Â Â Â Â Â iv\_travel\_id TYPE /dmo/travel\_id OPTIONAL
Â  Â Â Â Â Â Â Â Â it\_messagesÂ Â TYPE /dmo/if\_flight\_legacy=>tt\_message
Â Â Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â Â Â failedÂ Â Â Â Â Â  TYPE tt\_travel\_failed
Â Â Â Â Â Â Â Â Â Â reportedÂ Â Â Â  TYPE tt\_travel\_reported,
Â Â Â Â Â Â map\_travel\_message
Â Â Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â Â Â iv\_cidÂ Â Â Â Â Â Â Â Â Â  TYPE string OPTIONAL
Â Â Â Â Â Â Â Â Â Â iv\_travel\_idÂ Â Â Â  TYPE /dmo/travel\_id OPTIONAL
Â Â Â Â Â Â Â Â Â Â is\_messageÂ Â Â Â Â Â  TYPE LINE OF
Â Â Â Â Â Â Â Â Â Â Â Â /dmo/if\_flight\_legacy=>tt\_message
Â Â Â Â Â Â Â Â RETURNING VALUE(rs\_report) TYPE LINE OF tt\_travel\_reported,
Â Â Â Â Â Â map\_travel\_control
Â Â Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â Â Â is\_travelÂ Â Â Â Â Â Â Â  TYPE ts\_travel\_control\_map
Â Â Â Â Â Â Â Â RETURNING VALUE(rs\_travelx) TYPE
Â Â Â Â Â Â Â Â Â Â  /dmo/if\_flight\_legacy=>ts\_travel\_inx.
Â Â PROTECTED SECTION.
Â Â PRIVATE SECTION.
Â Â Â Â CLASS-METHODS:
Â Â Â Â Â Â new\_message
Â Â Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â Â Â idÂ Â Â Â Â Â Â Â  TYPE symsgid
Â Â Â Â Â Â Â Â Â Â numberÂ Â Â Â  TYPE symsgno
Â Â Â Â Â Â Â Â Â Â severityÂ Â  TYPE if\_abap\_behv\_message=>t\_severity
Â Â Â Â Â Â Â Â Â Â v1Â Â Â Â Â Â Â Â  TYPE simple OPTIONAL
Â Â Â Â Â Â Â Â Â Â v2Â Â Â Â Â Â Â Â  TYPE simple OPTIONAL
Â Â Â Â Â Â Â Â Â Â v3Â Â Â Â Â Â Â Â  TYPE simple OPTIONAL
Â Â Â Â Â Â Â Â Â Â v4Â Â Â Â Â Â Â Â  TYPE simple OPTIONAL
Â Â Â Â Â Â Â Â RETURNING VALUE(obj) TYPE REF TO if\_abap\_behv\_message.
ENDCLASS.

CLASS zcl\_messages IMPLEMENTATION.
Â Â METHOD handle\_messages.
Â Â Â Â LOOP AT it\_messages INTO DATA(ls\_message)
Â Â Â Â Â Â Â Â  WHERE msgty = 'E' OR msgty = 'A'.
Â Â Â Â Â Â INSERT VALUE #(
Â Â Â Â Â Â Â Â Â Â Â Â  %cidÂ Â Â Â Â Â Â Â  = iv\_cid
Â Â Â Â Â Â Â Â Â Â Â Â  travel\_idÂ Â Â Â = iv\_travel\_id )
Â Â Â Â Â Â Â Â  INTO TABLE failed.
Â Â Â Â Â Â INSERT map\_travel\_message(
Â Â Â Â Â Â Â Â Â Â Â Â  iv\_travel\_id = iv\_travel\_id
Â Â Â Â Â Â Â Â Â Â Â Â  is\_messageÂ Â  = ls\_message )
Â Â Â Â Â Â Â Â  INTO TABLE reported.
Â Â Â Â ENDLOOP.
Â Â ENDMETHOD.
Â Â METHOD map\_travel\_control.
Â Â Â Â rs\_travelx-travel\_idÂ Â Â Â  = is\_travel-travel\_id.
Â Â Â Â rs\_travelx-agency\_idÂ Â Â Â  = xsdbool(
Â Â Â  Â Â is\_travel-%control-agency\_idÂ Â Â Â  = cl\_abap\_behv=>flag\_changed ).
Â Â Â Â rs\_travelx-customer\_idÂ Â  = xsdbool(
Â Â Â Â Â Â is\_travel-%control-customer\_idÂ Â  = cl\_abap\_behv=>flag\_changed ).
Â Â Â Â rs\_travelx-begin\_dateÂ Â Â Â = xsdbool(
Â Â Â  Â Â is\_travel-%control-begin\_dateÂ Â Â Â = cl\_abap\_behv=>flag\_changed ).
Â Â Â Â rs\_travelx-end\_dateÂ Â Â Â Â Â = xsdbool(
Â Â Â Â Â Â is\_travel-%control-end\_dateÂ Â Â Â Â Â = cl\_abap\_behv=>flag\_changed ).
Â Â Â Â rs\_travelx-booking\_feeÂ Â  = xsdbool(
Â Â Â Â Â Â is\_travel-%control-booking\_feeÂ Â  = cl\_abap\_behv=>flag\_changed ).
Â Â Â Â rs\_travelx-total\_priceÂ Â  = xsdbool(
Â Â Â Â Â Â is\_travel-%control-total\_priceÂ Â  = cl\_abap\_behv=>flag\_changed ).
Â Â Â Â rs\_travelx-currency\_code = xsdbool(
Â Â Â Â Â Â is\_travel-%control-currency\_code = cl\_abap\_behv=>flag\_changed ).
Â Â Â Â rs\_travelx-descriptionÂ Â  = xsdbool(
Â Â Â Â Â Â is\_travel-%control-descriptionÂ Â  = cl\_abap\_behv=>flag\_changed ).
Â Â Â Â rs\_travelx-statusÂ Â Â Â Â Â Â Â = xsdbool(
Â Â Â Â Â Â is\_travel-%control-statusÂ Â Â Â Â Â Â Â = cl\_abap\_behv=>flag\_changed ).
ENDMETHOD.
Â Â METHOD map\_travel\_message.
Â Â Â Â DATA(lo) = new\_message(
Â Â Â Â Â Â idÂ Â Â Â Â Â  = is\_message-msgid
Â Â Â Â Â Â numberÂ Â  = is\_message-msgno
Â Â Â Â Â Â severity = if\_abap\_behv\_message=>severity-error
Â Â Â Â Â Â v1Â Â Â Â Â Â  = is\_message-msgv1
Â Â Â Â Â Â v2Â Â Â Â Â Â  = is\_message-msgv2
Â Â Â Â Â Â v3Â Â Â Â Â Â  = is\_message-msgv3
Â Â Â Â Â Â v4Â Â Â Â Â Â  = is\_message-msgv4 ).
Â Â Â Â rs\_report-%cidÂ Â Â Â Â Â = iv\_cid.
Â Â Â Â rs\_report-travel\_id = iv\_travel\_id.
Â Â Â Â rs\_report-%msgÂ Â Â Â Â Â = lo.
Â Â ENDMETHOD.
Â Â METHOD new\_message.
Â Â Â Â obj = NEW lcl\_abap\_behv\_msg(
Â Â Â Â Â Â textid = VALUE #(
Â Â Â Â Â Â Â Â msgid = id
Â Â Â Â Â Â Â Â msgno = number
Â Â Â Â Â Â Â Â attr1 = COND #( WHEN v1 IS NOT INITIAL
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â THEN 'IF\_T100\_DYN\_MSG~MSGV1' )
Â Â Â Â Â Â Â Â attr2 = COND #( WHEN v2 IS NOT INITIAL
Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â THEN 'IF\_T100\_DYN\_MSG~MSGV2' )
Â Â Â Â Â Â Â Â attr3 = COND #( WHEN v3 IS NOT INITIAL
Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â THEN 'IF\_T100\_DYN\_MSG~MSGV3' )
Â Â Â Â Â Â Â Â attr4 = COND #( WHEN v4 IS NOT INITIAL
Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â THEN 'IF\_T100\_DYN\_MSG~MSGV4' ))
Â Â Â Â Â Â msgty = SWITCH #( severity
Â Â Â Â Â Â  Â WHEN if\_abap\_behv\_message=>severity-errorÂ Â Â Â Â Â  THEN 'E'
Â  Â Â Â Â Â Â WHEN if\_abap\_behv\_message=>severity-warningÂ Â Â Â  THEN 'W'
Â Â Â Â Â Â Â Â WHEN if\_abap\_behv\_message=>severity-information THEN 'I'
Â Â Â Â Â Â Â Â WHEN if\_abap\_behv\_message=>severity-successÂ Â Â Â  THEN 'S' )
Â Â Â Â Â Â msgv1 = |{ v1 }|
Â Â Â Â Â Â msgv2 = |{ v2 }|
Â Â Â Â Â Â msgv3 = |{ v3 }|
Â Â Â Â Â Â msgv4 = |{ v4 }| ).
Â Â Â Â obj->m\_severity = severity.
Â Â ENDMETHOD.
ENDCLASS.
