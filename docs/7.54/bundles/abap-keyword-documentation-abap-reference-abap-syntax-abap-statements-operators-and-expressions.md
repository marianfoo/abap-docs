# ABAP Keyword Documentation / ABAP âˆ’ Reference / ABAP Syntax / ABAP Statements / Operators and Expressions

Included pages: 8



## ğŸ”— Source References

**Base URL**: [ABAP Keyword Documentation 7.54](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abenoperators_expressions.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenoperators_expressions.htm)
- [abenoperators.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenoperators.htm)
- [abenexpressions.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenexpressions.htm)
- [abenexpressions_abexas.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenexpressions_abexas.htm)
- [abenjaw_breaker_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenjaw_breaker_abexa.htm)
- [abenmine_sweeper_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenmine_sweeper_abexa.htm)
- [abengame_2048_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abengame_2048_abexa.htm)

**Bundle Contains**: 7 documentation pages
**Version**: ABAP 7.54
**Generated**: 2025-09-01T11:25:45.932Z

---

### abenoperators_expressions.htm

> **ğŸ“– Official SAP Documentation**: [abenoperators_expressions.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenoperators_expressions.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Syntax](javascript:call_link\('abenabap_syntax.htm'\))Â â†’Â  [ABAP Statements](javascript:call_link\('abenabap_statements.htm'\))Â â†’Â 

Operators and Expressions

Normally an operater links two operands with an [expression](javascript:call_link\('abenexpressions.htm'\)) or forms an expression with an operand. During the execution of a statement containing such expressions, the result of the expression is determined (operation) and used in the statement. Operators are represented either by special characters or [reserved indicators](javascript:call_link\('abenabap_words.htm'\)).

-   [Operators](javascript:call_link\('abenoperators.htm'\))

-   [Expressions](javascript:call_link\('abenexpressions.htm'\))

Continue
[Operators](javascript:call_link\('abenoperators.htm'\))
[Expressions](javascript:call_link\('abenexpressions.htm'\))
[Examples of Expression-Oriented Programming](javascript:call_link\('abenexpressions_abexas.htm'\))



**ğŸ“– Source**: [abenoperators_expressions.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenoperators_expressions.htm)

### abenoperators.htm

> **ğŸ“– Official SAP Documentation**: [abenoperators.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenoperators.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Syntax](javascript:call_link\('abenabap_syntax.htm'\))Â â†’Â  [ABAP Statements](javascript:call_link\('abenabap_statements.htm'\))Â â†’Â  [Operators and Expressions](javascript:call_link\('abenoperators_expressions.htm'\))Â â†’Â 

Operators

ABAP contains the following operators:

-   Operators that perform special operations in operand positions:

-   [Declaration Operators](javascript:call_link\('abeninline_declarations.htm'\))
    The declaration operators DATA and FIELD-SYMBOL declare variables or field symbols in operand positions.

-   [Constructor Operators](javascript:call_link\('abenconstructor_expressions.htm'\))
    The constructor operators NEW, VALUE, CONV, CORRESPONDING, CAST, REF, EXACT, REDUCE, FILTER, COND, and SWITCH construct values for data objects of specific data types in constructor expressions.

-   Operators that join multiple operands in a single expression:

-   [Assignment Operators](javascript:call_link\('abenequals_operator.htm'\))
    The assignment operator \= joins the source field and target field of an assignment. In the case of assignments, the special [casting operator](javascript:call_link\('abencasting_operator_glosry.htm'\) "Glossary Entry") ?= performs a [down cast](javascript:call_link\('abendown_cast_glosry.htm'\) "Glossary Entry"). The [addition assignment operator](javascript:call_link\('abenadd_assignment_op_glosry.htm'\) "Glossary Entry") ([+=](javascript:call_link\('abencalculation_assignments.htm'\))), [subtraction assignment operator](javascript:call_link\('abensub_assignment_op_glosry.htm'\) "Glossary Entry") ([\-=](javascript:call_link\('abencalculation_assignments.htm'\))), [multiplication assignment operator](javascript:call_link\('abenmult_assignment_op_glosry.htm'\) "Glossary Entry") ([\*=](javascript:call_link\('abencalculation_assignments.htm'\))), [division assignment operator](javascript:call_link\('abendiv_assignment_op_glosry.htm'\) "Glossary Entry") ([/=](javascript:call_link\('abencalculation_assignments.htm'\))), or [concatenation assignment operator](javascript:call_link\('abenconcat_assignment_op_glosry.htm'\) "Glossary Entry") ([\*\*=](javascript:call_link\('abencalculation_assignment_string.htm'\))) performs a [calculation assignment](javascript:call_link\('abencalculation_assignment_glosry.htm'\) "Glossary Entry"). The operator \= also joins actual parameters with formal parameters, for example in procedure calls or when working with [data clusters](javascript:call_link\('abendata_cluster_glosry.htm'\) "Glossary Entry").

-   [Arithmetic Operators](javascript:call_link\('abapcompute_arith.htm'\))
    The arithmetic operators +, \-, \*, /, DIV, MOD, and \*\* join two or more numeric operands with an arithmetic expression. In addition, the characters + and \- act as signs in arithmetic expressions.

-   [Bit Operators](javascript:call_link\('abapcompute_bit.htm'\))
    The bit operators BIT-AND, BIT-OR, and BIT-XOR join two or more byte-like operands with a bit expression. The bit operator BIT-NOT negates a byte-like operand.

-   [String Operators](javascript:call_link\('abapcompute_string.htm'\))
    The string operator && chains two character-like operands to a string expression.

-   [Relational Operators](javascript:call_link\('abenlogexp.htm'\))
    Relational operators occur in relational expressions, which themselves are components of logical expressions. The [relational operators](javascript:call_link\('abenrelational_operator_glosry.htm'\) "Glossary Entry") \=, <>, <, \>, <=,\>=, BETWEEN, and so on join two or more operands of any data type as a comparison expression. There are additional relational operators for specific data types. The [predicate operator](javascript:call_link\('abenpredicate_operator_glosry.htm'\) "Glossary Entry") IS qualifies an operand.

-   [Boolean Operators](javascript:call_link\('abenlogexp_boole.htm'\))
    The Boolean operators AND, OR, and EQUIV join the results of individual logical expressions with a logical expression. The result of a logical expression is either true or false. The Boolean operator NOT negates the result of a logical expression.

-   Operator that joins two operands in compilations:

-   [Literal Operator](javascript:call_link\('abenliteral_operator.htm'\))
    The literal operator & joins two literals or two string templates with a literal or a string template.



**ğŸ“– Source**: [abenoperators.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenoperators.htm)

### abenexpressions.htm

> **ğŸ“– Official SAP Documentation**: [abenexpressions.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenexpressions.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Syntax](javascript:call_link\('abenabap_syntax.htm'\))Â â†’Â  [ABAP Statements](javascript:call_link\('abenabap_statements.htm'\))Â â†’Â  [Operators and Expressions](javascript:call_link\('abenoperators_expressions.htm'\))Â â†’Â 

Expressions

The following expressions can be constructed in certain [operand positions](javascript:call_link\('abenexpression_positions.htm'\)):

-   Writer positions

-   [Declaration expressions](javascript:call_link\('abeninline_declarations.htm'\))

-   Certain [constructor expressions](javascript:call_link\('abenconstructor_expressions.htm'\))

-   Certain [table expressions](javascript:call_link\('abentable_expressions.htm'\))

-   Reader positions

-   [Logical expressions](javascript:call_link\('abenlogexp.htm'\))

-   [Constructor expressions](javascript:call_link\('abenconstructor_expressions.htm'\))

-   [Table expressions](javascript:call_link\('abentable_expressions.htm'\)) and [mesh path expressions](javascript:call_link\('abenmesh_path_expression.htm'\))

-   [Arithmetic expressions](javascript:call_link\('abapcompute_arith.htm'\))

-   [String expressions](javascript:call_link\('abapcompute_string.htm'\))

-   [Bit expressions](javascript:call_link\('abapcompute_bit.htm'\))

An expression in a [writer position](javascript:call_link\('abenwriting_position_glosry.htm'\) "Glossary Entry") produces a result to which a value is assigned.

An expression in a [reader position](javascript:call_link\('abenreading_position_glosry.htm'\) "Glossary Entry") produces a result that is used as an operand in the statement. Logical expressions can contain [calculation expressions](javascript:call_link\('abencalculation_expression_glosry.htm'\) "Glossary Entry") (arithmetic expressions, string expressions, and bit expressions) as relational expressions. The three different calculation expression types cannot currently be mixed in a single calculation expression.

Notes

-   Although the calculation expression types cannot be mixed directly, a different type of expression can be used as an argument of a permitted function in another expression. In the same way, logical expressions can be used as arguments of [Boolean functions](javascript:call_link\('abenboole_function_glosry.htm'\) "Glossary Entry") in calculation expressions.

-   In [ABAP SQL](javascript:call_link\('abenopen_sql_glosry.htm'\) "Glossary Entry"), three other kinds of expressions are possible:

-   [SQL expressions](javascript:call_link\('abensql_expression_glosry.htm'\) "Glossary Entry")

-   [Aggregate expressions](javascript:call_link\('abenaggregate_expression_glosry.htm'\) "Glossary Entry")

-   [SQL path expressions](javascript:call_link\('abencds_path_expression_glosry.htm'\) "Glossary Entry")

The ABAP expressions above can be used as [host expressions](javascript:call_link\('abenhost_expression_glosry.htm'\) "Glossary Entry") in ABAP SQL.



**ğŸ“– Source**: [abenexpressions.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenexpressions.htm)

### abenexpressions_abexas.htm

> **ğŸ“– Official SAP Documentation**: [abenexpressions_abexas.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenexpressions_abexas.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Syntax](javascript:call_link\('abenabap_syntax.htm'\))Â â†’Â  [ABAP Statements](javascript:call_link\('abenabap_statements.htm'\))Â â†’Â  [Operators and Expressions](javascript:call_link\('abenoperators_expressions.htm'\))Â â†’Â 

Examples of Expression-Oriented Programming

Continue
![Example](exa.gif "Example") [Jawbreaker](javascript:call_link\('abenjaw_breaker_abexa.htm'\))
![Example](exa.gif "Example") [Minesweeper](javascript:call_link\('abenmine_sweeper_abexa.htm'\))
![Example](exa.gif "Example") [2048 Game](javascript:call_link\('abengame_2048_abexa.htm'\))



**ğŸ“– Source**: [abenexpressions_abexas.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenexpressions_abexas.htm)

### abenjaw_breaker_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenjaw_breaker_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenjaw_breaker_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Syntax](javascript:call_link\('abenabap_syntax.htm'\))Â â†’Â  [ABAP Statements](javascript:call_link\('abenabap_statements.htm'\))Â â†’Â  [Operators and Expressions](javascript:call_link\('abenoperators_expressions.htm'\))Â â†’Â  [Examples of Expression-Oriented Programming](javascript:call_link\('abenexpressions_abexas.htm'\))Â â†’Â 

Jawbreaker

Expression-oriented implementation of a Jawbreaker game.

Source Code

PROGRAM demo\_jawbreaker\_html\_740.
CLASS game DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS:
Â Â Â Â Â Â constructor,
Â Â Â Â Â Â display.
Â Â PRIVATE SECTION.
Â Â Â Â TYPES:
Â Â Â Â Â Â t\_vector TYPE STANDARD TABLE OF i WITH EMPTY KEY,
Â Â Â Â Â Â t\_matrix TYPE STANDARD TABLE OF t\_vector WITH EMPTY KEY,
Â Â Â Â Â Â BEGIN OF coord,
Â Â Â Â Â Â Â Â x TYPE i,
Â Â Â Â Â Â Â Â y TYPE i,
Â Â Â Â Â Â END OF coord.
Â Â Â Â DATA:
Â Â Â Â Â Â nÂ Â Â Â Â Â TYPE i,
Â Â Â Â Â Â fieldÂ Â TYPE t\_matrix,
Â Â Â Â Â Â movesÂ Â TYPE i,
Â Â Â Â Â Â filled TYPE i,
Â Â Â Â Â Â colors TYPE TABLE OF string WITH EMPTY KEY,
Â Â Â Â Â Â header TYPE string.
Â Â Â Â METHODS:
Â Â Â Â Â Â at\_click FOR EVENT sapevent OF cl\_abap\_browser IMPORTING action,
Â Â Â Â Â Â delete IMPORTING VALUE(cursor) TYPE coord
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  VALUE(color) TYPE i OPTIONAL,
Â Â Â Â Â Â slide.
ENDCLASS.
CLASS game IMPLEMENTATION.
Â Â METHOD constructor.
Â Â Â Â DATA(wh) = \`width:18px;height:18px\`.
Â Â Â Â colors = VALUE #( ( \`lightblue\`)
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( \`cornflowerblue\`)
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( \`darkblue\`)
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( \`steelblue\`) ).
Â Â Â Â header = \`<html><head><style type="text/css">\` &
Â Â Â Â Â Â Â Â Â Â Â Â  \`.bx{text-decoration:none;cursor:hand;\` &&
Â Â Â Â Â Â Â Â Â Â Â Â  wh && \`} a{\` && wh && \`}\` &&
Â Â Â Â Â Â Â Â Â Â Â Â Â Â REDUCE string(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  INIT s = \`\` FOR co IN colors INDEX INTO ci
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  NEXT s = |{ s }.c{ ci }\\{background-color:{ co }\\}| ) &&
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â \`</style></head><body leftmargin="35" \` &
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â \`topmargin="60" scroll="no">\` &
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â \`<table border="0">\`.
Â Â Â Â DATA(size) = 5.
Â Â Â Â cl\_demo\_input=>request( CHANGING field = size ).
Â Â Â Â n = COND #( WHEN size < 2
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â THEN 2
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WHEN size > 15
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â THEN 15
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ELSE size ).
Â Â Â Â field = VALUE #( LET r = cl\_abap\_random\_int=>create(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  seed = CONV i( sy-uzeit )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  minÂ Â = 1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  max = lines( colors ) ) IN
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  FOR i = 1 UNTIL i > n
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( VALUE #( FOR j = 1 UNTIL j > n
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( r->get\_next( ) ) ) ) ).
Â Â Â Â filled = n \* n.
Â Â Â Â SET HANDLER at\_click.
Â Â ENDMETHOD.
Â Â METHOD display.
Â Â Â Â cl\_abap\_browser=>show\_html(
Â Â Â Â  titleÂ Â Â Â Â Â Â Â = CONV cl\_abap\_browser=>title( sy-title )
Â Â Â Â  sizeÂ Â Â Â Â Â Â Â  = cl\_abap\_browser=>small
Â Â Â Â  formatÂ Â Â Â Â Â  = cl\_abap\_browser=>portrait
Â Â Â Â  context\_menu = ' '
Â Â Â Â  html\_stringÂ Â = COND #( WHEN filled > 0 THEN
Â Â Â Â Â Â  REDUCE string(
Â Â Â Â Â Â Â Â INITÂ Â h = header
Â Â Â Â Â Â Â Â FORÂ Â  y = 1Â Â UNTIL y > n
Â Â Â Â Â Â Â Â NEXTÂ Â h = h && \`<tr>\` &&
Â Â Â Â Â Â Â Â  REDUCE string( INIT k = \`\`
Â Â Â Â Â Â Â Â Â Â FORÂ Â x = 1Â Â UNTIL x > n
Â Â Â Â Â Â Â Â Â Â LETÂ Â c = field\[ x \]\[ y \] IN
Â Â Â Â Â Â Â Â Â Â NEXT k = k &&
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  COND #( WHEN c = 0
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â  THEN \`<td><div class="bx"></div></td>\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ELSE |<td class=c{ c }>| &
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â |<a href="sapevent:| &
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â |x{ x WIDTH = 2
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ALIGN = right
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â PADÂ Â  = '0' }| &
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â |y{ y WIDTH = 2
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ALIGN = right
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â PADÂ Â Â Â = '0' }">| &
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â |<div class="bx"></div></a></td>| ) )
Â Â Â Â Â Â Â Â  && \`</tr>\` )
Â Â Â Â Â Â  && \`</table></body></html>\`
Â Â Â Â Â Â ELSE |Finished in { moves } moves!| )
Â Â Â Â ).
Â Â ENDMETHOD.
Â Â METHOD at\_click.
Â Â Â Â moves = moves + 1.
Â Â Â Â delete( VALUE #( x = CONV i( action+1(2) )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  y = CONV i( action+4(2) ) ) ).
Â Â Â Â slide( ).
Â Â Â Â display( ).
Â Â ENDMETHOD.
Â Â METHOD delete.
Â Â Â Â CHECK cursor-x >= 1 AND cursor-x <= n AND
Â Â Â Â Â Â Â Â Â Â cursor-y >= 1 AND cursor-y <= n.
Â Â Â Â ASSIGN field\[ cursor-x \]\[ cursor-y \] TO FIELD-SYMBOL(<color>).
Â Â Â Â IF color IS NOT SUPPLIED.
Â Â Â Â Â Â color = <color>.
Â Â Â Â ELSEIF <color> <> color OR color = 0.
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â <color> = 0.
Â Â Â Â filled = filled - 1.
Â Â Â Â delete( cursor = VALUE #( x = cursor-x - 1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â y = cursor-y ) color = color ). "left
Â Â Â Â delete( cursor = VALUE #( x = cursor-x + 1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â y = cursor-y ) color = color ). "right
Â Â Â Â delete( cursor = VALUE #( x = cursor-x
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â y = cursor-y - 1 ) color = color ). "up
Â Â Â Â delete( cursor = VALUE #( x = cursor-x
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â y = cursor-y + 1 ) color = color ). "down
Â Â ENDMETHOD.
Â Â METHOD slide.
Â Â Â Â field = VALUE #( LET fld = field IN
Â Â Â Â Â Â FOR <column> IN fld
Â Â Â Â Â Â Â Â LET rest = VALUE t\_vector(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â FOR j IN <column>
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WHERE ( table\_line <> 0 ) ( j ) )
Â Â Â Â Â Â Â Â Â Â Â Â zeroed = n - lines( rest ) IN
Â Â Â Â Â Â Â Â ( VALUE t\_vector( FOR k = 1 UNTIL k > n
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( COND #( WHEN k <= zeroed THEN 0
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ELSE rest\[ k - zeroed \] ) ) ) ) ).
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â NEW game( )->display( ).

Description

This example uses the implementation of a Jawbreaker game to demonstrate expression-oriented ABAP programming (as introduced in Release 7.40). As a comparison, the program DEMO\_JAWBREAKER\_HTML\_700 shows the same function implemented without expressions, mainly using language elements from Release 7.00 only.

To play the game, use the left mouse button to remove adjacent blocks with the same color.



**ğŸ“– Source**: [abenjaw_breaker_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenjaw_breaker_abexa.htm)

### abenmine_sweeper_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenmine_sweeper_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenmine_sweeper_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Syntax](javascript:call_link\('abenabap_syntax.htm'\))Â â†’Â  [ABAP Statements](javascript:call_link\('abenabap_statements.htm'\))Â â†’Â  [Operators and Expressions](javascript:call_link\('abenoperators_expressions.htm'\))Â â†’Â  [Examples of Expression-Oriented Programming](javascript:call_link\('abenexpressions_abexas.htm'\))Â â†’Â 

Minesweeper

Expression-oriented implementation of a Minesweeper game.

Source Code

PROGRAM demo\_minesweeper\_740.
CLASS game DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS:
Â Â Â Â Â Â constructor,
Â Â Â Â Â Â display.
Â Â PRIVATE SECTION.
Â Â Â Â TYPES:
Â Â Â Â Â Â BEGIN OF t\_cell,
Â Â Â Â Â Â Â Â bombÂ Â TYPE abap\_bool,Â Â " cell contains bomb y/n
Â Â Â Â Â Â Â Â bombs TYPE i,Â Â Â Â Â Â Â Â Â Â " # of neighboring bombs
Â Â Â Â Â Â Â Â state TYPE char1,Â Â Â Â Â Â " \[h\]idden, \[v\]isible, \[f\]lagged
Â Â Â Â Â Â END OF t\_cell,
Â Â Â Â Â Â t\_cells TYPE STANDARD TABLE OF t\_cellÂ Â WITH EMPTY KEY,
Â Â Â Â Â Â t\_field TYPE STANDARD TABLE OF t\_cells WITH EMPTY KEY.
Â Â Â Â DATA:
Â Â Â Â Â Â fieldÂ Â TYPE t\_field,
Â Â Â Â Â Â nÂ Â Â Â Â Â TYPE i,Â Â Â Â Â Â Â Â " dimension of field
Â Â Â Â Â Â bombsÂ Â TYPE i,Â Â Â Â Â Â Â Â " # of existing bombs
Â Â Â Â Â Â hidden TYPE i,Â Â Â Â Â Â Â Â " # of hidden cells
Â Â Â Â Â Â flagsÂ Â TYPE i,Â Â Â Â Â Â Â Â " # of flagged cells
Â Â Â Â Â Â movesÂ Â TYPE i,Â Â Â Â Â Â Â Â " # of moves
Â Â Â Â Â Â overÂ Â  TYPE char1,Â Â Â Â " game over: \[w\]in, \[d\]ead
Â Â Â Â Â Â header TYPE string.Â Â  " HTML header string
Â Â Â Â METHODS:
Â Â Â Â Â Â at\_click FOR EVENT sapevent OF cl\_abap\_browser IMPORTING action,
Â Â Â Â Â Â detect IMPORTING VALUE(x) TYPE i VALUE(y) TYPE i.
ENDCLASS.
CLASS game IMPLEMENTATION.
Â Â METHOD constructor.
Â Â Â Â DATA(wh) = \`width:13px;height:18px\`.
Â Â Â Â header = replace(
Â Â Â Â Â Â occ = 0 sub = \`@\` with = \`background-color:\` val =
Â Â Â Â  \`<html><head><style type="text/css">\` &&
Â Â Â Â  \`.bx{text-decoration:none;cursor:hand;\` &&
Â Â Â Â  wh && \`} a{\` && wh && \`}\` &&
Â Â Â Â  \`.hid{@#404080} .flg{@red} .bmb{@black}\` &&
Â Â Â Â  \`.b0{@#e0e0e0} .b1{@lightblue} .b2{@lightgreen} .b3{@orange}\` &&
Â Â Â Â  \`</style>\` &&
Â Â Â Â  \`<script>function setloc(e){window.location=e;}</script>\` &&
Â Â Â Â  \`</head><body scroll="no"><table border="0">\` ) ##NO\_TEXT.
Â Â Â Â DATA(size) = 10.
Â Â Â Â DATA(level) = 3.
Â Â Â Â cl\_demo\_input=>add\_field( CHANGING field = size ).
Â Â Â Â cl\_demo\_input=>request(Â Â  CHANGING field = level ).
Â Â Â Â " size: 2..32
Â Â Â Â nÂ Â Â Â  = nmax( val1 = 2 val2 = nmin( val1 = size val2 = 32 ) ).
Â Â Â Â " level: 1..5
Â Â Â Â level = nmax( val1 = 1 val2 = nmin( val1 = level val2 = 5 ) ).
Â Â Â Â DATA(threshold) = 100 - level \* 10.
Â Â Â Â " place hidden bombs randomly
Â Â Â Â field = VALUE #(
Â Â Â Â Â Â LET r = cl\_abap\_random\_int=>create( seed = CONV i( sy-uzeit )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â minÂ Â = 0
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â maxÂ Â = 99 ) IN
Â Â Â Â Â Â FOR i = 1 UNTIL i > n
Â Â Â Â Â Â ( VALUE #( FOR j = 1 UNTIL j > n
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( state = 'h'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  bomb = COND #( WHEN r->get\_next( ) > threshold
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â THEN 'X' ) ) ) ) ).
Â Â Â Â " compute neighboring-bombs count for each cell, and overall count
Â Â Â Â LOOP AT field ASSIGNING FIELD-SYMBOL(<cells>).
Â Â Â Â Â Â DATA(y) = sy-tabix.
Â Â Â Â Â Â LOOP AT <cells> ASSIGNING FIELD-SYMBOL(<cell>).
Â Â Â Â Â Â Â Â IF <cell>-bomb = 'X'.
Â Â Â Â Â Â Â Â Â Â bombs = bombs + 1.
Â Â Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â Â Â DATA(x) = sy-tabix.
Â Â Â Â Â Â Â Â Â Â <cell>-bombs = REDUCE i(
Â Â Â Â Â Â Â Â Â Â Â Â INIT b = 0
Â Â Â Â Â Â Â Â Â Â Â Â FORÂ Â i = nmax( val1 = 1 val2 = y - 1 )
Â Â Â Â Â Â Â Â Â Â Â Â WHILE i <= nmin( val1 = y + 1 val2 = n )
Â Â Â Â Â Â Â Â Â Â Â Â FORÂ Â j = nmax( val1 = 1 val2 = x - 1 )
Â Â Â Â Â Â Â Â Â Â Â Â WHILE j <= nmin( val1 = x + 1 val2 = n )
Â Â Â Â Â Â Â Â Â Â Â Â LET <f> = field\[ i \]\[ j \] IN
Â Â Â Â Â Â Â Â Â Â Â Â NEXT b = COND #( WHEN <f>-bomb = 'X' THEN b + 1 ELSE b ) ).
Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â ENDLOOP.
Â Â Â Â ENDLOOP.
Â Â Â Â hidden = n \* n.
Â Â Â Â SET HANDLER at\_click.
Â Â ENDMETHOD.
Â Â METHOD display.
Â Â Â Â cl\_abap\_browser=>show\_html(
Â Â Â Â  titleÂ Â Â Â Â Â Â Â = CONV cl\_abap\_browser=>title( sy-title )
Â Â Â Â  sizeÂ Â Â Â Â Â Â Â  = COND #( WHEN n < 20 THEN cl\_abap\_browser=>small
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ELSE cl\_abap\_browser=>medium )
Â Â Â Â  formatÂ Â Â Â Â Â  = cl\_abap\_browser=>portrait
Â Â Â Â  context\_menu = ' '
Â Â Â Â  html\_stringÂ Â =
Â Â Â Â Â Â  REDUCE string(
Â Â Â Â Â Â Â Â INITÂ Â h = header
Â Â Â Â Â Â Â Â FORÂ Â  y = 1 UNTIL y > n
Â Â Â Â Â Â Â Â NEXTÂ Â h = h && |<tr{ COND #( WHEN over <> '' THEN
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â \` onclick="setloc('sapevent:ovr');"\` ) }>| &&
Â Â Â Â Â Â Â Â  REDUCE string(
Â Â Â Â Â Â Â Â Â Â INIT k = \`\`
Â Â Â Â Â Â Â Â Â Â FORÂ Â x = 1Â Â UNTIL x > n
Â Â Â Â Â Â Â Â Â Â LETÂ Â c = field\[ y \]\[ x \]
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  " CSS style (hid,flg,b0,...,b3) of cell
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  style = COND string(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  WHEN over <> '' AND
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â c-bomb = 'X'Â Â " bomb
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  THEN \`bmb\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  WHEN c-state = 'f' " flagged
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  THEN \`flg\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  WHEN c-state = 'v' " visible
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  THEN |b{ nmin( val1 = c-bombs val2 = 3 ) }|
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  WHEN over <> ''Â Â Â Â " empty
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  THEN \`b0\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ELSEÂ Â \`hid\` ) " hidden
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  pos = |x{ x WIDTH = 2 ALIGN = RIGHT PAD = '0' }| &
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  |y{ y WIDTH = 2 ALIGN = RIGHT PAD = '0' }|
Â Â Â Â Â Â Â Â Â Â IN
Â Â Â Â Â Â Â Â Â Â NEXT k = |{ k }<td class={ style }| &&
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  COND #( WHEN c-state = 'v'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â THEN |><a>{ c-bombs }</a>| " bombs value
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ELSE " HTML events on cell (left: try; right: flag)
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  | oncontextmenu="setloc('sapevent:flg{ pos }');| &
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  |return false;"><a href="sapevent:try{ pos }">| &
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  |<div class="bx"></div></a>| )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  && \`</td>\` )
Â Â Â Â Â Â Â Â  && \`</tr>\` )
Â Â Â Â Â Â  && \`</table><br>\`
Â Â Â Â Â Â  && COND #(
Â Â Â Â Â Â Â Â Â Â  WHEN over = 'd' THEN \`\*\*\* BombÂ Â -Â Â Game over \*\*\*\`
Â Â Â Â Â Â Â Â Â Â  WHEN over = 'w' THEN |Finished in { moves } moves!| )
Â Â Â Â Â Â  && \`</body></html>\` ).
Â Â ENDMETHOD.
Â Â METHOD at\_click.
Â Â Â Â IF over <> ''.Â Â " game is over, final click
Â Â Â Â Â Â cl\_abap\_browser=>close\_browser( ).
Â Â Â Â Â Â LEAVE PROGRAM.
Â Â Â Â ENDIF.
Â Â Â Â moves = moves + 1.
Â Â Â Â DATA(x) = CONV i( action+4(2) ).
Â Â Â Â DATA(y) = CONV i( action+7(2) ).
Â Â Â Â ASSIGN field\[ y \]\[ x \] TO FIELD-SYMBOL(<cell>).
Â Â Â Â IF action(3) = 'try'.
Â Â Â Â Â Â IF <cell>-bomb = 'X'.
Â Â Â Â Â Â Â Â over = 'd'.Â Â " hit bomb -> game over
Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â detect( x = x y = y ).
Â Â Â Â Â Â ENDIF.
Â Â Â Â ELSE.Â Â " action(3) = 'flg'
Â Â Â Â Â Â IF <cell>-state = 'h'.
Â Â Â Â Â Â Â Â <cell>-state = 'f'.Â Â flags = flags + 1.Â Â hidden = hidden - 1.
Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â <cell>-state = 'h'.Â Â flags = flags - 1.Â Â hidden = hidden + 1.
Â Â Â Â Â Â ENDIF.
Â Â Â Â ENDIF.
Â Â Â Â IF hidden = 0 AND flags = bombs .
Â Â Â Â Â Â over = 'w'.Â Â " all cells opened, all bombs found -> win
Â Â Â Â ENDIF.
Â Â Â Â display( ).
Â Â ENDMETHOD.
Â Â METHOD detect.
Â Â Â Â CHECK x >= 1 AND x <= n AND y >= 1 AND y <= n.
Â Â Â Â ASSIGN field\[ y \]\[ x \] TO FIELD-SYMBOL(<cell>).
Â Â Â Â CASE <cell>-state.
Â Â Â Â Â Â WHEN 'v'.Â Â RETURN.
Â Â Â Â Â Â WHEN 'h'.Â Â hidden = hidden - 1.
Â Â Â Â Â Â WHEN 'f'.Â Â flags = flags - 1.
Â Â Â Â ENDCASE.
Â Â Â Â <cell>-state = 'v'.
Â Â Â Â CHECK <cell>-bombs = 0.
Â Â Â Â DATA(u) = y - 1.
Â Â Â Â DATA(d) = y + 1.
Â Â Â Â DATA(l) = x - 1.
Â Â Â Â DATA(r) = x + 1.
Â Â Â Â detect( y = u x = l ).
Â Â Â Â detect( y = u x = x ).
Â Â Â Â detect( y = u x = r ).
Â Â Â Â detect( y = y x = l ).
Â Â Â Â detect( y = y x = r ).
Â Â Â Â detect( y = d x = l ).
Â Â Â Â detect( y = d x = x ).
Â Â Â Â detect( y = d x = r ).
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â NEW game( )->display( ).

Description

This example uses the implementation of a Minesweeper game to demonstrate expression-oriented ABAP programming (as introduced in Release 7.40). As a comparison, the program DEMO\_MINESWEEPER\_700 shows the same function implemented without expressions, mainly using language elements from Release 7.00 only.

To play the game, use the left and right mouse buttons to enter or select fields.



**ğŸ“– Source**: [abenmine_sweeper_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenmine_sweeper_abexa.htm)

### abengame_2048_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abengame_2048_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abengame_2048_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Syntax](javascript:call_link\('abenabap_syntax.htm'\))Â â†’Â  [ABAP Statements](javascript:call_link\('abenabap_statements.htm'\))Â â†’Â  [Operators and Expressions](javascript:call_link\('abenoperators_expressions.htm'\))Â â†’Â  [Examples of Expression-Oriented Programming](javascript:call_link\('abenexpressions_abexas.htm'\))Â â†’Â 

2048 Game

Expression-oriented implementation of a 2048 game.

Source Code

PROGRAM demo\_game\_2048\_740.
CLASS game DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS:
Â Â Â Â Â Â constructor,
Â Â Â Â Â Â display.
Â Â PRIVATE SECTION.
Â Â Â Â TYPES:
Â Â Â Â Â Â BEGIN OF t\_cell,
Â Â Â Â Â Â Â Â value TYPE i,
Â Â Â Â Â Â Â Â idÂ Â Â Â TYPE string,
Â Â Â Â Â Â Â Â bornÂ Â TYPE i,
Â Â Â Â Â Â END OF t\_cell,
Â Â Â Â Â Â t\_cellsÂ Â TYPE STANDARD TABLE OF t\_cellÂ Â WITH EMPTY KEY,
Â Â Â Â Â Â t\_fieldÂ Â TYPE STANDARD TABLE OF t\_cells WITH EMPTY KEY,
Â Â Â Â Â Â t\_refÂ Â Â Â TYPE REF TO t\_cell,
Â Â Â Â Â Â t\_refsÂ Â  TYPE STANDARD TABLE OF t\_ref WITH EMPTY KEY.
Â Â Â Â CONSTANTS tile\_colors TYPE string VALUE
Â Â Â Â Â Â \`eee4da ede0c8 f2b179 f59563 f67c5f f65e3b \` &
Â Â Â Â Â Â \`edcf72 f67c5f edc850 edc53f edc22e\` ##NO\_TEXT.
Â Â Â Â DATA:
Â Â Â Â Â Â mark\_new\_tiles TYPE abap\_bool,
Â Â Â Â Â Â rndÂ Â Â Â  TYPE REF TO cl\_abap\_random\_int,
Â Â Â Â Â Â fieldÂ Â  TYPE t\_field,
Â Â Â Â Â Â nÂ Â Â Â Â Â  TYPE i,Â Â Â Â Â Â Â Â Â Â Â Â  " dimension of field
Â Â Â Â Â Â nsqÂ Â Â Â  TYPE i,Â Â Â Â Â Â Â Â Â Â Â Â  " n \* n
Â Â Â Â Â Â targetÂ Â TYPE i VALUE 2048,Â Â " target value
Â Â Â Â Â Â scoreÂ Â  TYPE i,Â Â Â Â Â Â Â Â Â Â Â Â  " current score
Â Â Â Â Â Â movesÂ Â  TYPE i,Â Â Â Â Â Â Â Â Â Â Â Â  " # of moves
Â Â Â Â Â Â headerÂ Â TYPE string,Â Â Â Â Â Â Â Â " HTML header string
Â Â Â Â Â Â overÂ Â Â Â TYPE string,Â Â Â Â Â Â Â Â " game-over message
Â Â Â Â Â Â lineÂ Â Â Â TYPE t\_refs.
Â Â Â Â METHODS:
Â Â Â Â Â Â at\_click FOR EVENT sapevent OF cl\_abap\_browser IMPORTING action,
Â Â Â Â Â Â build\_line IMPORTING VALUE(direction) TYPE char1 VALUE(i) TYPE i,
Â Â Â Â Â Â move\_lines IMPORTING VALUE(direction) TYPE char1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  RETURNING VALUE(valid) TYPE abap\_bool,
Â Â Â Â Â Â move\_lineÂ Â RETURNING VALUE(valid) TYPE abap\_bool,
Â Â Â Â Â Â new\_tilesÂ Â IMPORTING VALUE(factor) TYPE i DEFAULT 1,
Â Â Â Â Â Â check\_game.
ENDCLASS.
CLASS game IMPLEMENTATION.
Â Â METHOD constructor.
Â Â Â Â DATA(size) = 4.
Â Â Â Â cl\_demo\_input=>add\_field( CHANGING field = size ).
Â Â Â Â cl\_demo\_input=>add\_field( CHANGING field = target ).
Â Â Â Â cl\_demo\_input=>add\_field( EXPORTING text = 'Mark new tiles'
Â Â Â Â Â Â as\_checkbox = 'X'Â Â CHANGING field = mark\_new\_tiles ) ##NO\_TEXT.
Â Â Â Â cl\_demo\_input=>request( ).
Â Â Â Â nÂ Â Â Â Â Â = nmax( val1 = 3 val2 = nmin( val1 = size val2 = 8 ) ).
Â Â Â Â nsqÂ Â Â Â = n \* n.
Â Â Â Â " target tile value must be a power of 2, at least 8
Â Â Â Â target = nmax( val1 = 8 val2 = target ).
Â Â Â Â target = ipow( base = 2 exp = ceil( log( target ) / log( 2 ) ) ).
Â Â Â Â rnd = cl\_abap\_random\_int=>create( seed = CONV i( sy-uzeit )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â min = 0Â Â max = nsq - 1 ).
Â Â Â Â field = VALUE #( FOR i = 1 UNTIL i > n
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( VALUE #( FOR j = 1 UNTIL j > n
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( id = |{ i }{ j }| born = -1 ) ) ) ).
Â Â Â Â new\_tiles( 2 ).Â Â " place initial tiles in field
Â Â Â Â " build header with styles for tiles; cell/font sizes depend on n
Â Â Â Â header = REDUCE string( ##NO\_TEXT
Â Â Â Â Â Â LETÂ Â w = 360 / n IN
Â Â Â Â Â Â INIT h =
Â Â Â Â Â Â Â Â \`<html><head><style type="text/css">\` &
Â Â Â Â Â Â Â Â \`.t0{background-color:#cbbdb0}\` &
Â Â Â Â Â Â Â Â \`td{border:1px solid bbada0;color:#776e65\` &
Â Â Â Â Â Â Â Â \`;text-align:center;vertical-align:center\` &
Â Â Â Â Â Â Â Â \`;font-family:'Century Gothic',sans-serif;font-weight:bold\` &&
Â Â Â Â Â Â Â Â |;width:{ w }px;height:{ w }px;font-size:{ w / 2 }px\\}|
Â Â Â Â Â Â FOR i = 1 UNTIL i > 11
Â Â Â Â Â Â NEXT h =
Â Â Â Â Â Â Â Â |{ h }.t{ ipow( base = 2 exp = i ) }\\{| &
Â Â Â Â Â Â Â Â |background-color:#{ segment( valÂ Â  = tile\_colors
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â index = i ) }| &
Â Â Â Â Â Â Â Â |{ COND #( WHEN i >= 3Â Â THEN \`;color:#f9f6f2\` ) }| &
Â Â Â Â Â Â Â Â |{ COND #( WHEN i >= 10 THEN |;font-size:{ w / 4 }px|
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  WHEN i >= 7Â Â THEN |;font-size:{ w / 3 }px| ) }\\}| ).
Â Â Â Â header = header && ##NO\_TEXT
Â Â Â Â  \`div{text-align:center}</style><script type="text/javascript">\` &
Â Â Â Â  \`function okd(e)\` &
Â Â Â Â  \`{c=window.event.keyCode;window.location='sapevent:'+c;}\` &
Â Â Â Â  \`document.onkeydown = okd;</script></head> <body scroll=no \` &
Â Â Â Â  \`style="background-color:#bbada0;color:white;font-size:20pt">\`.
Â Â Â Â SET HANDLER at\_click.
Â Â ENDMETHOD.
Â Â METHOD display.
Â Â Â Â cl\_abap\_browser=>show\_html(
Â Â Â Â  titleÂ Â Â Â Â Â Â Â = |Join the numbers and get to the { target } tile!|
Â Â Â Â  sizeÂ Â Â Â Â Â Â Â  = cl\_abap\_browser=>small
Â Â Â Â  formatÂ Â Â Â Â Â  = cl\_abap\_browser=>portrait
Â Â Â Â  context\_menu = 'X'
Â Â Â Â  html\_stringÂ Â =
Â Â Â Â Â Â  REDUCE string(
Â Â Â Â Â Â Â Â INIT h = |{ header }<p align=right>Score { score }</p>| &
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  |<table align=center>|
Â Â Â Â Â Â Â Â FORÂ Â <cells> IN field
Â Â Â Â Â Â Â Â NEXT h = h && |<tr>| &&
Â Â Â Â Â Â Â Â  REDUCE string(
Â Â Â Â Â Â Â Â Â Â INIT k = \`\`
Â Â Â Â Â Â Â Â Â Â FORÂ Â <c> IN <cells>
Â Â Â Â Â Â Â Â Â Â NEXT k = |{ k }<td class=t{ <c>-value }| &
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  |{ COND #( WHEN <c>-born = moves
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â THEN \` style="border-color:red"\` ) }| &
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  |>{ COND #( WHEN <c>-value = 0 THEN \`&nbsp;\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â  ELSE <c>-value ) }</td>| )
Â Â Â Â Â Â Â Â  && \`</tr>\` )
Â Â Â Â Â Â  && \`</table>\` &&
Â Â Â Â Â Â  COND #( WHEN over = \`\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  THEN \`<div>Use arrow keys to join tiles</div>\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ELSE |<br>{ over }| )
Â Â Â Â Â Â  && \`</body></html>\` ).
Â Â ENDMETHOD.
Â Â METHOD at\_click.
Â Â Â Â DATA(direction) = SWITCH #( actionÂ Â WHEN 37 THEN 'L'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WHEN 38 THEN 'U'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WHEN 39 THEN 'R'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WHEN 40 THEN 'D' ).
Â Â Â Â IF over <> \`\`.Â Â " game is over; leave game with any non-arrow key
Â Â Â Â Â Â IF direction IS INITIAL.
Â Â Â Â Â Â Â Â cl\_abap\_browser=>close\_browser( ).
Â Â Â Â Â Â Â Â LEAVE PROGRAM.
Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â IF move\_lines( direction ).
Â Â Â Â Â Â  moves = moves + 1.
Â Â Â Â Â Â  new\_tiles( ).
Â Â Â Â Â Â  check\_game( ).
Â Â Â Â Â Â  display( ).
Â Â Â Â ENDIF.
Â Â ENDMETHOD.
Â Â METHOD check\_game.
Â Â Â Â CONSTANTS dirs TYPE char4 VALUE 'LRUD'.
Â Â Â Â " find highest tile value and check if an empty cell exists
Â Â Â Â DATA(fill) = REDUCE t\_cell(
Â Â Â Â Â Â INIT m = VALUE #( )
Â Â Â Â Â Â FORÂ Â <cells> IN field
Â Â Â Â Â Â FORÂ Â <c>Â Â Â Â  IN <cells>
Â Â Â Â Â Â NEXT m-value = nmax( val1 = m-value val2 = <c>-value )
Â Â Â Â Â Â Â Â Â Â  m-idÂ Â Â Â = COND #( WHEN <c>-value = 0 THEN \`!\` ELSE m-id ) ).
Â Â Â Â " game is won if target value is reached
Â Â Â Â IF fill-value >= target.
Â Â Â Â Â Â over = |<div>Finished in { moves } moves!</div>|.
Â Â Â Â ELSEIF fill-id <> \`!\`.
Â Â Â Â Â Â " no empty cell -> check if stuck: try move in all 4 directions
Â Â Â Â Â Â DO 4 TIMES.
Â Â Â Â Â Â Â Â DATA(off) = sy-index - 1.
Â Â Â Â Â Â Â Â DO n TIMES.
Â Â Â Â Â Â Â Â Â Â build\_line( direction = dirs+off(1) i = sy-index ).
Â Â Â Â Â Â Â Â Â Â LOOP AT line FROM 2 ASSIGNING FIELD-SYMBOL(<curr>).
Â Â Â Â Â Â Â Â Â Â Â Â IF <curr>->value = line\[ sy-tabix - 1 \]->value.
Â Â Â Â Â Â Â Â Â Â Â Â Â Â RETURN.Â Â " found a possible move; game not over
Â Â Â Â Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â Â Â ENDLOOP.
Â Â Â Â Â Â Â Â ENDDO.
Â Â Â Â Â Â ENDDO.
Â Â Â Â Â Â over = \`<div>\*\*\* StuckÂ Â -Â Â Game over \*\*\*</div>\`.
Â Â Â Â ENDIF.
Â Â ENDMETHOD.
Â Â METHOD move\_lines.
Â Â Â Â CHECK direction IS NOT INITIAL.
Â Â Â Â DO n TIMES.
Â Â Â Â Â Â build\_line( direction = direction i = sy-index ).
Â Â Â Â Â Â valid = cmax( val1 = move\_line( ) val2 = valid ).
Â Â Â Â ENDDO.
Â Â Â Â CLEAR line.
Â Â ENDMETHOD.
Â Â METHOD build\_line.
Â Â Â Â " build cell references to line i, starting at end (wrt direction)
Â Â Â Â line = VALUE #(
Â Â Â Â Â Â FOR j = n THEN j - 1 UNTIL j = 0Â Â " j = n...1
Â Â Â Â Â Â LET y = SWITCH i( direction
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WHEN 'L' OR 'R' THEN i
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WHEN 'D'Â Â Â Â Â Â Â Â THEN j
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WHEN 'U'Â Â Â Â Â Â Â Â THEN n + 1 - j )
Â Â Â Â Â Â Â Â Â Â x = SWITCH i( direction
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WHEN 'U' OR 'D' THEN i
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WHEN 'R'Â Â Â Â Â Â Â Â THEN j
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WHEN 'L'Â Â Â Â Â Â Â Â THEN n + 1 - j )
Â Â Â Â Â Â IN ( REF #( field\[ y \]\[ x \] ) ) ).
Â Â ENDMETHOD.
Â Â METHOD move\_line.
Â Â Â Â DATA: idx TYPE i VALUE 1, next\_idx TYPE i VALUE 2.
Â Â Â Â DATA(curr) = line\[ 1 \].
Â Â Â Â WHILE next\_idx <= n.
Â Â Â Â Â Â DATA(next) = line\[ next\_idx \].
Â Â Â Â Â Â IF curr->value > 0.
Â Â Â Â Â Â Â Â IF curr->value = next->value.
Â Â Â Â Â Â Â Â Â Â curr->value = curr->value \* 2.Â Â " join tiles
Â Â Â Â Â Â Â Â Â Â next->value = 0.
Â Â Â Â Â Â Â Â Â Â valid = abap\_true.
Â Â Â Â Â Â Â Â Â Â score = score + curr->value.
Â Â Â Â Â Â Â Â Â Â idx = idx + 1.Â Â curr = line\[ idx \].Â Â " proceed
Â Â Â Â Â Â Â Â ELSEIF next->value > 0 OR next\_idx = n.
Â Â Â Â Â Â Â Â Â Â idx = idx + 1.Â Â curr = line\[ idx \].Â Â " proceed
Â Â Â Â Â Â Â Â Â Â next\_idx = idx.
Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â ELSEIF next->value <> 0.
Â Â Â Â Â Â Â Â curr->value = next->value.Â Â " shift tile to empty cell
Â Â Â Â Â Â Â Â next->value = 0.
Â Â Â Â Â Â Â Â valid = abap\_true.
Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â next\_idx = next\_idx + 1.
Â Â Â Â ENDWHILE.
Â Â ENDMETHOD.
Â Â METHOD new\_tiles.
Â Â Â Â " place 1 or more (for n>4) new tiles (\* 2 at start of game)
Â Â Â Â DO nmax( val1 = n - 3 val2 = 1 ) \* factor TIMES.
Â Â Â Â Â Â DATA(pos) = rnd->get\_next( ).
Â Â Â Â Â Â DATA(attempts) = nsq.
Â Â Â Â Â Â DO. " try to place new tile at this or next free position
Â Â Â Â Â Â Â Â ASSIGN field\[ 1 + pos DIV n \]\[ 1 + pos MOD n \]
Â Â Â Â Â Â Â Â Â Â TO FIELD-SYMBOL(<cell>).
Â Â Â Â Â Â Â Â IF <cell>-value = 0.
Â Â Â Â Â Â Â Â Â Â " free position found - tile value: 2 (likelihood 75%) or 4
Â Â Â Â Â Â Â Â Â Â <cell>-value = COND #( WHEN rnd->get\_next( ) > nsq / 4
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  THEN 2 ELSE 4 ).
Â Â Â Â Â Â Â Â Â Â " remember when tile was born, to display it as 'new'
Â Â Â Â Â Â Â Â Â Â <cell>-born = COND #( WHEN moves > 0 AND
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  mark\_new\_tiles = abap\_true
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â THEN moves ELSE -1 ).
Â Â Â Â Â Â Â Â Â Â EXIT.
Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â pos = ( pos + 1 ) MOD nsq.
Â Â Â Â Â Â Â Â attempts = attempts - 1.
Â Â Â Â Â Â Â Â IF attempts = 0.
Â Â Â Â Â Â Â Â Â Â RETURN.
Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â ENDDO.
Â Â Â Â ENDDO.
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â NEW game( )->display( ).

Description

This example uses the implementation of a 2048 game to demonstrate expression-oriented ABAP programming (as introduced in Release 7.40). As a comparison, the program DEMO\_GAME\_2048\_700 shows the same function implemented without expressions, mainly using language elements from Release 7.00 only.

To play the game, use the arrow keys on the keyboard to move all tiles in one of the four directions.



**ğŸ“– Source**: [abengame_2048_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abengame_2048_abexa.htm)

### abenexpressions_abexas.htm

> **ğŸ“– Official SAP Documentation**: [abenexpressions_abexas.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenexpressions_abexas.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ğŸ“– Source**: [abenexpressions_abexas.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenexpressions_abexas.htm)


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Syntax](javascript:call_link\('abenabap_syntax.htm'\))Â â†’Â  [ABAP Statements](javascript:call_link\('abenabap_statements.htm'\))Â â†’Â  [Operators and Expressions](javascript:call_link\('abenoperators_expressions.htm'\))Â â†’Â 

Examples of Expression-Oriented Programming

Continue
![Example](exa.gif "Example") [Jawbreaker](javascript:call_link\('abenjaw_breaker_abexa.htm'\))
![Example](exa.gif "Example") [Minesweeper](javascript:call_link\('abenmine_sweeper_abexa.htm'\))
![Example](exa.gif "Example") [2048 Game](javascript:call_link\('abengame_2048_abexa.htm'\))
