# ABAP Keyword Documentation / ABAP âˆ’ Reference / Data Interfaces and Communication Interfaces / ABAP Channels / APC - ABAP Push Channels / Examples of APC

Included pages: 7



## ðŸ”— Source References

**Base URL**: [ABAP Keyword Documentation 7.54](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abenapc_abexas.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_abexas.htm)
- [abenapc_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_abexa.htm)
- [abenapc_ws_client_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_ws_client_abexa.htm)
- [abenapc_tcp_client_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_tcp_client_abexa.htm)
- [abenapc_detached_client_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_detached_client_abexa.htm)
- [abenapc_attached_client_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_attached_client_abexa.htm)
- [abenapc_connect_via_handle_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_connect_via_handle_abexa.htm)

**Bundle Contains**: 7 documentation pages
**Version**: ABAP 7.54
**Generated**: 2025-09-01T11:25:45.943Z

---

### abenapc_abexas.htm

> **ðŸ“– Official SAP Documentation**: [abenapc_abexas.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_abexas.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Data Interfaces and Communication Interfaces](javascript:call_link\('abenabap_data_communication.htm'\))Â â†’Â  [ABAP Channels](javascript:call_link\('abenabap_channels.htm'\))Â â†’Â  [APC - ABAP Push Channels](javascript:call_link\('abenapc.htm'\))Â â†’Â 

Examples of APC

Continue
![Example](exa.gif "Example") [APC, AS ABAP as WebSocket Server](javascript:call_link\('abenapc_abexa.htm'\))
![Example](exa.gif "Example") [APC, AS ABAP as WebSocket Client](javascript:call_link\('abenapc_ws_client_abexa.htm'\))
![Example](exa.gif "Example") [APC, AS ABAP as TCP Socket Client](javascript:call_link\('abenapc_tcp_client_abexa.htm'\))
![Example](exa.gif "Example") [APC, Creating a Detached Client](javascript:call_link\('abenapc_detached_client_abexa.htm'\))
![Example](exa.gif "Example") [APC, AS ABAP as Attached Client](javascript:call_link\('abenapc_attached_client_abexa.htm'\))
![Example](exa.gif "Example") [APC, System-Wide Access](javascript:call_link\('abenapc_connect_via_handle_abexa.htm'\))



**ðŸ“– Source**: [abenapc_abexas.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_abexas.htm)

### abenapc_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenapc_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Data Interfaces and Communication Interfaces](javascript:call_link\('abenabap_data_communication.htm'\))Â â†’Â  [ABAP Channels](javascript:call_link\('abenabap_channels.htm'\))Â â†’Â  [APC - ABAP Push Channels](javascript:call_link\('abenapc.htm'\))Â â†’Â  [Examples of APC](javascript:call_link\('abenapc_abexas.htm'\))Â â†’Â 

APC, AS ABAP as WebSocket Server

This example demonstrates [ABAP push channel](javascript:call_link\('abenapc_glosry.htm'\) "Glossary Entry")s in AS ABAP as a WebSocket server.

Source Code

REPORT demo\_apc\_wsp.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
Â Â Â Â CLASS-METHODS class\_constructor.
Â Â PRIVATE SECTION.
Â Â Â Â CLASS-DATA url TYPE string.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â IF url IS INITIAL.
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â DATA amc TYPE abap\_bool VALUE 'X'.
Â Â Â Â cl\_demo\_input=>add\_field( EXPORTING as\_checkbox = 'X'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â CHANGING fieldÂ Â = amc ).
Â Â Â Â cl\_demo\_input=>add\_line( ).
Â Â Â Â DATA pcp TYPE abap\_bool VALUE ' '.
Â Â Â Â cl\_demo\_input=>add\_field( EXPORTING as\_checkbox = 'X'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â CHANGING fieldÂ Â = pcp ).
Â Â Â Â DATA pcp\_stateful TYPE abap\_bool VALUE ' '.
Â Â Â Â cl\_demo\_input=>add\_field( EXPORTING as\_checkbox = 'X'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â CHANGING fieldÂ Â = pcp\_stateful ).
Â Â Â Â cl\_demo\_input=>request( ).
Â Â Â Â amc = to\_upper( amc ).
Â Â Â Â cl\_demo\_output=>display\_html(
Â Â Â Â Â Â |<html>| &&
Â Â Â Â Â Â |<body>| &&
Â Â Â Â Â Â |<a href="{ COND string( WHEN pcp IS INITIAL AND
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â pcp\_stateful IS INITIAL THEN url
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ELSE url && \`\_pcp\` )
Â Â Â Â Â Â Â Â }?sap-client={ sy-mandt
Â Â Â Â Â Â Â Â }&sap-language={ cl\_i18n\_languages=>sap1\_to\_sap2( sy-langu )
Â Â Â Â Â Â Â Â }{ COND string( WHEN amc = abap\_true
Â Â Â Â Â Â Â Â Â Â Â Â  THEN \`&amc=x\`
Â Â Â Â Â Â Â Â Â Â Â Â  ELSE \`\` )
Â Â Â Â Â Â Â Â }{ COND string( WHEN pcp\_stateful = abap\_true
Â Â Â Â Â Â Â Â Â Â Â Â  THEN \`&pcp\_stateful=x\`
Â Â Â Â Â Â Â Â Â Â Â Â  ELSE \`\` )
Â Â Â Â Â Â Â Â }" target="\_blank">| &&
Â Â Â Â Â Â Â Â |Open demo web page with standard browser</a><br><br>| &&
Â Â Â Â Â Â |If your browser does not support WebSocket, | &&
Â Â Â Â Â Â |copy the URL to another browser.| &&
Â Â Â Â Â Â |</body>| &&
Â Â Â Â Â Â |</html>| ).Â Â ENDMETHOD.
Â Â METHOD class\_constructor.
Â Â Â Â CONSTANTS path TYPE string VALUE \`/sap/bc/abap/demo\_apc\`.
Â Â Â Â DATA(location) =
Â Â Â Â Â Â cl\_http\_server=>get\_location( application = path ).
Â Â Â Â IF location IS NOT INITIAL.
Â Â Â Â Â Â url = location && path.
Â Â Â Â ENDIF.
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

This example demonstrates bidirectional communication between a browser that supports the WebSocket protocol as an APC client and AS ABAP as an APC server. The example program above has the task of displaying a webpage that communicates with the current AS ABAP. It is possible to specify the following:

-   Is the APC used associated with an [ABAP messaging channel (AMC)](javascript:call_link\('abenamc.htm'\))?

-   Is an APC used together with the [Push Channel Protocol (PCP)](javascript:call_link\('abenpush_channel_protocol_glosry.htm'\) "Glossary Entry") or is an APC used without this protocol?

-   Is a stateful service used when the subprotocol PCP is used?

The Web page is provided by an [ICF](javascript:call_link\('abenicf.htm'\)) HTTP service whose URL is constructed in the program for the current AS Instance. If the standard browser called does not support communication using the WebSocket protocol, you must open the URL using a suitable browser instead.

The communication partners are:

-   The Web pages generated in the HTTP request handler CL\_HTTP\_EXT\_APC\_DEMO or CL\_HTTP\_EXT\_APC\_PCP\_DEMO. One Web page makes it possible to open a WebSocket object without Push Channel Protocol (PCP) support and the other Web page makes it possible to open a WebSocket object with the support of this protocol. Both Web pages make it possible to send messages using the object and to close the object. These tasks are performed by a JavaScript function implemented in the Web page. If PCP is used, a JavaScript file sap-pcp-websocket.js is included from the Mime repository in AS ABAP. This file provides the client-side framework for using this protocol. This API is not needed if PCP is not used. If the PCP protocol is used, it is possible to access both a stateless ABAP channel and a stateful channel.

-   The ABAP push channel DEMO\_APC, DEMO\_APC\_PCP, or DEMO\_APC\_PCP\_STATEFUL from the package SABAPDEMOS in the current AS ABAP (with the latter two supporting the Push Channel Protocol (PCP) and the latter being stateful). The URL of the service assigned to the push channel is used in the JavaScript function of the associated Web page to open the WebSocket communication of this Web page. The form field amc is passed further. The APC handler classes CL\_APC\_WS\_EXT\_DEMO\_APC and CL\_APC\_WSP\_EXT\_DEMO\_APC\_PCP implement the methods ON\_START and ON\_MESSAGE of the interface IF\_APC\_WSP\_EXTENSION for communication with and without a connection to an [AMC](javascript:call_link\('abenamc.htm'\)). The APC handler class CL\_APC\_WSP\_EXT\_DEMO\_APC\_PCP\_ST demonstrates a stateful service by simply calling the identically named methods of an object of the class CL\_APC\_WSP\_EXT\_DEMO\_APC\_PCP. The AMC used here is /demo\_text or /demo\_pcp from the [executable examples](javascript:call_link\('abenamc_abexas.htm'\)) for AMC. When a message is received, the method ON\_MESSAGE creates a new message with information about the received message and sends it. If PCP is not used, a text message is sent. In the version for PCP, this protocol is used. CL\_APC\_WSP\_EXT\_DEMO\_APC\_PCP demonstrates the stateful service by raising a counter whose value stays the same in stateless services.

If the ABAP push channel is opened using the form field amc=x, communication takes place using AMC. Each Web page bound to the push channel receives every message sent using AMC. In the example, this means that the message sent by ON\_MESSAGE is not just received by the Web page that sent a message. Furthermore, the Web pages bound to the push channel receive messages sent, for example, using the program DEMO\_SEND\_AMC from the executable example [Sending AMC Messages](javascript:call_link\('abenamc_send_abexa.htm'\)). Conversely, the program DEMO\_RECEIVE\_AMC receives the messages sent from the Web pages from the executable example [Receiving AMC Messages](javascript:call_link\('abenamc_receive_abexa.htm'\)).

Note

Before this example can work, the protocols WEBSOCKET and WEBSOCKETS must be specified as active services in the Services function in the transaction SMICM.



**ðŸ“– Source**: [abenapc_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_abexa.htm)

### abenapc_ws_client_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenapc_ws_client_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_ws_client_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Data Interfaces and Communication Interfaces](javascript:call_link\('abenabap_data_communication.htm'\))Â â†’Â  [ABAP Channels](javascript:call_link\('abenabap_channels.htm'\))Â â†’Â  [APC - ABAP Push Channels](javascript:call_link\('abenapc.htm'\))Â â†’Â  [Examples of APC](javascript:call_link\('abenapc_abexas.htm'\))Â â†’Â 

APC, AS ABAP as WebSocket Client

This example demonstrates AS ABAP as a WebSocket client.

Source Code

REPORT demo\_apc\_client.
CLASS apc\_handler DEFINITION FINAL.
Â Â PUBLIC SECTION.
Â Â Â Â INTERFACES if\_apc\_wsp\_event\_handler\_pcp.
Â Â Â Â DATAÂ Â Â Â Â Â  message TYPE string.
ENDCLASS.
CLASS apc\_handler IMPLEMENTATION.
Â Â METHOD if\_apc\_wsp\_event\_handler\_pcp~on\_open.
Â Â ENDMETHOD.
Â Â METHOD if\_apc\_wsp\_event\_handler\_pcp~on\_message.
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â me->message = i\_message->get\_text( ).
Â Â Â Â Â Â CATCH cx\_apc\_error INTO DATA(apc\_error).
Â Â Â Â Â Â Â Â me->message = apc\_error->get\_text( ).
Â Â Â Â Â Â CATCH cx\_ac\_message\_type\_pcp\_error INTO DATA(pcp\_error).
Â Â Â Â Â Â Â Â cl\_demo\_output=>display( pcp\_error->get\_text( ) ).
Â Â Â Â Â Â Â Â LEAVE PROGRAM.
Â Â Â Â ENDTRY.
Â Â ENDMETHOD.
Â Â METHOD if\_apc\_wsp\_event\_handler\_pcp~on\_close.
Â Â Â Â me->message = 'Connection closed!'.
Â Â ENDMETHOD.
Â Â METHOD if\_apc\_wsp\_event\_handler\_pcp~on\_error.
Â Â Â Â cl\_demo\_output=>display( 'Error!' ).
Â Â Â Â LEAVE PROGRAM.
Â Â ENDMETHOD.
ENDCLASS.
CLASS apc\_demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS apc\_demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA(out) = cl\_demo\_output=>new( ).
Â Â Â Â DATA(msg) = \`Hello APC!\`.
Â Â Â Â cl\_demo\_input=>add\_field( CHANGING field = msg ).
Â Â Â Â DATA amc TYPE abap\_bool VALUE ' '.
Â Â Â Â cl\_demo\_input=>add\_field( EXPORTING as\_checkbox = 'X'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â CHANGING fieldÂ Â = amc ).
Â Â Â Â DATA stateful\_server TYPE abap\_bool VALUE ' '.
Â Â Â Â cl\_demo\_input=>add\_field( EXPORTING as\_checkbox = 'X'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â CHANGING fieldÂ Â = stateful\_server ).
Â Â Â Â DATA messages TYPE i VALUE '5'.
Â Â Â Â cl\_demo\_input=>add\_field(Â Â CHANGING fieldÂ Â = messages ).
Â Â Â Â DATA wait TYPE i VALUE '10'.
Â Â Â Â cl\_demo\_input=>add\_field(Â Â CHANGING fieldÂ Â = wait ).
Â Â Â Â cl\_demo\_input=>request( ).
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â DATA(event\_handler) = NEW apc\_handler( ).
Â Â Â Â Â Â Â Â "Client
Â Â Â Â Â Â Â Â DATA(client) =
Â Â Â Â Â Â Â Â Â Â cl\_apc\_wsp\_client\_manager=>create\_by\_destination(
Â Â Â Â Â Â Â Â Â Â Â Â i\_destination = 'NONE'
Â Â Â Â Â Â Â Â Â Â Â Â i\_event\_handler = event\_handler
Â Â Â Â Â Â Â Â Â Â Â Â i\_protocol =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â if\_apc\_wsp\_event\_handler\_pcp=>co\_event\_handler\_type ).
Â Â Â Â Â Â Â Â "Server
Â Â Â Â Â Â Â Â DATA(request) =
Â Â Â Â Â Â Â Â Â Â client->get\_context( )->get\_initial\_request( ).
Â Â Â Â Â Â Â Â request->set\_path\_and\_query(
Â Â Â Â Â Â Â Â Â Â i\_relative\_uri =
Â Â Â Â Â Â Â Â Â Â Â Â \`/sap/bc/apc/sap/demo\_apc\_pcp\` &&
Â Â Â Â Â Â Â Â Â Â Â Â COND #(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  WHEN stateful\_server = abap\_true THEN \`\_stateful\` ) &&
Â Â Â Â Â Â Â Â Â Â Â Â COND #(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â WHEN amc = abap\_true THEN \`?amc=x\` ) ).
Â Â Â Â Â Â Â Â client->connect( ).
Â Â Â Â Â Â Â Â "Sending messages
Â Â Â Â Â Â Â Â DATA(message\_manager) =
Â Â Â Â Â Â Â Â Â Â CAST if\_apc\_wsp\_message\_manager\_pcp(
Â Â Â Â Â Â Â Â Â Â Â Â client->get\_message\_manager( ) ).
Â Â Â Â Â Â Â Â DATA(message) =
Â Â Â Â Â Â Â Â Â Â CAST if\_ac\_message\_type\_pcp(
Â Â Â Â Â Â Â Â Â Â Â Â message\_manager->create\_message( ) ).
Â Â Â Â Â Â Â Â TRY.
Â Â Â Â Â Â Â Â Â Â Â Â IF amc = abap\_true.
Â Â Â Â Â Â Â Â Â Â Â Â Â Â message->set\_field( i\_name = 'amc' i\_value = 'x' ).
Â Â Â Â Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â Â Â Â Â message->set\_text( msg ).
Â Â Â Â Â Â Â Â Â Â CATCH cx\_ac\_message\_type\_pcp\_error INTO DATA(pcp\_error).
Â Â Â Â Â Â Â Â Â Â Â Â cl\_demo\_output=>display( pcp\_error->get\_text( ) ).
Â Â Â Â Â Â Â Â Â Â Â Â LEAVE PROGRAM.
Â Â Â Â Â Â Â Â ENDTRY.
Â Â Â Â Â Â Â Â DO messages TIMES.
Â Â Â Â Â Â Â Â Â Â message\_manager->send( message ).
Â Â Â Â Â Â Â Â ENDDO.
Â Â Â Â Â Â Â Â "Receiving messages
Â Â Â Â Â Â Â Â DO wait TIMES.
Â Â Â Â Â Â Â Â Â Â out->line( ).
Â Â Â Â Â Â Â Â Â Â CLEAR event\_handler->message.
Â Â Â Â Â Â Â Â Â Â WAIT FOR PUSH CHANNELS
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  UNTIL event\_handler->message IS NOT INITIAL
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  UP TO 1 SECONDS.
Â Â Â Â Â Â Â Â Â Â IF sy-subrc = 4.
Â Â Â Â Â Â Â Â Â Â Â Â out->write\_text(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â 'No handler for APC messages registered' ).
Â Â Â Â Â Â Â Â Â Â ELSEIF sy-subrc = 8.
Â Â Â Â Â Â Â Â Â Â Â Â out->write\_text(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â 'Timeout occurred!' ).
Â Â Â Â Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â Â Â Â Â out->write\_text(
Â Â Â Â Â Â Â Â Â Â Â Â  |Received APC message: \\n\\n{
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  event\_handler->message } | ).
Â Â Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â ENDDO.
Â Â Â Â Â Â Â Â out->line( ).
Â Â Â Â Â Â Â Â "Close connection
Â Â Â Â Â Â Â Â client->close( i\_reason = 'Application closed connection!' ).
Â Â Â Â Â Â Â Â out->display( ).
Â Â Â Â Â Â CATCH cx\_apc\_error INTO DATA(apc\_error).
Â Â Â Â Â Â Â Â cl\_demo\_output=>display( apc\_error->get\_text( ) ).
Â Â Â Â Â Â Â Â LEAVE PROGRAM.
Â Â Â Â ENDTRY.
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â apc\_demo=>main( ).

Description

This example demonstrates how an ABAP program can create an [APC client](javascript:call_link\('abenapc.htm'\)) for the WebSocket protocol.

-   The local class apc\_handler, which implements the interface IF\_APC\_WSP\_EVENT\_HANDLER\_PCP, is used as the handler class. When a message is received, the method on\_message sets its message attribute to the message text.

-   CL\_APC\_WSP\_CLIENT\_MANAGER is used to create a client object for the current AS ABAP as an APC server and opens a connection to one of its ABAP push channels. It is possible to select which of the two ABAP push channels, DEMO\_APC\_PCP or DEMO\_APC\_PCP\_STATEFUL, from the executable example [AS ABAP as WebSocket Server](javascript:call_link\('abenapc_abexa.htm'\)) is used. The server can also be instructed to send its messages using [ABAP messaging channels](javascript:call_link\('abenamc.htm'\)).

-   The message manager of the client object is used to create a message in [PCP format](javascript:call_link\('abenpcp.htm'\)) and send it multiple times.

-   The statement [WAIT FOR PUSH CHANNELS](javascript:call_link\('abapwait_apc.htm'\)) is then used to switch the program to a wait state so that messages sent back from the server can be handled. Here, the logical expression checks the attribute message of the handler class apc\_handler. A received text is displayed.

-   Finally, the connection is closed explicitly.

It is possible to select how many messages are sent and how often the wait state occurs.

-   If communication takes place with the stateful server, the counter level of the server is raised, indicating that multiple messages are being sent and received.

-   If the server sends its messages using ABAP messaging channels, other AMC receivers in the same messaging channel can also see the messages of the server. For example, the Web browser from the executable example [AS ABAP as WebSocket Server](javascript:call_link\('abenapc_abexa.htm'\)) can receive these messages. In the reverse direction, the APC client can then also receive those messages sent by the APC server as a response to messages from the Web browser, if they occur in one of the programmed wait periods.



**ðŸ“– Source**: [abenapc_ws_client_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_ws_client_abexa.htm)

### abenapc_tcp_client_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenapc_tcp_client_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_tcp_client_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Data Interfaces and Communication Interfaces](javascript:call_link\('abenabap_data_communication.htm'\))Â â†’Â  [ABAP Channels](javascript:call_link\('abenabap_channels.htm'\))Â â†’Â  [APC - ABAP Push Channels](javascript:call_link\('abenapc.htm'\))Â â†’Â  [Examples of APC](javascript:call_link\('abenapc_abexas.htm'\))Â â†’Â 

APC, AS ABAP as TCP Socket Client

This example demonstrates AS ABAP as a TCP socket client.

Source Code

REPORT demo\_apc\_tcp\_client.
CLASS apc\_handler DEFINITION FINAL .
Â Â PUBLIC SECTION.
Â Â Â Â INTERFACES if\_apc\_wsp\_event\_handler.
Â Â Â Â DATAÂ Â Â Â Â Â  message TYPE string.
ENDCLASS.
CLASS apc\_handler IMPLEMENTATION.
Â Â METHOD if\_apc\_wsp\_event\_handler~on\_open.
Â Â ENDMETHOD.
Â Â METHOD if\_apc\_wsp\_event\_handler~on\_message.
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â message = i\_message->get\_text( ).
Â Â Â Â Â Â CATCH cx\_apc\_error INTO DATA(apc\_error).
Â Â Â Â Â Â Â Â message = apc\_error->get\_text( ).
Â Â Â Â ENDTRY.
Â Â ENDMETHOD.
Â Â METHOD if\_apc\_wsp\_event\_handler~on\_close.
Â Â Â Â message = 'Connection closed!'.
Â Â ENDMETHOD.
Â Â METHOD if\_apc\_wsp\_event\_handler~on\_error.
Â Â ENDMETHOD.
ENDCLASS.
CLASS apc\_demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS apc\_demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA(tcp\_server) = \`C:\\ncat\\ncat.exe\`.
Â Â Â Â DATA(ip\_adress)Â Â = cl\_gui\_frontend\_services=>get\_ip\_address( ).
Â Â Â Â DATA(port)Â Â Â Â Â Â  = \`12345\`.
Â Â Â Â DATA(terminator) = \`0A\`.
Â Â Â Â DATA(msg)Â Â Â Â Â Â Â Â = \`Hello TCP, answer me!\`.
Â Â Â Â cl\_demo\_input=>new(
Â Â Â Â Â Â )->add\_text(
Â Â Â Â Â Â Â Â  \`For the TCP server, download the freely available NCAT.EXE\`
Â Â Â Â Â Â )->add\_field( CHANGING field = tcp\_server
Â Â Â Â Â Â )->add\_field( CHANGING field = ip\_adress
Â Â Â Â Â Â )->add\_field( CHANGING field = port
Â Â Â Â Â Â )->add\_field( CHANGING field = terminator
Â Â Â Â Â Â )->add\_field( CHANGING field = msg
Â Â Â Â Â Â )->request( ).
Â Â Â Â "Server
Â Â Â Â IF cl\_gui\_frontend\_services=>file\_exist(
Â Â Â Â Â Â Â Â  file = tcp\_server ) IS INITIAL.
Â Â Â Â Â Â cl\_demo\_output=>display( 'TCP Server not found!' ).
Â Â Â Â Â Â LEAVE PROGRAM.
Â Â Â Â ENDIF.
Â Â Â Â cl\_gui\_frontend\_services=>execute(
Â Â Â Â EXPORTING
Â Â Â Â Â Â application = \`cmd.exe\`
Â Â Â Â Â Â parameterÂ Â =Â Â \`/c \` && tcp\_server &&
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  \` -l \` && ip\_adress && \` -p \` && port ).
Â Â Â Â WAIT UP TO 1 SECONDS.
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â DATA(event\_handler) = NEW apc\_handler( ).
Â Â Â Â Â Â Â Â "Client
Â Â Â Â Â Â Â Â DATA(client) = cl\_apc\_tcp\_client\_manager=>create(
Â Â Â Â Â Â Â Â Â Â i\_hostÂ Â  = ip\_adress
Â Â Â Â Â Â Â Â Â Â i\_portÂ Â = port
Â Â Â Â Â Â Â Â Â Â i\_frame = VALUE apc\_tcp\_frame(
Â Â Â Â Â Â Â Â Â Â Â Â frame\_type =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â if\_apc\_tcp\_frame\_types=>co\_frame\_type\_terminator
Â Â Â Â Â Â Â Â Â Â Â Â terminator =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â terminator )
Â Â Â Â Â Â Â Â Â Â i\_event\_handler = event\_handler ).
Â Â Â Â Â Â Â Â client->connect( ).
Â Â Â Â Â Â Â Â "Send mesasage from client
Â Â Â Â Â Â Â Â DATA(message\_manager) = CAST if\_apc\_wsp\_message\_manager(
Â Â Â Â Â Â Â Â Â Â client->get\_message\_manager( ) ).
Â Â Â Â Â Â Â Â DATA(message) = CAST if\_apc\_wsp\_message(
Â Â Â Â Â Â Â Â Â Â message\_manager->create\_message( ) ).
Â Â Â Â Â Â Â Â DATA(binary\_terminator) = CONV xstring( terminator ).
Â Â Â Â Â Â Â Â DATA(binary\_msg) =
Â Â Â Â Â Â Â Â Â Â cl\_abap\_conv\_codepage=>create\_out( )->convert( msg ).
Â Â Â Â Â Â Â Â CONCATENATE binary\_msg binary\_terminator
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  INTO binary\_msg IN BYTE MODE.
Â Â Â Â Â Â Â Â message->set\_binary( binary\_msg ).
Â Â Â Â Â Â Â Â message\_manager->send( message ).
Â Â Â Â Â Â Â Â "Wait for a message from server
Â Â Â Â Â Â Â Â CLEAR event\_handler->message.
Â Â Â Â Â Â Â Â WAIT FOR PUSH CHANNELS
Â Â Â Â Â Â Â Â Â Â Â Â  UNTIL event\_handler->message IS NOT INITIAL
Â Â Â Â Â Â Â Â Â Â Â Â  UP TO 10 SECONDS.
Â Â Â Â Â Â Â Â IF sy-subrc = 4.
Â Â Â Â Â Â Â Â Â Â cl\_demo\_output=>display(
Â Â Â Â Â Â Â Â Â Â Â Â 'No handler for APC messages registered!' ).
Â Â Â Â Â Â Â Â ELSEIF sy-subrc = 8.
Â Â Â Â Â Â Â Â Â Â cl\_demo\_output=>display(
Â Â Â Â Â Â Â Â Â Â Â Â 'Timeout occurred!' ).
Â Â Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â Â Â cl\_demo\_output=>display(
Â Â Â Â Â Â Â Â Â Â Â Â |TCP client received:\\n\\n{ event\_handler->message }| ).
Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â client->close(
Â Â Â Â Â Â Â Â Â Â i\_reason = 'Application closed connection!' ).
Â Â Â Â Â Â CATCH cx\_apc\_error INTO DATA(apc\_error).
Â Â Â Â Â Â Â Â cl\_demo\_output=>display( apc\_error->get\_text( ) ).
Â Â Â Â ENDTRY.
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â apc\_demo=>main( ).

Description

This example demonstrates how an ABAP program can create an [APC client](javascript:call_link\('abenapc.htm'\)) for the TCP socket protocol. It is structured in exactly the same way as in the [executable example](javascript:call_link\('abenapc_ws_client_abexa.htm'\)) for the WebSocket client, but with the connection being made to a TCP socket server. The current front-end computer is used as the TCP socket server here and the available [Ncat](https://nmap.org/ncat/) can be downloaded to this computer.

This program requires the Ncat.exe directory and the address of the current front-end computer (proposals are made for this information). In addition, a terminator character is needed for the TCP framework structure.

When the program is executed, Ncat.exe is started and waits for messages from the APC client. Once a message is sent, this client waits for a message entered in the Ncat window and confirmed using Enter.



**ðŸ“– Source**: [abenapc_tcp_client_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_tcp_client_abexa.htm)

### abenapc_detached_client_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenapc_detached_client_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_detached_client_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Data Interfaces and Communication Interfaces](javascript:call_link\('abenabap_data_communication.htm'\))Â â†’Â  [ABAP Channels](javascript:call_link\('abenabap_channels.htm'\))Â â†’Â  [APC - ABAP Push Channels](javascript:call_link\('abenapc.htm'\))Â â†’Â  [Examples of APC](javascript:call_link\('abenapc_abexas.htm'\))Â â†’Â 

APC, Creating a Detached Client

This example demonstrates how a detached client is created for the WebSocket protocol.

Source Code

REPORT demo\_apc\_detached\_client.
CLASS apc\_handler DEFINITION FINAL.
Â Â PUBLIC SECTION.
Â Â Â Â INTERFACES if\_apc\_wsp\_event\_handler\_pcp.
Â Â Â Â DATA: connection\_attach\_handle TYPE string,
Â Â Â Â Â Â Â Â Â Â messageÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â TYPE string.
ENDCLASS.
CLASS apc\_handler IMPLEMENTATION.
Â Â METHOD if\_apc\_wsp\_event\_handler\_pcp~on\_open.
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â connection\_attach\_handle =
Â Â Â Â Â Â Â Â Â Â i\_context->get\_connection\_attach\_handle(
Â Â Â Â Â Â Â Â Â Â Â Â EXPORTING i\_connection\_security =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â i\_context->co\_con\_security\_by\_user\_id ).
Â Â Â Â Â Â CATCH cx\_apc\_error INTO DATA(apc\_error).
Â Â Â Â Â Â Â Â me->message = apc\_error->get\_text( ).
Â Â Â Â ENDTRY.
Â Â ENDMETHOD.
Â Â METHOD if\_apc\_wsp\_event\_handler\_pcp~on\_message.
Â Â ENDMETHOD.
Â Â METHOD if\_apc\_wsp\_event\_handler\_pcp~on\_close.
Â Â ENDMETHOD.
Â Â METHOD if\_apc\_wsp\_event\_handler\_pcp~on\_error.
Â Â ENDMETHOD.
ENDCLASS.
CLASS apc\_demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS apc\_demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA(out) = cl\_demo\_output=>new( ).
Â Â Â Â DATA(msg) = \`Hello APC!\`.
Â Â Â Â cl\_demo\_input=>add\_field( CHANGING field = msg ).
Â Â Â Â DATA amc TYPE abap\_bool VALUE ' '.
Â Â Â Â cl\_demo\_input=>add\_field( EXPORTING as\_checkbox = 'X'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â CHANGING fieldÂ Â = amc ).
Â Â Â Â DATA stateful\_server TYPE abap\_bool VALUE ' '.
Â Â Â Â cl\_demo\_input=>add\_field( EXPORTING as\_checkbox = 'X'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â CHANGING fieldÂ Â = stateful\_server ).
Â Â Â Â DATA show\_attach\_handle TYPE abap\_bool VALUE ' '.
Â Â Â Â cl\_demo\_input=>add\_field( EXPORTING as\_checkbox = 'X'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â CHANGING fieldÂ Â = show\_attach\_handle ).
Â Â Â Â cl\_demo\_input=>request( ).
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â DATA(event\_handler) = NEW apc\_handler( ).
Â Â Â Â Â Â Â Â "Detached client
Â Â Â Â Â Â Â Â DATA(client\_detach) =
Â Â Â Â Â Â Â Â Â Â cl\_apc\_wsp\_client\_conn\_manager=>create\_by\_destination(
Â Â Â Â Â Â Â Â Â Â Â Â i\_destination = 'NONE'
Â Â Â Â Â Â Â Â Â Â Â Â i\_application\_id = \`DEMO\_APC\_PCP\`
Â Â Â Â Â Â Â Â Â Â Â Â i\_event\_handler = event\_handler
Â Â Â Â Â Â Â Â Â Â Â Â i\_protocol =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â if\_apc\_wsp\_event\_handler\_pcp=>co\_event\_handler\_type ).
Â Â Â Â Â Â Â Â "Server
Â Â Â Â Â Â Â Â DATA(request) =
Â Â Â Â Â Â Â Â Â Â client\_detach->get\_context( )->get\_initial\_request( ).
Â Â Â Â Â Â Â Â request->set\_path\_and\_query(
Â Â Â Â Â Â Â Â Â Â i\_relative\_uri =
Â Â Â Â Â Â Â Â Â Â Â Â \`/sap/bc/apc/sap/demo\_apc\_pcp\` &&
Â Â Â Â Â Â Â Â Â Â Â Â COND #(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  WHEN stateful\_server = abap\_true THEN \`\_stateful\` ) &&
Â Â Â Â Â Â Â Â Â Â Â Â COND #( WHEN amc = abap\_true THEN \`?amc=x\` ) ).
Â Â Â Â Â Â Â Â client\_detach->connect\_and\_detach( ).
Â Â Â Â Â Â Â Â IF event\_handler->message IS NOT INITIAL.
Â Â Â Â Â Â Â Â Â Â out->display(
Â Â Â Â Â Â Â Â Â Â Â Â |Error during ON\_OPEN: { event\_handler->message }| ).
Â Â Â Â Â Â Â Â Â Â LEAVE PROGRAM.
Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â IF show\_attach\_handle IS INITIAL.
Â Â Â Â Â Â Â Â Â Â "Attached client
Â Â Â Â Â Â Â Â Â Â DATA(client\_attach) =
Â Â Â Â Â Â Â Â Â Â Â Â cl\_apc\_wsp\_client\_conn\_manager=>attach(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â event\_handler->connection\_attach\_handle ).
Â Â Â Â Â Â Â Â Â Â DATA(message\_manager) =
Â Â Â Â Â Â Â Â Â Â Â Â CAST if\_apc\_wsp\_message\_manager\_pcp(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â client\_attach->get\_message\_manager( ) ).
Â Â Â Â Â Â Â Â Â Â DATA(message) = CAST if\_ac\_message\_type\_pcp(
Â Â Â Â Â Â Â Â Â Â Â Â message\_manager->create\_message( ) ).
Â Â Â Â Â Â Â Â Â Â TRY.
Â Â Â Â Â Â Â Â Â Â Â Â Â Â IF amc = abap\_true.
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â message->set\_field( i\_name = 'amc' i\_value = 'x' ).
Â Â Â Â Â Â Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â Â Â Â Â Â Â message->set\_field(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â i\_name = 'detached\_client' i\_value = 'x' ).
Â Â Â Â Â Â Â Â Â Â Â Â Â Â message->set\_text( msg ).
Â Â Â Â Â Â Â Â Â Â Â Â CATCH cx\_ac\_message\_type\_pcp\_error INTO DATA(pcp\_error).
Â Â Â Â Â Â Â Â Â Â Â Â Â Â cl\_demo\_output=>display( pcp\_error->get\_text( ) ).
Â Â Â Â Â Â Â Â Â Â Â Â Â Â LEAVE PROGRAM.
Â Â Â Â Â Â Â Â Â Â ENDTRY.
Â Â Â Â Â Â Â Â Â Â message\_manager->send( message ).
Â Â Â Â Â Â Â Â Â Â client\_attach->close(
Â Â Â Â Â Â Â Â Â Â Â Â i\_reason = 'Application closed connection!' ).
Â Â Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â Â Â out->display(
Â Â Â Â Â Â Â Â Â Â Â Â |Attach Handle:\\n\\n{
Â Â Â Â Â Â Â Â Â Â Â Â event\_handler->connection\_attach\_handle
Â Â Â Â Â Â Â Â Â Â Â Â }\\n\\nUse as input to DEMO\_APC\_ATTACH\_CLIENT.| ).
Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â CATCH cx\_apc\_error INTO DATA(apc\_error).
Â Â Â Â Â Â Â Â cl\_demo\_output=>display( apc\_error->get\_text( ) ).
Â Â Â Â Â Â Â Â LEAVE PROGRAM.
Â Â Â Â ENDTRY.
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â apc\_demo=>main( ).

Description

This example demonstrates how an ABAP program can create a [detached APC client](javascript:call_link\('abenapc.htm'\)) for the WebSocket protocol.

-   The local class apc\_handler, which implements the interface IF\_APC\_WSP\_EVENT\_HANDLER\_PCP, is used as the handler class. The method on\_open gets a connection handle for the connection. The other methods are not needed.

-   CL\_APC\_WSP\_CLIENT\_CONN\_MANAGER is used to create a detached client object as an APC server for the current AS ABAP. It is possible to select which of the two ABAP push channels, DEMO\_APC\_PCP or DEMO\_APC\_PCP\_STATEFUL, from the executable example [AS ABAP as WebSocket Server](javascript:call_link\('abenapc_abexa.htm'\)) is used. The server can also be instructed to send its messages using [ABAP messaging channels](javascript:call_link\('abenamc.htm'\)). The method CONNECT\_AND\_DETACH is used to create a detached client for the ABAP push channel.

-   The connection handle obtained by the method on\_open of the handler class can either be displayed or used immediately in the program to create an attached client for the detached client.

-   A displayed connection handle can be used in the program of the executable [attached client](javascript:call_link\('abenapc_attached_client_abexa.htm'\)) example.

-   If not, the method ATTACH of the class CL\_APC\_WSP\_CLIENT\_CONN\_MANAGER is used to create an attached client object for the connection handle. Its message manager is then used to create a message in [PCP format](javascript:call_link\('abenpcp.htm'\)) and send it.

A breakpoint in the method ON\_MESSAGE of the APC handler class CL\_APC\_WSP\_EXT\_DEMO\_APC\_PCP can be used to monitor the arrival and processing of the message (it is not possible to respond to messages from the same class here). If the message is sent using [AMC](javascript:call_link\('abenamc.htm'\)), the response from the server is received by all associated AMC receivers. The attached client cannot itself wait for the response.



**ðŸ“– Source**: [abenapc_detached_client_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_detached_client_abexa.htm)

### abenapc_attached_client_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenapc_attached_client_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_attached_client_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Data Interfaces and Communication Interfaces](javascript:call_link\('abenabap_data_communication.htm'\))Â â†’Â  [ABAP Channels](javascript:call_link\('abenabap_channels.htm'\))Â â†’Â  [APC - ABAP Push Channels](javascript:call_link\('abenapc.htm'\))Â â†’Â  [Examples of APC](javascript:call_link\('abenapc_abexas.htm'\))Â â†’Â 

APC, AS ABAP as Attached Client

This example demonstrates the AS ABAP as an attached client for a detached client.

Source Code

REPORT demo\_apc\_attach\_client.
CLASS apc\_demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS apc\_demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA attach\_handle TYPE string VALUE ' '.
Â Â Â Â cl\_demo\_input=>add\_field( CHANGING fieldÂ Â = attach\_handle ).
Â Â Â Â DATA(msg) = \`Hello APC!\`.
Â Â Â Â cl\_demo\_input=>add\_field( CHANGING field = msg ).
Â Â Â Â DATA amc TYPE abap\_bool VALUE ' '.
Â Â Â Â cl\_demo\_input=>add\_field( EXPORTING as\_checkbox = 'X'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â CHANGING fieldÂ Â = amc ).
Â Â Â Â DATA close TYPE abap\_bool VALUE ' '.
Â Â Â Â cl\_demo\_input=>add\_field( EXPORTING as\_checkbox = 'X'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â CHANGING fieldÂ Â = close ).
Â Â Â Â cl\_demo\_input=>request( ).
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â "Attached client
Â Â Â Â Â Â Â Â DATA(client\_attach) =
Â Â Â Â Â Â Â Â Â Â cl\_apc\_wsp\_client\_conn\_manager=>attach( attach\_handle ).
Â Â Â Â Â Â Â Â DATA(message\_manager) =
Â Â Â Â Â Â Â Â Â Â CAST if\_apc\_wsp\_message\_manager\_pcp(
Â Â Â Â Â Â Â Â Â Â Â Â client\_attach->get\_message\_manager( ) ).
Â Â Â Â Â Â Â Â DATA(message) = CAST if\_ac\_message\_type\_pcp(
Â Â Â Â Â Â Â Â Â Â message\_manager->create\_message( ) ).
Â Â Â Â Â Â Â Â TRY.
Â Â Â Â Â Â Â Â Â Â Â Â IF amc = abap\_true.
Â Â Â Â Â Â Â Â Â Â Â Â Â Â message->set\_field( i\_name = 'amc' i\_value = 'x' ).
Â Â Â Â Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â Â Â Â Â message->set\_field(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â i\_name = 'detached\_client' i\_value = 'x' ).
Â Â Â Â Â Â Â Â Â Â Â Â message->set\_text( msg ).
Â Â Â Â Â Â Â Â Â Â CATCH cx\_ac\_message\_type\_pcp\_error INTO DATA(pcp\_error).
Â Â Â Â Â Â Â Â Â Â Â Â cl\_demo\_output=>display( pcp\_error->get\_text( ) ).
Â Â Â Â Â Â Â Â Â Â Â Â LEAVE PROGRAM.
Â Â Â Â Â Â Â Â ENDTRY.
Â Â Â Â Â Â Â Â message\_manager->send( message ).
Â Â Â Â Â Â Â Â IF closeÂ Â = abap\_true.
Â Â Â Â Â Â Â Â Â Â client\_attach->close(
Â Â Â Â Â Â Â Â Â Â Â Â i\_reason = 'Application closed connection!' ).
Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â CATCH cx\_apc\_error INTO DATA(apc\_error).
Â Â Â Â Â Â Â Â cl\_demo\_output=>display( apc\_error->get\_text( ) ).
Â Â Â Â Â Â Â Â LEAVE PROGRAM.
Â Â Â Â ENDTRY.
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â apc\_demo=>main( ).

Description

The program expects a connection handle as input, as provided in the output of the executable [detached client](javascript:call_link\('abenapc_detached_client_abexa.htm'\)) example. The connection handle is used to create and employ an attached client object precisely as described in the detached client example. The connection handle can be reused indefinitely until the connection is closed using the method CLOSE. If the connection is not closed by mistake, this can be done later in the transaction SMWS.



**ðŸ“– Source**: [abenapc_attached_client_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_attached_client_abexa.htm)

### abenapc_connect_via_handle_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenapc_connect_via_handle_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_connect_via_handle_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ðŸ“– Source**: [abenapc_connect_via_handle_abexa.htm](https://help.sap.com/doc/abapdocu_754_index_htm/7.54/en-US/abenapc_connect_via_handle_abexa.htm)


Â  

* * *

AS ABAP Release 754, Â©Copyright 2019 SAP SE. All rights reserved.

[ABAP Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP âˆ’ Reference](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Data Interfaces and Communication Interfaces](javascript:call_link\('abenabap_data_communication.htm'\))Â â†’Â  [ABAP Channels](javascript:call_link\('abenabap_channels.htm'\))Â â†’Â  [APC - ABAP Push Channels](javascript:call_link\('abenapc.htm'\))Â â†’Â  [Examples of APC](javascript:call_link\('abenapc_abexas.htm'\))Â â†’Â 

APC, System-Wide Access

This example demonstrates access to any ABAP channel using a connection handle.

Source Code

REPORT demo\_apc\_access\_connection.
CLASS apc\_demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS apc\_demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA attach\_handle TYPE string VALUE ' '.
Â Â Â Â cl\_demo\_input=>add\_field( CHANGING fieldÂ Â = attach\_handle ).
Â Â Â Â DATA(msg) = \`Hello APC!\`.
Â Â Â Â cl\_demo\_input=>add\_field( CHANGING field = msg ).
Â Â Â Â cl\_demo\_input=>request( ).
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â DATA(access\_object) =
Â Â Â Â Â Â Â Â Â Â cl\_apc\_wsp\_client\_conn\_manager=>attach( attach\_handle ).
Â Â Â Â Â Â Â Â DATA(message\_manager) =
Â Â Â Â Â Â Â Â Â Â CAST if\_apc\_wsp\_message\_manager(
Â Â Â Â Â Â Â Â Â Â Â Â access\_object->get\_message\_manager( ) ).
Â Â Â Â Â Â Â Â DATA(message) = CAST if\_apc\_wsp\_message(
Â Â Â Â Â Â Â Â Â Â message\_manager->create\_message( ) ).
Â Â Â Â Â Â Â Â message->set\_text( msg ).
Â Â Â Â Â Â Â Â message\_manager->send( message ).
Â Â Â Â Â Â CATCH cx\_apc\_error INTO DATA(apc\_error).
Â Â Â Â Â Â Â Â cl\_demo\_output=>display( apc\_error->get\_text( ) ).
Â Â Â Â Â Â Â Â LEAVE PROGRAM.
Â Â Â Â ENDTRY.
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â apc\_demo=>main( ).

Description

The program expects a valid connection handle for a WebSocket connection as its input, creates an access objects for this handle, and uses it to send a simple text message.

An APC connection from the executable example [AS ABAP as WebSocket Server](javascript:call_link\('abenapc_abexa.htm'\)) publishes its connection handle as follows:

-   Create a WebSocket server without PCP.

-   Open the connection on the Web page.

-   Send the text message "get handle".

This message is identified in the method ON\_MESSAGE of the APC handler class CL\_APC\_WS\_EXT\_DEMO\_APC and the connection handle is fetched and sent. The Web page displays it in a form that can be copied. If the connection handle is passed to the program above while the connection is open, the message sent by the program is received and displayed by the Web page.
