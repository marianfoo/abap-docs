# ABAP - Keyword Documentation / ABAP - Programming Language / SAP GUI User Dialogs / General Dynpros / dynpro - Screen and Screen Elements

Included pages: 12



## ðŸ”— Source References

**Base URL**: [ABAP Keyword Documentation 7.55](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abenabap_dynpros_screen.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenabap_dynpros_screen.htm)
- [abenscreen_elements_abexas.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenscreen_elements_abexas.htm)
- [abendynpro_status_icons_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abendynpro_status_icons_abexa.htm)
- [abendynpro_context_menu_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abendynpro_context_menu_abexa.htm)
- [abendynpro_cfw_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abendynpro_cfw_abexa.htm)
- [abendynpro_custom_control_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abendynpro_custom_control_abexa.htm)
- [abenhtml_browser_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenhtml_browser_abexa.htm)
- [abenhtml_input_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenhtml_input_abexa.htm)
- [abenmime_pictures_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenmime_pictures_abexa.htm)
- [abenmime_html_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenmime_html_abexa.htm)
- [abentext_output_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abentext_output_abexa.htm)

**Bundle Contains**: 11 documentation pages
**Version**: ABAP 7.55
**Generated**: 2025-09-01T11:25:45.767Z

---

### abenabap_dynpros_screen.htm

> **ðŸ“– Official SAP Documentation**: [abenabap_dynpros_screen.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenabap_dynpros_screen.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 755, Â©Copyright 2020 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [General Dynpros](javascript:call_link\('abenabap_dynpros.htm'\))Â â†’Â 

dynpro - Screen and Screen Elements

The screen of a [dynpro](javascript:call_link\('abendynpro_glosry.htm'\) "Glossary Entry") is created in [Layout Editor](javascript:call_link\('abenlayout_editor_glosry.htm'\) "Glossary Entry") in [Screen Painter](javascript:call_link\('abenscreen_painter_glosry.htm'\) "Glossary Entry"). The screen of the program DEMO\_DYNPRO contains all possible screen elements. Above the screen, the menu bar, standard toolbar, title bar, and the application toolbar can be seen, with the status bar at the bottom.

![Figure](abdoc_screen_elements.gif)

Continue
[dynpro - Examples of Screen Elements](javascript:call_link\('abenscreen_elements_abexas.htm'\))



**ðŸ“– Source**: [abenabap_dynpros_screen.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenabap_dynpros_screen.htm)

### abenscreen_elements_abexas.htm

> **ðŸ“– Official SAP Documentation**: [abenscreen_elements_abexas.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenscreen_elements_abexas.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 755, Â©Copyright 2020 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [General Dynpros](javascript:call_link\('abenabap_dynpros.htm'\))Â â†’Â  [dynpro - Screen and Screen Elements](javascript:call_link\('abenabap_dynpros_screen.htm'\))Â â†’Â 

dynpro - Examples of Screen Elements

Continue
![Example](exa.gif "Example") [dynpro - Status Icons](javascript:call_link\('abendynpro_status_icons_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - Context Menus](javascript:call_link\('abendynpro_context_menu_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - CFW](javascript:call_link\('abendynpro_cfw_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - CFW Events](javascript:call_link\('abendynpro_custom_control_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - HTML Browser](javascript:call_link\('abenhtml_browser_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - Input in HTML File](javascript:call_link\('abenhtml_input_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - Images in HTML](javascript:call_link\('abenmime_pictures_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - HTML from the MIME Repository](javascript:call_link\('abenmime_html_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - Text Output](javascript:call_link\('abentext_output_abexa.htm'\))



**ðŸ“– Source**: [abenscreen_elements_abexas.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenscreen_elements_abexas.htm)

### abendynpro_status_icons_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abendynpro_status_icons_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abendynpro_status_icons_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 755, Â©Copyright 2020 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [General Dynpros](javascript:call_link\('abenabap_dynpros.htm'\))Â â†’Â  [dynpro - Screen and Screen Elements](javascript:call_link\('abenabap_dynpros_screen.htm'\))Â â†’Â  [dynpro - Examples of Screen Elements](javascript:call_link\('abenscreen_elements_abexas.htm'\))Â â†’Â 

dynpro - Status Icons

This example illustrates how status icons are used on dynpros.

Source Code

REPORT demo\_dynpro\_status\_icons.
DATA value TYPE i VALUE 1.
DATA: status\_icon TYPE icons-text,
Â Â Â Â Â Â icon\_name(20) TYPE c,
Â Â Â Â Â Â icon\_text(10) TYPE c.
CALL SCREEN 100.
MODULE set\_icon OUTPUT.
Â Â SET PF-STATUS 'SCREEN\_100'.
Â Â CASE value.
Â Â Â Â WHEN 1.
Â Â Â Â Â Â icon\_name = 'ICON\_GREEN\_LIGHT'.
Â Â Â Â Â Â icon\_text =Â Â text-003.
Â Â Â Â WHEN 2.
Â Â Â Â Â Â icon\_name = 'ICON\_YELLOW\_LIGHT'.
Â Â Â Â Â Â icon\_text =Â Â text-002.
Â Â Â Â WHEN 3.
Â Â Â Â Â Â icon\_name = 'ICON\_RED\_LIGHT'.
Â Â Â Â Â Â icon\_text =Â Â text-001.
Â Â ENDCASE.
Â Â CALL FUNCTION 'ICON\_CREATE'
Â Â Â Â Â Â  EXPORTING
Â Â Â Â Â Â Â Â Â Â Â Â nameÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â = icon\_name
Â Â Â Â Â Â Â Â Â Â Â Â textÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â = icon\_text
Â Â Â Â Â Â Â Â Â Â Â Â infoÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â = 'Status'
Â Â Â Â Â Â Â Â Â Â Â Â add\_stdinfÂ Â Â Â Â Â Â Â Â Â Â Â = 'X'
Â Â Â Â Â Â  IMPORTING
Â Â Â Â Â Â Â Â Â Â Â Â resultÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â = status\_icon
Â Â Â Â Â Â  EXCEPTIONS
Â Â Â Â Â Â Â Â Â Â Â Â icon\_not\_foundÂ Â Â Â Â Â Â Â = 1
Â Â Â Â Â Â Â Â Â Â Â Â outputfield\_too\_short = 2
Â Â Â Â Â Â Â Â Â Â Â Â OTHERSÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â = 3.
Â Â CASE sy-subrc.
Â Â Â Â WHEN 1.
Â Â Â Â Â Â MESSAGE e888(sabapdemos) WITH text-004.
Â Â Â Â WHEN 2.
Â Â Â Â Â Â MESSAGE e888(sabapdemos) WITH text-005.
Â Â Â Â WHEN 3.
Â Â Â Â Â Â MESSAGE e888(sabapdemos) WITH text-006.
Â Â ENDCASE.
ENDMODULE.
MODULE cancel INPUT.
Â Â LEAVE PROGRAM.
ENDMODULE.
MODULE change.
Â Â CASE value.
Â Â Â Â WHEN 1.
Â Â Â Â Â Â value = 2.
Â Â Â Â WHEN 2.
Â Â Â Â Â Â value = 3.
Â Â Â Â WHEN 3.
Â Â Â Â Â Â value = 1.
Â Â ENDCASE.
ENDMODULE.

Description

The static next dynpro number of dynpro 100 is 100. It is a status field called status\_icon with a visible length of 16 and a defined length of 26. The status icon and the space for the text are represented by placeholders in Screen Painter. The screen flow logic is as follows:

PROCESS BEFORE OUTPUT.
Â Â MODULE set\_icon.
PROCESS AFTER INPUT.
Â Â MODULE cancel AT EXIT-COMMAND.
Â Â MODULE change.

Depending on the local value field in the program, the dialog module set\_icon passes different values to the function module ICON\_CREATE. The status field status\_icon is filled with the contents of the export parameter result of the function module. This displays the corresponding icon including the text and tooltip on the screen. When the user chooses Continue, the contents of the field value are changed in the PAI, and consequently a new icon is defined in the PBO event.



**ðŸ“– Source**: [abendynpro_status_icons_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abendynpro_status_icons_abexa.htm)

### abendynpro_context_menu_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abendynpro_context_menu_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abendynpro_context_menu_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 755, Â©Copyright 2020 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [General Dynpros](javascript:call_link\('abenabap_dynpros.htm'\))Â â†’Â  [dynpro - Screen and Screen Elements](javascript:call_link\('abenabap_dynpros_screen.htm'\))Â â†’Â  [dynpro - Examples of Screen Elements](javascript:call_link\('abenscreen_elements_abexas.htm'\))Â â†’Â 

dynpro - Context Menus

The example illustrates how context menus can be integrated into a program.

Source Code

REPORT demo\_dynpro\_context\_menu.
DATA: field1 TYPE i VALUE 10,
Â Â Â Â Â Â field2 TYPE p DECIMALS 4.
DATA: prog TYPE sy-repid,
Â Â Â Â Â Â flag(1) TYPE c VALUE 'X'.
DATA: ok\_code TYPE sy-ucomm,
Â Â Â Â Â Â save\_ok TYPE sy-ucomm.
prog = sy-repid.
CALL SCREEN 100.
MODULE status\_0100 OUTPUT.
Â Â SET TITLEBAR 'TIT100'.
Â Â IF flag = 'X'.
Â Â Â Â SET PF-STATUS 'SCREEN\_100' EXCLUDING 'REVEAL'.
Â Â ELSEIF flag = ' '.
Â Â Â Â SET PF-STATUS 'SCREEN\_100' EXCLUDING 'HIDE'.
Â Â ENDIF.
Â Â LOOP AT SCREEN INTO DATA(screen\_wa).
Â Â Â Â IF screen\_wa-group1 = 'MOD'.
Â Â Â Â Â Â IF flag = 'X'.
Â Â Â Â Â Â Â Â screen\_wa-active = '1'.
Â Â Â Â Â Â ELSEIF flag = ' '.
Â Â Â Â Â Â Â Â screen\_wa-active = '0'.
Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â MODIFY SCREEN FROM screen\_wa.
Â Â Â Â ELSEIF screen\_wa-name = 'TEXT\_IN\_FRAME'.
Â Â Â Â Â Â IF flag = 'X'.
Â Â Â Â Â Â Â Â screen\_wa-active = '0'.
Â Â Â Â Â Â ELSEIF flag = ' '.
Â Â Â Â Â Â Â Â screen\_wa-active = '1'.
Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â MODIFY SCREEN FROM screen\_wa.
Â Â Â Â ENDIF.
Â Â ENDLOOP.
ENDMODULE.
MODULE cancel INPUT.
Â Â LEAVE PROGRAM.
ENDMODULE.
MODULE user\_command\_0100.
Â Â save\_ok = ok\_code.
Â Â CLEAR ok\_code.
Â Â CASE save\_ok.
Â Â Â Â WHEN 'HIDE'.
Â Â Â Â Â Â flag = ' '.
Â Â Â Â WHEN 'REVEAL'.
Â Â Â Â Â Â flag = 'X'.
Â Â Â Â WHEN 'SQUARE'.
Â Â Â Â Â Â field2 = field1 \*\* 2.
Â Â Â Â WHEN 'CUBE'.
Â Â Â Â Â Â field2 = field1 \*\* 3.
Â Â Â Â WHEN 'SQUAREROOT'.
Â Â Â Â Â Â field2 = field1 \*\* ( 1 / 2 ).
Â Â Â Â WHEN 'CUBICROOT'.
Â Â Â Â Â Â field2 = field1 \*\* ( 1 / 3 ).
Â Â ENDCASE.
ENDMODULE.
\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*
\* Callback-Routines:
\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*
FORM on\_ctmenu\_text USING l\_menu TYPE REF TO cl\_ctmenu.
Â Â l\_menu->load\_gui\_status(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  EXPORTING program = prog
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â  statusÂ Â = 'CONTEXT\_MENU\_1'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â  menuÂ Â Â Â = l\_menu ).
ENDFORM.
FORM on\_ctmenu\_frame USING l\_menu TYPE REF TO cl\_ctmenu.
Â Â l\_menu->load\_gui\_status(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â EXPORTING program = prog
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â statusÂ Â = 'CONTEXT\_MENU\_2'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â menuÂ Â Â Â = l\_menu ).
Â Â l\_menu->load\_gui\_status(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â EXPORTING program = prog
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â statusÂ Â = 'CONTEXT\_MENU\_1'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â menuÂ Â Â Â = l\_menu ).
Â Â l\_menu->set\_default\_function(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â EXPORTING fcode = 'HIDE' ).
ENDFORM.
FORM on\_ctmenu\_reveal USING l\_menu TYPE REF TO cl\_ctmenu.
Â Â l\_menu->load\_gui\_status(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â EXPORTING program = prog
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â statusÂ Â = 'CONTEXT\_MENU\_3'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â menuÂ Â Â Â = l\_menu ).
Â Â l\_menu->load\_gui\_status(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â EXPORTING program = prog
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â statusÂ Â = 'CONTEXT\_MENU\_1'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â menuÂ Â Â Â = l\_menu ).
Â Â l\_menu->set\_default\_function(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â EXPORTING fcode = 'REVEAL' ).
ENDFORM.
FORM on\_ctmenu\_input USING l\_menu TYPE REF TO cl\_ctmenu.
Â Â DATA calculate\_menu TYPE REF TO cl\_ctmenu.
Â Â CREATE OBJECT calculate\_menu.
Â Â calculate\_menu->add\_function(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â EXPORTING fcode = 'SQUARE'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â textÂ Â = text-001 ).
Â Â calculate\_menu->add\_function(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  EXPORTING fcode = 'CUBE'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â  textÂ Â = text-002 ).
Â Â calculate\_menu->add\_function(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  EXPORTING fcode = 'SQUAREROOT'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â  textÂ Â = text-003 ).
Â Â calculate\_menu->add\_function(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  EXPORTING fcode = 'CUBICROOT'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â  textÂ Â = text-004 ).
Â Â l\_menu->add\_submenu(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  EXPORTING menu = calculate\_menu
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  text = text-005 ).
ENDFORM.

Description

The static next dynpro number of dynpro 100 is 100. The elements TEXT2 and FIELD2 do not have their own context menus. They inherit the context menu FRAME of the frame. The modification group MOD is assigned to them. The dynpro flow logic is:

PROCESS BEFORE OUTPUT.
Â Â MODULE status\_0100.
PROCESS AFTER INPUT.
Â Â MODULE cancel AT EXIT-COMMAND.
Â Â MODULE user\_command\_0100.

The GUI status SCREEN\_100 is set to PBO statically, in which, depending on the field flag, the function codes HIDE or REVEAL are hidden. The context menus for the screen elements are constructed in the callback routines as follows:

-   TEXT: Loads the static context menu CONTEXT\_MENU\_1 without modification. This context menu has a single line, Cancel.

-   FRAME: Constructs the context menu from the static context menus CONTEXT\_MENU\_2 and CONTEXT\_MENU\_1. This context menu has two lines, Hide Result and Cancel. The line for the function code HIDE is highlighted.

-   REVEAL: Constructs the context menus from the static context menus CONTEXT\_MENU\_3 and CONTEXT\_MENU\_1. This context menu has two lines, Display Result and Cancel. The line of the function code REVEAL is highlighted.

-   INPUT: Constructs the context menu by including the multiline local context menu CALCULATE\_MENU as a submenu. For the latter, a local reference variable with reference to CL\_CTMENU is created, an object is created, and the new function codes SQUARE, CUBE, SQUAREROOT, and CUBICROOT are added. When included in the context menu for INPUT, a text must be specified for the entry, after which the submenu is attached.

When the program is executed and the right (secondary) mouse button or SHIFT+F10 is pressed, the user sees the context menu TEXT in the first line, the context menu INPUT in the second line, and the context menu FRAME in the third line. The fourth line is hidden by the program. On all other dynpro components, the standard context menu appears with all static function codes and F1 and F4.

When a new dynamic function is selected, calculations are performed on the number in input field FIELD1 and then passed to FIELD2. When the function Hide Result (HIDE) is selected, the screen is modified dynamically. This makes the fourth line visible and the context menu REVEAL can be used.



**ðŸ“– Source**: [abendynpro_context_menu_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abendynpro_context_menu_abexa.htm)

### abendynpro_cfw_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abendynpro_cfw_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abendynpro_cfw_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 755, Â©Copyright 2020 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [General Dynpros](javascript:call_link\('abenabap_dynpros.htm'\))Â â†’Â  [dynpro - Screen and Screen Elements](javascript:call_link\('abenabap_dynpros_screen.htm'\))Â â†’Â  [dynpro - Examples of Screen Elements](javascript:call_link\('abenscreen_elements_abexas.htm'\))Â â†’Â 

dynpro - CFW

The example illustrates the use of different GUI controls in a dynpro.

Source Code

\*&---------------------------------------------------------------------\*
\*& ReportÂ Â demo\_cfwÂ Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â \*
\*&---------------------------------------------------------------------\*
REPORT demo\_cfw.
\*&---------------------------------------------------------------------\*
\*& Global DeclarationsÂ Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  \*
\*&---------------------------------------------------------------------\*
\* Class Definitions
CLASS screen\_handler DEFINITION CREATE PRIVATE.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-DATA screen TYPE REF TO screen\_handler.
Â Â Â Â CLASS-METHODS create\_screen.
Â Â Â Â METHODS constructor.
Â Â PRIVATE SECTION.
Â Â Â Â DATA: container\_htmlÂ Â TYPE REF TO cl\_gui\_custom\_container,
Â Â Â Â Â Â Â Â Â Â container\_boxÂ Â  TYPE REF TO cl\_gui\_dialogbox\_container,
Â Â Â Â Â Â Â Â Â Â pictureÂ Â Â Â Â Â Â Â  TYPE REF TO cl\_gui\_picture,
Â Â Â Â Â Â Â Â Â Â treeÂ Â Â Â Â Â Â Â Â Â Â Â TYPE REF TO cl\_gui\_simple\_tree,
Â Â Â Â Â Â Â Â Â Â html\_viewerÂ Â Â Â  TYPE REF TO cl\_gui\_html\_viewer,
Â Â Â Â Â Â Â Â Â Â list\_viewerÂ Â Â Â  TYPE REF TO cl\_gui\_alv\_grid.
Â Â Â Â METHODS: fill\_tree,
Â Â Â Â Â Â Â Â Â Â Â Â  fill\_picture,
Â Â Â Â Â Â Â Â Â Â Â Â  handle\_node\_double\_click
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  FOR EVENT node\_double\_click OF cl\_gui\_simple\_tree
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  IMPORTING node\_key,
Â Â Â Â Â Â Â Â Â Â Â Â  close\_box
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  FOR EVENT close OF cl\_gui\_dialogbox\_container,
Â Â Â Â Â Â Â Â Â Â Â Â  fill\_html IMPORTING i\_carrid TYPE spfli-carrid,
Â Â Â Â Â Â Â Â Â Â Â Â  fill\_list IMPORTING i\_carrid TYPE spfli-carrid
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â  i\_connid TYPE spfli-connid.
ENDCLASS.Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â "screen\_handler DEFINITION
\* Class Implementations
CLASS screen\_handler IMPLEMENTATION.
Â Â METHOD create\_screen.
Â Â Â Â IF screen IS INITIAL.
Â Â Â Â Â Â CREATE OBJECT screen.
Â Â Â Â ENDIF.
Â Â ENDMETHOD.Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â "create\_screen
Â Â METHOD constructor.
Â Â Â Â DATA: l\_event\_tabÂ Â Â Â Â Â Â Â TYPE cntl\_simple\_events,
Â Â Â Â Â Â Â Â Â Â l\_eventÂ Â Â Â Â Â Â Â Â Â Â Â LIKE LINE OF l\_event\_tab,
Â Â Â Â Â Â Â Â Â Â l\_dockingÂ Â Â Â Â Â Â Â Â Â TYPE REF TO cl\_gui\_docking\_container,
Â Â Â Â Â Â Â Â Â Â l\_splitterÂ Â Â Â Â Â Â Â  TYPE REF TO cl\_gui\_splitter\_container,
Â Â Â Â Â Â Â Â Â Â l\_container\_screen TYPE REF TO cl\_gui\_custom\_container,
Â Â Â Â Â Â Â Â Â Â l\_container\_topÂ Â Â Â TYPE REF TO cl\_gui\_container,
Â Â Â Â Â Â Â Â Â Â l\_container\_bottom TYPE REF TO cl\_gui\_container.
Â Â Â Â CREATE OBJECT container\_html
Â Â Â Â Â Â Â Â Â Â  EXPORTING container\_name = 'CUSTOM\_CONTROL'.
Â Â Â Â CREATE OBJECT l\_docking
Â Â Â Â Â Â Â Â Â Â  EXPORTING side = cl\_gui\_docking\_container=>dock\_at\_left
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  extension = 135.
Â Â Â Â CREATE OBJECT l\_splitter
Â Â Â Â Â Â Â Â Â Â  EXPORTING parent = l\_docking
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  rows = 2
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  columns = 1.
Â Â Â Â l\_splitter->set\_border(
Â Â Â Â Â Â Â Â  EXPORTING border = cl\_gui\_cfw=>false ).
Â Â Â Â l\_splitter->set\_row\_mode(
Â Â Â Â Â Â Â Â  EXPORTING mode = l\_splitter->mode\_absolute ).
Â Â Â Â l\_splitter->set\_row\_height(
Â Â Â Â Â Â Â Â  EXPORTING id = 1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  height = 180 ).
Â Â Â Â l\_container\_topÂ Â Â Â =
Â Â Â Â Â Â l\_splitter->get\_container( row = 1 column = 1 ).
Â Â Â Â l\_container\_bottom =
Â Â Â Â Â Â l\_splitter->get\_container( row = 2 column = 1 ).
Â Â Â Â CREATE OBJECT picture
Â Â Â Â Â Â Â Â Â Â  EXPORTING parent = l\_container\_top.
Â Â Â Â CREATE OBJECT tree
Â Â Â Â Â Â Â Â Â Â  EXPORTING parent = l\_container\_bottom
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  node\_selection\_mode =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  cl\_gui\_simple\_tree=>node\_sel\_mode\_single.
Â Â Â Â l\_event-eventid = cl\_gui\_simple\_tree=>eventid\_node\_double\_click.
Â Â Â Â l\_event-appl\_event = ' '.Â Â  "system event, does not trigger PAI
Â Â Â Â APPEND l\_event TO l\_event\_tab.
Â Â Â Â tree->set\_registered\_events(
Â Â Â Â Â Â Â Â  EXPORTING events = l\_event\_tab ).
Â Â Â Â SET HANDLER me->handle\_node\_double\_click FOR tree.
Â Â Â Â me->fill\_picture( ).
Â Â Â Â me->fill\_tree( ).
Â Â ENDMETHOD.Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â "constructor
Â Â METHOD fill\_picture.
Â Â Â Â TYPES pict\_line TYPE x LENGTH 1022.
Â Â Â Â DATA l\_mime\_apiÂ Â  TYPE REF TO if\_mr\_api.
Â Â Â Â DATA l\_pict\_waÂ Â Â Â TYPE xstring.
Â Â Â Â DATA l\_pict\_tabÂ Â  TYPE TABLE OF pict\_line.
Â Â Â Â DATA l\_urlÂ Â Â Â Â Â Â Â TYPE c LENGTH 255.
Â Â Â Â l\_mime\_api = cl\_mime\_repository\_api=>get\_api( ).
Â Â Â Â l\_mime\_api->get(
Â Â Â Â Â Â EXPORTING i\_url = '/SAP/PUBLIC/BC/ABAP/Sources/PLANE.GIF'
Â Â Â Â Â Â IMPORTING e\_content = l\_pict\_wa
Â Â Â Â Â Â EXCEPTIONS OTHERS = 4 ).
Â Â Â Â IF sy-subrc <> 0.
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â l\_pict\_tab =
Â Â Â Â Â Â VALUE #( LET l1 = xstrlen( l\_pict\_wa ) l2 = l1 - 1022 IN
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  FOR j = 0 THEN j + 1022Â Â UNTIL j >= l1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( COND #( WHEN j <= l2 THEN
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â l\_pict\_wa+j(1022)
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ELSE l\_pict\_wa+j ) ) ).
Â Â Â Â CALL FUNCTION 'DP\_CREATE\_URL'
Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â typeÂ Â Â Â = 'IMAGE'
Â Â Â Â Â Â Â Â subtype = 'GIF'
Â Â Â Â Â Â TABLES
Â Â Â Â Â Â Â Â dataÂ Â Â Â = l\_pict\_tab
Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â urlÂ Â Â Â  = l\_url.
Â Â Â Â picture->load\_picture\_from\_url(
Â Â Â Â Â Â Â Â  EXPORTING url = l\_url ).
Â Â Â Â picture->set\_display\_mode(
Â Â Â Â Â Â Â Â  EXPORTING display\_mode = picture->display\_mode\_stretch ).
Â Â ENDMETHOD.Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â "fill\_picture
Â Â METHOD fill\_tree.
Â Â Â Â DATA: l\_node\_table TYPE TABLE OF abdemonode,
Â Â Â Â Â Â Â Â Â Â l\_node TYPE abdemonode,
Â Â Â Â Â Â Â Â Â Â BEGIN OF l\_spfli,
Â Â Â Â Â Â Â Â Â Â Â Â carrid TYPE spfli-carrid,
Â Â Â Â Â Â Â Â Â Â Â Â connid TYPE spfli-connid,
Â Â Â Â Â Â Â Â Â Â END OF l\_spfli,
Â Â Â Â Â Â Â Â Â Â l\_spfli\_tab LIKE SORTED TABLE OF l\_spfli
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WITH UNIQUE KEY carrid connid.
Â Â Â Â SELECT carrid, connid
Â Â Â Â Â Â FROM spfli
Â Â Â Â Â Â INTO CORRESPONDING FIELDS OF TABLE @l\_spfli\_tab.
Â Â Â Â l\_node-hidden = ' '.Â Â Â Â Â Â Â Â Â Â Â Â Â Â  " All nodes are visible,
Â Â Â Â l\_node-disabled = ' '.Â Â Â Â Â Â Â Â Â Â Â Â  " selectable,
Â Â Â Â l\_node-isfolder = 'X'.Â Â Â Â Â Â Â Â Â Â Â Â  " a folder,
Â Â Â Â l\_node-expander = ' '.Â Â Â Â Â Â Â Â Â Â Â Â  " have no '+' sign for expansion.
Â Â Â Â LOOP AT l\_spfli\_tab INTO l\_spfli.
Â Â Â Â Â Â AT NEW carrid.
Â Â Â Â Â Â Â Â l\_node-node\_key = l\_spfli-carrid.
Â Â Â Â Â Â Â Â CLEAR l\_node-relatkey.
Â Â Â Â Â Â Â Â CLEAR l\_node-relatship.
Â Â Â Â Â Â Â Â l\_node-text = l\_spfli-carrid.
Â Â Â Â Â Â Â Â l\_node-n\_image =Â Â  ' '.
Â Â Â Â Â Â Â Â l\_node-exp\_image = ' '.
Â Â Â Â Â Â Â Â APPEND l\_node TO l\_node\_table.
Â Â Â Â Â Â ENDAT.
Â Â Â Â Â Â AT NEW connid.
Â Â Â Â Â Â Â Â l\_node-node\_key = l\_spfli-carrid && l\_spfli-connid.
Â Â Â Â Â Â Â Â l\_node-relatkey = l\_spfli-carrid.
Â Â Â Â Â Â Â Â l\_node-relatship = cl\_gui\_simple\_tree=>relat\_last\_child.
Â Â Â Â Â Â Â Â l\_node-text = l\_spfli-connid.
Â Â Â Â Â Â Â Â l\_node-n\_image =Â Â  '@AV@'.Â Â Â Â  "AV is the internal code
Â Â Â Â Â Â Â Â l\_node-exp\_image = '@AV@'.Â Â Â Â  "for an airplane icon
Â Â Â Â Â Â ENDAT.
Â Â Â Â Â Â APPEND l\_node TO l\_node\_table.
Â Â Â Â ENDLOOP.
Â Â Â Â tree->add\_nodes(
Â Â Â Â Â Â Â Â  EXPORTING table\_structure\_name = 'ABDEMONODE'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  node\_table = l\_node\_table ).
Â Â ENDMETHOD.Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â "fill\_tree
Â Â METHOD handle\_node\_double\_click.
Â Â Â Â DATA: l\_carrid TYPE spfli-carrid,
Â Â Â Â Â Â Â Â Â Â l\_connid TYPE spfli-connid.
Â Â Â Â l\_carrid = node\_key(2).
Â Â Â Â l\_connid = node\_key+2(4).
Â Â Â Â IF l\_connid IS INITIAL.
Â Â Â Â Â Â fill\_html( EXPORTING i\_carrid = l\_carrid ).
Â Â Â Â ELSE.
Â Â Â Â Â Â fill\_list( EXPORTING i\_carrid = l\_carrid
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  i\_connid = l\_connid ).
Â Â Â Â ENDIF.
Â Â ENDMETHOD.Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â "handle\_node\_double\_click
Â Â METHOD fill\_html.
Â Â Â Â DATA l\_url TYPE scarr-url.
Â Â Â Â IF html\_viewer IS INITIAL.
Â Â Â Â Â Â CREATE OBJECT html\_viewer
Â Â Â Â Â Â Â Â Â Â Â Â  EXPORTING parent = container\_html.
Â Â Â Â ENDIF.
Â Â Â Â SELECT SINGLE url
Â Â Â Â Â Â Â Â Â Â  FROMÂ Â  scarr
Â Â Â Â Â Â Â Â Â Â  WHEREÂ Â carrid = @i\_carrid
Â Â Â Â Â Â Â Â Â Â  INTOÂ Â  @l\_url.
Â Â Â Â html\_viewer->show\_url(
Â Â Â Â Â Â Â Â  EXPORTING url = l\_url ).
Â Â ENDMETHOD.Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â "fill\_html
Â Â METHOD fill\_list.
Â Â Â Â DATA: l\_flight\_tab TYPE TABLE OF demofli,
Â Â Â Â Â Â Â Â Â Â BEGIN OF l\_flight\_title,
Â Â Â Â Â Â Â Â Â Â Â Â carrname TYPE scarr-carrname,
Â Â Â Â Â Â Â Â Â Â Â Â cityfrom TYPE spfli-cityfrom,
Â Â Â Â Â Â Â Â Â Â Â Â citytoÂ Â  TYPE spfli-cityto,
Â Â Â Â Â Â Â Â Â Â END OF l\_flight\_title,
Â Â Â Â Â Â Â Â Â Â l\_list\_layout TYPE lvc\_s\_layo.
Â Â Â Â IF container\_box IS INITIAL.
Â Â Â Â Â Â CREATE OBJECT container\_box
Â Â Â Â Â Â Â Â Â Â Â Â  EXPORTING widthÂ Â = 250
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  height = 200
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  topÂ Â Â Â = 100
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  leftÂ Â  = 400
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  caption = 'Flight List'.
Â Â Â Â Â Â SET HANDLER close\_box FOR container\_box.
Â Â Â Â Â Â CREATE OBJECT list\_viewer
Â Â Â Â Â Â Â Â Â Â Â Â  EXPORTING i\_parent = container\_box.
Â Â Â Â ENDIF.
Â Â Â Â SELECT SINGLE c~carrname, p~cityfrom, p~cityto "#EC CI\_BUFFJOIN
Â Â Â Â Â Â Â Â Â Â  FROMÂ Â  ( scarr AS c
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â INNER JOIN spfli AS p ON c~carrid = p~carrid )
Â Â Â Â Â Â Â Â Â Â  WHEREÂ Â p~carrid = @i\_carrid AND
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â p~connid = @i\_connid
Â Â Â Â Â Â Â Â Â Â  INTOÂ Â  CORRESPONDING FIELDS OF @l\_flight\_title.
Â Â Â Â SELECTÂ Â  fldate, seatsmax, seatsocc
Â Â Â Â Â Â Â Â Â Â Â Â  FROMÂ Â Â Â  sflight
Â Â Â Â Â Â Â Â Â Â Â Â  WHEREÂ Â Â Â carrid = @i\_carrid AND connid = @i\_connid
Â Â Â Â Â Â Â Â Â Â Â Â  ORDER BY fldate
Â Â Â Â Â Â Â Â Â Â Â Â  INTOÂ Â Â Â  CORRESPONDING FIELDS OF TABLE @l\_flight\_tab.
Â Â Â Â l\_list\_layout-grid\_title = l\_flight\_title-carrname && \` \` &&
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â  i\_connidÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â && \` \` &&
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â  l\_flight\_title-cityfrom && \` \` &&
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  l\_flight\_title-cityto.
Â Â Â Â l\_list\_layout-smalltitle = 'X'.Â Â Â Â "The list title has small fonts,
Â Â Â Â l\_list\_layout-cwidth\_opt = 'X'.Â Â Â Â "the column width is adjusted,
Â Â Â Â l\_list\_layout-no\_toolbar = 'X'.Â Â Â Â "the toolbar is suppressed.
Â Â Â Â list\_viewer->set\_table\_for\_first\_display(
Â Â Â Â Â Â Â Â  EXPORTING i\_structure\_name = 'DEMOFLI'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  is\_layoutÂ Â Â Â Â Â Â Â = l\_list\_layout
Â Â Â Â Â Â Â Â  CHANGINGÂ Â it\_outtabÂ Â Â Â Â Â Â Â = l\_flight\_tab ).
Â Â ENDMETHOD.Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â "fill\_list
Â Â METHOD close\_box.
Â Â Â Â list\_viewer->free( ).
Â Â Â Â container\_box->free( ).
Â Â Â Â CLEAR: list\_viewer,
Â Â Â Â Â Â Â Â Â Â  container\_box.
Â Â ENDMETHOD.Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â "close\_box
ENDCLASS.Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â "screen\_handler IMPLEMENTATION
\*&---------------------------------------------------------------------\*
\*& Processing Blocks called by the Runtime EnvironmentÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  \*
\*&---------------------------------------------------------------------\*
\* Event Block START-OF-SELECTION
START-OF-SELECTION.
Â Â CALL SCREEN 100.
\* Dialog Module PBO
MODULE status\_0100 OUTPUT.
Â Â SET PF-STATUS 'SCREEN\_100'.
Â Â SET TITLEBAR 'TIT\_100'.
Â Â screen\_handler=>create\_screen( ).
ENDMODULE.Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â "status\_0100 OUTPUT
\* Dialog Module PAI
MODULE cancel INPUT.
Â Â LEAVE PROGRAM.
ENDMODULE.Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â "cancel INPUT

Description

The screen of dynpro 100 contains a custom control named CUSTOM\_CONTROL that covers the entire screen area. In the flow logic, both dialog modules status\_0100 and cancel are called. The addition AT EXIT-COMMAND is used after PAI.

In the GUI status SCREEN\_100, the usual symbols of the status bar are filled with function codes of the type E. The PAI module is only used to exit the program. The most important action at PBO is calling the static method create\_screen of the local class screen\_handler, in which an object of this class is created.

The instance constructor of the class screen\_handler creates container controls for a picture control and a tree control and afterwards both application controls.

-   First, a docking control is created with the local reference variable l\_docking. By passing the constant dock\_at\_left of the class CL\_GUI\_DOCKING\_CONTAINER to the parameter side, a new area is created on the left side of the screen with a width of 135 pixels.

-   This screen area is split into two horizontal areas. This is done using the local reference variable l\_splitter, which creates an object of the class CL\_GUI\_SPLITTER\_CONTAINER and passes the reference to the docking control to the parameter parent.

-   Using the functional method get\_container, the references to both areas of the splitter control are determined in the local reference variables l\_container\_top and l\_container\_bottom . Afterwards, the application controls picture and tree are created, at which the parameter parent of each constructor receives one of the references to the areas of the splitter control.

By calling the method set\_registered\_events, the double-click event for the nodes of the tree is activated as a system event. The method handle\_node\_double\_click is registered as the event handler.

When the instance constructor is exited, all reference variables are deleted. However, the control objects created in the constructor with local reference variables are not collected by the garbage collector, since they are persisted by references from the CFW.

The method fill\_picture imports an image file in GIF format from the MIME Repository into a byte string l\_pict\_wa, which is then filled into an internal table l\_pict\_tab. The function module DP\_CREATE\_URL creates an URL address for this table in the local variable l\_url, which is passed to the method load\_picture\_from\_url of the picture control.

The method fill\_tree creates the tree structure of the tree control in the internal table l\_node\_table of the line structure ABDEMONODE. Every line of the table describes a node of the tree structure and must have a unique node key node\_key for it. The components relatkey and relatship describe the relationships among the nodes.

The node table is generated from the content of the database table SPFLI. In the subnodes, the standard icons for non-expanded and expanded nodes are replaced with aircraft icons.

The event handler method handle\_node\_double\_click imports the parameter node\_key of the event node\_double\_click of the class CL\_GUI\_SIMPLE\_TREE, which contains the key of the selected node. Depending on the content, the methods fill\_html or fill\_list are called.

When the method fill\_html is called for the first time, it creates an object of the class CL\_GUI\_HTML\_VIEWER and links the associated HTML control with the area of the container control to which the reference in container\_html points (with the custom control on the dynpro). In accordance with the selected node, the web address of an airline is read and passed to the method show\_url of the HTML control, which displays the Homepage of the airline.

The method fill\_list creates a dialog box container and an ALV grid control that is displayed in the dialog box. The event handler close\_box is registered for the dialog box container. Activation using the method set\_registered\_events is not necessary here, as the event is activated by the class itself as system event when the object is created. A list is created for the selected node. By assigning values to some components of the structure l\_list\_layout of the type lvc\_s\_layo, it is determined that the list title is displayed in small format, and that the column width of the list is optimized, and that the list should not have an application toolbar. By calling the method set\_table\_for\_first\_display of the ALV grid control, the list is displayed in the dialog box.

The event handler method close\_box closes the dialog box. To do this, the methods free of the controls in question are called one after another and the associated reference variables initialized. After that, if a node is selected to create a list, both controls are created again in the method fill\_list. All other controls of the program cannot be deleted by the user and are available until the program ends.

Hint

The class CL\_SALV\_TABLE and other classes with the prefix CL\_SALV\_ offer easier to use ways of displaying ALV controls.



**ðŸ“– Source**: [abendynpro_cfw_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abendynpro_cfw_abexa.htm)

### abendynpro_custom_control_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abendynpro_custom_control_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abendynpro_custom_control_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 755, Â©Copyright 2020 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [General Dynpros](javascript:call_link\('abenabap_dynpros.htm'\))Â â†’Â  [dynpro - Screen and Screen Elements](javascript:call_link\('abenabap_dynpros_screen.htm'\))Â â†’Â  [dynpro - Examples of Screen Elements](javascript:call_link\('abenscreen_elements_abexas.htm'\))Â â†’Â 

dynpro - CFW Events

This example shows how CFW events are handled.

Source Code

REPORT demo\_custom\_control .
\* Declarations \*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*
CLASS event\_handler DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS: handle\_f1 FOR EVENT f1 OF cl\_gui\_textedit
Â Â Â Â Â Â Â Â Â Â Â Â  IMPORTING sender,
Â Â Â Â Â Â Â Â Â Â Â Â  handle\_f4 FOR EVENT f4 OF cl\_gui\_textedit
Â Â Â Â Â Â Â Â Â Â Â Â  IMPORTING sender.
ENDCLASS.
DATA: ok\_code LIKE sy-ucomm,
Â Â Â Â Â Â save\_ok LIKE sy-ucomm.
DATA: init,
Â Â Â Â Â Â container TYPE REF TO cl\_gui\_custom\_container,
Â Â Â Â Â Â editorÂ Â Â Â TYPE REF TO cl\_gui\_textedit.
DATA: event\_tab TYPE cntl\_simple\_events,
Â Â Â Â Â Â eventÂ Â Â Â  TYPE cntl\_simple\_event.
DATA: line(256) TYPE c,
Â Â Â Â Â Â text\_tab LIKE STANDARD TABLE OF line,
Â Â Â Â Â Â field LIKE line.
DATA handle TYPE REF TO event\_handler.
\* Reporting Events \*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*
START-OF-SELECTION.
Â Â line = 'First line in TextEditControl'.
Â Â APPEND line TO text\_tab.
Â Â line = '--------------------------------------------------'.
Â Â APPEND line TO text\_tab.
Â Â line = '...'.
Â Â APPEND line TO text\_tab.
Â Â CALL SCREEN 100.
\* Dialog Modules \*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*
MODULE status\_0100 OUTPUT.
Â Â SET PF-STATUS 'SCREEN\_100'.
Â Â IF init is initial.
Â Â Â Â init = 'X'.
Â Â Â Â CREATE OBJECT: container EXPORTING container\_name = 'TEXTEDIT',
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  editorÂ Â Â Â EXPORTING parent = container,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  handle.
Â Â Â Â event-eventid = cl\_gui\_textedit=>event\_f1.
Â Â Â Â event-appl\_event = ' '.Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  "system event
Â Â Â Â APPEND event TO event\_tab.
Â Â Â Â event-eventid = cl\_gui\_textedit=>event\_f4.
Â Â Â Â event-appl\_event = 'X'.Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  "application event
Â Â Â Â APPEND event TO event\_tab.
Â Â Â Â editor->set\_registered\_events(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  EXPORTING events = event\_tab ).
Â Â Â Â SET HANDLER handle->handle\_f1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â handle->handle\_f4 FOR editor.
Â Â ENDIF.
Â Â editor->set\_text\_as\_stream( EXPORTING text = text\_tab ).
ENDMODULE.
MODULE cancel INPUT.
Â Â LEAVE PROGRAM.
ENDMODULE.
MODULE user\_command\_0100 INPUT.
Â Â save\_ok = ok\_code.
Â Â CLEAR ok\_code.
Â Â CASE save\_ok.
Â Â Â Â WHEN 'INSERT'.
Â Â Â Â Â Â editor->get\_text\_as\_stream( IMPORTING text = text\_tab ).
Â Â Â Â WHEN 'F1'.
Â Â Â Â Â Â MESSAGE i888(sabapdemos) WITH text-001.
Â Â Â Â WHEN OTHERS.
Â Â Â Â Â Â MESSAGE i888(sabapdemos) WITH text-002.
Â Â Â Â Â Â cl\_gui\_cfw=>dispatch( ).Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â "for application events
Â Â Â Â Â Â MESSAGE i888(sabapdemos) WITH text-003.
Â Â ENDCASE.
Â Â SET SCREEN 100.
ENDMODULE.
\* Class Implementations \*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*
CLASS event\_handler IMPLEMENTATION.
Â Â METHOD handle\_f1.
Â Â Â Â DATA row TYPE i.
Â Â Â Â MESSAGE i888(sabapdemos) WITH text-004.
Â Â Â Â sender->get\_selection\_pos(
Â Â Â Â Â Â Â Â  IMPORTING from\_line = row ).
Â Â Â Â sender->get\_line\_text(
Â Â Â Â Â Â Â Â  EXPORTING line\_number = row
Â Â Â Â Â Â Â Â  IMPORTING text = field ).
Â Â Â Â cl\_gui\_cfw=>set\_new\_ok\_code(Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â "raise PAI for
Â Â Â Â Â Â Â Â  EXPORTING new\_code = 'F1' ).Â Â Â Â Â Â Â Â Â Â  "system events
Â Â Â Â cl\_gui\_cfw=>flush( ).
Â Â ENDMETHOD.
Â Â METHOD handle\_f4.
Â Â Â Â DATA row TYPE i.
Â Â Â Â MESSAGE i888(sabapdemos) WITH text-005.
Â Â Â Â sender->get\_selection\_pos(
Â Â Â Â Â Â Â Â  IMPORTING from\_line = row ).
Â Â Â Â sender->get\_line\_text(
Â Â Â Â Â Â Â Â  EXPORTING line\_number = row
Â Â Â Â Â Â Â Â  IMPORTING text = field ).
Â Â Â Â cl\_gui\_cfw=>flush( ).
Â Â ENDMETHOD.
ENDCLASS.

Description

Dynpro 100 contains an output field field and a custom control called textedit. The flow logic of dynpro 100 is:

PROCESS BEFORE OUTPUT.
Â Â MODULE status\_0100.
PROCESS AFTER INPUT.
Â Â MODULE cancel AT EXIT-COMMAND.
Â Â MODULE user\_command\_0100.

In the GUI status SCREEN\_100, the function codes BACK, EXIT, and CANCEL are created with type E and the function code INSERT is created without any specific type.

The program contains a local class called event\_handler with event handlers for the events F1 and F4 of global class CL\_GUI\_TEXTEDIT. When the program is executed, dynpro 100 instantiates objects of the classes CL\_GUI\_CUSTOM\_CONTAINER, CL\_GUI\_TEXTEDIT, and event\_handler after PBO.

The container control is associated with the custom control on the dynpro, and the instance of the textedit control is associated with this container. The events F1 and F4 of the textedit control are registered using the method SET\_REGISTERED\_EVENTS to ensure that they are passed to the current AS instance. F1 is defined as a system event and F4 as an application event. The event handling methods of the instance handle of the class event\_handler are registered as handlers for the events.

Before the dynpro is sent, the textedit control is filled with the contents of table text\_tab. As long as the dynpro is displayed, the user is allowed to edit the text. When INSERT is selected, PAI is raised, and the current text from the textedit control is passed to the table text\_tab.

If F1 is selected on the textedit control, the method handle\_f1 is executed. This assigns the contents of the line to the field field: Calling the method set\_new\_ok\_code raises PAI. This is the only way to ensure that the PBO is processed and the contents of field are sent to the dynpro.

If F4 is selected on the textedit control, PAI is raised. The method DISPATCH is called and raises the method handle\_f4. This assigns the contents of the line to the field field: Since the system then automatically returns to PAI processing, PBO is also processed and the field contents are transported.

In either case, regardless of whether the user chooses F1 or F4, the contents of the textedit control are not passed to the internal table text\_tab. Consequently, the textedit control is overwritten with the previous content of text\_tab after PBO.



**ðŸ“– Source**: [abendynpro_custom_control_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abendynpro_custom_control_abexa.htm)

### abenhtml_browser_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenhtml_browser_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenhtml_browser_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 755, Â©Copyright 2020 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [General Dynpros](javascript:call_link\('abenabap_dynpros.htm'\))Â â†’Â  [dynpro - Screen and Screen Elements](javascript:call_link\('abenabap_dynpros_screen.htm'\))Â â†’Â  [dynpro - Examples of Screen Elements](javascript:call_link\('abenscreen_elements_abexas.htm'\))Â â†’Â 

dynpro - HTML Browser

This example demonstrates the encapsulation of the browser controls in a class.

Source Code

REPORT demo\_html\_browser.
PARAMETERS: modalÂ Â AS CHECKBOX DEFAULT 'X',
Â Â Â Â Â Â Â Â Â Â Â Â no\_box AS CHECKBOX.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
Â Â PRIVATE SECTION.
Â Â Â Â TYPES: pict\_line(1022) TYPE x,
Â Â Â Â Â Â Â Â Â Â  pict\_tabÂ Â Â Â Â Â Â Â TYPE STANDARD TABLE OF pict\_line
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WITH EMPTY KEY.
Â Â Â Â CLASS-METHODS get\_pict\_tab
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â mime\_urlÂ Â Â Â Â Â Â Â TYPE csequence
Â Â Â Â Â Â RETURNING
Â Â Â Â Â Â Â Â VALUE(pict\_tab) TYPE pict\_tab.
Â Â Â Â CLASS-METHODS handle\_sapevent
Â Â Â Â Â Â FOR EVENT sapevent
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â OF cl\_abap\_browser
Â Â Â Â Â Â IMPORTING action.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA error\_list TYPE cl\_abap\_browser=>html\_table.
Â Â Â Â DATA(title) = CONV cl\_abap\_browser=>title( 'HTML Browser Demo' ).
Â Â Â Â SET HANDLER handle\_sapevent.
Â Â Â Â DATA(pict\_tab) = get\_pict\_tab(
Â Â Â Â Â Â mime\_url = '/SAP/PUBLIC/BC/ABAP/mime\_demo/ABAP\_Docu\_Logo.gif' ).
Â Â Â Â DATA(ext\_data) =
Â Â Â Â Â Â VALUE cl\_abap\_browser=>load\_tab( ( name = 'PICT.GIF'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  type = 'image'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  dref = REF #( pict\_tab ) ) ).
Â Â Â Â DATA(html\_str) =
Â Â Â Â Â Â '<htm1 lang="EN">' &&
Â Â Â Â Â Â '<head>' &&
Â Â Â Â Â Â '<meta name="Demo" content="Test">' &&
Â Â Â Â Â Â '<style type="text/css">' &&
Â Â Â Â Â Â 'span.h1 {font-size: 150%; color:#000080; ' &&
Â Â Â Â Â Â 'font-weight:bold;}' &&
Â Â Â Â Â Â '</style>' &&
Â Â Â Â Â Â '</head>' &&
Â Â Â Â Â Â '<body>' &&
Â Â Â Â Â Â '<p><span class="h1">HTML</span></p>' &&
Â Â Â Â Â Â '<A HREF="http://help.sap.com/">Weblink</A>' &&
Â Â Â Â Â Â '<br><br><A href=sapevent:"ActionCode">SAPevent</A>' &&
Â Â Â Â Â Â '<br><br>External Picture:' &&
Â Â Â Â Â Â '<br><br><img src="PICT.GIF" alt="An example figure" >' &&
Â Â Â Â Â Â '</body>' &&
Â Â Â Â Â Â '</html>'.
Â Â Â Â cl\_abap\_browser=>show\_html(
Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â html\_string = html\_str
Â Â Â Â Â Â Â Â modalÂ Â Â Â Â Â  = modal
Â Â Â Â Â Â Â Â dialogÂ Â Â Â Â Â = COND #( WHEN no\_box = abap\_false
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  THEN abap\_true )
Â Â Â Â Â Â Â Â titleÂ Â Â Â Â Â  = title
Â Â Â Â Â Â Â Â buttonsÂ Â Â Â  = cl\_abap\_browser=>navigate\_html
Â Â Â Â Â Â Â Â formatÂ Â Â Â Â Â = cl\_abap\_browser=>landscape
Â Â Â Â Â Â Â Â sizeÂ Â Â Â Â Â Â Â = cl\_abap\_browser=>medium
Â Â Â Â Â Â Â Â data\_tableÂ Â = ext\_data
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â  html\_errors = error\_list ).
Â Â Â Â IF modal IS INITIAL AND no\_box IS INITIAL.
Â Â Â Â Â Â MESSAGE 'First call of browser' TYPE 'I'.
Â Â Â Â ENDIF.
Â Â Â Â IF error\_list IS NOT INITIAL.
Â Â Â Â Â Â LOOP AT error\_list ASSIGNING FIELD-SYMBOL(<error>).
Â Â Â Â Â Â Â Â <error> = escape( valÂ Â Â Â = <error>
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â format = cl\_abap\_format=>e\_html\_text ).
Â Â Â Â Â Â Â Â <error> = <error> && '<br>'.
Â Â Â Â Â Â ENDLOOP.
Â Â Â Â Â Â INSERT '<html><body>' INTO error\_list INDEX 1.
Â Â Â Â Â Â APPEND '</body></html>' TO error\_list.
Â Â Â Â Â Â cl\_abap\_browser=>show\_html(
Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â htmlÂ Â Â Â Â Â  = error\_list
Â Â Â Â Â Â Â Â Â Â modalÂ Â Â Â Â Â  = modal
Â Â Â Â Â Â Â Â Â Â dialogÂ Â Â Â Â Â = COND #( WHEN no\_box = abap\_false
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  THEN abap\_true )
Â Â Â Â Â Â Â Â Â Â titleÂ Â Â Â Â Â = 'HTML Validation Errors'
Â Â Â Â Â Â Â Â Â Â check\_html = abap\_false
Â Â Â Â Â Â Â Â Â Â printingÂ Â  = abap\_true ).
Â Â Â Â Â Â IF modal IS INITIAL AND no\_box IS INITIAL.
Â Â Â Â Â Â Â Â MESSAGE 'Second call of browser' TYPE 'I'.
Â Â Â Â Â Â ENDIF.
Â Â Â Â ENDIF.
Â Â Â Â REPLACE '<htm1'Â Â IN html\_str WITHÂ Â '<html'.
Â Â Â Â cl\_abap\_browser=>show\_html(
Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â html\_string = html\_str
Â Â Â Â Â Â Â Â modalÂ Â Â Â Â Â  = modal
Â Â Â Â Â Â Â Â dialogÂ Â Â Â Â Â = COND #( WHEN no\_box = abap\_false
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  THEN abap\_true )
Â Â Â Â Â Â Â Â titleÂ Â Â Â Â Â = title
Â Â Â Â Â Â Â Â buttonsÂ Â Â Â = cl\_abap\_browser=>navigate\_html
Â Â Â Â Â Â Â Â formatÂ Â Â Â  = cl\_abap\_browser=>landscape
Â Â Â Â Â Â Â Â sizeÂ Â Â Â Â Â  = cl\_abap\_browser=>medium
Â Â Â Â Â Â Â Â data\_table = ext\_data
Â Â Â Â Â Â Â Â check\_html = abap\_true
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â  html\_errors = error\_list ).
Â Â Â Â IF modal IS INITIAL AND no\_box IS INITIAL.
Â Â Â Â Â Â MESSAGE 'Third call of browser' TYPE 'I'.
Â Â Â Â ENDIF.
Â Â Â Â IF error\_list IS INITIAL.
Â Â Â Â Â Â MESSAGE 'No errors in HTML' TYPE 'S'.
Â Â Â Â ENDIF.Â Â ENDMETHOD.
Â Â METHOD handle\_sapevent.
Â Â Â Â MESSAGE \`Handling: \` && action TYPE 'I'.
Â Â ENDMETHOD.
Â Â METHOD get\_pict\_tab.
Â Â Â Â cl\_mime\_repository\_api=>get\_api( )->get(
Â Â Â Â Â Â EXPORTING i\_url = mime\_url
Â Â Â Â Â Â IMPORTING e\_content = DATA(pict\_wa)
Â Â Â Â Â Â EXCEPTIONS OTHERS = 4 ).
Â Â Â Â IF sy-subrc = 4.
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â pict\_tab =
Â Â Â Â Â Â VALUE #( LET l1 = xstrlen( pict\_wa ) l2 = l1 - 1022 IN
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  FOR j = 0 THEN j + 1022Â Â UNTIL j >= l1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( COND #( WHEN j <= l2 THEN
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â pict\_wa+j(1022)
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ELSE pict\_wa+j ) ) ).
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

The class CL\_ABAP\_BROWSER encapsulates the use of the class CL\_GUI\_HTML\_VIEWER. The static method SHOW\_HTML can be used to display a HTML file in a modal or modeless dialog box or in the same window as the current dynpro. External data, in this case a picture loaded from the MIME Repository, can be passed and displayed. The SAPEVENT events are still passed on and can be handled by the user.

-   The HTML file passed first has errors, since the <html> tag is written incorrectly. In SAP systems, this case always produces an appropriate message.

-   The error list is also represented using SHOW\_HTML, where the error check for this simple purpose is disabled by passing a blank to the appropriate parameter.

-   The error in question is corrected in the HTML file and passed again, with the error check enabled this time.

When the program starts, the file can be display in a modal window, a modeless window, or without a dialog box. If a modeless dialog box is chosen, container dynpros and information messages must be used to keep the dialog box alive. If no messages were sent, the modeless dialog box would not be visible.



**ðŸ“– Source**: [abenhtml_browser_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenhtml_browser_abexa.htm)

### abenhtml_input_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenhtml_input_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenhtml_input_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 755, Â©Copyright 2020 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [General Dynpros](javascript:call_link\('abenabap_dynpros.htm'\))Â â†’Â  [dynpro - Screen and Screen Elements](javascript:call_link\('abenabap_dynpros_screen.htm'\))Â â†’Â  [dynpro - Examples of Screen Elements](javascript:call_link\('abenscreen_elements_abexas.htm'\))Â â†’Â 

dynpro - Input in HTML File

This example demonstrates how input in a HTML file can be processed.

Source Code

REPORT demo\_html\_input.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
Â Â PRIVATE SECTION.
Â Â Â Â CLASS-METHODS handle\_sapevent
Â Â Â Â Â Â FOR EVENT sapevent
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â OF cl\_abap\_browser
Â Â Â Â Â Â IMPORTING action
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â query\_table.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA error\_list TYPE cl\_abap\_browser=>html\_table.
Â Â Â Â SET HANDLER handle\_sapevent.
Â Â Â Â DATA(html\_str) =
Â Â Â Â Â Â  \`<html>\`
Â Â Â Â && \`Â Â <head>\`
Â Â Â Â && \`Â Â Â Â <meta http-equiv="content-type" \`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â content="text/html; \`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â charset=utf-8">\`
Â Â Â Â && \`Â Â Â Â <script language="JavaScript">\`
Â Â Â Â && \`Â Â Â Â Â Â function sendInput(form) \`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â { fname=form.name;Â Â Â Â Â Â  \`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â Â Â document\[fname\].submit();} \`
Â Â Â Â && \`Â Â Â Â Â Â function InputKeyDown(form) {\`
Â Â Â Â && \`Â Â Â Â Â Â Â Â if(event.keyCode == 13) {\`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â Â Â fname=form.name;\`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â Â Â document\[fname\].submit();} }\`
Â Â Â Â && \`Â Â Â Â </script>\`
Â Â Â Â && \`Â Â </head>\`
Â Â Â Â && \`Â Â <body>\`
Â Â Â Â && \`Â Â Â Â <form name="INPUT" accept-charset="utf-8" \`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â method="post" action="SAPEVENT:INPUT"> \`
Â Â Â Â && \`Â Â Â Â Â Â <input type="text" id="in1" name="field1" \`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â Â Â  size=30 maxlength=30 title="" value="aaa" \`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â Â Â  onKeyDown="InputKeyDown(this.form);"><br>\`
Â Â Â Â && \`Â Â Â Â Â Â <input type="text" id="in2" name="field2" \`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â Â Â  size=30 maxlength=30 title="" value="bbb" \`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â Â Â  onKeyDown="InputKeyDown(this.form);"><br>\`
Â Â Â Â && \`Â Â Â Â Â Â <input type="text" id="in3" name="field3" \`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â Â Â  size=30 maxlength=30 title="" value="ccc" \`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â Â Â  onKeyDown="InputKeyDown(this.form);"><br><br>\`
Â Â Â Â && \`Â Â Â Â  <button id="enterButton" type="button" \`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â Â Â  title="Enter" onClick="sendInput(INPUT);" \`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â Â Â  onKeypress="if(event.keycode=13) \`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â Â Â  sendInput(INPUT);">\`
Â Â Â Â && \`Â Â Â Â Â Â Â Â Â Â Â Â  Enter</button>\`
Â Â Â Â && \`Â Â Â Â </form>\`
Â Â Â Â && \`Â Â </body>\`
Â Â Â Â && \`</html>\`.
Â Â Â Â cl\_abap\_browser=>show\_html(
Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â html\_string = html\_str
Â Â Â Â Â Â Â Â titleÂ Â Â Â Â Â  = 'Input Demo'
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â  html\_errors = error\_list ).
Â Â Â Â IF error\_list IS NOT INITIAL.
Â Â Â Â Â Â MESSAGE 'Error in HTML' TYPE 'I' DISPLAY LIKE 'E'.
Â Â Â Â ENDIF.Â Â ENDMETHOD.
Â Â METHOD handle\_sapevent.
Â Â Â Â DATA(out) = cl\_demo\_output\_stream=>open( ).
Â Â Â Â SET HANDLER cl\_demo\_output\_html=>handle\_output FOR out.
Â Â Â Â out->write\_data( iv\_name = 'ACTION'Â Â Â Â Â Â ia\_value = action ).
Â Â Â Â out->write\_data( iv\_name = 'QUERY\_TABLE' ia\_value = query\_table ).
Â Â Â Â out->close( ).
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

This example creates a HTML file containing multiple input fields, a pushbutton, and JavaScript functions for handling the input. The form INPUT uses method="post" to send the input data. The HTML control in CFW uses the parameter QUERY\_TABLE of the event SAPEVENT to pass this data to its handler. The class CL\_ABAP\_BROWSER (a wrapper for the class CL\_GUI\_HTML\_VIEWER also passes this parameter and the user input can be used in the ABAP program.

Also see the corresponding executable example for [ICF](javascript:call_link\('abenicf_post_service_abexa.htm'\)).



**ðŸ“– Source**: [abenhtml_input_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenhtml_input_abexa.htm)

### abenmime_pictures_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenmime_pictures_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenmime_pictures_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 755, Â©Copyright 2020 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [General Dynpros](javascript:call_link\('abenabap_dynpros.htm'\))Â â†’Â  [dynpro - Screen and Screen Elements](javascript:call_link\('abenabap_dynpros_screen.htm'\))Â â†’Â  [dynpro - Examples of Screen Elements](javascript:call_link\('abenscreen_elements_abexas.htm'\))Â â†’Â 

dynpro - Images in HTML

The example demonstrates how images are displayed in HTML controls.

Source Code

REPORT demo\_picture\_in\_html.
CLASS picture\_demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS: main1, main2.
Â Â PRIVATE SECTION.
Â Â Â Â TYPES: htmlÂ Â Â Â  TYPE c LENGTH 255,
Â Â Â Â Â Â Â Â Â Â  html\_tab TYPE STANDARD TABLE OF html WITH EMPTY KEY.
Â Â Â Â TYPES: pict\_line(1022) TYPE x,
Â Â Â Â Â Â Â Â Â Â  pict\_tabÂ Â Â Â Â Â Â Â TYPE STANDARD TABLE OF pict\_line
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WITH EMPTY KEY.
Â Â Â Â CLASS-METHODS get\_pict\_tab
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â mime\_urlÂ Â Â Â Â Â Â Â TYPE csequence
Â Â Â Â Â Â RETURNING
Â Â Â Â Â Â Â Â VALUE(pict\_tab) TYPE pict\_tab.
ENDCLASS.
CLASS picture\_demo IMPLEMENTATION.
Â Â METHOD main1.
Â Â Â Â DATA html\_url TYPE c LENGTH 255.
Â Â Â Â DATA pict\_url TYPE c LENGTH 255.
Â Â Â Â DATA(custom\_container) = NEW
Â Â Â Â Â Â cl\_gui\_custom\_container( container\_name = 'CUSTOM\_CONTAINER1' ).
Â Â Â Â DATA(html\_control) = NEW
Â Â Â Â  cl\_gui\_html\_viewer( parent = custom\_container ).
Â Â Â Â DATA(pict\_tab) = get\_pict\_tab(
Â Â Â Â Â Â mime\_url = '/SAP/PUBLIC/BC/ABAP/mime\_demo/ABAP\_Docu\_Logo.gif' ).
Â Â Â Â html\_control->load\_data(
Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â urlÂ Â Â Â Â Â Â Â Â Â = 'picture\_url'
Â Â Â Â Â Â Â Â typeÂ Â Â Â Â Â Â Â  = 'image'
Â Â Â Â Â Â Â Â subtypeÂ Â Â Â Â Â = '.gif'
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â assigned\_url = pict\_url
Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â data\_tableÂ Â  = pict\_tab ).
Â Â Â Â DATA(html\_tab) = VALUE html\_tab(
Â Â Â Â Â Â ( '<html><body><basefont face="arial">' )
Â Â Â Â Â Â ( 'Picture with CL\_GUI\_HTML\_VIEWER<br><br>' )
Â Â Â Â Â Â ( '<img src="' && pict\_url && '">' )
Â Â Â Â Â Â ( '</body></html>' ) ).
Â Â Â Â html\_control->load\_data(
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â assigned\_url = html\_url
Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â data\_tableÂ Â  = html\_tab ).
Â Â Â Â html\_control->show\_url(
Â Â Â Â Â Â  EXPORTING
Â Â Â Â Â Â Â Â  url = html\_url ).
Â Â ENDMETHOD.
Â Â METHOD main2.
Â Â Â Â DATA(custom\_container) = NEW
Â Â Â Â Â Â cl\_gui\_custom\_container( container\_name = 'CUSTOM\_CONTAINER2' ).
Â Â Â Â DATA(pict\_tab) = get\_pict\_tab(
Â Â Â Â Â Â mime\_url = '/SAP/PUBLIC/BC/ABAP/mime\_demo/ABAP\_Docu\_Logo.gif' ).
Â Â Â Â DATA(ext\_data) =
Â Â Â Â Â Â VALUE cl\_abap\_browser=>load\_tab( ( name = 'PICT.GIF'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  type = 'image'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  dref = REF #( pict\_tab ) ) ).
Â Â Â Â DATA(html\_tab) = VALUE cl\_abap\_browser=>html\_table(
Â Â Â Â Â Â ( '<html><body><basefont face="arial">' )
Â Â Â Â Â Â ( 'Picture with CL\_ABAP\_BROWSER<br><br>' )
Â Â Â Â Â Â ( '<img src="PICT.GIF">' )
Â Â Â Â Â Â ( '</body></html>' ) ).
Â Â Â Â cl\_abap\_browser=>show\_html( html = html\_tab
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â container = custom\_container
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â data\_tableÂ Â = ext\_data ).
Â Â ENDMETHOD.
Â Â METHOD get\_pict\_tab.
Â Â Â Â cl\_mime\_repository\_api=>get\_api( )->get(
Â Â Â Â Â Â EXPORTING i\_url = mime\_url
Â Â Â Â Â Â IMPORTING e\_content = DATA(pict\_wa)
Â Â Â Â Â Â EXCEPTIONS OTHERS = 4 ).
Â Â Â Â IF sy-subrc = 4.
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â pict\_tab =
Â Â Â Â Â Â VALUE #( LET l1 = xstrlen( pict\_wa ) l2 = l1 - 1022 IN
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  FOR j = 0 THEN j + 1022Â Â UNTIL j >= l1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( COND #( WHEN j <= l2 THEN
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â pict\_wa+j(1022)
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ELSE pict\_wa+j ) ) ).
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â picture\_demo=>main1( ).
Â Â picture\_demo=>main2( ).
Â Â CALL SCREEN 100.

Description

Images loaded here from the MIME repository using an API are displayed in two browser controls.

-   The method main1 uses the class CL\_GUI\_HTML\_VIEWER directly. Its method LOAD\_DATA is used to provide a URL for the image in the internal table, which is then used on the HTML page.

-   The method main2 uses the class CL\_ABAP\_BROWSER as a wrapper for the direct use of CL\_GUI\_HTML\_VIEWER and calls of the method LOAD\_DATA. A reference, associated with the name used in the HTML page, is passed to the internal table.

See also the [executable example](javascript:call_link\('abenicf_mime_pictures_abexa.htm'\)) for direct access to objects from the MIME repository using ICF.



**ðŸ“– Source**: [abenmime_pictures_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenmime_pictures_abexa.htm)

### abenmime_html_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenmime_html_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenmime_html_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 755, Â©Copyright 2020 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [General Dynpros](javascript:call_link\('abenabap_dynpros.htm'\))Â â†’Â  [dynpro - Screen and Screen Elements](javascript:call_link\('abenabap_dynpros_screen.htm'\))Â â†’Â  [dynpro - Examples of Screen Elements](javascript:call_link\('abenscreen_elements_abexas.htm'\))Â â†’Â 

dynpro - HTML from the MIME Repository

This example demonstrates how a HTML file is displayed from the MIME repository.

Source Code

REPORT demo\_html\_from\_mime.
CLASS mime\_demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
Â Â PRIVATE SECTION.
Â Â Â Â TYPES: mime\_line(1022) TYPE x,
Â Â Â Â Â Â Â Â Â Â  mime\_tabÂ Â Â Â Â Â Â Â TYPE STANDARD TABLE OF mime\_line
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â WITH EMPTY KEY.
Â Â Â Â CLASS-METHODS get\_mime\_obj
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â mime\_urlÂ Â Â Â Â Â Â Â TYPE csequence
Â Â Â Â Â Â RETURNING
Â Â Â Â Â Â Â Â VALUE(mime\_tab) TYPE mime\_tab.
ENDCLASS.
CLASS mime\_demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA html\_url TYPE c LENGTH 255.
Â Â Â Â DATA(custom\_container) = NEW
Â Â Â Â Â Â cl\_gui\_custom\_container( container\_name = 'CUSTOM\_CONTAINER' ).
Â Â Â Â DATA(html\_control) = NEW
Â Â Â Â  cl\_gui\_html\_viewer( parent = custom\_container ).
Â Â Â Â DATA(pict\_tab) = get\_mime\_obj(
Â Â Â Â Â Â mime\_url = '/SAP/PUBLIC/BC/ABAP/mime\_demo/ABAP\_Docu\_Logo.gif' ).
Â Â Â Â html\_control->load\_data(
Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â urlÂ Â Â Â Â Â Â Â Â Â = 'picture\_url'
Â Â Â Â Â Â Â Â typeÂ Â Â Â Â Â Â Â  = 'image'
Â Â Â Â Â Â Â Â subtypeÂ Â Â Â Â Â = '.gif'
Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â data\_tableÂ Â  = pict\_tab ).
Â Â Â Â DATA(html\_tab) = get\_mime\_obj(
Â Â Â Â Â Â mime\_url = '/SAP/PUBLIC/BC/ABAP/mime\_demo/demo\_html.html' ).
Â Â Â Â html\_control->load\_data(
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â assigned\_url = html\_url
Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â data\_tableÂ Â  = html\_tab ).
Â Â Â Â html\_control->show\_url(
Â Â Â Â Â Â  EXPORTING
Â Â Â Â Â Â Â Â  url = html\_url ).
Â Â ENDMETHOD.
Â Â METHOD get\_mime\_obj.
Â Â Â Â cl\_mime\_repository\_api=>get\_api( )->get(
Â Â Â Â Â Â EXPORTING i\_url = mime\_url
Â Â Â Â Â Â IMPORTING e\_content = DATA(mime\_wa)
Â Â Â Â Â Â EXCEPTIONS OTHERS = 4 ).
Â Â Â Â IF sy-subrc = 4.
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â mime\_tab =
Â Â Â Â Â Â VALUE #( LET l1 = xstrlen( mime\_wa ) l2 = l1 - 1022 IN
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  FOR j = 0 THEN j + 1022Â Â UNTIL j >= l1
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( COND #( WHEN j <= l2 THEN
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â mime\_wa+j(1022)
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ELSE mime\_wa+j ) ) ).
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â mime\_demo=>main( ).
Â Â CALL SCREEN 100.

Description

An API is used to load a HTML file and an image from the MIME repository and save them in internal tables. The method LOAD\_DATA of the class CL\_GUI\_HTML\_VIEWER is used to associate the data with the HTML control of CFW and the HTML file is displayed. The name of the image in the HTML file is the same as the URL passed to the method LOAD\_DATA for the image. LOAD\_DATA is given a URL for the image in the internal table and is used on the HTML page.

See also the [executable example](javascript:call_link\('abenicf_mime_pictures_abexa.htm'\)) for direct access to objects from the MIME repository using ICF.



**ðŸ“– Source**: [abenmime_html_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenmime_html_abexa.htm)

### abentext_output_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abentext_output_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abentext_output_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 755, Â©Copyright 2020 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [General Dynpros](javascript:call_link\('abenabap_dynpros.htm'\))Â â†’Â  [dynpro - Screen and Screen Elements](javascript:call_link\('abenabap_dynpros_screen.htm'\))Â â†’Â  [dynpro - Examples of Screen Elements](javascript:call_link\('abenscreen_elements_abexas.htm'\))Â â†’Â 

dynpro - Text Output

This example demonstrates the output of unformatted text.

Source Code

REPORTÂ Â demo\_show\_text.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA: textÂ Â Â Â Â Â  TYPE REF TO cl\_demo\_text,
Â Â Â Â Â Â Â Â Â Â text\_table TYPE cl\_demo\_text=>t\_text,
Â Â Â Â Â Â Â Â Â Â text\_lineÂ Â TYPE cl\_demo\_text=>t\_line.
Â Â Â Â text = cl\_demo\_text=>new( ).
Â Â Â Â text\_line = 'First line of text'.
Â Â Â Â text->add\_line( text\_line ).
Â Â Â Â text->add\_line( ' ' ).
Â Â Â Â DO 10 TIMES.
Â Â Â Â Â Â CLEAR text\_line.
Â Â Â Â Â Â text\_line(3) = sy-index.
Â Â Â Â Â Â text\_lineÂ Â = |Table line { text\_line }|.
Â Â Â Â Â Â APPEND text\_line TO text\_table.
Â Â Â Â ENDDO.
Â Â Â Â text->add\_table( text\_table ).
Â Â Â Â text->add\_line( ' ' ).
Â Â Â Â text\_line = 'Last line of text'.
Â Â Â Â text->add\_line( text\_line ).
Â Â Â Â text->display( ).
Â Â Â Â text->delete( ).
Â Â Â Â text->add\_line( 'New text' ).
Â Â Â Â text->display( ).Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description

The program uses the methods of the class CL\_DEMO\_TEXT, which was designed for this purpose. The class encapsulates the use of a text edit control in a dialog box.

Wrappers like this can replace the use of the statement [WRITE](javascript:call_link\('abapwrite-.htm'\)) for simple text output.



**ðŸ“– Source**: [abentext_output_abexa.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abentext_output_abexa.htm)

### abenscreen_elements_abexas.htm

> **ðŸ“– Official SAP Documentation**: [abenscreen_elements_abexas.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenscreen_elements_abexas.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ðŸ“– Source**: [abenscreen_elements_abexas.htm](https://help.sap.com/doc/abapdocu_755_index_htm/7.55/en-US/abenscreen_elements_abexas.htm)


Â  

* * *

AS ABAP Release 755, Â©Copyright 2020 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [General Dynpros](javascript:call_link\('abenabap_dynpros.htm'\))Â â†’Â  [dynpro - Screen and Screen Elements](javascript:call_link\('abenabap_dynpros_screen.htm'\))Â â†’Â 

dynpro - Examples of Screen Elements

Continue
![Example](exa.gif "Example") [dynpro - Status Icons](javascript:call_link\('abendynpro_status_icons_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - Context Menus](javascript:call_link\('abendynpro_context_menu_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - CFW](javascript:call_link\('abendynpro_cfw_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - CFW Events](javascript:call_link\('abendynpro_custom_control_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - HTML Browser](javascript:call_link\('abenhtml_browser_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - Input in HTML File](javascript:call_link\('abenhtml_input_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - Images in HTML](javascript:call_link\('abenmime_pictures_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - HTML from the MIME Repository](javascript:call_link\('abenmime_html_abexa.htm'\))
![Example](exa.gif "Example") [dynpro - Text Output](javascript:call_link\('abentext_output_abexa.htm'\))
