# ABAP - Keyword Documentation / ABAP - Programming Language / Creating Objects and Values / Shared Objects / Shared Objects - Examples

Included pages: 3



## ðŸ”— Source References

**Base URL**: [ABAP Keyword Documentation 7.57](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abenshm_examples_abexas.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenshm_examples_abexas.htm)
- [abenshared_objects_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenshared_objects_abexa.htm)
- [abenshared_objects2_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenshared_objects2_abexa.htm)

**Bundle Contains**: 3 documentation pages
**Version**: ABAP 7.57
**Generated**: 2025-09-01T11:25:45.242Z

---

### abenshm_examples_abexas.htm

> **ðŸ“– Official SAP Documentation**: [abenshm_examples_abexas.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenshm_examples_abexas.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 757, Â©Copyright 2023 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Creating Objects and Values](javascript:call_link\('abencreate_objects.htm'\))Â â†’Â  [Shared Objects](javascript:call_link\('abenabap_shared_objects.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif&sap-language=EN "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback on ABAP Documentation&body=Document: Shared Objects - Examples, ABENSHM_EXAMPLES_ABEXAS, 757%0D%0A%0D%0AError:%0D%0A%0D%0A
%0D%0A%0D%0ASuggestion for improvement:)

Shared Objects - Examples

Continue
![Example](exa.gif "Example") [Shared Objects - Example](javascript:call_link\('abenshared_objects_abexa.htm'\))
![Example](exa.gif "Example") [Shared Objects - Writing to and Reading from an Area](javascript:call_link\('abenshared_objects2_abexa.htm'\))



**ðŸ“– Source**: [abenshm_examples_abexas.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenshm_examples_abexas.htm)

### abenshared_objects_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenshared_objects_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenshared_objects_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 757, Â©Copyright 2023 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Creating Objects and Values](javascript:call_link\('abencreate_objects.htm'\))Â â†’Â  [Shared Objects](javascript:call_link\('abenabap_shared_objects.htm'\))Â â†’Â  [Shared Objects - Examples](javascript:call_link\('abenshm_examples_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif&sap-language=EN "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback on ABAP Documentation&body=Document: Shared Objects - Example, ABENSHARED_OBJECTS_ABEXA, 757%0D%0A%0D%0AError:%0D%0A%0D%0A
%0D%0A%0D%0ASuggestion for improvement:)

Shared Objects - Example

This example demonstrates how shared objects are used.

Source Code Â  

REPORT demo\_shared\_objects.
CLASS demo\_flight\_list\_handler DEFINITION FINAL CREATE PRIVATE.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-DATA flight\_list\_handler
Â Â Â Â TYPE REF TO demo\_flight\_list\_handler.
Â Â Â Â CLASS-METHODS class\_constructor.
Â Â Â Â METHODS get\_flight\_list
Â Â Â Â Â Â RETURNING
Â Â Â Â Â Â Â Â VALUE(flights) TYPE REF TO spfli\_tab
Â Â Â Â Â Â RAISING
Â Â Â Â Â Â  cx\_no\_flights.
Â Â PRIVATE SECTION.
Â Â Â Â DATA area\_handle TYPE REF TO cl\_demo\_flights.
Â Â Â Â METHODS create\_flight\_list
Â Â Â Â Â Â RAISING
Â Â Â Â Â Â Â Â cx\_shm\_attach\_error
Â Â Â Â Â Â Â Â cx\_no\_flights.
ENDCLASS.
CLASS demo\_flight\_list\_handler IMPLEMENTATION.
Â Â METHOD class\_constructor.
Â Â Â Â CREATE OBJECT flight\_list\_handler.
Â Â ENDMETHOD.
Â Â METHOD get\_flight\_list.
Â Â Â Â DATA flight\_list TYPE REF TO cl\_demo\_flight\_list.
Â Â Â Â IF area\_handle IS INITIAL.
Â Â Â Â Â Â TRY.
Â Â Â Â Â Â Â Â Â Â area\_handle = cl\_demo\_flights=>attach\_for\_read( ).
Â Â Â Â Â Â Â Â CATCH cx\_shm\_attach\_error.
Â Â Â Â Â Â Â Â Â Â TRY.
Â Â Â Â Â Â Â Â Â Â Â Â Â Â me->create\_flight\_list( ).
Â Â Â Â Â Â Â Â Â Â Â Â Â Â area\_handle = cl\_demo\_flights=>attach\_for\_read( ).
Â Â Â Â Â Â Â Â Â Â Â Â CATCH cx\_shm\_attach\_error.
Â Â Â Â Â Â Â Â Â Â Â Â Â Â CREATE OBJECT flight\_list.
Â Â Â Â Â Â Â Â Â Â Â Â Â Â flight\_list->set\_flight\_list( ).
Â Â Â Â Â Â Â Â Â Â ENDTRY.
Â Â Â Â Â Â ENDTRY.
Â Â Â Â ENDIF.
Â Â Â Â IF area\_handle IS NOT INITIAL.
Â Â Â Â Â Â flights = REF #( area\_handle->root->flight\_list ).
Â Â Â Â ELSEIF flight\_list IS NOT INITIAL.
Â Â Â Â Â Â flights = REF #( flight\_list->flight\_list ).
Â Â Â Â ELSE.
Â Â Â Â Â Â RAISE EXCEPTION TYPE cx\_no\_flights.
Â Â Â Â ENDIF.
Â Â ENDMETHOD.
Â Â METHOD create\_flight\_list.
Â Â Â Â DATA: flight\_listÂ Â TYPE REF TO cl\_demo\_flight\_list,
Â Â Â Â Â Â Â Â Â Â exc\_refÂ Â Â Â Â Â TYPE REF TO cx\_no\_flights.
Â Â Â Â FINAL(area\_handle) = cl\_demo\_flights=>attach\_for\_write( ).
Â Â Â Â CREATE OBJECT flight\_list AREA HANDLE area\_handle.
Â Â Â Â area\_handle->set\_root( flight\_list ).
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â flight\_list->set\_flight\_list( ).
Â Â Â Â Â Â CATCH cx\_no\_flights INTO exc\_ref.
Â Â Â Â Â Â Â Â area\_handle->detach\_rollback( ).
Â Â Â Â Â Â Â Â RAISE EXCEPTION exc\_ref.
Â Â Â Â ENDTRY.
Â Â Â Â area\_handle->detach\_commit( ).
Â Â ENDMETHOD.
ENDCLASS.
CLASS shm\_demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS shm\_demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA: flight\_list\_handler TYPE REF TO demo\_flight\_list\_handler,
Â Â Â Â Â Â Â Â Â Â flight\_list TYPE REF TO spfli\_tab.
Â Â Â Â flight\_list\_handler =
Â Â Â Â Â Â demo\_flight\_list\_handler=>flight\_list\_handler.
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â flight\_list = flight\_list\_handler->get\_flight\_list( ).
Â Â Â Â Â Â CATCH cx\_no\_flights.
Â Â Â Â Â Â Â Â  cl\_demo\_output=>display\_text( 'No flight list available' ).
Â Â Â Â Â Â Â Â RETURN.
Â Â Â Â ENDTRY.
Â Â Â Â cl\_demo\_output=>display\_data( flight\_list->\* ).
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â shm\_demo=>main( ).

Description Â  

Area Root Class Â  

The global class CL\_DEMO\_FLIGHT\_LIST is used as an area root class. It contains the internal table FLIGHT\_LIST of type SPFLI\_TAB from the ABAP Dictionary as a public attribute, which is filled by the method SET\_FLIGHT\_LIST. This table represents the non-modifiable data that is accessed from different programs.

Area Â  

CL\_DEMO\_FLIGHTS is used as an area whose properties are maintained in the transaction SHMA. Apart from two exceptions the default values were applied:

-   Versioning is deactivated because the data of the area instance should not be changed at program runtime.
-   The lifetime after the last access is limited to five minutes to prevent memory space from being occupied in the shared memory without a program requiring it.

Brokers and Loaders Â  

The class demo\_flight\_list\_handler encapsulates access to the area for the user (in the real world, this would be a global class). The static constructor creates an instance of this class (singleton). The method get\_flight\_list is used as a broker. It attempts to set a read lock on an area instance. Since there can only be one read lock within an [internal session](javascript:call_link\('abeninternal_session_glosry.htm'\) "Glossary Entry"), a query is first made to establish whether an area handle area already exists. Alternatively, the exception CX\_SHM\_READ\_LOCK\_ACTIVE could have been handled, but this is less effective in this example. If the operation is not successful, the method create\_flight\_list is called, which acts as a loader. This method attempts to set a write lock and to build an area instance with a root object. Any exceptions are propagated to the calling method. A DETACH\_ROLLBACK is executed before any possible CX\_NO\_FLIGHTS exception from SET\_FLIGHT\_LIST is forwarded to the caller. If the change lock is not removed explicitly, the program is terminated at the end of the current internal session at the latest.

If the area was built successfully, get\_flight\_list attempts to set a read lock again. If no area instance could be built, get\_flight\_list creates an object of the class CL\_DEMO\_FLIGHT\_LIST in the current internal session as an emergency measure and fills the internal table flight\_list. Finally, a data reference to the flight list is assigned to the return value of the method either in the root object of the shared object or in the local object.

The write lock in create\_flight\_list is closed explicitly, whereas a read lock in get\_flight\_list is persisted until the end of the internal session. The latter are possible for areas without versioning if no more writes are to be expected after an area has been built.

Users Â  

The class shm\_demo demonstrates a user. The method main creates an object of the class demo\_flight\_list\_handler and attempts to read a reference to the flight list. If accessed successfully, the flight list is displayed in the method display. It is not important for the user whether the data is actually contained in the shared memory or in a local object of the class CL\_DEMO\_FLIGHT\_LIST in case of an error. If no data could be read from the database, a corresponding message is displayed.

After the program call, the area instance version can be examined in transaction SHMM.



**ðŸ“– Source**: [abenshared_objects_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenshared_objects_abexa.htm)

### abenshared_objects2_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenshared_objects2_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenshared_objects2_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ðŸ“– Source**: [abenshared_objects2_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenshared_objects2_abexa.htm)


Â  

* * *

AS ABAP Release 757, Â©Copyright 2023 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Creating Objects and Values](javascript:call_link\('abencreate_objects.htm'\))Â â†’Â  [Shared Objects](javascript:call_link\('abenabap_shared_objects.htm'\))Â â†’Â  [Shared Objects - Examples](javascript:call_link\('abenshm_examples_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif&sap-language=EN "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback on ABAP Documentation&body=Document: Shared Objects - Writing to and Reading from an Area, ABENSHARED_OBJECTS2_ABEXA, 757%
0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion for improvement:)

Shared Objects - Writing to and Reading from an Area

This example demonstrates the writing to and reading from an area.

Source Code Â  

REPORT demo\_shared\_objects\_rw.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS:
Â Â Â Â Â Â class\_constructor,
Â Â Â Â Â Â main.
Â Â PRIVATE SECTION.
Â Â Â Â CLASS-DATA:
Â Â Â Â Â Â o TYPE REF TO if\_demo\_output,
Â Â Â Â Â Â BEGIN OF struc,
Â Â Â Â Â Â Â Â instance\_name TYPE shm\_inst\_name,
Â Â Â Â Â Â Â Â numberÂ Â Â Â Â Â Â Â TYPE int4,
Â Â Â Â Â Â Â Â first\_nameÂ Â Â Â TYPE string,
Â Â Â Â Â Â Â Â last\_nameÂ Â Â Â  TYPE string,
Â Â Â Â Â Â END OF struc,
Â Â Â Â Â Â valuesÂ Â Â Â Â Â LIKE TABLE OF struc WITH EMPTY KEY,
Â Â Â Â Â Â area\_handle TYPE REF TO cl\_demo\_sh\_ob\_area,
Â Â Â Â Â Â area\_rootÂ Â  TYPE REF TOÂ Â cl\_demo\_so\_root.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â o->begin\_section( \`Shared objects demo: \` &&
Â Â Â Â Â Â \`Write to and read from area\` ).
Â Â Â Â "Set values for instances
Â Â Â Â values = VALUE #(
Â Â Â Â ( instance\_name = 'INSTANCE\_1' number = 1
Â Â Â Â Â Â first\_name = \`John\` last\_name = \`Lennon\` )
Â Â Â Â ( instance\_name = 'INSTANCE\_2' number = 2
Â Â Â Â Â Â first\_name = \`Paul\` last\_name = \`McCartney\` )
Â Â Â Â ( instance\_name = 'INSTANCE\_3' number = 3
Â Â Â Â Â Â first\_name = \`Ringo\` last\_name = \`Starr\` )
Â Â Â Â ( instance\_name = 'INSTANCE\_4' number = 4
Â Â Â Â Â Â first\_name = \`George\` last\_name = \`Harrison\` )Â Â ).
Â Â Â Â "Write to area
Â Â Â Â LOOP AT values ASSIGNING FIELD-SYMBOL(<write>).
Â Â Â Â Â Â TRY.
Â Â Â Â Â Â Â Â Â Â cl\_demo\_sh\_ob\_area=>attach\_for\_write(
Â Â Â Â Â Â Â Â Â Â Â Â EXPORTING inst\_name = <write>-instance\_name
Â Â Â Â Â Â Â Â Â Â Â Â RECEIVING handle = area\_handle ).
Â Â Â Â Â Â Â Â Â Â CREATE OBJECT area\_root AREA HANDLE area\_handle.
Â Â Â Â Â Â Â Â Â Â area\_handle->set\_root( EXPORTING root = area\_root ).
Â Â Â Â Â Â Â Â Â Â area\_root->write( w\_num = <write>-number
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â w\_name1 = <write>-first\_name
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â w\_name2 = <write>-last\_name ).
Â Â Â Â Â Â Â Â Â Â area\_handle->detach\_commit( ).
Â Â Â Â Â Â Â Â CATCH cx\_shm\_attach\_error.
Â Â Â Â Â Â Â Â Â Â o->write( 'Error when writing to area!' ).
Â Â Â Â Â Â ENDTRY.
Â Â Â Â ENDLOOP.
Â Â Â Â "Read from area
Â Â Â Â LOOP AT values ASSIGNING FIELD-SYMBOL(<read>).
Â Â Â Â Â Â TRY.
Â Â Â Â Â Â Â Â Â Â cl\_demo\_sh\_ob\_area=>attach\_for\_read(
Â Â Â Â Â Â Â Â Â Â Â Â EXPORTING inst\_name = <read>-instance\_name
Â Â Â Â Â Â Â Â Â Â Â Â RECEIVING handle = area\_handle ).
Â Â Â Â Â Â Â Â Â Â area\_handle->root->read(
Â Â Â Â Â Â Â Â Â Â Â Â IMPORTING r\_numÂ Â  = FINAL(read\_number)
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â r\_name1 = FINAL(read\_first\_name)
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â r\_name2 = FINAL(read\_last\_name) ).
Â Â Â Â Â Â Â Â Â Â struc = VALUE #( instance\_name = <read>-instance\_name
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â  numberÂ Â Â Â Â Â Â Â = read\_number
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  first\_nameÂ Â Â Â = read\_first\_name
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  last\_name = read\_last\_name ).
Â Â Â Â Â Â Â Â Â Â o->write\_doc(
Â Â Â Â Â Â | <b>Read result for area { <read>-instance\_name }:</b> | ).
Â Â Â Â Â Â Â Â Â Â o->write( struc ).
Â Â Â Â Â Â Â Â Â Â o->line( ).
Â Â Â Â Â Â Â Â Â Â area\_handle->detach( ).
Â Â Â Â Â Â Â Â CATCH cx\_shm\_attach\_error.
Â Â Â Â Â Â Â Â Â Â o->write( 'Error when reading from area!' ).
Â Â Â Â Â Â ENDTRY.
Â Â Â Â ENDLOOP.
Â Â Â Â o->display( ).Â Â ENDMETHOD.
Â Â METHOD class\_constructor.
Â Â Â Â o = cl\_demo\_output=>new( ).
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description Â  

Area Root Class Â  

The global class CL\_DEMO\_SO\_ROOT is used as an area root class. It contains three data objects number, first\_name and last\_name as private attributes. The methods WRITE and READ are used to set values for the data objects and read those values.

Area Â  

CL\_DEMO\_SH\_OB\_AREA is used as an area whose properties are maintained in the transaction SHMA. The lifetime after the last access is limited to five minutes to prevent memory space from being occupied in the shared memory without a program requiring it.

Description Â  

The internal table values is filled with data the writing and reading procedure is based on. It also provides instance names.

The table is looped across to write the entries to the area. First, an area is created before any data can be written to the shared memory using the method [ATTACH\_FOR\_WRITE](javascript:call_link\('abenshm_cl_shm_area.htm'\)). This method call includes the instance name that is provided in the internal table. This method returns a handle to the area (or instance), that has just been created in the shared memory. Then, a root object is created for the instance using a [CREATE OBJECT ... AREA HANDLE ...](javascript:call_link\('abapcreate_object_area_handle.htm'\)) statement that includes the handle returned by the method call before. The WRITE method of the root reference is called providing the values for the attributes that are stored in the shared memory. Calling the [DETACH\_COMMIT](javascript:call_link\('abenshm_cl_shm_area.htm'\)) method releases the change lock.

The internal table is looped across again to read from the instances using the instance names. The [ATTACH\_FOR\_READ](javascript:call_link\('abenshm_cl_shm_area.htm'\)) method is called using those instance names and the area handle as parameters. Using the READ method, the values of the attributes of the individual instances that have been created before are retrieved from the shared memory. Calling the [DETACH](javascript:call_link\('abenshm_cl_shm_area.htm'\)) method releases the read lock.

After the program call, the area instance versions can be examined in transaction SHMM.
