# ABAP - Keyword Documentation / ABAP - Programming Language / Processing External Data / ABAP Database Access / ABAP and SAP HANA / ABAP and SAP HANA, Examples

Included pages: 6



## ðŸ”— Source References

**Base URL**: [ABAP Keyword Documentation 7.57](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abenabap_and_hana_abexas.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenabap_and_hana_abexas.htm)
- [abenhana_session_variables_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenhana_session_variables_abexa.htm)
- [abenhana_cached_views_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenhana_cached_views_abexa.htm)
- [abenfrom_adbc_to_amdp_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenfrom_adbc_to_amdp_abexa.htm)
- [abencall_hana_db_proc_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abencall_hana_db_proc_abexa.htm)
- [abensql_script_curr_conv_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abensql_script_curr_conv_abexa.htm)

**Bundle Contains**: 6 documentation pages
**Version**: ABAP 7.57
**Generated**: 2025-09-01T11:25:45.257Z

---

### abenabap_and_hana_abexas.htm

> **ðŸ“– Official SAP Documentation**: [abenabap_and_hana_abexas.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenabap_and_hana_abexas.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 757, Â©Copyright 2023 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\))Â â†’Â  [ABAP Database Access](javascript:call_link\('abendb_access.htm'\))Â â†’Â  [ABAP and SAP HANA](javascript:call_link\('abenabap_hana.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif&sap-language=EN "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback on ABAP Documentation&body=Document: ABAP and SAP HANA, Examples, ABENABAP_AND_HANA_ABEXAS, 757%0D%0A%0D%0AError:%0D%0A%0D
%0A%0D%0A%0D%0ASuggestion for improvement:)

ABAP and SAP HANA, Examples

Continue
![Example](exa.gif "Example") [SAP HANA, ABAP-Specific Session Variables](javascript:call_link\('abenhana_session_variables_abexa.htm'\))
![Example](exa.gif "Example") [SAP HANA, Cached Views](javascript:call_link\('abenhana_cached_views_abexa.htm'\))
![Example](exa.gif "Example") [SAP HANA, from ADBC to AMDP](javascript:call_link\('abenfrom_adbc_to_amdp_abexa.htm'\))
![Example](exa.gif "Example") [SAP HANA, Call Database Procedure](javascript:call_link\('abencall_hana_db_proc_abexa.htm'\))
![Example](exa.gif "Example") [SAP HANA, Currency Conversion with SQLScript](javascript:call_link\('abensql_script_curr_conv_abexa.htm'\))



**ðŸ“– Source**: [abenabap_and_hana_abexas.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenabap_and_hana_abexas.htm)

### abenhana_session_variables_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenhana_session_variables_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenhana_session_variables_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 757, Â©Copyright 2023 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\))Â â†’Â  [ABAP Database Access](javascript:call_link\('abendb_access.htm'\))Â â†’Â  [ABAP and SAP HANA](javascript:call_link\('abenabap_hana.htm'\))Â â†’Â  [ABAP and SAP HANA, Examples](javascript:call_link\('abenabap_and_hana_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif&sap-language=EN "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback on ABAP Documentation&body=Document: SAP HANA, ABAP-Specific Session Variables, ABENHANA_SESSION_VARIABLES_ABEXA, 757%0D%0
A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion for improvement:)

SAP HANA, ABAP-Specific Session Variables

This example demonstrates potential Native SQL reads on [session variables](javascript:call_link\('abenhana_session_variables.htm'\)) on the SAP HANA database.

Source Code Â  

REPORT demo\_hana\_session\_variables.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â FINAL(out) = cl\_demo\_output=>new( ).
Â Â Â Â IF cl\_db\_sys=>is\_in\_memory\_db = abap\_false.
Â Â Â Â Â Â out->display(
Â Â Â Â Â Â Â Â \`Example can be executed on SAP HANA Database only\` ).
Â Â Â Â Â Â LEAVE PROGRAM.
Â Â Â Â ENDIF.
Â Â Â Â FINAL(oref) = NEW cl\_demo\_hana\_session\_variables( ).
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â FINAL(result\_exec\_sql) =
Â Â Â Â Â Â Â Â Â Â oref->get\_session\_variables\_exec\_sql( ).
Â Â Â Â Â Â Â Â out->write( result\_exec\_sql ).
Â Â Â Â Â Â CATCH cx\_sy\_native\_sql\_error INTO FINAL(exec\_sql\_exc).
Â Â Â Â Â Â Â Â out->write( exec\_sql\_exc->get\_text( ) ).
Â Â Â Â ENDTRY.
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â FINAL(result\_adbc) =
Â Â Â Â Â Â Â Â Â Â oref->get\_session\_variables\_adbc( ).
Â Â Â Â Â Â Â Â out->write( result\_adbc ).
Â Â Â Â Â Â CATCH cx\_sql\_exception INTO FINAL(adbc\_exc).
Â Â Â Â Â Â Â Â out->write( adbc\_exc->get\_text( ) ).
Â Â Â Â ENDTRY.
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â DATA result\_amdp
Â Â Â Â Â Â Â Â  TYPE cl\_demo\_hana\_session\_variables=>session\_variables.
Â Â Â Â Â Â Â Â oref->get\_session\_variables\_amdp(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â IMPORTING clntÂ Â Â Â  = result\_amdp-client
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â cds\_clnt = result\_amdp-cds\_client
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â unamÂ Â Â Â  = result\_amdp-uname
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â langÂ Â Â Â  = result\_amdp-langu
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â dateÂ Â Â Â  = result\_amdp-datum ).
Â Â Â Â Â Â Â Â out->write( result\_amdp ).
Â Â Â Â Â Â CATCH cx\_amdp\_error INTO FINAL(amdp\_exc).
Â Â Â Â Â Â Â Â out->write( amdp\_exc->get\_text( ) ).
Â Â Â Â ENDTRY.
Â Â Â Â out->display( ).Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description Â  

This program calls the following methods of the class CL\_DEMO\_HANA\_SESSION\_VARIABLES:

-   GET\_SESSION\_VARIABLES\_EXEC\_SQL
    
    METHOD get\_session\_variables\_exec\_sql.
    Â Â EXEC SQL.
    Â Â Â Â select SESSION\_CONTEXT('CLIENT')
    Â Â Â Â Â Â Â Â Â Â  from DUMMY
    Â Â Â Â Â Â Â Â Â Â  into :session\_variables-client
    Â Â ENDEXEC.
    Â Â EXEC SQL.
    Â Â Â Â select SESSION\_CONTEXT('CDS\_CLIENT')
    Â Â Â Â Â Â Â Â Â Â  from DUMMY
    Â Â Â Â Â Â Â Â Â Â  into :session\_variables-cds\_client
    Â Â ENDEXEC.
    Â Â EXEC SQL.
    Â Â Â Â select SESSION\_CONTEXT('APPLICATIONUSER')
    Â Â Â Â Â Â Â Â Â Â  from DUMMY
    Â Â Â Â Â Â Â Â Â Â  into :session\_variables-uname
    Â Â ENDEXEC.
    Â Â EXEC SQL.
    Â Â Â Â select SESSION\_CONTEXT('LOCALE\_SAP')
    Â Â Â Â Â Â Â Â Â Â  from DUMMY
    Â Â Â Â Â Â Â Â Â Â  into :session\_variables-langu
    Â Â ENDEXEC.
    Â Â EXEC SQL.
    Â Â Â Â select SESSION\_CONTEXT('SAP\_SYSTEM\_DATE')
    Â Â Â Â Â Â Â Â Â Â  from DUMMY
    Â Â Â Â Â Â Â Â Â Â  into :session\_variables-datum
    Â Â ENDEXEC.
    ENDMETHOD.
    
    This method calls the built-in function SESSION\_CONTEXT for the four ABAP-specific [session variables](javascript:call_link\('abenhana_session_variables.htm'\)) on the SAP HANA database in three static Native SQL statements between [EXEC - ENDEXEC](javascript:call_link\('abapexec.htm'\)). The result is returned as a column of the SELECT list of an access to the pseudo table DUMMY.
    
-   GET\_SESSION\_VARIABLES\_ADBC
    
    METHOD get\_session\_variables\_adbc.
    Â Â DATA(result) = NEW cl\_sql\_statement( )->execute\_query(
    Â Â Â Â \`select SESSION\_CONTEXT('CLIENT') from DUMMY\` ).
    Â Â result->set\_param( REF #( session\_variables-client ) ).
    Â Â result->next( ).
    Â Â result = NEW cl\_sql\_statement( )->execute\_query(
    Â Â Â Â \`select SESSION\_CONTEXT('CDS\_CLIENT') from DUMMY\` ).
    Â Â result->set\_param( REF #( session\_variables-cds\_client ) ).
    Â Â result->next( ).
    Â Â result = NEW cl\_sql\_statement( )->execute\_query(
    Â Â Â Â \`select SESSION\_CONTEXT('APPLICATIONUSER') from DUMMY\` ).
    Â Â result->set\_param( REF #( session\_variables-uname ) ).
    Â Â result->next( ).
    Â Â result = NEW cl\_sql\_statement( )->execute\_query(
    Â Â Â Â \`select SESSION\_CONTEXT('LOCALE\_SAP') from DUMMY\` ).
    Â Â result->set\_param( REF #( session\_variables-langu ) ).
    Â Â result->next( ).
    Â Â result = NEW cl\_sql\_statement( )->execute\_query(
    Â Â Â Â \`select SESSION\_CONTEXT('SAP\_SYSTEM\_DATE') from DUMMY\` ).
    Â Â result->set\_param( REF #( session\_variables-datum ) ).
    Â Â result->next( ).
    ENDMETHOD.
    
    This method replaces the static Native SQL statements of the preceding method with corresponding Native SQL statements in [ADBC](javascript:call_link\('abenadbc_glosry.htm'\) "Glossary Entry").
    
-   GET\_SESSION\_VARIABLES\_AMDP
    
    METHOD get\_session\_variables\_amdp
    Â Â Â Â Â Â  BY DATABASE PROCEDURE FOR HDB
    Â Â Â Â Â Â  LANGUAGE SQLSCRIPT.
    Â Â clnt := session\_context('CLIENT');
    Â Â cds\_clnt := session\_context('CDS\_CLIENT');
    Â Â unam := session\_context('APPLICATIONUSER');
    Â Â lang := session\_context('LOCALE\_SAP');
    Â Â date := session\_context('SAP\_SYSTEM\_DATE');
    ENDMETHOD.
    
    This method is an [AMDP method](javascript:call_link\('abenamdp_method_glosry.htm'\) "Glossary Entry") and shows how the result of the built-in function SESSION\_CONTEXT can be used directly in [SQLScript](javascript:call_link\('abensql_script_glosry.htm'\) "Glossary Entry").



**ðŸ“– Source**: [abenhana_session_variables_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenhana_session_variables_abexa.htm)

### abenhana_cached_views_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenhana_cached_views_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenhana_cached_views_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 757, Â©Copyright 2023 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\))Â â†’Â  [ABAP Database Access](javascript:call_link\('abendb_access.htm'\))Â â†’Â  [ABAP and SAP HANA](javascript:call_link\('abenabap_hana.htm'\))Â â†’Â  [ABAP and SAP HANA, Examples](javascript:call_link\('abenabap_and_hana_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif&sap-language=EN "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback on ABAP Documentation&body=Document: SAP HANA, Cached Views, ABENHANA_CACHED_VIEWS_ABEXA, 757%0D%0A%0D%0AError:%0D%0A%0D%0
A%0D%0A%0D%0ASuggestion for improvement:)

SAP HANA, Cached Views

This example demonstrates ABAP SQL access to a [cached view](javascript:call_link\('abenhana_cached_views.htm'\)) on the SAP HANA database.

Source Code Â  

REPORT demo\_select\_cached\_view.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â FINAL(out) = cl\_demo\_output=>new( ).
Â Â Â Â IF cl\_db\_sys=>is\_in\_memory\_db = abap\_false.
Â Â Â Â Â Â out->display(
Â Â Â Â Â Â Â Â \`Example can be executed on SAP HANA Database only\` ).
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â DATA reset TYPE abap\_bool.
Â Â Â Â cl\_demo\_input=>add\_field( EXPORTING
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â text = 'Reset cache'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â as\_checkbox = abap\_true
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â field = reset ).
Â Â Â Â DATA minutes TYPE i VALUE 30.
Â Â Â Â cl\_demo\_input=>request( EXPORTING
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â text = 'Maximal age in minutes'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â CHANGING
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â field = minutes ).
Â Â Â Â minutes = COND i( WHEN minutes < 1 OR minutes > 60 THEN 30
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ELSE minutes ).
Â Â Â Â IF reset = abap\_true.
Â Â Â Â Â Â TRY.
Â Â Â Â Â Â Â Â Â Â NEW cl\_sql\_statement( )->execute\_ddl(
Â Â Â Â Â Â Â Â Â Â Â Â \`alter view DEMOCDSCACH drop cache;\` ).
Â Â Â Â Â Â Â Â CATCH cx\_sql\_exception.
Â Â Â Â Â Â ENDTRY.
Â Â Â Â Â Â TRY.
Â Â Â Â Â Â Â Â Â Â NEW cl\_sql\_statement( )->execute\_ddl(
Â Â Â Â Â Â Â Â Â Â Â Â |alter view DEMOCDSCACH add cache retention {
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  minutes } of | &&
Â Â Â Â Â Â Â Â Â Â Â Â |MANDT, CARRID, MAX(FLDATE), MIN(PRICE), SUM(SEATSOCC);| ).
Â Â Â Â Â Â Â Â CATCH cx\_sql\_exception INTO FINAL(exc).
Â Â Â Â Â Â Â Â Â Â out->display( exc->get\_text( ) ).
Â Â Â Â Â Â Â Â Â Â RETURN.
Â Â Â Â Â Â ENDTRY.
Â Â Â Â ENDIF.
Â Â Â Â FINAL(extended\_result) =
Â Â Â Â Â Â NEW cl\_osql\_extended\_result( iv\_cached\_view = abap\_true ).
Â Â Â Â SELECT carrid,
Â Â Â Â Â Â Â Â Â Â  MAX( fldate )Â Â  AS max\_fldate ,
Â Â Â Â Â Â Â Â Â Â  MIN( price )Â Â Â Â AS min\_price,
Â Â Â Â Â Â Â Â Â Â  SUM( seatsocc ) AS sum\_seatsocc
Â Â Â Â Â Â Â Â Â Â  FROM demo\_cds\_cached\_view
Â Â Â Â Â Â Â Â Â Â  GROUP BY carrid
Â Â Â Â Â Â Â Â Â Â  %\_HINTS HDB 'RESULT\_CACHE' "#EC CI\_HINTS
Â Â Â Â Â Â Â Â Â Â  INTO TABLE @FINAL(result)
Â Â Â Â Â Â Â Â Â Â  EXTENDED RESULT @extended\_result.
Â Â Â Â out->write( result
Â Â Â Â Â Â )->write\_html( \`<b>Extended Result</b>\` ).
Â Â Â Â IF extended\_result->cview\_valid( ).
Â Â Â Â Â Â out->display(
Â Â Â Â Â Â  | CVIEW\_MAX\_AGE: {
Â Â Â Â Â Â Â Â Â Â extended\_result->cview\_max\_age( ) } seconds\\n| &&
Â Â Â Â Â Â  | CVIEW\_LAST\_REFRESH\_TSTMPL: {
Â Â Â Â Â Â Â Â Â Â extended\_result->cview\_last\_refresh\_tstmpl( )
Â Â Â Â Â Â Â Â Â Â Â Â TIMESTAMP = ISO }\\n| ).
Â Â Â Â ELSE.
Â Â Â Â Â Â out->display( \`Invalid result\` ).
Â Â Â Â ENDIF.Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description Â  

The instance DEMOCDSCACH of the [CDS view](javascript:call_link\('abencds_view_glosry.htm'\) "Glossary Entry") DEMO\_CDS\_CACHED\_VIEW on the [SAP HANA database](javascript:call_link\('abenhana_database_glosry.htm'\) "Glossary Entry") is designated for caching using [ADBC](javascript:call_link\('abenadbc_glosry.htm'\) "Glossary Entry") and the SQL statement ALTER TABLE. The maximum cache lifetime cannot be specified. It is also possible to delete an existing cache.

When the CDS view is accessed using the ABAP SQL statement [SELECT](javascript:call_link\('abapselect.htm'\)), the [database hint](javascript:call_link\('abendatabase_hint_glosry.htm'\) "Glossary Entry") [%\_HINTS](javascript:call_link\('abenabap_sql_db_hints.htm'\)) HDB 'RESULT\_CACHE' is used to specify that the cache is accessed.

The addition [EXTENDED RESULT](javascript:call_link\('abapselect_extended_result.htm'\)) of the [INTO](javascript:call_link\('abapinto_clause.htm'\)) clause can be used to determine whether a cache is available, and the cache properties are read.



**ðŸ“– Source**: [abenhana_cached_views_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenhana_cached_views_abexa.htm)

### abenfrom_adbc_to_amdp_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenfrom_adbc_to_amdp_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenfrom_adbc_to_amdp_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 757, Â©Copyright 2023 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\))Â â†’Â  [ABAP Database Access](javascript:call_link\('abendb_access.htm'\))Â â†’Â  [ABAP and SAP HANA](javascript:call_link\('abenabap_hana.htm'\))Â â†’Â  [ABAP and SAP HANA, Examples](javascript:call_link\('abenabap_and_hana_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif&sap-language=EN "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback on ABAP Documentation&body=Document: SAP HANA, from ADBC to AMDP, ABENFROM_ADBC_TO_AMDP_ABEXA, 757%0D%0A%0D%0AError:%0D%0A
%0D%0A%0D%0A%0D%0ASuggestion for improvement:)

SAP HANA, from ADBC to AMDP

This example demonstrates how a task can be performed using HANA-specific language elements.

Source Code Â  

REPORT demo\_from\_adbc\_to\_amdp.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â FINAL(out) = cl\_demo\_output=>new( ).
Â Â Â Â IF NOT cl\_abap\_dbfeatures=>use\_features(
Â Â Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â Â Â requested\_features =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â VALUE #( ( cl\_abap\_dbfeatures=>call\_database\_procedure )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( cl\_abap\_dbfeatures=>call\_amdp\_method ) ) ).
Â Â Â Â Â Â cl\_demo\_output=>display(
Â Â Â Â Â Â \`Current database system does not support all procedure calls\` ).
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â DATA carrier TYPE spfli-carrid VALUE 'LH'.
Â Â Â Â cl\_demo\_input=>request( CHANGING field = carrier ).
Â Â Â Â FINAL(oref) = NEW cl\_demo\_from\_adbc\_to\_amdp( to\_upper( carrier ) ).
Â Â Â Â FINAL(asql\_result) = oref->asql( ).
Â Â Â Â FINAL(adbc\_result) = oref->adbc( ).
Â Â Â Â FINAL(cdbp\_result) = oref->cdbp( ).
Â Â Â Â FINAL(amdp\_result) = oref->amdp( ).
Â Â Â Â IF NOT cl\_shdb\_hc=>is\_hana\_cloud(Â Â ).
Â Â Â Â Â Â IF asql\_result = adbc\_result AND
Â Â Â Â Â Â Â Â  asql\_result = cdbp\_result AND
Â Â Â Â Â Â Â Â  asql\_result = amdp\_result.
Â Â Â Â Â Â Â Â out->begin\_section(
Â Â Â Â Â Â Â Â Â Â \`Result of ABAP SQL, ADBC, CALL DATABASE PROCEDURE, and AMDP\`
Â Â Â Â Â Â Â Â Â Â )->write( amdp\_result ).
Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â out->write( 'Error' ).
Â Â Â Â Â Â ENDIF.
Â Â Â Â ELSE.
Â Â Â Â Â Â IF asql\_result = adbc\_result AND
Â Â Â Â Â Â Â Â  asql\_result = amdp\_result.
Â Â Â Â Â Â Â Â out->begin\_section(
Â Â Â Â Â Â Â Â Â Â \`Result of ABAP SQL, ADBC, and AMDP\`
Â Â Â Â Â Â Â Â Â Â )->write( amdp\_result ).
Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â out->write( 'Error' ).
Â Â Â Â Â Â ENDIF.
Â Â Â Â ENDIF.
Â Â Â Â out->display( ).Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description Â  

The task is to read all rows from the database table SFLIGHT whose key fields MANDT, CARRID, and CONNID occur in a predefined three-column internal table. To do this, the example program calls various methods of the class CL\_DEMO\_FROM\_ADBC\_TO\_AMDP in whose instance constructor the internal table connection\_tab is filled in accordance with user input.

Hint

The examples of using HANA-specific language elements shown in the methods are syntax-only examples. The task presented here can be solved just as well using ABAP SQL, which is why ABAP SQL is the preferred method as specified in the [programming guidelines](javascript:call_link\('abendatabase_access_guidl.htm'\) "Guideline").

Reference Implementation Using ABAP SQL Â  

The method ASQL shows how the task can be solved in ABAP SQL by simply using the addition [FOR ALL ENTRIES](javascript:call_link\('abenwhere_all_entries.htm'\)). The result of the method is used as a reference for the HANA-specific implementations.

METHOD asql.
Â Â IF connection\_tab IS NOT INITIAL.
Â Â Â Â SELECT \*
Â Â Â Â Â Â Â Â Â Â FROM sflight
Â Â Â Â Â Â Â Â Â Â FOR ALL ENTRIES IN @connection\_tab
Â Â Â Â Â Â Â Â Â Â WHERE carrid = @connection\_tab-carrid AND
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â connid = @connection\_tab-connid
Â Â Â Â Â Â Â Â Â Â INTO TABLE @flights.
Â Â ENDIF.
ENDMETHOD.

Using Native SQL with ADBC Â  

The method ADBC solves the task by using HANA-specific [Native SQL statements](javascript:call_link\('abennative_sql.htm'\)) that are passed to the SAP HANA database using [ADBC](javascript:call_link\('abenadbc.htm'\)). The table with the key values is evaluated after the addition exists in a subquery of a select statement. To do this, a temporary table DEMO\_ADBC\_CONNECTIONS is created on the database and filled with the content of the connection\_tab using the statement insert. To access the result of the select statement, a standard table std\_flights must be declared as a local internal table of the method, since the return value flights cannot be used for ADBC. The temporary table DEMO\_ADBC\_CONNECTIONS is removed again after SPFLI is read.

METHOD adbc.
Â Â DATA(sql) = NEW cl\_sql\_statement( ).
Â Â TRY.
Â Â Â Â Â Â sql->execute\_update( 'truncate table DEMO\_ADBC\_CONNECTIONS' ).
Â Â Â Â Â Â sql->execute\_ddl(Â Â Â Â 'drop table DEMO\_ADBC\_CONNECTIONS' ).
Â Â Â Â CATCH cx\_sql\_exception ##no\_handler.
Â Â ENDTRY.
Â Â TRY.
Â Â Â Â Â Â sql->execute\_ddl(
Â Â Â Â Â Â Â Â \`create global temporary row table DEMO\_ADBC\_CONNECTIONS \` &&
Â Â Â Â Â Â Â Â \`( MANDTÂ Â NVARCHAR(3), \`Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  &&
Â Â Â Â Â Â Â Â Â Â \`CARRID NVARCHAR(3), \`Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  &&
Â Â Â Â Â Â Â Â Â Â \`CONNID NVARCHAR(4) )\` ).
Â Â Â Â Â Â sql->set\_param\_table( REF #( connection\_tab ) ).
Â Â Â Â Â Â sql->execute\_update(
Â Â Â Â Â Â Â Â \`insert into DEMO\_ADBC\_CONNECTIONS values ( ?, ?, ? )\` ).
Â Â Â Â Â Â DATA(result) = sql->execute\_query(
Â Â Â  Â Â Â Â \`Â Â Â Â select \* \`Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â from SFLIGHT as S \` Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â where exists (\` Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â Â Â select MANDT, CARRID, CONNID \`Â Â Â Â Â Â Â Â Â Â  &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â Â Â Â Â Â Â Â Â  fromÂ Â DEMO\_ADBC\_CONNECTIONS as C\` &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â Â Â Â  Â Â Â Â  where C.MANDTÂ Â = S.MANDT and \`Â Â Â Â &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â  C.CARRID = S.CARRID and \`Â Â  &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  C.CONNID = S.CONNID )\` ).
Â Â Â Â Â Â DATA std\_flights TYPE STANDARD TABLE OF sflight WITH EMPTY KEY.
Â Â Â Â Â Â result->set\_param\_table( REF #( std\_flights ) ).
Â Â Â Â Â Â result->next\_package( ).
Â Â Â Â Â Â result->close( ).
Â Â Â Â Â Â flights = std\_flights.
Â Â Â Â Â Â sql->execute\_update( 'truncate table DEMO\_ADBC\_CONNECTIONS' ).
Â Â Â Â Â Â sql->execute\_ddl( 'Â Â  drop table DEMO\_ADBC\_CONNECTIONS' ).
Â Â Â Â CATCH cx\_sql\_exception.
Â Â Â Â Â Â CLEAR flights.
Â Â ENDTRY.
ENDMETHOD.

Call of a Database Procedure Using a Database Procedure Proxy Â  

The method CDBP solves the task by implementing the HANA-specific select statement in a [database procedure](javascript:call_link\('abendatabase_procedure_glosry.htm'\) "Glossary Entry") that is called by specifying a [database procedure proxy](javascript:call_link\('abendatabase_proc_proxy_glosry.htm'\) "Glossary Entry") in the statement [CALL DATABASE PROCEDURE](javascript:call_link\('abapcall_database_procedure.htm'\)). In a more realistic example, the database procedure DEMO\_ADBC\_GET\_FLIGHTS and the database procedure proxy DEMO\_ADBC\_GET\_FLIGHTS\_PROXY would already exist and the implementation of the method would be restricted to the call CALL DATABASE PROCEDURE (see the executable example for [procedure calls](javascript:call_link\('abencall_hana_db_proc_abexa.htm'\))). In this example, the database procedure and the database procedure proxy are created temporarily and deleted again using ADBC methods and the associated API. The input parameter connections of the procedure references an additional temporary HANA table type, DEMO\_ADBC\_CONNECTIONS\_TYPE. HANA does not support a separate type for numeric text, which means that the type NVARCHAR(4) must be used for the column CONNID. Therefore, the internal table connection\_tab must be assigned to a temporary table connections with the correct row type before the procedure is called. No auxiliary table is required for the result, since the row type is described using an ABAP Dictionary type. An appropriate mapping can be performed for this type using the mapping table params. As an alternative to an auxiliary table for connection\_tab, its data type could also be declared in the ABAP Dictionary and an appropriate mapping performed.

The statement CALL DATABASE PROCEDURE is not possible on a [SAP HANA Cloud database](javascript:call_link\('abensap_hana_cloud_db_glosry.htm'\) "Glossary Entry").

METHOD cdbp.
Â Â IF cl\_shdb\_hc=>is\_hana\_cloud(Â Â ).
Â Â Â Â RETURN.
Â Â ENDIF.
Â Â DATA(sql) = NEW cl\_sql\_statement( ).
Â Â TRY.
Â Â Â Â Â Â sql->execute\_ddl( 'drop type DEMO\_ADBC\_CONNECTIONS\_TYPE' ).
Â Â Â Â CATCH cx\_sql\_exception ##no\_handler.
Â Â ENDTRY.
Â Â TRY.
Â Â Â Â Â Â sql->execute\_ddl( \`drop procedure DEMO\_ADBC\_GET\_FLIGHTS\` ).
Â Â Â Â CATCH cx\_sql\_exception ##no\_handler.
Â Â ENDTRY.
Â Â TRY.
Â Â Â Â Â Â sql->execute\_ddl(
Â Â Â Â Â Â Â Â \`create type DEMO\_ADBC\_CONNECTIONS\_TYPE as table \` &&
Â Â Â Â Â Â Â Â \`( MANDTÂ Â NVARCHAR(3), \`Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  &&
Â Â Â Â Â Â Â Â Â Â \`CARRID NVARCHAR(3), \`Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  &&
Â Â Â Â Â Â Â Â Â Â \`CONNID NVARCHAR(4) )\` ).
Â Â Â Â Â Â sql->execute\_ddl(
Â Â Â Â Â Â Â Â \`create procedure DEMO\_ADBC\_GET\_FLIGHTSÂ Â \`Â Â Â Â Â Â Â Â Â Â Â Â &&
Â Â Â Â Â Â Â Â \`Â Â ( in CONNECTIONS DEMO\_ADBC\_CONNECTIONS\_TYPE,Â Â \`Â Â Â Â &&
Â Â Â Â Â Â Â Â \`Â Â Â Â out FLIGHTS SFLIGHT ) language sqlscript as \`Â Â Â Â &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â begin \`Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â Â Â FLIGHTS = \` Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â Â Â Â Â select \* \` Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â Â Â Â  Â Â from SFLIGHT as S \`Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â Â Â Â  Â Â where exists (Â Â \`Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â Â Â Â  Â Â Â Â select MANDT, CARRID, CONNID \`Â Â Â Â Â Â Â Â  &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â  from :CONNECTIONS as C\`Â Â Â Â Â Â Â Â  &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â  where C.MANDTÂ Â = S.MANDT and \`Â Â &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  C.CARRID = S.CARRID and \` &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  C.CONNID = S.CONNID );\`Â Â  &&
Â Â Â Â Â Â Â Â \`Â Â Â Â Â Â end\` ).
Â Â Â Â Â Â DATA db\_schema TYPE if\_dbproc\_proxy\_basic\_types=>ty\_db\_name.
Â Â Â Â Â Â CALL FUNCTION 'DB\_DBSCHEMA\_CURRENT'
Â Â Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â Â Â dbschema = db\_schema.
Â Â Â Â Â Â DATA(params) =
Â Â Â Â Â Â Â Â VALUE if\_dbproc\_proxy\_basic\_types=>ty\_param\_override\_t(
Â Â Â Â Â Â Â Â ( db\_nameÂ Â  = 'FLIGHTS'
Â Â Â Â Â Â Â Â Â Â abap\_name = 'FLIGHTS'
Â Â Â Â Â Â Â Â Â Â descrÂ Â Â Â  = cl\_abap\_typedescr=>describe\_by\_name(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â 'SFLIGHT' ) ) ).
Â Â Â Â Â Â DATA(api) = cl\_dbproc\_proxy\_factory=>get\_proxy\_public\_api(
Â Â Â Â Â Â Â Â if\_proxy\_name = 'DEMO\_ADBC\_GET\_FLIGHTS\_PROXY' ).
Â Â Â Â Â Â api->delete( ).
Â Â Â Â Â Â api->create\_proxy( EXPORTING
Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â if\_proc\_schemaÂ Â Â Â = db\_schema
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â it\_param\_override = params
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â if\_proc\_nameÂ Â Â Â Â Â = 'DEMO\_ADBC\_GET\_FLIGHTS' ).
Â Â Â Â Â Â COMMIT CONNECTION default.
Â Â Â Â Â Â TYPES:
Â Â Â Â Â Â Â Â BEGIN OF connection,
Â Â Â Â Â Â Â Â Â Â mandtÂ Â TYPE c LENGTH 3,
Â Â Â Â Â Â Â Â Â Â carrid TYPE c LENGTH 3,
Â Â Â Â Â Â Â Â Â Â connid TYPE c LENGTH 4,
Â Â Â Â Â Â Â Â END OF connection.
Â Â Â Â Â Â DATA connections TYPE STANDARD TABLE OF connection
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  WITH EMPTY KEY.
Â Â Â Â Â Â connections = connection\_tab.
Â Â Â Â Â Â CALL DATABASE PROCEDURE ('DEMO\_ADBC\_GET\_FLIGHTS\_PROXY')
Â Â Â Â Â Â Â Â EXPORTING connections = connections
Â Â Â Â Â Â Â Â IMPORTING flightsÂ Â Â Â  = flights.
Â Â Â Â Â Â COMMIT CONNECTION default.
Â Â Â Â Â Â api->delete( ).
Â Â Â Â Â Â sql->execute\_ddl( \`drop procedure DEMO\_ADBC\_GET\_FLIGHTS\` ).
Â Â Â Â Â Â sql->execute\_ddl( 'drop type DEMO\_ADBC\_CONNECTIONS\_TYPE' ).
Â Â Â Â CATCH cx\_sql\_exception cx\_dbproc\_proxy.
Â Â Â Â Â Â CLEAR flights.
Â Â ENDTRY.
ENDMETHOD.

Call an AMDP Procedure Â  

The method AMDP solves the task in the easiest possible way by calling an [AMDP method](javascript:call_link\('abenamdp_method_glosry.htm'\) "Glossary Entry") AMDP\_METH in which the HANA-specific select statement is implemented in an [AMDP procedure](javascript:call_link\('abenamdp_procedure_glosry.htm'\) "Glossary Entry"). The main advantage of the AMDP method is that it can be called in the same way as any ABAP method and no auxiliary tables must be introduced, as was the case in the preceding examples.

METHOD amdp.
Â Â amdp\_meth( EXPORTING connections = connection\_tab
Â Â Â Â Â Â Â Â Â Â Â Â  IMPORTING flightsÂ Â Â Â  = flights ).
ENDMETHOD.

METHOD amdp\_meth BY DATABASE PROCEDURE FOR HDB
Â Â Â Â Â Â  LANGUAGE SQLSCRIPT OPTIONS READ-ONLY
Â Â Â Â Â Â  USING sflight.
Â Â FLIGHTS = select \*
Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  from SFLIGHT as S
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  where exists ( select MANDT, CARRID, CONNID
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  from :CONNECTIONS as C
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  where C.MANDTÂ Â = S.MANDT and
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  C.CARRID = S.CARRID and
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  C.CONNID = S.CONNID );
ENDMETHOD.



**ðŸ“– Source**: [abenfrom_adbc_to_amdp_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenfrom_adbc_to_amdp_abexa.htm)

### abencall_hana_db_proc_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abencall_hana_db_proc_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abencall_hana_db_proc_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 757, Â©Copyright 2023 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\))Â â†’Â  [ABAP Database Access](javascript:call_link\('abendb_access.htm'\))Â â†’Â  [ABAP and SAP HANA](javascript:call_link\('abenabap_hana.htm'\))Â â†’Â  [ABAP and SAP HANA, Examples](javascript:call_link\('abenabap_and_hana_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif&sap-language=EN "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback on ABAP Documentation&body=Document: SAP HANA, Call Database Procedure, ABENCALL_HANA_DB_PROC_ABEXA, 757%0D%0A%0D%0AError:
%0D%0A%0D%0A%0D%0A%0D%0ASuggestion for improvement:)

SAP HANA, Call Database Procedure

This example demonstrates how a database procedure in the SAP HANA database can be called.

Source Code Â  

REPORT demo\_call\_hana\_db\_procedure.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â FINAL(out) = cl\_demo\_output=>new( ).
Â Â Â Â IF NOT cl\_abap\_dbfeatures=>use\_features(
Â Â Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â Â Â requested\_features =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â VALUE #( ( cl\_abap\_dbfeatures=>call\_database\_procedure )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ( cl\_abap\_dbfeatures=>call\_amdp\_method ) ) ).
Â Â Â Â Â Â cl\_demo\_output=>display(
Â Â Â Â Â Â \`Current database system does not support all procedure calls\` ).
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â DATA carrier TYPE spfli-carrid VALUE 'LH'.
Â Â Â Â cl\_demo\_input=>request( CHANGING field = carrier ).
Â Â Â Â FINAL(oref) =
Â Â Â Â Â Â NEW cl\_demo\_call\_hana\_db\_procedure( to\_upper( carrier ) ).
Â Â Â Â IF oref->ok IS INITIAL.
Â Â Â Â Â Â out->display( 'Error in setup' ).
Â Â Â Â Â Â RETURN.
Â Â Â Â ENDIF.
Â Â Â Â FINAL(osql\_result) = oref->osql( ).
Â Â Â Â FINAL(adbc\_result) = oref->adbc( ).
Â Â Â Â FINAL(cdbp\_result) = oref->cdbp( ).
Â Â Â Â FINAL(amdp\_result) = oref->amdp( ).
Â Â Â Â IF NOT cl\_shdb\_hc=>is\_hana\_cloud(Â Â ).
Â Â Â Â Â Â IF osql\_result = adbc\_result AND
Â Â Â Â Â Â Â Â  osql\_result = cdbp\_result AND
Â Â Â Â Â Â Â Â  osql\_result = amdp\_result.
Â Â Â Â Â Â Â Â out->begin\_section(
Â Â Â Â Â Â Â Â Â Â \`Result of ABAP SQL, ADBC, CALL DATABASE PROCEDURE, and AMDP\`
Â Â Â Â Â Â Â Â Â Â )->write( amdp\_result ).
Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â out->write( 'Error' ).
Â Â Â Â Â Â ENDIF.
Â Â Â Â ELSE.
Â Â Â Â Â Â IF osql\_result = adbc\_result AND
Â Â Â Â Â Â Â Â  osql\_result = amdp\_result.
Â Â Â Â Â Â Â Â out->begin\_section(
Â Â Â Â Â Â Â Â Â Â \`Result of ABAP SQL, ADBC, and AMDP\`
Â Â Â Â Â Â Â Â Â Â )->write( amdp\_result ).
Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â out->write( 'Error' ).
Â Â Â Â Â Â ENDIF.
Â Â Â Â ENDIF.
Â Â Â Â out->display( ).Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description Â  

Take a database procedure called /1BCAMDP/DEMO\_ADBC\_GET\_FLIGHTS in the [ABAP database schema](javascript:call_link\('abenabap_db_schema_glosry.htm'\) "Glossary Entry"):

begin
Â Â FLIGHTS =
Â Â Â Â select \*
Â Â Â Â Â Â Â Â Â Â  from SFLIGHT as S
Â Â Â Â Â Â Â Â Â Â  where exists (
Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â select MANDT, CARRID, CONNID
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â from :CONNECTIONS as C
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â where C.MANDTÂ Â = S.MANDT and
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â C.CARRID = S.CARRID and
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â C.CONNID = S.CONNID );
end

The procedure has a tabular input parameter CONNECTIONS with the three-column HANA table type DEMO\_ADBC\_CONNECTIONS\_TYPE and an output parameter FLIGHTS with the type SFLIGHT defined in the ABAP Dictionary. The HANA table type has three character-like columns, MANDT with length 3, CARRID with length 3, and CONNID with length 4. The task is to call this procedure from ABAP while passing an internal table to the input parameter and copying the output parameter to an internal table.

To do this, the example program calls various methods of the class CL\_DEMO\_CALL\_HANA\_DB\_PROCEDURE. The table to be passed connection\_tab is filled in the instance constructor of the class in accordance with user input. Furthermore, the database procedure, the HANA table type, and a database procedure proxy, which is usually static, are created here temporarily.

Comparison Implementation Using ABAP SQL Â  

The method OSQL shows how the implementation of the database procedure can also be expressed in ABAP SQL. If the function is required only in ABAP, an implementation in SQLScript is unnecessary in this case and should be avoided in accordance with the relevant [programming guideline](javascript:call_link\('abendatabase_access_guidl.htm'\) "Guideline"). In our example, the database procedure is a replacement for a globally available procedure that is also to be called in ABAP. The result of the method OSQL is compared with the results of the various procedure calls to determine whether they are correct.

METHOD osql.
Â Â IF connection\_tab IS NOT INITIAL.
Â Â Â Â SELECT \*
Â Â Â Â Â Â Â Â Â Â FROM sflight
Â Â Â Â Â Â Â Â Â Â FOR ALL ENTRIES IN @connection\_tab
Â Â Â Â Â Â Â Â Â Â WHERE carrid = @connection\_tab-carrid AND
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â connid = @connection\_tab-connid
Â Â Â Â Â Â Â Â Â Â INTO TABLE @flights.
Â Â ENDIF.
ENDMETHOD.

Call with Native SQL Using ADBC Â  

The method ADBC uses [Native SQL](javascript:call_link\('abennative_sql_glosry.htm'\) "Glossary Entry") and [ADBC](javascript:call_link\('abenadbc_glosry.htm'\) "Glossary Entry") to call the database procedure. To pass the internal table connection\_tab to the database procedure, a temporary table DEMO\_ADBC\_CONNECTIONS with the required type DEMO\_ADBC\_CONNECTIONS\_TYPE is created on the database and provided with the values of the internal table using insert. The database procedure is then called with the temporary table as an input parameter and the result is assigned to an ADBC result set. To read from the result set, a standard table flights\_std is required as an auxiliary table, since the return value flights is a sorted table that cannot be used for ADBC. The temporary table DEMO\_ADBC\_CONNECTIONS is removed again after the database procedure is called.

METHOD adbc.
Â Â DATA(sql) = NEW cl\_sql\_statement( ).
Â Â TRY.
Â Â Â Â Â Â sql->execute\_update( 'truncate table DEMO\_ADBC\_CONNECTIONS' ).
Â Â Â Â Â Â sql->execute\_ddl(Â Â Â Â 'drop table DEMO\_ADBC\_CONNECTIONS' ).
Â Â Â Â CATCH cx\_sql\_exception ##no\_handler.
Â Â ENDTRY.
Â Â TRY.
Â Â Â Â Â Â sql->execute\_ddl(
Â Â Â Â Â Â Â Â \`create global temporary row table DEMO\_ADBC\_CONNECTIONS \` &&
Â Â Â Â Â Â Â Â Â Â  \`like DEMO\_ADBC\_CONNECTIONS\_TYPE\` ).
Â Â Â Â Â Â sql->set\_param\_table( REF #( connection\_tab ) ).
Â Â Â Â Â Â sql->execute\_update(
Â Â Â Â Â Â Â Â \`insert into DEMO\_ADBC\_CONNECTIONS values ( ?, ?, ? )\` ).
Â Â Â Â Â Â DATA(result) =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â sql->execute\_query(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â \`call "/1BCAMDP/DEMO\_ADBC\_GET\_FLIGHTS"( \` &&
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â \`Â Â CONNECTIONS =>DEMO\_ADBC\_CONNECTIONS, \` &&
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â \`Â Â FLIGHTS => NULL )\` ).
Â Â Â Â Â Â DATA flights\_std TYPE STANDARD TABLE OF sflight WITH EMPTY KEY.
Â Â Â Â Â Â result->set\_param\_table( REF #( flights\_std ) ).
Â Â Â Â Â Â result->next\_package( ).
Â Â Â Â Â Â result->close( ).
Â Â Â Â Â Â flights = flights\_std.
Â Â Â Â Â Â sql->execute\_update( 'truncate table DEMO\_ADBC\_CONNECTIONS' ).
Â Â Â Â Â Â sql->execute\_ddl(Â Â Â Â 'drop table DEMO\_ADBC\_CONNECTIONS' ).
Â Â Â Â CATCH cx\_sql\_exception.
Â Â Â Â Â Â CLEAR flights.
Â Â ENDTRY.
ENDMETHOD.

Call Using Database Procedure Proxy Â  

The method CDBP calls the database procedure with the statement [CALL DATABASE PROCEDURE](javascript:call_link\('abapcall_database_procedure.htm'\)) using the assigned [database procedure proxy](javascript:call_link\('abendatabase_proc_proxy_glosry.htm'\) "Glossary Entry") DEMO\_ADBC\_GET\_FLIGHTS\_PROXY. The only thing to note here is that input parameters and output parameters of the database procedure have different row types than the internal tables that are to be assigned to them. In both cases, the column CONNID has the type NVARCHAR(4) on the database, but the type n with length 4 in ABAP. For this reason, the internal table connection\_tab is first assigned to a temporary table connections with the correct row type before the procedure is called. No auxiliary table is required for the result flights, since the row type is described using an ABAP Dictionary type. An appropriate mapping can be performed for this type using the mapping table params. As an alternative to an auxiliary table for connection\_tab, its data type could also be declared in the ABAP Dictionary and an appropriate mapping performed.

The statement CALL DATABASE PROCEDURE is not possible on a [SAP HANA Cloud database](javascript:call_link\('abensap_hana_cloud_db_glosry.htm'\) "Glossary Entry").

METHOD cdbp.
Â Â TYPES:
Â Â Â Â BEGIN OF connection,
Â Â Â Â Â Â mandtÂ Â TYPE c LENGTH 3,
Â Â Â Â Â Â carrid TYPE c LENGTH 3,
Â Â Â Â Â Â connid TYPE c LENGTH 4,
Â Â Â Â END OF connection.
Â Â DATA connections TYPE STANDARD TABLE OF connection
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  WITH EMPTY KEY.
Â Â IF cl\_shdb\_hc=>is\_hana\_cloud(Â Â ).
Â Â Â Â RETURN.
Â Â ENDIF.
Â Â connections = connection\_tab.
Â Â CALL DATABASE PROCEDURE ('DEMO\_ADBC\_GET\_FLIGHTS\_PROXY')
Â Â Â Â EXPORTING connections = connections
Â Â Â Â IMPORTING flightsÂ Â Â Â  = flights.
ENDMETHOD.

Call Using AMDP Â  

The method AMDP calls the [AMDP method](javascript:call_link\('abenamdp_method_glosry.htm'\) "Glossary Entry") AMDP\_METH, in which a call of the database procedure is implemented in SQLScript. In this case, all type-dependent actions required are performed by the AMDP framework.

Method was not found



**ðŸ“– Source**: [abencall_hana_db_proc_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abencall_hana_db_proc_abexa.htm)

### abensql_script_curr_conv_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abensql_script_curr_conv_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abensql_script_curr_conv_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ðŸ“– Source**: [abensql_script_curr_conv_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abensql_script_curr_conv_abexa.htm)


Â  

* * *

AS ABAP Release 757, Â©Copyright 2023 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Processing External Data](javascript:call_link\('abenabap_language_external_data.htm'\))Â â†’Â  [ABAP Database Access](javascript:call_link\('abendb_access.htm'\))Â â†’Â  [ABAP and SAP HANA](javascript:call_link\('abenabap_hana.htm'\))Â â†’Â  [ABAP and SAP HANA, Examples](javascript:call_link\('abenabap_and_hana_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif&sap-language=EN "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback on ABAP Documentation&body=Document: SAP HANA, Currency Conversion with SQLScript, ABENSQL_SCRIPT_CURR_CONV_ABEXA, 757%0D%
0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion for improvement:)

SAP HANA, Currency Conversion with SQLScript

This example demonstrates how a built-in HANA SQL function is called in SQLScript.

Source Code Â  

REPORT demo\_sqlscript\_curr\_conv.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
Â Â PRIVATE SECTION.
Â Â Â Â CLASS-METHODS setup.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â FINAL(out) = cl\_demo\_output=>new( ).
Â Â Â Â DATA currency TYPE c LENGTH 5 VALUE 'USD'.
Â Â Â Â cl\_demo\_input=>request( CHANGING field = currency ).
Â Â Â Â setup( ).
Â Â Â Â SELECT \*
Â Â Â Â Â Â Â Â Â Â  FROM demo\_prices
Â Â Â Â Â Â Â Â Â Â  ORDER BY id
Â Â Â Â Â Â Â Â Â Â  INTO TABLE @FINAL(original\_prices).
Â Â Â Â out->begin\_section( \`Original Prices\`
Â Â Â Â Â Â )->write( original\_prices ).
Â Â Â Â IF cl\_abap\_dbfeatures=>use\_features(
Â Â Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â Â Â requested\_features =
Â Â Â Â Â Â Â Â Â Â Â Â Â Â VALUE #( ( cl\_abap\_dbfeatures=>call\_amdp\_method ) ) ).
Â Â Â Â Â Â NEW cl\_demo\_sqlscript\_curr\_conv(
Â Â Â Â Â Â Â Â )->convert(
Â Â Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â Â Â to\_currencyÂ Â Â Â Â Â = to\_upper( currency )
Â Â Â Â Â Â Â Â Â Â Â Â mandtÂ Â Â Â Â Â Â Â Â Â Â Â = sy-mandt
Â Â Â Â Â Â Â Â Â Â Â Â dateÂ Â Â Â Â Â Â Â Â Â Â Â  = sy-datlo ).
Â Â Â Â ELSE.
Â Â Â Â Â Â NEW cl\_demo\_sqlscript\_curr\_conv(
Â Â Â Â Â Â Â Â )->abap\_convert(
Â Â Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â Â Â to\_currencyÂ Â Â Â Â Â = to\_upper( currency )
Â Â Â Â Â Â Â Â Â Â Â Â mandtÂ Â Â Â Â Â Â Â Â Â Â Â = sy-mandt
Â Â Â Â Â Â Â Â Â Â Â Â dateÂ Â Â Â Â Â Â Â Â Â Â Â  = sy-datlo ).
Â Â Â Â ENDIF.
Â Â Â Â SELECT \*
Â Â Â Â Â Â Â Â Â Â  FROM demo\_prices
Â Â Â Â Â Â Â Â Â Â  ORDER BY id
Â Â Â Â Â Â Â Â Â Â  INTO TABLE @FINAL(converted\_prices).
Â Â Â Â out->next\_section( \`Converted Prices\`
Â Â Â Â Â Â )->write( converted\_prices
Â Â Â Â Â Â )->display( ).Â Â ENDMETHOD.
Â Â METHOD setup.
Â Â Â Â DELETE FROM demo\_prices.
Â Â Â Â INSERT demo\_prices FROM TABLE @( VALUE #(
Â Â Â Â Â Â ( id = 1 amount = '1.00' currency = 'EUR' )
Â Â Â Â Â Â ( id = 2 amount = '1.00' currency = 'GBP' )
Â Â Â Â Â Â ( id = 3 amount = '1.00' currency = 'JPY' )
Â Â Â Â Â Â ( id = 4 amount = '1.00' currency = 'USD' ) ) ).
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description Â  

If possible, the example program calls the [AMDP method](javascript:call_link\('abenamdp_method_glosry.htm'\) "Glossary Entry") CONVERT of the [AMDP class](javascript:call_link\('abenamdp_class_glosry.htm'\) "Glossary Entry") CL\_DEMO\_SQLSCRIPT\_CURR\_CONV.

METHOD convert BY DATABASE PROCEDURE FOR HDB
Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  LANGUAGE SQLSCRIPT
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  USING demo\_prices.
Â Â replace DEMO\_PRICES
Â Â Â Â select client,
Â Â Â Â Â Â Â Â Â Â  id,
Â Â Â Â Â Â Â Â Â Â  CONVERT\_CURRENCY(
Â Â Â  Â Â Â Â Â Â Â Â Â Â "AMOUNT" => "DEMO\_PRICES"."AMOUNT",
Â Â Â Â Â Â Â Â Â Â Â Â Â Â "SOURCE\_UNIT" => "DEMO\_PRICES"."CURRENCY",
Â Â Â Â Â Â Â Â Â Â Â Â Â Â "TARGET\_UNIT" => :TO\_CURRENCY,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â "REFERENCE\_DATE" => :DATE,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â "CONVERSION\_TYPE" => N'M',
Â Â Â Â Â Â Â Â Â Â Â Â Â Â "CLIENT" => :MANDT,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â "ERROR\_HANDLING" => N'SET\_TO\_NULL',
Â Â Â Â Â Â Â Â Â Â Â Â Â Â "SCHEMA" => CURRENT\_SCHEMA,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â "ACCURACY" => N'highest',
Â Â Â Â Â Â Â Â Â Â Â Â Â Â "LOOKUP" => N'regular',
Â Â Â Â Â Â Â Â Â Â Â Â Â Â "STEPS" => 'convert,shift\_back,shift,round' ) as amount,
Â Â Â Â Â Â Â Â Â Â Â Â :TO\_CURRENCY as currency
Â Â Â Â Â Â from DEMO\_PRICES;
ENDMETHOD.

The SQLScript implementation of this method uses the built-in SQL function CONVERT\_CURRENCY to convert the amounts in the column amount in the database DEMO\_PRICES to a currency entered by the user.

If the current database is not an SAP HANA database, the method ABAP\_CONVERT is called as an alternative.

METHOD abap\_convert.
Â Â DATA prices TYPE STANDARD TABLE OF demo\_prices.
Â Â SELECT \*
Â Â Â Â Â Â Â Â  FROM demo\_prices
Â Â Â Â Â Â Â Â  INTO TABLE prices.
Â Â LOOP AT prices ASSIGNING FIELD-SYMBOL(<price>).
Â Â Â Â CALL FUNCTION 'CONVERT\_TO\_LOCAL\_CURRENCY'
Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â clientÂ Â Â Â Â Â Â Â Â Â  = mandt
Â Â Â Â Â Â Â Â dateÂ Â Â Â Â Â Â Â Â Â Â Â  = date
Â Â Â Â Â Â Â Â foreign\_amountÂ Â  = <price>-amount
Â Â Â Â Â Â Â Â foreign\_currency = <price>-currency
Â Â Â Â Â Â Â Â local\_currencyÂ Â  = to\_currency
Â Â Â Â Â Â IMPORTING
Â Â Â Â Â Â Â Â local\_amountÂ Â Â Â  = <price>-amount
Â Â Â Â Â Â EXCEPTIONS
Â Â Â Â Â Â Â Â OTHERSÂ Â Â Â Â Â Â Â Â Â  = 4.
Â Â Â Â IF sy-subrc <> 0.
Â Â Â Â Â Â CONTINUE.
Â Â Â Â ENDIF.
Â Â Â Â <price>-currency = to\_currency.
Â Â ENDLOOP.
Â Â MODIFY demo\_prices FROM TABLE prices.
ENDMETHOD.

This method uses the function module CONVERT\_TO\_LOCAL\_CURRENCY. Here, the table to b converted is first read to an internal table in AS ABAP and must be written again after the conversion.

Hint

See also

-   [Unit and Currency Conversion Functions in ABAP SQL](javascript:call_link\('abensql_curr_unit_conv_func.htm'\)).
    
    The built-in SQL function CURRENCY\_CONVERSION wraps the HANA function CONVERT\_CURRENCY. The program DEMO\_ASQL\_FUNC\_CURR\_CONV shows how the same function as above can be achieved by using an ABAP SQL SELECT that calls the SQL function.
    
-   [Unit and Currency Conversion Functions in ABAP CDS](javascript:call_link\('abencds_conversion_functions_v2.htm'\)).
    
    The built-in CDS function CURRENCY\_CONVERSION wraps the HANA function CONVERT\_CURRENCY. The program DEMO\_CDS\_FUNC\_CURR\_CONV\_VE shows how the same function as above can be achieved by using a CDS view that calls the CDS function.
    

A complete code relocation using a subquery in the ABAP SQL statement MODIFY is only possible if no table logging takes place.
