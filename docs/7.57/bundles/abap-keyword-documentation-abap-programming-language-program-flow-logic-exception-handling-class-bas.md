# ABAP - Keyword Documentation / ABAP - Programming Language / Program Flow Logic / Exception Handling / Class-Based Exceptions / Examples of Exceptions

Included pages: 4



## ðŸ”— Source References

**Base URL**: [ABAP Keyword Documentation 7.57](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abenexception_abexas.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenexception_abexas.htm)
- [abenraise_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenraise_abexa.htm)
- [abentry_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abentry_abexa.htm)
- [abencatch_exception_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abencatch_exception_abexa.htm)

**Bundle Contains**: 4 documentation pages
**Version**: ABAP 7.57
**Generated**: 2025-09-01T11:25:45.269Z

---

### abenexception_abexas.htm

> **ðŸ“– Official SAP Documentation**: [abenexception_abexas.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenexception_abexas.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 757, Â©Copyright 2023 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Program Flow Logic](javascript:call_link\('abenabap_flow_logic.htm'\))Â â†’Â  [Exception Handling](javascript:call_link\('abenabap_exceptions.htm'\))Â â†’Â  [Class-Based Exceptions](javascript:call_link\('abenexceptions.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif&sap-language=EN "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback on ABAP Documentation&body=Document: Examples of Exceptions, ABENEXCEPTION_ABEXAS, 757%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A
%0D%0ASuggestion for improvement:)

Examples of Exceptions

Continue
![Example](exa.gif "Example") [Exceptions, RAISE](javascript:call_link\('abenraise_abexa.htm'\))
![Example](exa.gif "Example") [Exceptions, TRY](javascript:call_link\('abentry_abexa.htm'\))
![Example](exa.gif "Example") [Exceptions, CATCH](javascript:call_link\('abencatch_exception_abexa.htm'\))



**ðŸ“– Source**: [abenexception_abexas.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenexception_abexas.htm)

### abenraise_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abenraise_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenraise_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 757, Â©Copyright 2023 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Program Flow Logic](javascript:call_link\('abenabap_flow_logic.htm'\))Â â†’Â  [Exception Handling](javascript:call_link\('abenabap_exceptions.htm'\))Â â†’Â  [Class-Based Exceptions](javascript:call_link\('abenexceptions.htm'\))Â â†’Â  [Examples of Exceptions](javascript:call_link\('abenexception_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif&sap-language=EN "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback on ABAP Documentation&body=Document: Exceptions, RAISE, ABENRAISE_ABEXA, 757%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASugg
estion for improvement:)

Exceptions, RAISE

This example demonstrates the statement RAISE EXCEPTION.

Source Code Â  

REPORT demo\_raise\_exception.
CLASS demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA: orefÂ Â Â Â TYPE REF TO cx\_demo\_constructor,
Â Â Â Â Â Â Â Â Â Â textÂ Â Â Â  TYPE string,
Â Â Â Â Â Â Â Â Â Â position TYPE i.
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â TRY.
Â Â Â Â Â Â Â Â Â Â Â Â RAISE EXCEPTION TYPE cx\_demo\_constructor
Â Â Â Â Â Â Â Â Â Â Â Â Â Â EXPORTING
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â my\_text = sy-repid.
Â Â Â Â Â Â Â Â Â Â CATCH cx\_demo\_constructor INTO oref.
Â Â Â Â Â Â Â Â Â Â Â Â text = oref->get\_text( ).
Â Â Â Â Â Â Â Â Â Â Â Â oref->get\_source\_position(
Â Â Â Â Â Â Â Â Â Â Â Â Â Â IMPORTINGÂ Â source\_lineÂ Â = position ).
Â Â Â Â Â Â Â Â Â Â Â Â cl\_demo\_output=>WRITE\_text( |{ position } { text }| ).
Â Â Â Â Â Â Â Â Â Â Â Â RAISE EXCEPTION oref.
Â Â Â Â Â Â Â Â ENDTRY.
Â Â Â Â Â Â CATCH cx\_demo\_constructor INTO oref.
Â Â Â Â Â Â Â Â text = oref->get\_text( ).
Â Â Â Â Â Â Â Â oref->get\_source\_position(
Â Â Â Â Â Â Â Â Â Â IMPORTING source\_lineÂ Â = position ).
Â Â Â Â Â Â Â Â cl\_demo\_output=>WRITE\_text( |{ position } { text }| ).
Â Â Â Â ENDTRY.
Â Â Â Â cl\_demo\_output=>display( ).Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â demo=>main( ).

Description Â  

This example shows the two variants of the statement RAISE EXCEPTION. The first statement raises an exception of the class CX\_DEMO\_CONSTRUCTOR in the inner TRY block, creates the relevant object, and passes the program name to the instance constructor. The inner CATCH block handles the exception, produces the exception text as output and raises the exception again without creating a new object. The outer CATCH block handles the exception again. The class CX\_DEMO\_CONSTRUCTOR is defined in such a way that the passed program name appears in the exception text. The generated instance constructor ensures this.

The output of the line number in which the exception was raised indicates that, when the existing exception object was reused, its information was changed.



**ðŸ“– Source**: [abenraise_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abenraise_abexa.htm)

### abentry_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abentry_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abentry_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 757, Â©Copyright 2023 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Program Flow Logic](javascript:call_link\('abenabap_flow_logic.htm'\))Â â†’Â  [Exception Handling](javascript:call_link\('abenabap_exceptions.htm'\))Â â†’Â  [Class-Based Exceptions](javascript:call_link\('abenexceptions.htm'\))Â â†’Â  [Examples of Exceptions](javascript:call_link\('abenexception_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif&sap-language=EN "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback on ABAP Documentation&body=Document: Exceptions, TRY, ABENTRY_ABEXA, 757%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggesti
on for improvement:)

Exceptions, TRY

This example demonstrates the TRY control structure.

Source Code Â  

REPORT demo\_try.
CLASS try\_demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-DATA: result TYPE p LENGTH 8 DECIMALS 2,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â orefÂ Â  TYPE REF TO cx\_root,
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â textÂ Â  TYPE string.
Â Â Â Â CLASS-METHODS main.
Â Â PRIVATE SECTION.
Â Â Â Â CLASS-DATA number TYPE i.
Â Â Â Â CLASS-DATA out TYPE REF TO if\_demo\_output.
Â Â Â Â CLASS-METHODS calculation
Â Â Â Â Â Â IMPORTINGÂ Â p\_number LIKE number
Â Â Â Â Â Â CHANGING p\_result LIKE result
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  p\_textÂ Â  LIKE text
Â Â Â Â Â Â RAISINGÂ Â cx\_sy\_arithmetic\_error.
ENDCLASS.
CLASS try\_demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â cl\_demo\_input=>request( CHANGING field = number ).
Â Â Â Â out = cl\_demo\_output=>new( ).
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â IF abs( number ) > 100.
Â Â Â Â Â Â Â Â Â Â RAISE EXCEPTION TYPE cx\_demo\_abs\_too\_large.
Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â calculation( EXPORTING p\_number = number
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  CHANGINGÂ Â p\_result = result
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  p\_textÂ Â  = text ).
Â Â Â Â Â Â CATCH cx\_sy\_arithmetic\_error INTO oref.
Â Â Â Â Â Â Â Â text = oref->get\_text( ).
Â Â Â Â Â Â CATCH cx\_root INTO oref.
Â Â Â Â Â Â Â Â text = oref->get\_text( ).
Â Â Â Â ENDTRY.
Â Â Â Â IF NOT text IS INITIAL.
Â Â Â Â Â Â out->write( text ).
Â Â Â Â ENDIF.
Â Â Â Â out->display( |Final result: { result ALIGN = LEFT }| ).
Â Â ENDMETHOD.
Â Â METHOD calculation.
Â Â Â Â DATA l\_oref TYPE REF TO cx\_root.
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â p\_result =Â Â 1 / p\_number.
Â Â Â Â Â Â Â Â out->write(
Â Â Â Â Â Â Â Â Â Â |Result of division: { p\_result ALIGN = LEFT }| ).
Â Â Â Â Â Â Â Â p\_result = sqrt( p\_number ).
Â Â Â Â Â Â Â Â out->write(
Â Â Â Â Â Â Â Â Â Â |Result of square root: { p\_result ALIGN = LEFT }| ).
Â Â Â Â Â Â CATCH cx\_sy\_zerodivide INTO l\_oref.
Â Â Â Â Â Â Â Â p\_text = l\_oref->get\_text( ).
Â Â Â Â Â Â CLEANUP.
Â Â Â Â Â Â Â Â CLEAR p\_result.
Â Â Â Â ENDTRY.
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â try\_demo=>main( ).

Description Â  

-   If the content of number is greater than 100, the TRY control structure of the [compilation unit](javascript:call_link\('abencompilation_unit_glosry.htm'\) "Glossary Entry") raises an exception of the class CX\_DEMO\_ABS\_TOO\_LARGE in the TRY block. This exception is handled by the second CATCH block of the same TRY control structure, since the subclass of the most general exception is CX\_ROOT.
-   If the content of number is 0, the runtime framework in the TRY block of the TRY control structure of the called method calculation raises an exception of the predefined class CX\_SY\_ZERODIVIDE. This is handled in the CATCH block of the same TRY control structure.
-   If the content of number is a negative number, the runtime framework in the TRY block of the TRY control structure of the called method calculation raises an exception of the predefined class CX\_SY\_ARG\_OUT\_OF\_DOMAIN. Since no handler is defined for this exception in this TRY control structure, the exception is propagated from the method, which is made possible by the declaration of the superclass CX\_SY\_ARITHMETIC\_ERROR using RAISING in the method interface. The CLEANUP block of the inner TRY control structure is executed first.
-   Any other exceptions are handled in the final CATCH block of the TRY control structure of the compilation unit, which catches all possible exceptions by specifying the most generic exception class CX\_ROOT. If, for example, CX\_SY\_ARG\_OUT\_OF\_DOMAIN or one of its superclasses was not declared in the method interface, the exception CX\_SY\_NO\_HANDLER would be raised and would be handled in the final CATCH block.



**ðŸ“– Source**: [abentry_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abentry_abexa.htm)

### abencatch_exception_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abencatch_exception_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abencatch_exception_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ðŸ“– Source**: [abencatch_exception_abexa.htm](https://help.sap.com/doc/abapdocu_757_index_htm/7.57/en-US/abencatch_exception_abexa.htm)


Â  

* * *

AS ABAP Release 757, Â©Copyright 2023 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Program Flow Logic](javascript:call_link\('abenabap_flow_logic.htm'\))Â â†’Â  [Exception Handling](javascript:call_link\('abenabap_exceptions.htm'\))Â â†’Â  [Class-Based Exceptions](javascript:call_link\('abenexceptions.htm'\))Â â†’Â  [Examples of Exceptions](javascript:call_link\('abenexception_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif&sap-language=EN "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback on ABAP Documentation&body=Document: Exceptions, CATCH, ABENCATCH_EXCEPTION_ABEXA, 757%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A
%0D%0ASuggestion for improvement:)

Exceptions, CATCH

This example demonstrates how class-based exceptions are caught.

Source Code Â  

REPORT demo\_catch\_exception.
FINAL(in) = cl\_demo\_input=>new( ).
DATA: resumableÂ Â Â Â  TYPE abap\_bool VALUE abap\_false,
Â Â Â Â Â Â before\_unwind TYPE abap\_bool VALUE abap\_false,
Â Â Â Â Â Â resumeÂ Â Â Â Â Â Â Â TYPE abap\_bool VALUE abap\_false.
in->add\_field( EXPORTING as\_checkbox = 'X'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  text = 'RAISE RESUMABLE'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  CHANGINGÂ Â field = resumable
)->add\_field( EXPORTING as\_checkbox = 'X'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  text = 'CATCH BEFORE UNWIND'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  CHANGINGÂ Â field = before\_unwind
)->add\_field( EXPORTING as\_checkbox = 'X'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  text = 'RESUME'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  CHANGINGÂ Â field = resume
)->request( ).
CLASS lcx\_exception DEFINITION INHERITING FROM cx\_static\_check.
ENDCLASS.
CLASS exc\_demo DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-DATA out TYPE REF TO if\_demo\_output.
Â Â Â Â CLASS-METHODS: main,
Â Â Â Â Â Â meth1 RAISING lcx\_exception,
Â Â Â Â Â Â meth2 RAISING RESUMABLE(lcx\_exception).
ENDCLASS.
FIELD-SYMBOLS <fs> TYPE any.
CLASS exc\_demo IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â out = cl\_demo\_output=>new( ).
Â Â Â Â DATA exc TYPE REF TO lcx\_exception.
Â Â Â Â IF before\_unwind = abap\_false.
Â Â Â Â Â Â TRY.
Â Â Â Â Â Â Â Â Â Â out->write( 'Trying method call' ).
Â Â Â Â Â Â Â Â Â Â IF resumable = abap\_false.
Â Â Â Â Â Â Â Â Â Â Â Â exc\_demo=>meth1( ).
Â Â Â Â Â Â Â Â Â Â ELSEIF resumable = abap\_true.
Â Â Â Â Â Â Â Â Â Â Â Â exc\_demo=>meth2( ).
Â Â Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â CATCH lcx\_exception.
Â Â Â Â Â Â Â Â Â Â IF <fs> IS ASSIGNED.
Â Â Â Â Â Â Â Â Â Â Â Â out->write( 'Context of method available' ).
Â Â Â Â Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â Â Â Â Â out->write( 'Context of method not available' ).
Â Â Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â ENDTRY.
Â Â Â Â Â Â out->write( 'Continue after main TRY block' ).
Â Â Â Â ELSEIF before\_unwind = abap\_true.
Â Â Â Â Â Â TRY.
Â Â Â Â Â Â Â Â Â Â out->write( 'Trying method call' ).
Â Â Â Â Â Â Â Â Â Â IF resumable = abap\_false.
Â Â Â Â Â Â Â Â Â Â Â Â exc\_demo=>meth1( ).
Â Â Â Â Â Â Â Â Â Â ELSEIF resumable = abap\_true.
Â Â Â Â Â Â Â Â Â Â Â Â exc\_demo=>meth2( ).
Â Â Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â CATCH BEFORE UNWIND lcx\_exception INTO exc.
Â Â Â Â Â Â Â Â Â Â IF <fs> IS ASSIGNED.
Â Â Â Â Â Â Â Â Â Â Â Â out->write( 'Context of method available' ).
Â Â Â Â Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â Â Â Â Â out->write( 'Context of method not available' ).
Â Â Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â Â Â IF resume = abap\_true.
Â Â Â Â Â Â Â Â Â Â Â Â IF exc->is\_resumable = abap\_true.
Â Â Â Â Â Â Â Â Â Â Â Â Â Â RESUME.
Â Â Â Â Â Â Â Â Â Â Â Â ELSE.
Â Â Â Â Â Â Â Â Â Â Â Â Â Â out->write( 'Resumption not possible' ).
Â Â Â Â Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â ENDTRY.
Â Â Â Â Â Â out->write( 'Continue after main TRY block' ).
Â Â Â Â ENDIF.
Â Â Â Â out->display( ).
Â Â ENDMETHOD.
Â Â METHOD meth1.
Â Â Â Â DATA loc TYPE i.
Â Â Â Â ASSIGN loc TO <fs>.
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â out->write( 'Raising non-resumable exception' ).
Â Â Â Â Â Â Â Â RAISE EXCEPTION TYPE lcx\_exception.
Â Â Â Â Â Â Â Â out->write( 'Never executed' ).
Â Â Â Â Â Â CLEANUP.
Â Â Â Â Â Â Â Â out->write( 'Cleanup in method' ).
Â Â Â Â ENDTRY.
Â Â Â Â out->write( 'Continue after TRY block in method' ).
Â Â ENDMETHOD.
Â Â METHOD meth2.
Â Â Â Â DATA loc TYPE i.
Â Â Â Â ASSIGN loc TO <fs>.
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â out->write( 'Raising resumable exception' ).
Â Â Â Â Â Â Â Â RAISE RESUMABLE EXCEPTION TYPE lcx\_exception.
Â Â Â Â Â Â Â Â out->write( 'Resuming method' ).
Â Â Â Â Â Â CLEANUP.
Â Â Â Â Â Â Â Â out->write( 'Cleanup in method' ).
Â Â Â Â ENDTRY.
Â Â Â Â out->write( 'Continue after TRY block in method' ).
Â Â ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
Â Â exc\_demo=>main( ).

Description Â  

The method meth1 raises a non-resumable exception and the method meth2 raises a resumable exception that is handled in the TRY control structures of the method main using CATCH.

-   If handled without BEFORE UNWIND, the CLEANUP block is executed in both cases before handling and the context of the called method does not exist during handling.
-   If handled with BEFORE UNWIND, the context of the called method exists in both cases during handling and the CLEANUP block is executed after handling.
-   If a resumable exception is raised, the statement RESUME can be executed during handling, which ensures that processing in the called method is continued without its CLEANUP block being executed.
