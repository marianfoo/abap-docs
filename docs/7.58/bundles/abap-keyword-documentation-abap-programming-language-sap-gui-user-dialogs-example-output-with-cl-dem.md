# ABAP - Keyword Documentation / ABAP - Programming Language / SAP GUI User Dialogs / Example Output with CL_DEMO_OUTPUT

Included pages: 2



## ğŸ”— Source References

**Base URL**: [ABAP Keyword Documentation 7.58](https://help.sap.com/doc/abapdocu_758_index_htm/7.58/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abencl_demo_output.htm](https://help.sap.com/doc/abapdocu_758_index_htm/7.58/en-US/abencl_demo_output.htm)
- [abencl_demo_output_abexa.htm](https://help.sap.com/doc/abapdocu_758_index_htm/7.58/en-US/abencl_demo_output_abexa.htm)

**Bundle Contains**: 2 documentation pages
**Version**: ABAP 7.58
**Generated**: 2025-09-01T11:25:29.002Z

---

### abencl_demo_output.htm

> **ğŸ“– Official SAP Documentation**: [abencl_demo_output.htm](https://help.sap.com/doc/abapdocu_758_index_htm/7.58/en-US/abencl_demo_output.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


> **ğŸ“– Official SAP Documentation**: [abencl_demo_output.htm](https://help.sap.com/doc/abapdocu_758_index_htm/7.58/en-US/abencl_demo_output.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 758, Â©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20Example%20Output%20with%20CL_DEMO_OUTPUT%2C%20ABENCL_DEMO_OUTPUT%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

Example Output with CL\_DEMO\_OUTPUT

Class CL\_DEMO\_OUTPUT is provided by the ABAP Keyword Documentation to create simple outputs of data in example programs without the need of [classic lists](javascript:call_link\('abenabap_dynpro_list.htm'\)). It is not intended for production usage. It can be used in demonstration programs, local test programs or for temporary testing in production programs. It must not be used in production programs.

The class can be used via static methods and instance methods. For using instance methods, interface IF\_DEMO\_OUTPUT can be used. The following methods create output in an output stream:

-   Methods BEGIN\_SECTION, NEXT\_SECTION, and END\_SECTION create headers and open or close header levels.
-   Methods WRITE\_DATA, WRITE\_TEXT, WRITE\_XML, WRITE\_JSON, WRITE\_HTML and so on write different kinds of output to the output stream.
    -   With method WRITE\_DATA any kind of ABAP data can be written to the output stream.
    -   The other methods create formatted outputs of texts, XML, JSON, or HTML data.
-   Method WRITE is generic and handles ABAP data as well as texts in non-proportional format.
-   Methods DISPLAY\_... (available as static methods only) work as WRITE\_... but close the current output stream and open a new one. If a SAP GUI is available, the output is displayed in a window.
-   Method LINE creates a horizontal line.
-   Method DISPLAY closes the current output stream and opens a new one. If a SAP GUI is available, the output is displayed in a window. Optionally, data can be passed to DISPLAY in the same way as for WRITE.
-   Method GET works like DISPLAY but does not display the data. Instead the formatted output data are returned in a text string and can be handled further.

The standard output format is HTML. Optionally, also formats TEXT, JSON and XML can be set. The format is set with method SET\_MODE for the static methods or using the input parameter MODE of the factory method NEW for the instance methods.

Each instance method returns the self reference me in order to enable method chainings.

Hints

-   CL\_DEMO\_OUTPUT is based on a class CL\_DEMO\_OUTPUT\_STREAM that collects the data in an internal table that is converted to an XML file and raises an event on completion. Special handler classes convert the XML file to HTML, plain text, or JSON depending on the output mode.
-   CL\_DEMO\_OUTPUT is widely used in the example classes and programs of the ABAP Keyword Documentation.
-   CL\_DEMO\_OUTPUT is integrated into the ABAP console of [ADT](javascript:call_link\('abenadt_glosry.htm'\) "Glossary Entry") that can be filled by executing classes (inheriting from CL\_DEMO\_CLASSRUN or implementing IF\_OO\_ADT\_CLASSRUN) or executable programs by F9.
-   There is also a class CL\_DEMO\_INPUT for rudimentary inputs. But this class is not as powerful as [selection screens](javascript:call_link\('abenselection_screen_glosry.htm'\) "Glossary Entry").

Executable Example

[Usage of CL\_DEMO\_OUTPUT](javascript:call_link\('abencl_demo_output_abexa.htm'\))

Continue
![Example](exa.gif "Example") [Usage of CL\_DEMO\_OUTPUT](javascript:call_link\('abencl_demo_output_abexa.htm'\))



**ğŸ“– Source**: [abencl_demo_output.htm](https://help.sap.com/doc/abapdocu_758_index_htm/7.58/en-US/abencl_demo_output.htm)

### abencl_demo_output_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abencl_demo_output_abexa.htm](https://help.sap.com/doc/abapdocu_758_index_htm/7.58/en-US/abencl_demo_output_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


> **ğŸ“– Official SAP Documentation**: [abencl_demo_output_abexa.htm](https://help.sap.com/doc/abapdocu_758_index_htm/7.58/en-US/abencl_demo_output_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ğŸ“– Source**: [abencl_demo_output_abexa.htm](https://help.sap.com/doc/abapdocu_758_index_htm/7.58/en-US/abencl_demo_output_abexa.htm)


Â  

* * *

AS ABAP Release 758, Â©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [SAP GUI User Dialogs](javascript:call_link\('abenabap_screens.htm'\))Â â†’Â  [Example Output with CL\_DEMO\_OUTPUT](javascript:call_link\('abencl_demo_output.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20Usage%20of%20CL_DEMO_OUTPUT%2C%20ABENCL_DEMO_OUTPUT_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

Usage of CL\_DEMO\_OUTPUT

Source Code Â  

\* Public class definition
CLASS cl\_demo\_output\_usage DEFINITION
Â Â PUBLIC
Â Â INHERITING FROM cl\_demo\_classrun
Â Â CREATE PUBLIC .
Â Â PUBLIC SECTION.
Â Â Â Â METHODS main
Â Â Â Â Â Â Â Â REDEFINITION .
ENDCLASS.
\* CCIMP
CLASS demo\_class DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â TYPES:
Â Â Â Â BEGIN OF spfli\_line,
Â Â Â Â carrid TYPE spfli-carrid,
Â Â Â Â connid TYPE spfli-connid,
Â Â Â Â cityfrom TYPE spfli-cityfrom,
Â Â Â Â cityto TYPE spfli-cityto,
Â Â Â Â END OF spfli\_line,
Â Â Â Â spfli\_tab TYPE HASHED TABLE OF spfli\_line
Â Â Â Â WITH UNIQUE KEY carrid connid,
Â Â Â Â BEGIN OF struct,
Â Â Â Â carrname TYPE scarr-carrname,
Â Â Â Â spfli TYPE REF TO spfli\_tab,
Â Â Â Â END OF struct.
Â Â Â Â DATA result TYPE SORTED TABLE OF struct WITH UNIQUE KEY carrname.
Â Â Â Â METHODS constructor.
ENDCLASS.
CLASS demo\_class IMPLEMENTATION.
Â Â METHOD constructor.
Â Â Â Â SELECT s~carrname, p~carrid, p~connid, p~cityfrom, p~cityto
Â Â Â Â FROM scarr AS s
Â Â Â Â INNER JOIN spfli AS p
Â Â Â Â ON s~carrid = p~carrid
Â Â Â Â ORDER BY s~carrname
Â Â Â Â INTO TABLE @DATA(itab).
Â Â Â Â LOOP AT itab ASSIGNING FIELD-SYMBOL(<fs>) GROUP BY <fs>-carrname.
Â Â Â Â Â Â INSERT VALUE #( carrname = <fs>-carrname ) INTO TABLE result
Â Â Â Â Â Â ASSIGNING FIELD-SYMBOL(<line>).
Â Â Â Â Â Â <line>-spfli = NEW #(
Â Â Â Â Â Â FOR <wa> IN GROUP <fs> ( CORRESPONDING #( <wa> ) ) ).
Â Â Â Â ENDLOOP.
Â Â ENDMETHOD.
ENDCLASS.
\* Public class implementation
CLASS cl\_demo\_output\_usage IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â DATA mode TYPE string VALUE 'HTML'.
Â Â Â Â Â Â Â Â cl\_demo\_input=>new(
Â Â Â Â Â Â Â Â  )->add\_text( 'HTML, TEXT, JSON, XML'
Â Â Â Â Â Â Â Â  )->add\_field( CHANGING field = mode
Â Â Â Â Â Â Â Â  )->request( ).
Â Â Â Â Â Â Â Â mode = to\_upper( mode ).
Â Â Â Â Â Â Â Â out->set\_mode( mode ).
Â Â Â Â Â Â Â Â out->line(
Â Â Â Â Â Â Â Â  )->write( 'Example output produced by CL\_DEMO\_OUTPUT'
Â Â Â Â Â Â Â Â  )->line( ).
Â Â Â Â Â Â Â Â DATA(number) = 111.
Â Â Â Â Â Â Â Â out->next\_section( 'Elementary Field'
Â Â Â Â Â Â Â Â )->write( number ).
Â Â Â Â Â Â Â Â SELECT SINGLE \*
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  FROM scarr
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  WHERE carrid = 'UA'
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  INTO @FINAL(scarr\_wa).
Â Â Â Â Â Â Â Â out->next\_section( 'Structure with Elementary Components'
Â Â Â Â Â Â Â Â )->write( scarr\_wa ).
Â Â Â Â Â Â Â Â SELECT \*
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  FROM scarr
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ORDER BY carrid
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  INTO TABLE @FINAL(scarr\_tab).
Â Â Â Â Â Â Â Â out->next\_section( 'Table with Elementary Columns'
Â Â Â Â Â Â Â Â )->write( scarr\_tab ).
Â Â Â Â Â Â Â Â DATA:
Â Â Â Â Â Â Â Â Â Â BEGIN OF complex\_structure,
Â Â Â Â Â Â Â Â Â Â Â Â col1 LIKE number,
Â Â Â Â Â Â Â Â Â Â Â Â col2 LIKE scarr\_wa,
Â Â Â Â Â Â Â Â Â Â Â Â col3 LIKE scarr\_tab,
Â Â Â Â Â Â Â Â Â Â END OF complex\_structure.
Â Â Â Â Â Â Â Â complex\_structure = VALUE #( col1 = number
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  col2 = scarr\_wa
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  col3 = scarr\_tab ).
Â Â Â Â Â Â Â Â out->next\_section( 'Structure with Complex Components'
Â Â Â Â Â Â Â Â )->write( complex\_structure ).
Â Â Â Â Â Â Â Â DATA complex\_tab LIKE STANDARD TABLE OF complex\_structure.
Â Â Â Â Â Â Â Â complex\_tab = VALUE #(
Â Â Â Â Â Â Â Â Â Â ( col1 = 1
Â Â Â Â Â Â Â Â Â Â Â Â col2 = scarr\_tab\[ 1 \]
Â Â Â Â Â Â Â Â Â Â Â Â col3 = VALUE #( ( LINES OF scarr\_tab FROM 1 TO 3 ) ) )
Â Â Â Â Â Â Â Â Â Â ( col1 = 2
Â Â Â Â Â Â Â Â Â Â Â Â col2 = scarr\_tab\[ 4 \]
Â Â Â Â Â Â Â Â Â Â Â Â col3 = VALUE #( ( LINES OF scarr\_tab FROM 4 TO 6 ) ) ) ).
Â Â Â Â Â Â Â Â out->next\_section( 'Table with Complex Columns'
Â Â Â Â Â Â Â Â )->write( complex\_tab ).
Â Â Â Â Â Â Â Â DATA tab\_in\_tab LIKE TABLE OF scarr\_tab.
Â Â Â Â Â Â Â Â tab\_in\_tab = VALUE #(
Â Â Â Â Â Â Â Â Â Â ( VALUE #( ( LINES OF scarr\_tab FROM 1 TO 3 ) ) )
Â Â Â Â Â Â Â Â Â Â ( VALUE #( ( LINES OF scarr\_tab FROM 4 TO 6 ) ) ) ).
Â Â Â Â Â Â Â Â out->next\_section( 'Table in Table'
Â Â Â Â Â Â Â Â )->write( tab\_in\_tab ).
Â Â Â Â Â Â Â Â DATA(oref) = NEW demo\_class( ).
Â Â Â Â Â Â Â Â out->next\_section( 'Referenced Object and Data Object'
Â Â Â Â Â Â Â Â )->write( oref ).
Â Â Â Â Â Â CATCH cx\_root INTO FINAL(exc).
Â Â Â Â Â Â Â Â out->write( exc->get\_text( ) ).
Â Â Â Â ENDTRY.
Â Â ENDMETHOD.
ENDCLASS.

Description Â  

The class shows the output produced by [CL\_DEMO\_OUTPUT](javascript:call_link\('abencl_demo_output.htm'\)) for some ABAP data types. The output mode can be entered. More usage of CL\_DEMO\_OUTPUT can be found in many examples of the ABAP Keyword Documentation.
