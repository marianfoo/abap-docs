# ABAP - Keyword Documentation / ABAP - Programming Language / ABAP Objects / ABAP Objects - Examples

Included pages: 8



## ğŸ”— Source References

**Base URL**: [ABAP Keyword Documentation latest](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abenabap_objects_abexas.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_objects_abexas.htm)
- [abenabap_objects_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_objects_abexa.htm)
- [abenclass_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenclass_abexa.htm)
- [abeninheritance_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abeninheritance_abexa.htm)
- [abeninterface_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abeninterface_abexa.htm)
- [abenevents_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenevents_abexa.htm)
- [abenfriends_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenfriends_abexa.htm)
- [abenoo_transaction_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenoo_transaction_abexa.htm)

**Bundle Contains**: 8 documentation pages
**Version**: ABAP latest
**Generated**: 2025-09-01T11:25:44.895Z

---

### abenabap_objects_abexas.htm

> **ğŸ“– Official SAP Documentation**: [abenabap_objects_abexas.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_objects_abexas.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 758, Â©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Objects](javascript:call_link\('abenabap_objects.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20ABAP%20Objects%20-%20Examples%2C%20ABENABAP_OBJECTS_ABEXAS%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

ABAP Objects - Examples

-   [ABAP Objects - Overview](javascript:call_link\('abenabap_objects_abexa.htm'\))
-   [ABAP Objects - Classes](javascript:call_link\('abenclass_abexa.htm'\))
-   [ABAP Objects - Inheritance](javascript:call_link\('abeninheritance_abexa.htm'\))
-   [ABAP Objects - Interfaces](javascript:call_link\('abeninterface_abexa.htm'\))
-   [ABAP Objects - Events](javascript:call_link\('abenevents_abexa.htm'\))
-   [ABAP Objects - Friendship](javascript:call_link\('abenfriends_abexa.htm'\))
-   [ABAP Objects - Object Transaction](javascript:call_link\('abenoo_transaction_abexa.htm'\))

Continue
![Example](exa.gif "Example") [ABAP Objects - Overview](javascript:call_link\('abenabap_objects_abexa.htm'\))
![Example](exa.gif "Example") [ABAP Objects - Classes](javascript:call_link\('abenclass_abexa.htm'\))
![Example](exa.gif "Example") [ABAP Objects - Inheritance](javascript:call_link\('abeninheritance_abexa.htm'\))
![Example](exa.gif "Example") [ABAP Objects - Interfaces](javascript:call_link\('abeninterface_abexa.htm'\))
![Example](exa.gif "Example") [ABAP Objects - Events](javascript:call_link\('abenevents_abexa.htm'\))
![Example](exa.gif "Example") [ABAP Objects - Friendship](javascript:call_link\('abenfriends_abexa.htm'\))
![Example](exa.gif "Example") [ABAP Objects - Object Transaction](javascript:call_link\('abenoo_transaction_abexa.htm'\))



**ğŸ“– Source**: [abenabap_objects_abexas.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_objects_abexas.htm)

### abenabap_objects_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenabap_objects_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_objects_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 758, Â©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Objects](javascript:call_link\('abenabap_objects.htm'\))Â â†’Â  [ABAP Objects - Examples](javascript:call_link\('abenabap_objects_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20ABAP%20Objects%20-%20Overview%2C%20ABENABAP_OBJECTS_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

ABAP Objects - Overview

The example demonstrates objects, object references, inheritance, interfaces, and events.

Source Code Â  

\* CCDEF
CLASS o DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-DATA out TYPE REF TO if\_demo\_output.
ENDCLASS.
\*---------------------------------------------------------------------\*
\* INTERFACE status
\*---------------------------------------------------------------------\*
\* Interface definition \*
\*---------------------------------------------------------------------\*
INTERFACE status.
Â Â METHODS write.
ENDINTERFACE.
\* Public class definition
CLASS cl\_demo\_abap\_objects DEFINITION
Â Â INHERITING FROM cl\_demo\_classrun
Â Â PUBLIC
Â Â CREATE PUBLIC.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS main REDEFINITION.
Â Â Â Â METHODS:
Â Â Â Â Â Â objects,
Â Â Â Â Â Â inheritance,
Â Â Â Â Â Â interfaces,
Â Â Â Â Â Â events.
ENDCLASS.
\* CCIMP
\*---------------------------------------------------------------------\*
\* CLASS vessel DEFINITION
\*---------------------------------------------------------------------\*
\* Superclass definition \*
\*---------------------------------------------------------------------\*
CLASS vessel DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS:
Â Â Â Â constructor,
Â Â Â Â drive
Â Â Â Â IMPORTING speed\_up TYPE i,
Â Â Â Â get\_id
Â Â Â Â RETURNING VALUE(id) TYPE i.
Â Â PROTECTED SECTION.
Â Â Â Â DATA: speed TYPE i,
Â Â Â Â max\_speed TYPE i VALUE 100.
Â Â PRIVATE SECTION.
Â Â Â Â CLASS-DATA object\_count TYPE i.
Â Â Â Â DATA id TYPE i.
ENDCLASS.
\*---------------------------------------------------------------------\*
\* CLASS vessel IMPLEMENTATION
\*---------------------------------------------------------------------\*
\* Superclass implementation \*
\*---------------------------------------------------------------------\*
CLASS vessel IMPLEMENTATION.
Â Â METHOD constructor.
Â Â Â Â object\_count += 1.
Â Â Â Â id = object\_count.
Â Â ENDMETHOD.
Â Â METHOD drive.
Â Â Â Â speed += speed\_up.
Â Â Â Â IF speed > max\_speed.
Â Â Â Â Â Â speed = max\_speed.
Â Â Â Â ENDIF.
Â Â ENDMETHOD.
Â Â METHOD get\_id.
Â Â Â Â id = me->id.
Â Â ENDMETHOD.
ENDCLASS.
\*---------------------------------------------------------------------\*
\* CLASS ship DEFINITION
\*---------------------------------------------------------------------\*
\* Subclass definition \*
\*---------------------------------------------------------------------\*
CLASS ship DEFINITION INHERITING FROM vessel.
Â Â PUBLIC SECTION.
Â Â Â Â INTERFACES status.
Â Â Â Â DATA name TYPE string READ-ONLY.
Â Â Â Â METHODS:
Â Â Â Â constructor
Â Â Â Â IMPORTING name TYPE string,
Â Â Â Â drive
Â Â Â Â REDEFINITION.
Â Â Â Â EVENTS emergency\_call.
ENDCLASS.
\*---------------------------------------------------------------------\*
\* CLASS ship IMPLEMENTATION
\*---------------------------------------------------------------------\*
\* Subclass implementation \*
\*---------------------------------------------------------------------\*
CLASS ship IMPLEMENTATION.
Â Â METHOD constructor.
Â Â Â Â super->constructor( ).
Â Â Â Â max\_speed = 30.
Â Â Â Â me->name = name.
Â Â ENDMETHOD.
Â Â METHOD status~write.
Â Â Â Â DATA id TYPE c LENGTH 1.
Â Â Â Â id = me->get\_id( ).
Â Â Â Â o=>out->write( |{ name } is vessel { id
Â Â Â Â } and has speed { speed }| ).
Â Â ENDMETHOD.
Â Â METHOD drive.
Â Â Â Â speed += speed\_up.
Â Â Â Â IF speed > max\_speed.
Â Â Â Â Â Â max\_speed = 0.
Â Â Â Â Â Â speed = 0.
Â Â Â Â Â Â RAISE EVENT emergency\_call.
Â Â Â Â ENDIF.
Â Â ENDMETHOD.
ENDCLASS.
\*---------------------------------------------------------------------\*
\* CLASS coast\_guard DEFINITION
\*---------------------------------------------------------------------\*
\* Event handler definition \*
\*---------------------------------------------------------------------\*
CLASS coast\_guard DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â INTERFACES status.
Â Â Â Â METHODS receive
Â Â Â Â FOR EVENT emergency\_call OF ship
Â Â Â Â IMPORTING sender.
Â Â Â Â ALIASES write FOR status~write.
Â Â PRIVATE SECTION.
Â Â Â Â DATA caller TYPE string.
ENDCLASS.
\*---------------------------------------------------------------------\*
\* CLASS coast\_guard IMPLEMENTATION
\*---------------------------------------------------------------------\*
\* Event handler implementation \*
\*---------------------------------------------------------------------\*
CLASS coast\_guard IMPLEMENTATION.
Â Â METHOD status~write.
Â Â Â Â IF caller IS INITIAL.
Â Â Â Â Â Â o=>out->write(
Â Â Â Â Â Â \`Coast guard received no call\` ).
Â Â Â Â ELSE.
Â Â Â Â Â Â o=>out->write(
Â Â Â Â Â Â |Coast guard received a call from { caller }| ).
Â Â Â Â ENDIF.
Â Â ENDMETHOD.
Â Â METHOD receive.
Â Â Â Â caller = sender->name.
Â Â Â Â write( ).
Â Â ENDMETHOD.
ENDCLASS.
\* Public class implementation
CLASS cl\_demo\_abap\_objects IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â o=>out = out.
Â Â Â Â DATA: button1 TYPE abap\_bool VALUE abap\_true,
Â Â Â Â Â Â Â Â Â Â button2 TYPE abap\_bool,
Â Â Â Â Â Â Â Â Â Â button3 TYPE abap\_bool,
Â Â Â Â Â Â Â Â Â Â button4 TYPE abap\_bool.
Â Â Â Â cl\_demo\_input=>new(
Â Â Â Â Â Â )->add\_field( EXPORTING text = \`Objects and Object References\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â as\_checkbox = abap\_true
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â CHANGINGÂ Â field = button1
Â Â Â Â Â Â )->add\_field( EXPORTING text = \`Inheritance\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â as\_checkbox = abap\_true
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â CHANGINGÂ Â field = button2
Â Â Â Â Â Â )->add\_field( EXPORTING text = \`Interfaces\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â as\_checkbox = abap\_true
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â CHANGINGÂ Â field = button3
Â Â Â Â Â Â )->add\_field( EXPORTING text = \`Events\`
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â as\_checkbox = abap\_true
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â CHANGINGÂ Â field = button4
Â Â Â Â Â Â )->request( ).
Â Â Â Â IF strlen( button1 && button2 && button3 && button4 ) > 1.
Â Â Â Â Â Â EXIT.
Â Â Â Â ELSEIF button1 = abap\_true.
Â Â Â Â Â Â objects( ).
Â Â Â Â ELSEIF button2 = abap\_true.
Â Â Â Â Â Â inheritance( ).
Â Â Â Â ELSEIF button3 = abap\_true.
Â Â Â Â Â Â interfaces( ).
Â Â Â Â ELSEIF button4 = abap\_true.
Â Â Â Â Â Â events( ).
Â Â Â Â ENDIF.
Â Â ENDMETHOD.
Â Â METHOD events.
Â Â Â Â FINAL(ship)Â Â Â Â = NEW ship( 'Titanic' ).
Â Â Â Â FINAL(station) = NEW coast\_guard( ).
Â Â Â Â SET HANDLER station->receive FOR ship.
Â Â Â Â DO 5 TIMES.
Â Â Â Â Â Â ship->drive( 10 ).
Â Â Â Â ENDDO.
Â Â ENDMETHOD.
Â Â METHOD inheritance.
Â Â Â Â FINAL(ship) = NEW ship( 'Titanic' ).
Â Â Â Â ship->drive( 20 ).
Â Â Â Â FINAL(vessel) = CAST vessel( ship ).
Â Â Â Â vessel->drive( 10 ).
Â Â Â Â ship->status~write( ).
Â Â ENDMETHOD.
Â Â METHOD interfaces.
Â Â Â Â DATA status\_tab TYPE TABLE OF REF TO status WITH EMPTY KEY.
Â Â Â Â status\_tab = VALUE #( ( NEW ship( name = 'Titanic' ) )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( NEW coast\_guard( ) ) ).
Â Â Â Â LOOP AT status\_tab ASSIGNING FIELD-SYMBOL(<status>).
Â Â Â Â Â Â <status>->write( ).
Â Â Â Â ENDLOOP.
Â Â ENDMETHOD.
Â Â METHOD objects.
Â Â Â Â FINAL(vessel1) = NEW vessel( ).
Â Â Â Â FINAL(vessel2) = NEW vessel( ).
Â Â Â Â vessel1->drive( 50 ).
Â Â Â Â vessel2->drive( 80 ).
Â Â Â Â o=>out->write( |Vessel ID is { vessel1->get\_id( ) }| ).
Â Â Â Â o=>out->write( |Vessel ID is { vessel2->get\_id( ) }| ).
Â Â ENDMETHOD.
ENDCLASS.

Description Â  

The class demonstrates how ABAP Objects can be used based on example classes for boats.



**ğŸ“– Source**: [abenabap_objects_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_objects_abexa.htm)

### abenclass_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenclass_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenclass_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 758, Â©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Objects](javascript:call_link\('abenabap_objects.htm'\))Â â†’Â  [ABAP Objects - Examples](javascript:call_link\('abenabap_objects_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20ABAP%20Objects%20-%20Classes%2C%20ABENCLASS_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

ABAP Objects - Classes

This example demonstrates a class for counters.

Source Code Â  

\* Public class definition
CLASS cl\_demo\_class\_counter DEFINITION
Â Â INHERITING FROM cl\_demo\_classrun
Â Â PUBLIC
Â Â CREATE PUBLIC.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS main REDEFINITION.
ENDCLASS.
\* CCIMP
CLASS counter DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS: set
Â Â Â Â IMPORTING VALUE(set\_value) TYPE i,
Â Â Â Â increment,
Â Â Â Â get
Â Â Â Â EXPORTING VALUE(get\_value) TYPE i.
Â Â PRIVATE SECTION.
Â Â Â Â DATA count TYPE i.
ENDCLASS.
CLASS counter IMPLEMENTATION.
Â Â METHOD set.
Â Â Â Â count = set\_value.
Â Â ENDMETHOD.
Â Â METHOD increment.
Â Â Â Â count += 1.
Â Â ENDMETHOD.
Â Â METHOD get.
Â Â Â Â get\_value = count.
Â Â ENDMETHOD.
ENDCLASS.
\* Public class implementation
CLASS cl\_demo\_class\_counter IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA number TYPE i VALUE 5.
Â Â Â Â FINAL(cnt) = NEW counter( ).
Â Â Â Â cnt->set( number ).
Â Â Â Â DO 3 TIMES.
Â Â Â Â Â Â cnt->increment( ).
Â Â Â Â ENDDO.
Â Â Â Â cnt->get( IMPORTING get\_value = number ).
Â Â Â Â out->write( number ).
Â Â ENDMETHOD.
ENDCLASS.

Description Â  

The local class counter contains three public methods, set, increment, and get, which work with the private integer field count. Two of the methods have input and output parameters and therefore define the data interface of the class. The field count, on the other hand, is not visible externally.



**ğŸ“– Source**: [abenclass_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenclass_abexa.htm)

### abeninheritance_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abeninheritance_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abeninheritance_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 758, Â©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Objects](javascript:call_link\('abenabap_objects.htm'\))Â â†’Â  [ABAP Objects - Examples](javascript:call_link\('abenabap_objects_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20ABAP%20Objects%20-%20Inheritance%2C%20ABENINHERITANCE_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

ABAP Objects - Inheritance

This example demonstrates how a counter is specialized using inheritance.

Source Code Â  

\* CCDEF
CLASS o DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-DATA out TYPE REF TO if\_demo\_output.
ENDCLASS.
\* Public class definition
CLASS cl\_demo\_inheritance DEFINITION
Â Â INHERITING FROM cl\_demo\_classrun
Â Â PUBLIC
Â Â CREATE PUBLIC.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS main REDEFINITION.
ENDCLASS.
\* CCIMP
CLASS counter DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS:
Â Â Â Â set
Â Â Â Â IMPORTING VALUE(set\_value) TYPE i,
Â Â Â Â increment,
Â Â Â Â get
Â Â Â Â EXPORTING VALUE(get\_value) TYPE i.
Â Â PROTECTED SECTION.
Â Â Â Â DATA count TYPE i.
ENDCLASS.
CLASS counter IMPLEMENTATION.
Â Â METHOD set.
Â Â Â Â count = set\_value.
Â Â ENDMETHOD.
Â Â METHOD increment.
Â Â Â Â count += 1.
Â Â ENDMETHOD.
Â Â METHOD get.
Â Â Â Â get\_value = count.
Â Â ENDMETHOD.
ENDCLASS.
CLASS counter\_ten DEFINITION INHERITING FROM counter.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS increment REDEFINITION.
Â Â Â Â DATA count\_ten TYPE c LENGTH 1.
ENDCLASS.
CLASS counter\_ten IMPLEMENTATION.
Â Â METHOD increment.
Â Â Â Â DATA modulo TYPE i.
Â Â Â Â super->increment( ).
Â Â Â Â modulo = count MOD 10.
Â Â Â Â IF modulo = 0.
Â Â Â Â Â Â count\_ten += 1.
Â Â Â Â Â Â o=>out->write\_text(
Â Â Â Â Â Â |{ count } - { count\_ten }| ).
Â Â Â Â ELSE.
Â Â Â Â Â Â o=>out->write\_text( |{ count }| ).
Â Â Â Â ENDIF.
Â Â ENDMETHOD.
ENDCLASS.
\* Public class implementation
CLASS cl\_demo\_inheritance IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â o=>out = out.
Â Â Â Â DATA: countÂ Â TYPE REF TO counter,
Â Â Â Â Â Â Â Â Â Â number TYPE i VALUE 5.
Â Â Â Â count = NEW counter\_ten( ).
Â Â Â Â count->set( number ).
Â Â Â Â DO 20 TIMES.
Â Â Â Â Â Â count->increment( ).
Â Â Â Â ENDDO.
Â Â ENDMETHOD.
ENDCLASS.

Description Â  

The local class counter\_ten is derived from counter and redefines the method increment. In counter, the visibility of the attribute count must be set from PRIVATE to PROTECTED. In the redefined method, the hidden method with the pseudo reference super-> is called. The redefined method specializes the inherited method.

An object of the subclass is created to which a reference variable of the superclass type points. When the method increment is executed using the superclass reference, the redefined method of the subclass is executed.



**ğŸ“– Source**: [abeninheritance_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abeninheritance_abexa.htm)

### abeninterface_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abeninterface_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abeninterface_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 758, Â©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Objects](javascript:call_link\('abenabap_objects.htm'\))Â â†’Â  [ABAP Objects - Examples](javascript:call_link\('abenabap_objects_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20ABAP%20Objects%20-%20Interfaces%2C%20ABENINTERFACE_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

ABAP Objects - Interfaces

This example demonstrates the use of interfaces.

Source Code Â  

\* CCDEF
CLASS o DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-DATA out TYPE REF TO if\_demo\_output.
ENDCLASS.
\* Public class definition
CLASS cl\_demo\_interface DEFINITION
Â Â INHERITING FROM cl\_demo\_classrun
Â Â PUBLIC
Â Â CREATE PUBLIC.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS main REDEFINITION.
ENDCLASS.
\* CCIMP
INTERFACE status.
Â Â METHODS write.
ENDINTERFACE.
CLASS counter DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â INTERFACES status.
Â Â Â Â METHODS increment.
Â Â PRIVATE SECTION.
Â Â Â Â DATA count TYPE i.
ENDCLASS.
CLASS counter IMPLEMENTATION.
Â Â METHOD status~write.
Â Â Â Â o=>out->write\_text(
Â Â Â Â |Count in counter is { count }| ).
Â Â ENDMETHOD.
Â Â METHOD increment.
Â Â Â Â count += 1.
Â Â ENDMETHOD.
ENDCLASS.
CLASS bicycle DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â INTERFACES status.
Â Â Â Â METHODS drive.
Â Â PRIVATE SECTION.
Â Â Â Â DATA speed TYPE i.
ENDCLASS.
CLASS bicycle IMPLEMENTATION.
Â Â METHOD status~write.
Â Â Â Â o=>out->write\_text(
Â Â Â Â |Speed of bicycle is { speed }| ).
Â Â ENDMETHOD.
Â Â METHOD drive.
Â Â Â Â speed += 10.
Â Â ENDMETHOD.
ENDCLASS.
\* Public class implementation
CLASS cl\_demo\_interface IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â o=>out = out.
Â Â Â Â DATA status\_tab TYPE TABLE OF REF TO status WITH EMPTY KEY.
Â Â Â Â FINAL(count) = NEW counter( ).
Â Â Â Â FINAL(bike) =Â Â NEW bicycle( ).
Â Â Â Â DO 5 TIMES.
Â Â Â Â Â Â count->increment( ).
Â Â Â Â Â Â bike->drive( ).
Â Â Â Â ENDDO.
Â Â Â Â status\_tab = VALUE #( ( count )
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( bike ) ).
Â Â Â Â LOOP AT status\_tab ASSIGNING FIELD-SYMBOL(<status>).
Â Â Â Â Â Â <status>->write( ).
Â Â Â Â ENDLOOP.
Â Â ENDMETHOD.
ENDCLASS.

Description Â  

This example shows a local interface status for displaying the attributes of an object and its implementation in two different local classes.

The interface status contains a method write. The classes counter and bicycle implement the interface in the public area. Both classes must implement the interface method in the implementation part according to the required semantics.

First, two class reference variables count and bike are declared for the classes counter and bicycle. An interface reference variable status and an internal table status\_tab with a suitable line type for the interface reference variable are created for the interface status. All reference variables begin with initial values.

Using the constructor operator NEW, an object is created for each class to which the references in count and bike point. Using the class reference variable, the methods increment and drive are called in the respective objects.

Class reference variables are inserted in the interface reference table to have the lines in status\_tab point to the two objects as well. Using the interface references, it is possible to call the interface method write in the objects, but not the class methods increment or drive.



**ğŸ“– Source**: [abeninterface_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abeninterface_abexa.htm)

### abenevents_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenevents_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenevents_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 758, Â©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Objects](javascript:call_link\('abenabap_objects.htm'\))Â â†’Â  [ABAP Objects - Examples](javascript:call_link\('abenabap_objects_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20ABAP%20Objects%20-%20Events%2C%20ABENEVENTS_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

ABAP Objects - Events

This example demonstrates how events are raised and handled.

Source Code Â  

REPORT demo\_abap\_objects\_events NO STANDARD PAGE HEADING.
\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*
\* Declarations
\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*
INTERFACE vehicle.
Â Â DATA
Â Â Â Â max\_speed TYPE i.
Â Â EVENTS
Â Â Â Â speed\_change EXPORTING
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  VALUE(id) TYPE i
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  VALUE(new\_speed) TYPE i.
Â Â METHODS:
Â Â Â Â drive IMPORTING id TYPE i,
Â Â Â Â stopÂ Â IMPORTING id TYPE i.
ENDINTERFACE.
CLASS ship DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS
Â Â Â Â Â Â constructor.
Â Â Â Â INTERFACES
Â Â Â Â Â Â vehicle.
Â Â PRIVATE SECTION.
Â Â Â Â ALIASES
Â Â Â Â Â Â max FOR vehicle~max\_speed.
Â Â Â Â DATA
Â Â Â Â Â Â ship\_speed TYPE i.
ENDCLASS.
CLASS truck DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS
Â Â Â Â Â Â constructor.
Â Â Â Â INTERFACES
Â Â Â Â Â Â vehicle.
Â Â PRIVATE SECTION.
Â Â Â Â ALIASES
Â Â Â Â Â Â max FOR vehicle~max\_speed.
Â Â Â Â DATA
Â Â Â Â Â Â truck\_speed TYPE i.
ENDCLASS.
CLASS status DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-EVENTS
Â Â Â Â Â Â button\_clicked
Â Â Â Â Â Â Â Â EXPORTING VALUE(fcode) TYPE sy-ucomm.
Â Â Â Â CLASS-METHODS:
Â Â Â Â Â Â class\_constructor,
Â Â Â Â Â Â user\_action.
ENDCLASS.
CLASS list DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS:
Â Â Â Â Â Â fcode\_handler
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â FOR EVENT button\_clicked OF status
Â Â Â Â Â Â Â Â IMPORTING fcode,
Â Â Â Â Â Â list\_change
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â FOR EVENT speed\_change OF vehicle
Â Â Â Â Â Â Â Â IMPORTING id
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â new\_speed,
Â Â Â Â Â Â list\_output.
Â Â PRIVATE SECTION.
Â Â Â Â TYPES:
Â Â Â Â Â Â BEGIN OF line,
Â Â Â Â Â Â Â Â irefÂ Â TYPE REF TO vehicle,
Â Â Â Â Â Â Â Â speed TYPE i,
Â Â Â Â Â Â END OF line.
Â Â Â Â DATA
Â Â Â Â Â Â list TYPE standard TABLE OF line WITH empty key.
ENDCLASS.
\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*
\* Implementations
\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*
CLASS ship IMPLEMENTATION.
Â Â METHOD constructor.
Â Â Â Â max = 30.
Â Â ENDMETHOD.
Â Â METHOD vehicle~drive.
Â Â Â Â CHECK ship\_speed < max.
Â Â Â Â ship\_speed += 10.
Â Â Â Â RAISE EVENT vehicle~speed\_change
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â EXPORTING id = id
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â new\_speed = ship\_speed.
Â Â ENDMETHOD.
Â Â METHOD vehicle~stop.
Â Â Â Â CHECK ship\_speed > 0.
Â Â Â Â ship\_speed = 0.
Â Â Â Â RAISE EVENT vehicle~speed\_change
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â EXPORTING id = id
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â new\_speed = ship\_speed.
Â Â ENDMETHOD.
ENDCLASS.
CLASS truck IMPLEMENTATION.
Â Â METHOD constructor.
Â Â Â Â max = 100.
Â Â ENDMETHOD.
Â Â METHOD vehicle~drive.
Â Â Â Â CHECK truck\_speed < max.
Â Â Â Â truck\_speed += 50.
Â Â Â Â RAISE EVENT vehicle~speed\_change
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â EXPORTING id = id
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â new\_speed = truck\_speed.
Â Â ENDMETHOD.
Â Â METHOD vehicle~stop.
Â Â Â Â CHECK truck\_speed > 0.
Â Â Â Â truck\_speed = 0.
Â Â Â Â RAISE EVENT vehicle~speed\_change
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â EXPORTINGÂ Â id = id
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  new\_speed = truck\_speed.
Â Â ENDMETHOD.
ENDCLASS.
CLASS status IMPLEMENTATION.
Â Â METHOD class\_constructor.
Â Â Â Â SET PF-STATUS 'VEHICLE'.
Â Â Â Â WRITE 'Click a button!'.
Â Â ENDMETHOD.
Â Â METHOD user\_action.
Â Â Â Â RAISE EVENT button\_clicked EXPORTING fcode = sy-ucomm.
Â Â ENDMETHOD.
ENDCLASS.
CLASS list IMPLEMENTATION.
Â Â METHOD fcode\_handler.
Â Â Â Â CASE fcode.
Â Â Â Â Â Â WHEN 'CREA\_SHIP'.
Â Â Â Â Â Â Â Â list = VALUE #( BASE list
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( iref = NEW ship( ) ) ).
Â Â Â Â Â Â WHEN 'CREA\_TRUCK'.
Â Â Â Â Â Â Â Â list = VALUE #( BASE list
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â ( iref = NEW truck( ) ) ).
Â Â Â Â Â Â WHEN 'DRIVE'.
Â Â Â Â Â Â Â Â CHECK sy-lilli > 0.
Â Â Â Â Â Â Â Â list\[ sy-lilli \]-iref->drive( sy-lilli ).
Â Â Â Â Â Â WHEN 'STOP'.
Â Â Â Â Â Â Â Â LOOP AT list ASSIGNING FIELD-SYMBOL(<line>).
Â Â Â Â Â Â Â Â Â Â data(id) = sy-tabix.
Â Â Â Â Â Â Â Â Â Â <line>-iref->stop( id ).
Â Â Â Â Â Â Â Â ENDLOOP.
Â Â Â Â Â Â WHEN 'CANCEL'.
Â Â Â Â Â Â Â Â LEAVE PROGRAM.
Â Â Â Â ENDCASE.
Â Â Â Â list\_output( ).
Â Â ENDMETHOD.
Â Â METHOD list\_change.
Â Â Â Â list\[ id \]-speedÂ Â = new\_speed.
Â Â ENDMETHOD.
Â Â METHOD list\_output.
Â Â Â Â sy-lsind = 0.
Â Â Â Â SET TITLEBAR 'TIT'.
Â Â Â Â LOOP AT list ASSIGNING FIELD-SYMBOL(<line>).
Â Â Â Â Â Â IF <line>-iref is INSTANCE OF ship.
Â Â Â Â Â Â Â Â WRITE / icon\_ws\_ship AS ICON.
Â Â Â Â Â Â ELSEIF <line>-iref is INSTANCE OF truck.
Â Â Â Â Â Â Â Â WRITE / icon\_ws\_truck AS ICON.
Â Â Â Â Â Â ENDIF.
Â Â Â Â Â Â WRITE: 'Speed = ', <line>-speed.
Â Â Â Â ENDLOOP.
Â Â ENDMETHOD.
ENDCLASS.
\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*
\* Program events
\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*
START-OF-SELECTION.
Â Â FINAL(list) = NEW list( ).
Â Â SET HANDLER:
Â Â Â Â list->fcode\_handler,
Â Â Â Â list->list\_change FOR ALL INSTANCES.
AT USER-COMMAND.
Â Â status=>user\_action( ).

Description Â  

The user actions on a classic list raise events in ABAP Objects. The list and its data are implemented in a class list. User actions are processed by the class status, which raises an event button\_clicked at AT USER-COMMAND time. The event is handled in the class list. The class list contains an object of the class ship or truck for each line of the list, which both implement the interface vehicle. If the speed of one of these objects changes, the event speed\_change is raised, to which the class list responds by adapting the list output.



**ğŸ“– Source**: [abenevents_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenevents_abexa.htm)

### abenfriends_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenfriends_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenfriends_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 758, Â©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Objects](javascript:call_link\('abenabap_objects.htm'\))Â â†’Â  [ABAP Objects - Examples](javascript:call_link\('abenabap_objects_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20ABAP%20Objects%20-%20Friendship%2C%20ABENFRIENDS_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

ABAP Objects - Friendship

This example demonstrates an interface that is a [friend](javascript:call_link\('abenfriends.htm'\)) of a class.

Source Code Â  

\* Public class definition
CLASS cl\_demo\_oo\_friendship DEFINITION
Â Â INHERITING FROM cl\_demo\_classrun
Â Â PUBLIC
Â Â CREATE PUBLIC.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS main REDEFINITION.
Â Â Â Â INTERFACES if\_demo\_friend.
ENDCLASS.
\* Public class implementation
CLASS cl\_demo\_oo\_friendship IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â out->write( cl\_demo\_friendship=>attr ).
Â Â ENDMETHOD.
ENDCLASS.

Description Â  

The empty global interface IF\_DEMO\_FRIEND is a friend of the global class CL\_DEMO\_FRIENDSHIP. Every class that implements IF\_DEMO\_FRIEND can access the private components of CL\_DEMO\_FRIENDSHIP.



**ğŸ“– Source**: [abenfriends_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenfriends_abexa.htm)

### abenoo_transaction_abexa.htm

> **ğŸ“– Official SAP Documentation**: [abenoo_transaction_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenoo_transaction_abexa.htm)
> **ğŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ğŸ“– Source**: [abenoo_transaction_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenoo_transaction_abexa.htm)


Â  

* * *

AS ABAP Release 758, Â©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [ABAP Objects](javascript:call_link\('abenabap_objects.htm'\))Â â†’Â  [ABAP Objects - Examples](javascript:call_link\('abenabap_objects_abexas.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20ABAP%20Objects%20-%20Object%20Transaction%2C%20ABENOO_TRANSACTION_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

ABAP Objects - Object Transaction

This example demonstrates how a transaction code is linked with the method of a local class.

Source Code Â  

\*&---------------------------------------------------------------------\*
\*& Subroutine poolÂ Â DEMO\_OO\_TRANSACTIONÂ Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â \*
\*&Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  \*
\*&---------------------------------------------------------------------\*
PROGRAMÂ Â demo\_oo\_transaction.
\*
CLASS demo\_class DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS instance\_method.
ENDCLASS.
\*
CLASS demo\_class IMPLEMENTATION.
Â Â METHOD instance\_method.
Â Â Â Â cl\_demo\_output=>display( 'Instance method in local class' ).
Â Â ENDMETHOD.
ENDCLASS.

Description Â  

The program DEMO\_OO\_TRANSACTION is a subroutine pool that does not contain any subroutines. Instead, the program contains the definition of the local class demo\_class, linked with the transaction code DEMO\_OO\_METHOD. When the transaction is called, the program is loaded, an instance of the class is created, and the method is called.
