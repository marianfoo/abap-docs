# ABAP - Keyword Documentation / ABAP - Programming Language / Creating Objects and Values / NEW, Instance Operator / NEW, Classes

Included pages: 2



## ðŸ”— Source References

**Base URL**: [ABAP Keyword Documentation latest](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm)

**Individual Pages in this Bundle**:
- [abennew_constructor_params_class.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abennew_constructor_params_class.htm)
- [abennew_on_the_left_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abennew_on_the_left_abexa.htm)

**Bundle Contains**: 2 documentation pages
**Version**: ABAP latest
**Generated**: 2025-09-01T11:25:44.903Z

---

### abennew_constructor_params_class.htm

> **ðŸ“– Official SAP Documentation**: [abennew_constructor_params_class.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abennew_constructor_params_class.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


Â  

* * *

AS ABAP Release 758, Â©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Creating Objects and Values](javascript:call_link\('abencreate_objects.htm'\))Â â†’Â  [NEW, Instance Operator](javascript:call_link\('abenconstructor_expression_new.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20NEW%2C%20Classes%2C%20ABENNEW_CONSTRUCTOR_PARAMS_CLASS%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

NEW, Classes

Syntax

... NEW class*|*#( *\[*[let\_exp](javascript:call_link\('abaplet.htm'\))*\]* p1 = a1 p2 = a2 ... ) ...

Effect

If a class class is specified or # stands for a class like this, the non-optional input parameters p1 p2 ... of the instance constructor of the instantiated class must be filled with the type-compliant actual parameters a1 a2 ... The optional input parameters do not need to but can be filled. The description of the statement [CREATE OBJECT](javascript:call_link\('abapcreate_object_parameters.htm'\)) specifies how parameters are passed and which classes can be used. [No parameters](javascript:call_link\('abennew_constructor_params_initial.htm'\)) are passed for a class without an explicit instance constructor. An optional LET expression [let\_exp](javascript:call_link\('abaplet.htm'\)) can be specified in front of the parameter to define local helper fields.

Like an object reference variable of the type class, a constructor expression NEW class( ... ) can be specified in front of the [object component selector](javascript:call_link\('abenobject_component_select_glosry.htm'\) "Glossary Entry") \-> and in [chained attribute accesses](javascript:call_link\('abenchained_attribute_acc_glosry.htm'\) "Glossary Entry") and [chained method calls](javascript:call_link\('abenchained_method_call_glosry.htm'\) "Glossary Entry").

... NEW class( ... )->attr*|*('attr\_name') ...

... NEW class( ... )->meth( ... ) ...

An attribute can be specified [statically](javascript:call_link\('abenobject_component_selector.htm'\)) as attr or [dynamically](javascript:call_link\('abenobject_component_selector.htm'\)) as content of a character-like data object attr\_name in parentheses. If a dynamically specified attribute is not found, an exception of class CX\_SY\_ASSIGN\_ILLEGAL\_COMPONENT is raised. A method meth can be specified statically only.

A chained method call of this type can be specified as a [standalone statement](javascript:call_link\('abapcall_method_static_chain.htm'\)) or a [functional method call](javascript:call_link\('abapcall_method_functional.htm'\)). The result is then preserved for the duration of the statement. Even a single expression without a subsequent object component selector can be executed as a standalone statement. In this case, only the constructor that has a reference to the created object elsewhere is executed if this object needs to be preserved.

In the case of an attribute access NEW class( ... )->attr, the following applies specifically:

-   It can be specified as a [writable expression](javascript:call_link\('abenwritable_expression_glosry.htm'\) "Glossary Entry") in a [result position](javascript:call_link\('abenresult_position_glosry.htm'\) "Glossary Entry").
-   If the data type is suitable, an [offset/length specification](javascript:call_link\('abenoffset_length_specific_glosry.htm'\) "Glossary Entry") [+off(len)](javascript:call_link\('abenoffset_length.htm'\)) can be appended unless the chaining is used as [memory area](javascript:call_link\('abapassign_mem_area_writable_exp.htm'\)) in the statement ASSIGN.

[Method chainings](javascript:call_link\('abenmethod_chaining_glosry.htm'\) "Glossary Entry") cannot be specified between the constructor expression and the attribute here.

Hints

-   This is a functional call of the instance constructor. As in [functional calls](javascript:call_link\('abapcall_method_functional.htm'\)) of [functional methods](javascript:call_link\('abapmethods_functional.htm'\)), no [non-class-based exceptions](javascript:call_link\('abenexceptions_non_class.htm'\)) can be handled.
-   When the instance constructor is called, the return code sy-subrc is set to 0. Values other than 0 are not possible, since non-class-based exceptions cannot be handled.

Example

Construction of an instance of the class c1 when the method m2 of the class c2 is called, which requires an input parameter of this type.

CLASS c1 DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â METHODS constructor IMPORTING p1 TYPE i p2 TYPE i.
ENDCLASS.
CLASS c1 IMPLEMENTATION.
Â Â METHOD constructor.
Â Â Â Â ...
Â Â ENDMETHOD.
ENDCLASS.
CLASS c2 DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS m2 IMPORTING oref TYPE REF TO c1.
ENDCLASS.
CLASS c2 IMPLEMENTATION.
Â Â METHOD m2.
Â Â Â Â ...
Â Â ENDMETHOD.
ENDCLASS.
CLASS exa DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS exa IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â c2=>m2( NEW #( p1 = 1 p2 = 2 ) ).
Â Â ENDMETHOD.
ENDCLASS.

Example

Use of a constructor expression with NEW in various positions. The temporary reference variable created using NEW is persisted during each statement. The example also shows dynamic specifications and the exception that is raised in case of an invalid attribute.

CLASS c1 DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â DATAÂ Â Â Â  a1 TYPE i.
Â Â Â Â METHODS: m1 RETURNING VALUE(p) TYPE i,
Â Â Â Â Â Â m2.
ENDCLASS.
CLASS c1 IMPLEMENTATION.
Â Â METHOD m1.
Â Â Â Â ...
Â Â ENDMETHOD.
Â Â METHOD m2.
Â Â Â Â ...
Â Â ENDMETHOD.
ENDCLASS.
CLASS exa DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS exa IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â DATA num TYPE i.
Â Â Â Â num = NEW c1( )->a1.
Â Â Â Â NEW c1( )->a1 = num.
Â Â Â Â num = NEW c1( )->m1( ).
Â Â Â Â NEW c1( )->m2( ).
Â Â Â Â NEW c1( ).
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â num = NEW c1( )->('a1').
Â Â Â Â Â Â CATCH cx\_sy\_assign\_illegal\_component.
Â Â Â Â Â Â Â Â DATA(exc) = 'X'.
Â Â Â Â ENDTRY.
Â Â Â Â ASSERT exc IS INITIAL.
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â num = NEW c1( )->('x1').
Â Â Â Â Â Â CATCH cx\_sy\_assign\_illegal\_component.
Â Â Â Â Â Â Â Â exc = 'X'.
Â Â Â Â ENDTRY.
Â Â Â Â ASSERT exc IS NOT INITIAL.
Â Â ENDMETHOD.
ENDCLASS.

Example

Display of a time stamp created in a constructor that only exists while the statement is being executed.

CLASS utc DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â DATA ts TYPE utclong.
Â Â Â Â METHODS constructor.
ENDCLASS.
CLASS utc IMPLEMENTATION.
Â Â METHOD constructor.
Â Â Â Â ts = utclong\_current( ).
Â Â ENDMETHOD.
ENDCLASS.
CLASS exa DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-METHODS main.
ENDCLASS.
CLASS exa IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â cl\_demo\_output=>display\_text(
Â Â Â Â Â Â |{ NEW utc( )->ts TIMESTAMP = ISO }| ).
Â Â ENDMETHOD.
ENDCLASS.

Executable Example

[Instance Operator NEW on the Left](javascript:call_link\('abennew_on_the_left_abexa.htm'\))

Continue
![Example](exa.gif "Example") [Instance Operator NEW on the Left](javascript:call_link\('abennew_on_the_left_abexa.htm'\))



**ðŸ“– Source**: [abennew_constructor_params_class.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abennew_constructor_params_class.htm)

### abennew_on_the_left_abexa.htm

> **ðŸ“– Official SAP Documentation**: [abennew_on_the_left_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abennew_on_the_left_abexa.htm)
> **ðŸ” Direct Link**: This section contains the complete content from the official SAP ABAP documentation page.


**ðŸ“– Source**: [abennew_on_the_left_abexa.htm](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abennew_on_the_left_abexa.htm)


Â  

* * *

AS ABAP Release 758, Â©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](javascript:call_link\('abenabap.htm'\))Â â†’Â  [ABAP - Programming Language](javascript:call_link\('abenabap_reference.htm'\))Â â†’Â  [Creating Objects and Values](javascript:call_link\('abencreate_objects.htm'\))Â â†’Â  [NEW, Instance Operator](javascript:call_link\('abenconstructor_expression_new.htm'\))Â â†’Â  [NEW, Classes](javascript:call_link\('abennew_constructor_params_class.htm'\))Â â†’Â 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20Instance%20Operator%20NEW%20on%20the%20Left%2C%20ABENNEW_ON_THE_LEFT_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

Instance Operator NEW on the Left

Constructor expression with NEW on the left side of an expression.

Source Code Â  

\* Public class definition
CLASS cl\_demo\_new\_on\_the\_left DEFINITION
Â Â INHERITING FROM cl\_demo\_classrun
Â Â PUBLIC
Â Â CREATE PUBLIC .
Â Â PUBLIC SECTION.
Â Â Â Â METHODS main REDEFINITION.
ENDCLASS.
\* CCIMP
CLASS class DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â DATA attr TYPE string.
Â Â Â Â METHODS constructor.
ENDCLASS.
CLASS frame DEFINITION.
Â Â PUBLIC SECTION.
Â Â Â Â CLASS-DATA oref TYPE REF TO class.
ENDCLASS.
CLASS class IMPLEMENTATION.
Â Â METHOD constructor.
Â Â Â Â frame=>oref = me.
Â Â ENDMETHOD.
ENDCLASS.
\* Public class implementation
CLASS cl\_demo\_new\_on\_the\_left IMPLEMENTATION.
Â Â METHOD main.
Â Â Â Â NEW class( )->attr = 'foo'.
Â Â Â Â out->write( frame=>oref->attr ).
Â Â Â Â TRY.
Â Â Â Â Â Â Â Â NEW class( )->('attr') = 'foo'.
Â Â Â Â Â Â Â Â out->write( frame=>oref->attr ).
Â Â Â Â Â Â Â Â NEW class( )->('foo') = 'foo'.
Â Â Â Â Â Â Â Â out->write( frame=>oref->attr ).
Â Â Â Â Â Â CATCH cx\_sy\_assign\_illegal\_component INTO FINAL(exc).
Â Â Â Â Â Â Â Â out->write( exc->get\_text( ) ).
Â Â Â Â ENDTRY.
Â Â ENDMETHOD.
ENDCLASS.

Description Â  

Although the reference variable only exists during the statement, the reference to the object is assigned to the reference variable user=>oref in the constructor of the class class, thus preserving the object. The example also shows dynamic specifications and the exception that is raised in case of an invalid attribute.
