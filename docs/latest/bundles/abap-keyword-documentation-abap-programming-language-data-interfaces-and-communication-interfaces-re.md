# ABAP - Keyword Documentation / ABAP - Programming Language / Data Interfaces and Communication Interfaces / Remote Function Call (RFC) / RFC - Examples

Included pages: 7


### abenrfc_abexas.htm

---
title: "RFC - Examples"
description: |
  -   RFC - Parameter Passing in RFC(https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_parameters_abexa.htm) -   RFC - Exception Handling(https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_exceptions_abexa.htm) -   RFC - Dynamic Parameter Passing in sRFC
version: "latest"
category: "general"
type: "abap-reference"
sourceUrl: "https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_abexas.htm"
abapFile: "abenrfc_abexas.htm"
keywords: ["do", "if", "data", "exception-handling", "abenrfc", "abexas"]
---

* * *

AS ABAP Release 758, ©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap.htm) →  [ABAP - Programming Language](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_reference.htm) →  [Data Interfaces and Communication Interfaces](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_data_communication.htm) →  [Remote Function Call (RFC)](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc.htm) → 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20RFC%20-%20Examples%2C%20ABENRFC_ABEXAS%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

RFC - Examples

-   [RFC - Parameter Passing in RFC](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_parameters_abexa.htm)
-   [RFC - Exception Handling](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_exceptions_abexa.htm)
-   [RFC - Dynamic Parameter Passing in sRFC](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_parameter_tables_abexa.htm)
-   [RFC - Parallel aRFC](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenparallel_rfc_abexa.htm)
-   [RFC - Dynamic Destination](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_dynamic_dest_abexa.htm)
-   [RFC - Implicit Logon Data](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_logon_data_abexa.htm)

Continue
![Example](exa.gif "Example") [RFC - Parameter Passing in RFC](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_parameters_abexa.htm)
![Example](exa.gif "Example") [RFC - Exception Handling](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_exceptions_abexa.htm)
![Example](exa.gif "Example") [RFC - Dynamic Parameter Passing in sRFC](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_parameter_tables_abexa.htm)
![Example](exa.gif "Example") [RFC - Parallel aRFC](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenparallel_rfc_abexa.htm)
![Example](exa.gif "Example") [RFC - Dynamic Destination](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_dynamic_dest_abexa.htm)
![Example](exa.gif "Example") [RFC - Implicit Logon Data](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_logon_data_abexa.htm)


### abenrfc_parameters_abexa.htm

---
title: "RFC - Parameter Passing in RFC"
description: |
  This example demonstrates parameter passing in two RFC variants. Source Code  Public class definition CLASS cl_demo_rfc_params_srfc_arfc DEFINITION PUBLIC INHERITING FROM cl_demo_classrun CREATE PUBLIC . PUBLIC SECTION. METHODS callback IMPORTING !p_task TYPE clike . METHODS main REDEFINIT
version: "latest"
category: "general"
type: "abap-reference"
sourceUrl: "https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_parameters_abexa.htm"
abapFile: "abenrfc_parameters_abexa.htm"
keywords: ["do", "if", "method", "class", "data", "abenrfc", "parameters", "abexa"]
---

* * *

AS ABAP Release 758, ©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap.htm) →  [ABAP - Programming Language](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_reference.htm) →  [Data Interfaces and Communication Interfaces](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_data_communication.htm) →  [Remote Function Call (RFC)](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc.htm) →  [RFC - Examples](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_abexas.htm) → 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20RFC%20-%20Parameter%20Passing%20in%20RFC%2C%20ABENRFC_PARAMETERS_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

RFC - Parameter Passing in RFC

This example demonstrates parameter passing in two RFC variants.

Source Code   

\* Public class definition
CLASS cl\_demo\_rfc\_params\_srfc\_arfc DEFINITION
  PUBLIC
  INHERITING FROM cl\_demo\_classrun
  CREATE PUBLIC .
  PUBLIC SECTION.
    METHODS callback
      IMPORTING
        !p\_task TYPE clike .
    METHODS main
      REDEFINITION .
  PRIVATE SECTION.
    DATA:
      val\_in     TYPE string,
      val\_out    TYPE string,
      val\_in\_out TYPE string,
      msg        TYPE c LENGTH 80.
ENDCLASS.
\* Public class implementation
CLASS cl\_demo\_rfc\_params\_srfc\_arfc IMPLEMENTATION.
  METHOD main.
    "Synchronous RFC
    val\_in     = \`val\_in\`.
    val\_out    = \`\`.
    val\_in\_out = \`val\_in\_out\`.
    CALL FUNCTION 'DEMO\_RFM\_PARAMETERS'
      DESTINATION 'NONE'
      EXPORTING
        p\_in                  = val\_in
      IMPORTING
        p\_out                 = val\_out
      CHANGING
        p\_in\_out              = val\_in\_out
      EXCEPTIONS
        system\_failure        = 2 MESSAGE msg
        communication\_failure = 4 MESSAGE msg.
    IF sy-subrc <> 0.
      out->write( |Error when calling sRFC.\\n{ msg }| ).
      RETURN.
    ENDIF.
    out->next\_section( 'sRFC'
      )->write( |{ val\_out }\\n{ val\_in\_out }| ).
    "Asynchronous RFC
    val\_in     = \`val\_in\`.
    val\_out    = \`\`.
    val\_in\_out = \`val\_in\_out\`.
    CALL FUNCTION 'DEMO\_RFM\_PARAMETERS'
      DESTINATION 'NONE'
      STARTING NEW TASK 'DEMO'
      CALLING me->callback ON END OF TASK
      EXPORTING
        p\_in                  = val\_in
      CHANGING
        p\_in\_out              = val\_in\_out
      EXCEPTIONS
        system\_failure        = 2 MESSAGE msg
        communication\_failure = 4 MESSAGE msg.
    IF sy-subrc <> 0.
      out->write( |Error when calling aRFC.\\n{ msg }| ).
      RETURN.
    ENDIF.
    WAIT FOR ASYNCHRONOUS TASKS UNTIL val\_out IS NOT INITIAL
                                UP TO 10 SECONDS.
    out->next\_section( 'aRFC'
      )->write( |{ val\_out }\\n{ val\_in\_out }| ).
  ENDMETHOD.
  METHOD callback.
    val\_out    = \`\`.
    val\_in\_out = \`\`.
    RECEIVE RESULTS FROM FUNCTION 'DEMO\_RFM\_PARAMETERS'
      IMPORTING
        p\_out                 = val\_out
      CHANGING
        p\_in\_out              = val\_in\_out
      EXCEPTIONS
        system\_failure        = 2 MESSAGE msg
        communication\_failure = 4 MESSAGE msg.
    IF sy-subrc <> 0.
      out->write( '|Error when receiving aRFC.\\n{ msg }|' ).
      RETURN.
    ENDIF.
  ENDMETHOD.
ENDCLASS.

Description   

The function module DEMO\_RFM\_PARAMETERS is called using [synchronous](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abapcall_function_destination.htm) and [asynchronous](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abapcall_function_starting.htm) RFC.

-   In synchronous calls, parameters are passed and applied directly in the statement CALL FUNCTION.
-   In asynchronous calls, parameters can only be passed in the statement CALL FUNCTION. Parameters are applied using the statement [RECEIVE](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abapreceive.htm) in a callback routine. The statement [WAIT FOR ASYNCHRONOUS TASKS](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abapwait_arfc.htm) is used to wait for this routine.


### abenrfc_exceptions_abexa.htm

---
title: "RFC - Exception Handling"
description: |
  This example demonstrates exception handling in RFC. Source Code  Public class definition CLASS cl_demo_rfc_exceptions DEFINITION PUBLIC INHERITING FROM cl_demo_classrun CREATE PUBLIC . PUBLIC SECTION. METHODS main REDEFINITION . ENDCLASS.  Public class implementation CLASS cl_demo_rfc_e
version: "latest"
category: "error-handling"
type: "abap-reference"
sourceUrl: "https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_exceptions_abexa.htm"
abapFile: "abenrfc_exceptions_abexa.htm"
keywords: ["do", "if", "case", "method", "class", "data", "exception-handling", "abenrfc", "exceptions", "abexa"]
---

* * *

AS ABAP Release 758, ©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap.htm) →  [ABAP - Programming Language](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_reference.htm) →  [Data Interfaces and Communication Interfaces](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_data_communication.htm) →  [Remote Function Call (RFC)](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc.htm) →  [RFC - Examples](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_abexas.htm) → 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20RFC%20-%20Exception%20Handling%2C%20ABENRFC_EXCEPTIONS_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

RFC - Exception Handling

This example demonstrates exception handling in RFC.

Source Code   

\* Public class definition
CLASS cl\_demo\_rfc\_exceptions DEFINITION
  PUBLIC
  INHERITING FROM cl\_demo\_classrun
  CREATE PUBLIC .
  PUBLIC SECTION.
    METHODS main
      REDEFINITION .
ENDCLASS.
\* Public class implementation
CLASS cl\_demo\_rfc\_exceptions IMPLEMENTATION.
  METHOD main.
    DATA msg TYPE c LENGTH 255.
    "Classical exception handling
    CALL FUNCTION 'DEMO\_RFM\_CLASSIC\_EXCEPTION'
      DESTINATION 'SELF'
      EXCEPTIONS
        system\_failure        = 1 MESSAGE msg
        communication\_failure = 2 MESSAGE msg
        OTHERS                = 3.
    CASE sy-subrc.
      WHEN 1.
        out->write( \`EXCEPTION SYSTEM\_FAILURE \` && msg ).
      WHEN 2.
        out->write( \`EXCEPTION COMMUNICATION\_FAILURE \` && msg ).
      WHEN 3.
        out->write( \`EXCEPTION OTHERS\` ).
    ENDCASE.
  ENDMETHOD.
ENDCLASS.

Description   

The DEMO\_RFM\_CLASSIC\_EXCEPTION function module raises the non-class-based exception CLASSIC\_EXCEPTION.

When DEMO\_RFM\_CLASSIC\_EXCEPTION is called, EXCEPTIONS is specified and values for sy-subrc are assigned to the possible classic exceptions. If none of the predefined RFC interface exceptions are raised, sy-subrc is set to 3.


### abenrfc_parameter_tables_abexa.htm

---
title: "RFC - Dynamic Parameter Passing in sRFC"
description: |
  This example demonstrates parameter passing with parameter and exception tables for sRFC. Source Code  Public class definition CLASS cl_demo_srfc_parameter_tables DEFINITION PUBLIC INHERITING FROM cl_demo_classrun CREATE PUBLIC . PUBLIC SECTION. METHODS main REDEFINITION . PRIVATE SECTION. D
version: "latest"
category: "general"
type: "abap-reference"
sourceUrl: "https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_parameter_tables_abexa.htm"
abapFile: "abenrfc_parameter_tables_abexa.htm"
keywords: ["do", "if", "try", "catch", "method", "class", "data", "internal-table", "abenrfc", "parameter", "tables", "abexa"]
---

* * *

AS ABAP Release 758, ©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap.htm) →  [ABAP - Programming Language](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_reference.htm) →  [Data Interfaces and Communication Interfaces](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_data_communication.htm) →  [Remote Function Call (RFC)](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc.htm) →  [RFC - Examples](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_abexas.htm) → 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20RFC%20-%20Dynamic%20Parameter%20Passing%20in%20sRFC%2C%20ABENRFC_PARAMETER_TABLES_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20
improvement:)

RFC - Dynamic Parameter Passing in sRFC

This example demonstrates parameter passing with parameter and exception tables for sRFC.

Source Code   

\* Public class definition
CLASS cl\_demo\_srfc\_parameter\_tables DEFINITION
  PUBLIC
  INHERITING FROM cl\_demo\_classrun
  CREATE PUBLIC .
  PUBLIC SECTION.
    METHODS main
      REDEFINITION .
  PRIVATE SECTION.
    DATA:
      val\_in     TYPE string,
      val\_out    TYPE string,
      val\_in\_out TYPE string,
      msg        TYPE string.
ENDCLASS.
\* Public class implementation
CLASS cl\_demo\_srfc\_parameter\_tables IMPLEMENTATION.
  METHOD main.
    val\_in     = \`val\_in\`.
    val\_out    = \`\`.
    val\_in\_out = \`val\_in\_out\`.
    DATA(rfm)  = \`DEMO\_RFM\_PARAMETERS\`.
    cl\_demo\_input=>new( )->request( CHANGING field = rfm ).
    FINAL(dest) = 'NONE'.
    TRY.
        rfm = cl\_abap\_dyn\_prg=>check\_whitelist\_str(
            EXPORTING
              val       =  rfm
              whitelist = \`DEMO\_RFM\_PARAMETERS, DEMO\_RFM\_PARAMETERX\` ).
      CATCH cx\_abap\_not\_in\_whitelist.
        out->write(
          \`Call either DEMO\_RFM\_PARAMETERS OR DEMO\_RFM\_PARAMETERX\` ).
        RETURN.
    ENDTRY.
    DATA: ptab TYPE abap\_func\_parmbind\_tab,
          etab TYPE abap\_func\_excpbind\_tab.
    ptab = VALUE #( ( name  = 'P\_IN'
                      kind  = abap\_func\_exporting
                      value = REF #( val\_in ) )
                    ( name  = 'P\_OUT'
                      kind  = abap\_func\_importing
                      value = REF #( val\_out ) )
                    ( name  = 'P\_IN\_OUT'
                      kind  = abap\_func\_changing
                      value = REF #( val\_in\_out ) ) ).
    etab = VALUE #( ( name = 'SYSTEM\_FAILURE'
                      value = 2
                      message = REF #( msg ) )
                    ( name = 'COMMUNICATION\_FAILURE'
                      value = 4
                      message = REF #( msg ) ) ).
    CALL FUNCTION rfm
      DESTINATION dest
      PARAMETER-TABLE ptab
      EXCEPTION-TABLE etab.
    IF sy-subrc <> 0.
      out->write( |Error when calling sRFC.\\n{ msg }| ).
      RETURN.
    ENDIF.
    out->write( |{ val\_out }\\n{ val\_in\_out }| ).
  ENDMETHOD.
ENDCLASS.

Description   

The function module DEMO\_RFM\_PARAMETERS is called using [sRFC](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abensrfc_glosry.htm "Glossary Entry"). Parameters are passed and exceptions are handled dynamically using internal tables ptab and etab.


### abenparallel_rfc_abexa.htm

---
title: "RFC - Parallel aRFC"
description: |
  This example demonstrates parallel asynchronous RFC processing. Source Code  Public class definition CLASS cl_demo_parallel_rfc DEFINITION PUBLIC INHERITING FROM cl_demo_classrun CREATE PUBLIC . PUBLIC SECTION. METHODS callbback_meth IMPORTING !p_task TYPE clike . METHODS main REDEFINITION
version: "latest"
category: "general"
type: "abap-reference"
sourceUrl: "https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenparallel_rfc_abexa.htm"
abapFile: "abenparallel_rfc_abexa.htm"
keywords: ["do", "if", "case", "try", "method", "class", "data", "types", "internal-table", "abenparallel", "rfc", "abexa"]
---

* * *

AS ABAP Release 758, ©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap.htm) →  [ABAP - Programming Language](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_reference.htm) →  [Data Interfaces and Communication Interfaces](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_data_communication.htm) →  [Remote Function Call (RFC)](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc.htm) →  [RFC - Examples](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_abexas.htm) → 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20RFC%20-%20Parallel%20aRFC%2C%20ABENPARALLEL_RFC_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

RFC - Parallel aRFC

This example demonstrates parallel asynchronous RFC processing.

Source Code   

\* Public class definition
CLASS cl\_demo\_parallel\_rfc DEFINITION
  PUBLIC
  INHERITING FROM cl\_demo\_classrun
  CREATE PUBLIC .
  PUBLIC SECTION.
    METHODS callbback\_meth
      IMPORTING
        !p\_task TYPE clike .
    METHODS main
      REDEFINITION .
  PRIVATE SECTION.
    TYPES: BEGIN OF task\_type,
             name TYPE string,
             dest TYPE string,
           END OF task\_type.
    DATA: task\_list TYPE STANDARD TABLE OF task\_type,
          task\_wa   TYPE task\_type,
          rcv\_jobs  TYPE i,
          mess      TYPE c LENGTH 80.
ENDCLASS.
\* Public class implementation
CLASS cl\_demo\_parallel\_rfc IMPLEMENTATION.
  METHOD main.
    DATA: snd\_jobs TYPE i,
          exc\_flag TYPE i,
          indx     TYPE c LENGTH 4,
          name     TYPE c LENGTH 8.
    DO 10 TIMES.
      indx = sy-index.
      name = 'Task' && indx.
      CALL FUNCTION 'RFC\_SYSTEM\_INFO'
        STARTING NEW TASK name
        DESTINATION IN GROUP DEFAULT
        CALLING callbback\_meth ON END OF TASK
        EXCEPTIONS
          system\_failure        = 1 MESSAGE mess
          communication\_failure = 2 MESSAGE mess
          resource\_failure      = 3.
      CASE sy-subrc.
        WHEN 0.
          snd\_jobs += 1.
        WHEN 1 OR 2.
          out->write\_text( mess ).
        WHEN 3.
          IF snd\_jobs >= 1 AND
             exc\_flag = 0.
            exc\_flag = 1.
            WAIT UNTIL rcv\_jobs >= snd\_jobs
                 UP TO 5 SECONDS.
          ENDIF.
          IF sy-subrc = 0.
            exc\_flag = 0.
          ELSE.
            out->write( 'Resource failure' ).
          ENDIF.
        WHEN OTHERS.
          out->write( 'Other error' ).
      ENDCASE.
    ENDDO.
    WAIT FOR ASYNCHRONOUS TASKS UNTIL rcv\_jobs >= snd\_jobs.
    out->write( task\_list ).
  ENDMETHOD.
  METHOD callbback\_meth.
    DATA info TYPE rfcsi.
    task\_wa-name = p\_task.
    rcv\_jobs += 1.
    RECEIVE RESULTS FROM FUNCTION 'RFC\_SYSTEM\_INFO'
      IMPORTING
        rfcsi\_export = info
      EXCEPTIONS
        system\_failure        = 1 MESSAGE mess
        communication\_failure = 2 MESSAGE mess.
    IF sy-subrc = 0.
      task\_wa-dest = info-rfcdest.
    ELSE.
      task\_wa-dest = mess.
    ENDIF.
    APPEND task\_wa TO task\_list.
  ENDMETHOD.
ENDCLASS.

Description   

Parallel asynchronous processing of the function module RFC\_SYSTEM\_INFO using [asynchronous remote function calls](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenasynchronous_rfc_glosry.htm "Glossary Entry"). Ten calls with different task IDs name are made, which each run in a separate [work process](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenwork_process_glosry.htm "Glossary Entry"). The callback routine callbback\_meth counts the completed function modules and receives information about the target system.

The addition GROUP DEFAULT is used to distribute the execution across all [AS instances](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenas_instance_glosry.htm "Glossary Entry") of the current AS ABAP. If no more free work processes are available after at least one successful call, the execution is stopped until all function modules started up to that point have been completed. This interruption is limited to a maximum of 5 seconds.

After all function modules have been started, the system waits until all callback routines have been executed. After that, the internal table task\_list filled there is produced. The output shows the order in which the individual tasks were completed and on which AS instance they were executed.


### abenrfc_dynamic_dest_abexa.htm

---
title: "RFC - Dynamic Destination"
description: |
  This example demonstrates how dynamic destinations(https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_destination.htm) are created. Source Code  Public class definition CLASS cl_demo_rfc_dynamic_dest DEFINITION PUBLIC INHERITING FROM cl_demo_classrun CREATE PUBLIC . PU
version: "latest"
category: "general"
type: "abap-reference"
sourceUrl: "https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_dynamic_dest_abexa.htm"
abapFile: "abenrfc_dynamic_dest_abexa.htm"
keywords: ["do", "if", "method", "class", "data", "abenrfc", "dynamic", "dest", "abexa"]
---

* * *

AS ABAP Release 758, ©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap.htm) →  [ABAP - Programming Language](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_reference.htm) →  [Data Interfaces and Communication Interfaces](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_data_communication.htm) →  [Remote Function Call (RFC)](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc.htm) →  [RFC - Examples](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_abexas.htm) → 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20RFC%20-%20Dynamic%20Destination%2C%20ABENRFC_DYNAMIC_DEST_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

RFC - Dynamic Destination

This example demonstrates how [dynamic destinations](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_destination.htm) are created.

Source Code   

\* Public class definition
CLASS cl\_demo\_rfc\_dynamic\_dest DEFINITION
  PUBLIC
  INHERITING FROM cl\_demo\_classrun
  CREATE PUBLIC .
  PUBLIC SECTION.
    METHODS main
      REDEFINITION .
ENDCLASS.
\* Public class implementation
CLASS cl\_demo\_rfc\_dynamic\_dest IMPLEMENTATION.
  METHOD main.
    DATA:
      val\_in     TYPE string VALUE \`val\_in\`,
      val\_in\_out TYPE string VALUE \`val\_in\_out\`,
      val\_out    TYPE string,
      msg        TYPE c LENGTH 80.
    IF sy-uname IS INITIAL.
      out->write(
        \`Example not possible for anonymous user\` ).
      RETURN.
    ENDIF.
    FINAL(in) = cl\_demo\_input=>new( ).
    DATA(client) = sy-mandt.
    in->add\_field( CHANGING field = client ).
    DATA(uname) = sy-uname.
    in->add\_field( CHANGING field = uname ).
    DATA(langu) = sy-langu.
    in->add\_field( CHANGING field = langu ).
    DATA(sysid) = sy-sysid.
    in->add\_field( CHANGING field = sysid ).
    DATA(host) = CONV rfchost( sy-host ).
    in->add\_field( CHANGING field = host ).
    DATA(group) = CONV rfcload( 'PUBLIC' ).
    in->add\_field( CHANGING field = group ).
    in->request( ).
    FINAL(dest) = cl\_dynamic\_destination=>create\_rfc\_destination(
                   logon\_client   = client
                   logon\_user     = uname
                   logon\_language = langu
                   sid            = sysid
                   server         = host
                   group          = group ).
    CALL FUNCTION 'DEMO\_RFM\_PARAMETERS'
      DESTINATION dest
      EXPORTING
        p\_in                  = val\_in
      IMPORTING
        p\_out                 = val\_out
      CHANGING
        p\_in\_out              = val\_in\_out
      EXCEPTIONS
        system\_failure        = 2 MESSAGE msg
        communication\_failure = 4 MESSAGE msg.
    IF sy-subrc <> 0.
      out->write( |Error when calling sRFC.\\n{ msg }| ).
      RETURN.
    ENDIF.
    out->write( |{ val\_out }\\n{ val\_in\_out }| ).
  ENDMETHOD.
ENDCLASS.

Description   

The method CREATE\_RFC\_DESTINATION of class CL\_DYNAMIC\_DESTINATION is used to create a [dynamic destination](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_destination.htm). Input values, whose default values are determined from system fields for the current AS ABAP, are passed as parameters. A remote-enabled function module is called using the dynamic destination. The call is only successful if the destination is created correctly.


### abenrfc_logon_data_abexa.htm

---
title: "RFC - Implicit Logon Data"
description: |
  This example demonstrates how the logon data of an RFC session is set implicitly. Source Code  Public class definition CLASS cl_demo_rfc_implct_logon_data DEFINITION PUBLIC INHERITING FROM cl_demo_classrun CREATE PUBLIC . PUBLIC SECTION. METHODS main REDEFINITION . ENDCLASS.  Public class
version: "latest"
category: "general"
type: "abap-reference"
sourceUrl: "https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_logon_data_abexa.htm"
abapFile: "abenrfc_logon_data_abexa.htm"
keywords: ["do", "if", "try", "method", "class", "data", "abenrfc", "logon", "abexa"]
---

* * *

AS ABAP Release 758, ©Copyright 2024 SAP SE. All rights reserved.

[ABAP - Keyword Documentation](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap.htm) →  [ABAP - Programming Language](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_reference.htm) →  [Data Interfaces and Communication Interfaces](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenabap_data_communication.htm) →  [Remote Function Call (RFC)](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc.htm) →  [RFC - Examples](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_abexas.htm) → 

 [![](Mail.gif?object=Mail.gif "Feedback mail for displayed topic") Mail Feedback](mailto:f1_help@sap.com?subject=Feedback%20on%20ABAP%20Documentation&body=Document:%20RFC%20-%20Implicit%20Logon%20Data%2C%20ABENRFC_LOGON_DATA_ABEXA%2C%20758%0D%0A%0D%0AError:%0D%0A%0D%0A%0D%0A%0D%0ASuggestion%20for%20improvement:)

RFC - Implicit Logon Data

This example demonstrates how the logon data of an RFC session is set implicitly.

Source Code   

\* Public class definition
CLASS cl\_demo\_rfc\_implct\_logon\_data DEFINITION
  PUBLIC
  INHERITING FROM cl\_demo\_classrun
  CREATE PUBLIC .
  PUBLIC SECTION.
    METHODS main
      REDEFINITION .
ENDCLASS.
\* Public class implementation
CLASS cl\_demo\_rfc\_implct\_logon\_data IMPLEMENTATION.
  METHOD main.
    DATA:
      BEGIN OF logon\_data,
        uname       TYPE sy-uname,
        mandt       TYPE sy-mandt,
        logon\_langu TYPE sy-langu,
        langu       TYPE sy-langu,
      END OF logon\_data.
    IF cl\_abap\_syst=>get\_logon\_language( ) <> 'E'.
      SET LOCALE LANGUAGE 'E'.
    ELSE.
      SET LOCALE LANGUAGE 'D'.
    ENDIF.
    logon\_data = VALUE #(
        uname       = sy-uname
        mandt       = sy-mandt
        logon\_langu = cl\_abap\_syst=>get\_logon\_language( )
        langu       = cl\_abap\_syst=>get\_language( ) ).
    ASSERT logon\_data-langu = sy-langu.
    out->next\_section( 'Parameters of Caller'
      )->write( logon\_data ).
    CALL FUNCTION 'DEMO\_RFM\_LOGON\_DATA' DESTINATION 'NONE'
      IMPORTING
        uname       = logon\_data-uname
        mandt       = logon\_data-mandt
        logon\_langu = logon\_data-logon\_langu
        langu       = logon\_data-langu.
    out->next\_section( 'Parameters of Callee'
      )->write( logon\_data ).
  ENDMETHOD.
ENDCLASS.

Description   

A remote-enabled function module DEMO\_RFM\_LOGON\_DATA is called with the destination NONE using the RFC interface. The logon data user name, client, and logon language of the corresponding [RFC session](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenrfc_context.htm) are provided implicitly. To demonstrate that the [logon language](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abenlogon_language_glosry.htm "Glossary Entry") of the RFC session is set by the language of the [text environment](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abentext_environment_glosry.htm "Glossary Entry") of the caller, it is set to a language other than the logon language of the caller before the call with the statement [SET LOCALE LANGUAGE](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/abapset_locale.htm). In the called RFM the language of the text environment of the RFC session is also changed and is then also different to the logon language.

If the function module DEMO\_RFM\_LOGON\_DATA is called remotely again, the RFC session is reused with the existing logon data. Changing the language of the text environment again in the caller would no longer have any effect.
